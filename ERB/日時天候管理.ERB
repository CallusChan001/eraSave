;FileName_COMMON.ERB ------------------------------- Rev1.00
;季節管理
;CALL		USER
;ARG		VOID
;RETURN		VOID
;COMMENT
;-----------------------------------------------------------
@CHANGE_MONTH
#LOCALSIZE 1
#LOCALSSIZE 1
#DIM 神簽代
#DIM IFS_COST
;日期が月を跨いだら月変更    日期跨月变更
IF DAY:日期 > 31
	DAY:日期 = 1
	DAY:季節 ++
	;冬の次は春
	IF DAY:季節 > 季节_冬
		DAY:季節 = 季节_春
		FLAG:年始貸付金 = FLAG:地主貸付金
		;(天候パッチ)長期予報を行う    （天气补丁）进行长期预报/年预报
		CALL WEATHER_FORECAST_1YEAR
	ENDIF
	;夏になったらバレンタインフラグリセット    夏天重置情人节标记
	SIF DAY:季節 == 季节_夏
		CVARSET CFLAG, 383
	;徴収発生
	SIF FLAG:地主
		FLAG:年貢 = 1
	IF FLAG:澆水代行
		FLAG:澆水代行 = 0
		PRINTFORMW 浇水代理的期限到期了。
	ENDIF
	IF FLAG:新聞購読
		IF MONEY < 3000 && MONEY:2 < 10
			PRINTFORML 因为缺钱所以文文。新闻的订阅停止了。
		ELSE
			PRINTL
			PRINTFORML ★继续订阅文文。新闻吗？（每月%金額表示(1500)% or 5筹码）
			CALL ASK_M("解约",1,@"更新（%金額表示(1500)%）",MONEY >= 1500,"更新（5筹码）",MONEY:2 >= 5)
			SELECTCASE RESULT
				CASE 0
					PRINTFORML 停止了订阅。
					FLAG:新聞購読 = 0
				CASE 1
					CALL PAY_MONEY_YEN(3000, 2)
					PRINTFORML 本月继续订阅。
				CASE 2
					CALL PAY_MONEY_CM(10, 2)
					PRINTFORML 本月继续订阅。
			ENDSELECT
		ENDIF
	ENDIF
	IF FLAG:神簽契約
		神簽代 = 50
		SIF !MAIN_MAP
			神簽代 = 30
		IF MONEY:2 < 神簽代
			PRINTFORML 因为筹码不足、所以神签契约被中止了
			FLAG:神簽契約 = 0
		ELSE
			PRINTFORML 要继续神签契约吗？
			CALL ASK_YN
			IF !RESULT
				PRINTFORML 更新了契约
				CALL PAY_MONEY_CM(神簽代, 2)
			ELSE
				FLAG:神簽契約 = 0
				PRINTFORML 结束了契约
			ENDIF
		ENDIF
	ENDIF
	;虫采集服务续订
	SIF FLAG:蟲の採集サービス
		CALL IFS_SUBSCRIBE
	;堇子的超能力加护续订
	SIF ENCOUNTDLC
		CALL SP_ENCOUNT
ENDIF
;異常値を固定修正
SIF DAY:3 < 1
	DAY:3 = 1
SIF DAY:2 < 1 || DAY:2 > 4
	DAY:2 = 1

;FileName_COMMON.ERB ------------------------------- Rev1.00
;時間帯管理
;CALL		USER
;ARG		VOID
;RETURN		VOID
;COMMENT
;-----------------------------------------------------------
@CHANGE_TIMEZONE
#LOCALSIZE 1
#LOCALSSIZE 1
#DIM 現在時間帯

;SELECTCASE TIME
;	CASE 240 TO 599
;		LOCAL = 1
;	CASE 600 TO 1139
;		LOCAL = 2
;	CASE 1140 TO 1440 , 0 TO 239
;		LOCAL = 3
;ENDSELECT

SELECTCASE TIME
	;AM4~6　早朝
	CASE 240 TO 359
		LOCAL = 1
	;AM6~12　正午前
	CASE 360 TO 719
		LOCAL = 2
	;PM12~3　昼
	CASE 720 TO 899
		LOCAL = 3
	;PM3~6　昼下がり
	CASE 900 TO 1079
		LOCAL = 4
	;PM6~8　夕
	CASE 1080 TO 1199
		LOCAL = 5
	;PM8~11　夜
	CASE 1200 TO 1379
		LOCAL = 6
	;PM11~AM4　深夜
	CASE 0 TO 239, IS >= 1380
		LOCAL = 7
ENDSELECT

;(天候パッチ)天候システム上の時間帯管理    天气系统的时间带管理
SELECTCASE TIME
	;AM0~3　未明
	CASE 0 TO 179
		現在時間帯 = 1
	;AM3~6　明け方
	CASE 180 TO 359
		現在時間帯 = 2
	;AM6~9　朝
	CASE 360 TO 539
		現在時間帯 = 3
	;AM9~12　昼前
	CASE 540 TO 719
		現在時間帯 = 4
	;PM0~3　昼過ぎ
	CASE 720 TO 899
		現在時間帯 = 5
	;PM3~6　夕方
	CASE 900 TO 1079
		現在時間帯 = 6
	;PM6~9　夜のはじめ頃
	CASE 1080 TO 1259
		現在時間帯 = 7
	;PM9~12　夜遅く
	CASE IS >= 1260
		現在時間帯 = 8
ENDSELECT

;(天候パッチ)現在的天候傾向を判定    当前天气倾向判定，主要是天气系统的倾向
CALL CHANGE_WEATHER_SYSTEM

IF LOCAL != TIME:2
	TIME:2 = LOCAL
	TRYCALLFORM MOB_PLACE_{MAIN_MAP}
	CALL TIMEZONE_MOVE
ENDIF

;(天候パッチ)天候システム上の時間帯変動・天候変化    天气系统的时间带变动和天气变化，主要是具体天气变动
IF WEATHER_TIMEZOME != 現在時間帯
	WEATHER_TIMEZOME = 現在時間帯
	CALL CHANGE_WEATHER
ENDIF
;(天候パッチ)時刻・天候に応じて背景色を変更    时间和天气对应的背景色变更
CALL SET_MAP_WEATHER_BGCOLOR

;FileName_COMMON.ERB ------------------------------- Rev1.00
;季節名取得
;CALL		USER
;ARG		引数がない場合DAY:2
;RETURN		VOID
;COMMENT
;-----------------------------------------------------------
@GET_MONTH(ARG)
#FUNCTIONS
#LOCALSIZE 1
#LOCALSSIZE 5
SIF !ARG
	ARG = DAY:2
SIF !STRLENS(LOCALS)
	SPLIT "无之月/春之月/夏之月/秋之月/冬之月", "/", LOCALS
RETURNF LOCALS:(ARG)

;FileName_COMMON.ERB ------------------------------- Rev1.00
;天気名取得
;CALL		USER
;ARG		VOID
;RETURN		VOID
;COMMENT
;-----------------------------------------------------------
@GET_WEATHER(ARG = -1)
#FUNCTIONS
#LOCALSIZE 1
#LOCALSSIZE 24
#DIMS 表示_异常气象,15
SIF ARG == -1
	ARG = TIME:5
;(天候パッチ)台風・猛吹雪は表示しない
;为什么不表示呢qwq，好多地方都在引用你呢，INFO_SHOW_WEATHER我帮你处理了哦
IF WEATHERDLC == 1
	IF FLAG:異常気象
		SPLIT " /Ｃ敏感/Ｖ敏感/Ａ敏感/Ｂ敏感/Ｍ敏感/裙子/胖次/桃色之风/台风/暴风雪/钻石尘/黄沙/烈日/极光", "/", 表示_异常气象
		RETURNF 表示_异常气象:(FLAG:異常気象)
	ENDIF
	SIF !STRLENS(LOCALS)
		SPLIT "晴天/晴朗/多云/阴天/雨/大雨/雾雨/雾/雪/吹雪/细雪/雾雪/雨雪交加/霰/豪雨/冰雹/太阳雨/疏雨/风雨/晴岚/川雾/无风/梅雨", "/", LOCALS
ELSE
	IF FLAG:異常気象; && FLAG:異常気象 != 9 && FLAG:異常気象 != 10
		SPLIT " /Ｃ敏感/Ｖ敏感/Ａ敏感/Ｂ敏感/Ｍ敏感/裙子/胖次/桃色之风/台风/暴风雪", "/", 表示_异常气象
		RETURNF 表示_异常气象:(FLAG:異常気象)
	ENDIF
	;(天候パッチ)豪雨・ひょうを追加
	SIF !STRLENS(LOCALS)
		;SPLIT "晴天/晴朗/多云/阴天/雨/大雨/雾雨/雾/雪/大雪/细雪/雾雪/雨雪交加/霰/豪雨/冰雹", "/", LOCALS
		SPLIT "少云/晴/多云/阴/雨/大雨/雾雨/雾/雪/大雪/细雪/雾雪/雨夹雪/冰霰/暴雨/冰雹", "/", LOCALS
ENDIF
;RETURNF LOCALS:(TIME:5)
RETURNF LOCALS:(ARG)

;-------------------------------------------------
;平日判定(ARG)
;DAYを渡して月～金曜日なら1
;-------------------------------------------------
@IS_WEEKDAY(ARG)
#FUNCTION
#LOCALSIZE 1
#LOCALSSIZE 1
SIF !ARG
	ARG = DAY
RETURNF INRANGE(ARG % 7, 1, 5)

;-------------------------------------------------
;週末判定(ARG)
;DAYを渡して土日なら1
;-------------------------------------------------
@IS_WEEKEND(ARG)
#FUNCTION
#LOCALSIZE 1
#LOCALSSIZE 1
SIF !ARG
	ARG = DAY
RETURNF !INRANGE(ARG % 7, 1, 5)

;-------------------------------------------------
;曜日一致判定(ARG)
;DAYと文字列を渡し、一致していれば1
;判定用文字列は日月火水木金土で各曜日を表す（＝複数指定可能）
;-------------------------------------------------
@YOUBI_MATCH(ARG, CHECK_STR)
#FUNCTION
#LOCALSIZE 1
#LOCALSSIZE 1
#DIMS  CHECK_STR
SIF !ARG
	ARG = DAY
SIF STRCOUNT(CHECK_STR, GET_DAY(ARG))
	RETURNF 1
RETURNF 0

;-------------------------------------------------
;関数名:GET_DAY
;概　要:曜日を表す文字列を取得する関数
;引　数:引数がない場合DAY
;戻り値:曜日を表す文字列(日/月/火/水/木/金/土)
;備　考:文中関数
;-------------------------------------------------
@GET_DAY(ARG)
#FUNCTIONS
#LOCALSIZE 1
#LOCALSSIZE 1
#DIMS CONST YOUBI = "日", "月", "火", "水", "木", "金", "土"
SIF !ARG
	ARG = DAY
RETURNF YOUBI:(ARG % 7)

@GET_DAY_CN(ARG)
#FUNCTIONS
#LOCALSIZE 1
#LOCALSSIZE 1
#DIMS CONST 星期 = "日", "一", "二", "三", "四", "五", "六"
SIF !ARG
	ARG = DAY
RETURNF 星期:(ARG % 7)
;FileName_COMMON.ERB ------------------------------- Rev1.00
;天気管理
;CALL		USER
;ARG		VOID
;RETURN		VOID
;COMMENT
;-----------------------------------------------------------
@CHANGE_WEATHER
#LOCALSIZE 1
#LOCALSSIZE 1
;#DIM 无天气
#DIM 入手胖次
#DIM 持ち主
#DIM ループ開始
#DIM ループ終了
#DIM ループ加算
DEBUGPRINTL 【CHANGE_WEATHER】CALL CHANGE_WATHER
;天候管理にメッセージ表示処理させてすみましぇん＞＜；
;诶嘿嘿中は読取周圍環境程度的能力
;天候変化しなくても虹は消える
;无天气 = IS_NO_WEATHER_SEE(AT_AREA(MASTER))
TIME:7 = 0
;IF !无天气		地底都有刮裙子的妖风没道理把胖次风和桃色风拦住
SELECTCASE FLAG:異常気象
	CASE 7
		;こうでもしないとまた没穿内裤が飛んできてしまうと思うの
		DO
			CALL RAND_CHARASELECT("主人公以外的角色列表")
			持ち主 = RESULT
			入手胖次 = 今天穿的内裤(持ち主)
		LOOP !入手胖次
		SIF CFLAG:持ち主:(入手胖次 + 胖次管理) == 0
			SETCOLOR C_PINK
		PRINTFORML 强风把%CALLNAME:持ち主%的%PANTSNAME(入手胖次,持ち主)%吹过来了
		RESETCOLOR
		CALL ASK_YN("捡起","不捡")
		IF !RESULT
			PRINTFORMW %CALLNAME:MASTER%捡到了%CALLNAME:持ち主%的%PANTSNAME(入手胖次,持ち主)%
			CALL PANTS_GETTING(持ち主, 入手胖次)
		ELSE
			PRINTFORML %PANTSNAME(入手胖次,持ち主)%就这样飞到不知道哪里去了
		ENDIF
	CASE 8
		CALL PINK_WIND(1)
ENDSELECT
;ENDIF

SIF FLAG:異常気象 >= 1 && FLAG:異常気象 <= 8
	RETURN

SIF TIME:5 > 15
	TIME:5 = 0

;(天候パッチ)天候変更禁止ならば処理終了
SIF FORBIDDEN_CHANGE_WEATHER == 1
	RETURN

;(天候パッチ)新しい天候管理システムへ
;TRYCALLFORM CHANGE_WEATHER_SYSTEM_{NOW_WEATHER_CONTROL}
TRYCALLFORM qol_CHANGE_WEATHER_SYSTEM_{NOW_WEATHER_CONTROL}(IS_NO_WEATHER_SEE(AT_AREA(MASTER)))
TRYCALLFORM qol_REGION_CHANGE_WEATHER_SYSTEM

[SKIPSTART]
@現在気温設定
;高低差を５分割
SELECTCASE TIME
	;PM11~AM4
	CASE 0 TO 299, IS >= 1380
		LOCAL = FLAG:62
	;AM5~7
	CASE 300 TO 479
		LOCAL = FLAG:62 + ((FLAG:61 - FLAG:62) / 5)
	;AM8~9
	CASE 480 TO 599
		LOCAL = FLAG:62 + ((FLAG:61 - FLAG:62) / 5 * 2)
	;AM10~11
	CASE 600 TO 719
		LOCAL = FLAG:62 + ((FLAG:61 - FLAG:62) / 5 * 3)
	;PM12~2
	CASE 720 TO 899
		LOCAL = FLAG:62 + ((FLAG:61 - FLAG:62) / 5 * 4)
	;PM3~5
	CASE 900 TO 1079
		LOCAL = FLAG:61
	;PM6~7
	CASE 1080 TO 1199
		LOCAL = FLAG:62 + ((FLAG:61 - FLAG:62) / 5 * 4)
	;PM8~9
	CASE 1200 TO 1259
		LOCAL = FLAG:62 + ((FLAG:61 - FLAG:62) / 5 * 2)
	;PM9~10
	CASE 1260 TO 1379
		LOCAL = FLAG:62 + ((FLAG:61 - FLAG:62) / 5)
ENDSELECT
;日が出ていないと温度低下
SELECTCASE TIME
	;AM5~PM7
	CASE 300 TO 1199
		SIF TIME:5 > 1
			LOCAL -= ((FLAG:61 - FLAG:62) / 5)
ENDSELECT
;----------------------------------------------------------------
@年間基礎最高気温(ARG, ARG:1)
#FUNCTION
;ARG   指定の月
;ARG:1 指定の日
SELECTCASE ARG
	CASE 1
		SELECTCASE ARG:1
			CASE 1 TO 3
				RETURNF 17
			CASE 4, 5
				RETURNF 18
			CASE 6 TO 8
				RETURNF 19
			CASE 9 TO 11
				RETURNF 20
			CASE 12 TO 14
				RETURNF 21
			CASE 15 TO 18
				RETURNF 22
			CASE 19 TO 22
				RETURNF 23
			CASE 23 TO 27
				RETURNF 24
			CASE 28 TO 30
				RETURNF 25
		ENDSELECT
	CASE 2
		SELECTCASE ARG:1
			CASE 1, 2
				RETURNF 25
			CASE 3 TO 7
				RETURNF 26
			CASE 8 TO 13
				RETURNF 27
			CASE 14 TO 17
				RETURNF 28
			CASE 18 TO 22
				RETURNF 29
			CASE 23 TO 28
				RETURNF 30
			CASE 29, 30
				RETURNF 31
		ENDSELECT
	CASE 3
		SELECTCASE ARG:1
			CASE 1, 2
				RETURNF 31
			CASE 3, 4
				RETURNF 30
			CASE 5 TO 7
				RETURNF 29
			CASE 8 TO 10
				RETURNF 28
			CASE 11, 12
				RETURNF 27
			CASE 13, 14
				RETURNF 26
			CASE 15, 16
				RETURNF 25
			CASE 17 TO 19
				RETURNF 24
			CASE 20 TO 22
				RETURNF 23
			CASE 23, 24
				RETURNF 22
			CASE 25 TO 27
				RETURNF 21
			CASE 28, 29
				RETURNF 20
			CASE 30
				RETURNF 19
		ENDSELECT
	CASE 4
		SELECTCASE ARG:1
			CASE 1 TO 4
				RETURNF 19
			CASE 5 TO 7
				RETURNF 18
			CASE 8 TO 10
				RETURNF 17
			CASE 11, 12
				RETURNF 16
			CASE 13, 14
				RETURNF 15
			CASE 15, 16
				RETURNF 14
			CASE 17, 18
				RETURNF 13
			CASE 19, 20
				RETURNF 12
			CASE 21, 22
				RETURNF 11
			CASE 23 TO 25
				RETURNF 10
			CASE 26, 27
				RETURNF 9
			CASE 28, 29
				RETURNF 8
			CASE 30
				RETURNF 7
		ENDSELECT
	CASE 5
		SELECTCASE ARG:1
			CASE 1 TO 3
				RETURNF 7
			CASE 4 TO 9
				RETURNF 6
			CASE 10 TO 14
				RETURNF 5
			CASE 15 TO 21
				RETURNF 4
			CASE 22 TO 28
				RETURNF 5
			CASE 29, 30
				RETURNF 6
		ENDSELECT
	CASE 6
		SELECTCASE ARG:1
			CASE 1 TO 6
				RETURNF 6
			CASE 7 TO 12
				RETURNF 7
			CASE 13, 14
				RETURNF 8
			CASE 15, 16
				RETURNF 9
			CASE 17, 18
				RETURNF 10
			CASE 19, 20
				RETURNF 11
			CASE 21, 22
				RETURNF 12
			CASE 23, 24
				RETURNF 13
			CASE 25, 26
				RETURNF 14
			CASE 27, 28
				RETURNF 15
			CASE 29
				RETURNF 16
			CASE 30
				RETURNF 17
		ENDSELECT
ENDSELECT

;----------------------------------------------------------------
@年間基礎最低気温(ARG, ARG:1)
#FUNCTION
;ARG   指定の月
;ARG:1 指定の日
SELECTCASE ARG
	CASE 1
		SELECTCASE ARG:1
			CASE 1 TO 3
				RETURNF 11
			CASE 4 TO 6
				RETURNF 12
			CASE 7, 8
				RETURNF 13
			CASE 9 TO 11
				RETURNF 14
			CASE 12 TO 14
				RETURNF 15
			CASE 15 TO 17
				RETURNF 16
			CASE 18 TO 20
				RETURNF 17
			CASE 21 TO 23
				RETURNF 18
			CASE 24 TO 27
				RETURNF 19
			CASE 28 TO 30
				RETURNF 20
		ENDSELECT
	CASE 2
		SELECTCASE ARG:1
			CASE 1 TO 3
				RETURNF 20
			CASE 4 TO 7
				RETURNF 21
			CASE 8 TO 10
				RETURNF 22
			CASE 11 TO 14
				RETURNF 23
			CASE 15 TO 22
				RETURNF 24
			CASE 23 TO 30
				RETURNF 25
		ENDSELECT
	CASE 3
		SELECTCASE ARG:1
			CASE 1 TO 5
				RETURNF 25
			CASE 6 TO 9
				RETURNF 24
			CASE 10 TO 12
				RETURNF 23
			CASE 13, 14
				RETURNF 22
			CASE 15 TO 17
				RETURNF 21
			CASE 18 TO 20
				RETURNF 20
			CASE 21 TO 23
				RETURNF 19
			CASE 24, 25
				RETURNF 18
			CASE 26
				RETURNF 17
			CASE 27, 28
				RETURNF 16
			CASE 29
				RETURNF 15
			CASE 30
				RETURNF 14
		ENDSELECT
	CASE 4
		SELECTCASE ARG:1
			CASE 1 TO 3
				RETURNF 14
			CASE 4, 5
				RETURNF 13
			CASE 6, 7
				RETURNF 12
			CASE 8, 9
				RETURNF 11
			CASE 10, 11
				RETURNF 10
			CASE 12 TO 14
				RETURNF 9
			CASE 15 TO 17
				RETURNF 8
			CASE 18 TO 20
				RETURNF 7
			CASE 21 TO 23
				RETURNF 6
			CASE 24 TO 26
				RETURNF 5
			CASE 27 TO 29
				RETURNF 4
			CASE 30
				RETURNF 3
		ENDSELECT
	CASE 5
		SELECTCASE ARG:1
			CASE 1, 2
				RETURNF 3
			CASE 3 TO 6
				RETURNF 2
			CASE 7 TO 10
				RETURNF 1
			CASE 11 TO 13
				RETURNF 0
			CASE 14 TO 21
				RETURNF -1
			CASE 22 TO 30
				RETURNF 0
		ENDSELECT
	CASE 6
		SELECTCASE ARG:1
			CASE 1 TO 3
				RETURNF 0
			CASE 4 TO 6
				RETURNF 1
			CASE 7, 8
				RETURNF 2
			CASE 9, 10
				RETURNF 3
			CASE 11 TO 14
				RETURNF 4
			CASE 15, 16
				RETURNF 5
			CASE 17, 18
				RETURNF 6
			CASE 19, 20
				RETURNF 7
			CASE 21 TO 23
				RETURNF 8
			CASE 24, 25
				RETURNF 9
			CASE 26 TO 28
				RETURNF 10
			CASE 29, 30
				RETURNF 11
		ENDSELECT
ENDSELECT
[SKIPEND]

;定時遭遇位置再設定    该函数不变更本地、来访、宴会、招待、同行、仕事、睡眠的角色
@TIMEZONE_MOVE
#LOCALSIZE 1
#LOCALSSIZE 1
#DIMS 移動前
#DIMS 移動後
FOR LOCAL,1,CHARANUM
	SIF 出現状態(LOCAL) != 1
		CONTINUE
	SIF !CHARA_HOLIDAY(LOCAL)
		CONTINUE
	SIF CFLAG:LOCAL:招待
		CONTINUE
	SIF CFLAG:LOCAL:同行 || CHK_DATENOW(CFLAG:LOCAL:约会中)
		CONTINUE
	SIF !CFLAG:LOCAL:前一次的位置
		CONTINUE
	SIF CFLAG:LOCAL:睡眠
		CONTINUE
	;你と同じ場所にいるときは確率が下がる    和你处在同一位置时概率降低
	IF CFLAG:MASTER:現在位置 == CFLAG:LOCAL:現在位置
		IF TALENT:LOCAL:恋慕
			;恋慕は5分の1
			SIF RAND:5
				CONTINUE
		ELSEIF TALENT:LOCAL:思慕
			;思慕は4分の1
			SIF RAND:4
				CONTINUE
		ELSE
			;それ以外は3分の1
			SIF RAND:3
				CONTINUE
		ENDIF
	ELSE
		;見てない場所なら2分の1
		SIF RAND:2
			CONTINUE
	ENDIF
	CALL 遭遇フラグ決定(LOCAL)
NEXT
;全角色決定後、連動角色を設定する
FOR LOCAL, 1, CHARANUM
	VARSET RESULT
	;新增剧本侧接口，调用剧本侧设定的角色移动数值
	CALL KOJO_CHECK(LOCAL, "CHARADATA", "MOVE", 1)
	IF RESULT == -1
		;剧本侧禁用该机制，但是请作者务必清楚自己在做什么
		VARSET RESULT
	ELSEIF RESULT || RESULT:1
		;采用剧本设定的值
	ELSE
		;剧本没有采用CHECK函数或者剧本需要是结果为0，从而采用原版移动数值
		CALLFORM CHARAMOVE_DATA_{NO:LOCAL}(1)
	ENDIF
	;依存連動
	IF RESULT
		CFLAG:LOCAL:遭遇位置 = CFLAG:RESULT:遭遇位置
	;対等連動
	ELSEIF RESULT:1
		;連動設定する
		CFLAG:LOCAL:遭遇位置 = CFLAG:連動角色設定(RESULT:1, RESULT:2, RESULT:3):遭遇位置
	ENDIF
NEXT
