[SKIPSTART]
小作人用各種設定関数

カネはくれるが拘束時間で割ればすこぶる薄給、つまりフレーバーです（でも貰えると嬉しいよね）
標準の報酬係数を400として格差あり、東方くわしくないので違和感あったら変えてくらさい
450は商売やってるようなひとびと

思いつきをありったけ積んだ
よくわかんなかった人々は実装してない
追加・訂正求む

ボツネタ
・華扇の屋敷の裏山に果樹（栗、柿、梅など）
なんとなくイメージでありそう
いろいろ曖昧すぎるのでとりあえず保留
・哆来咪さんが砂糖を生産
とにかく蘇伊特なものをと思った
サトウキビか甜菜だがなんだかシュールなので保留
養蜂農家で蜂蜜を作る手もあるか
後に甜菜は琪露諾の作物となった
・妹紅or輝夜で竹林でタケノコ農家
タケノコは採れそうだがこの２人が作物を徴収しに来るイメージがしない
地主という形で他人と関わり合いになるだろうか？？？と思ってやめ
・蓮子の蓮根畑、鈴湖の林檎園
梅莉とせーらんの作物を思いつかなかった
かたっぽだけ地主設定してると違和感
・正邪でミラクルフルーツ畑
苦味＆酸味を甜と感じさせる＝ひっくり返す
大量生産するようなもんでもないようなと思ってやめ
・純狐さんの生姜畑
すごい形相で叫びながら齧ったりするの？あまりにも一発ネタすぎるので却下
・魅魔様の牧場
でも（ｒｙ


果樹では柿や蜜柑なんかがあると和風感が増すが任せられる人が思いつかなかった
このほか繊維系で綿花、麻、染料系で茜などを挙げてみる


2018/05/19
・各種拠点Mapの実装に伴ってバグってた番号を修正
・農民系のデータの記述は各地主のキャラデータ関数側に移動させました
　小作人パッチの構想段階でキャラデータ関数的な技術は考えてましたが
　関数群が中々に巨大なので小作人パッチだけのために実装するのはなぁと思ってやめてました
　作っちゃったからキャラデータ関数側に移動
・@STR_NOUMIN()の仕様変更
　１．STR_NOUMIN/INT_NOUMINの二種類に分けてTOINTの必要を無くした
　２．キャラデータ関数で管理するようになったので第一引数で渡す内容をCALLNAMEから登録番号に変更
　３．STR/INT_NOUMINの第二引数は従来街道、STR_NOUMIN(1, "説明")等で動きます
　４．キャラデータ関数側の記述で使ってる"農民："の部分はSTR/INT_NOUMINで勝手に付与します

[SKIPEND]
@INT_NOUMIN(C_ID, TYPE)
#FUNCTION
#LOCALSIZE 1
#LOCALSSIZE 1
#DIM  C_ID;地主の登録番号
#DIMS TYPE;取得情報の種類
RETURNF GET_INT(, "キャラデータ", C_ID, "農民：" + TYPE)

@STR_NOUMIN(C_ID, TYPE)
#FUNCTIONS
#LOCALSIZE 1
#LOCALSSIZE 1
#DIM  C_ID;地主の登録番号
#DIMS TYPE;取得情報の種類
RETURNF GET_STR(, "キャラデータ", C_ID, "農民：" + TYPE)



;農民初期設定関数
@SET_NOUMIN
#LOCALSIZE 1000
#LOCALSSIZE 1
#DIM LCOUNT
#DIM C_ID
#DIM LIST_ID
#DIM CHARA_COUNT
#DIM LIST_COUNT
#DIM PAGE
#DIMS NAME_地主
PRINTFORML 你是农民吗？
PRINTFORML [1] - 是
PRINTFORML [0] - 不是
DO
	INPUT
	SELECTCASE RESULT
	CASE 0
		RETURN
	CASE 1
	CASEELSE
		REUSELASTLINE 无效输入
		CONTINUE
	ENDSELECT
	BREAK
LOOP 1

PAGE = 0
CHARA_COUNT = 0
;地主一覧の取得
FOR C_ID, 1, CHARANUM
	SIF C_ID == FLAG:扮演
		CONTINUE
	SIF !STRLENS(STR_NOUMIN(C_ID, "説明"))
		CONTINUE
	LOCAL:CHARA_COUNT = C_ID
	CHARA_COUNT ++
NEXT

FLAG:地主 = 0
FLAG:田地工作的成果 = 0
FLAG:返済延滞回数 = 0
FLAG:収穫納入回数 = 0
FLAG:累積収穫評価 = 0
FLAG:前回収穫評価 = 0


DO
	LIST_COUNT = 0
	PRINTFORML 请选择雇用你的地主
	PRINTFORML 
	FOR LIST_ID, (PAGE * 20), CHARA_COUNT
		C_ID = LOCAL:LIST_ID
		PRINTFORM [{C_ID ,3}] - 地主：%CALLNAME:C_ID, 10%　　
		PRINTFORM %STR_NOUMIN(C_ID, "説明"), 14%　　
		PRINTFORML %STR_NOUMIN(C_ID, "田的种类")%的位置在%STR_NOUMIN(C_ID, "畑の所在地")%的附近
		LIST_COUNT ++
		SIF LIST_COUNT > 19
			BREAK
	NEXT
	PRINTFORML 
	PRINTFORML [901] - 上一页
	PRINTFORML [902] - 下一页
	PRINTFORML [  0] - 还是算了
	DO
		INPUT
		SELECTCASE RESULT
		CASE 0
			RETURN
		;qol custom code, allow looping
		CASE 901, 902
			PAGE = PAGE_HANDLE(RESULT, 901, PAGE, 0, 20 / CHARA_COUNT + 1)
		; CASE 901
		; 	SIF PAGE > 0
		; 		PAGE --
		; CASE 902
		; 	SIF (PAGE + 1) * 20 < CHARA_COUNT
		; 		PAGE ++
		CASE 1 TO CHARANUM - 1
			SIF !STRLENS(STR_NOUMIN(RESULT, "説明"))
				GOTO RETRY
			FLAG:地主 = RESULT
		CASEELSE
			$RETRY
			REUSELASTLINE 无效输入
			CONTINUE
		ENDSELECT
		BREAK
	LOOP 1
LOOP !(FLAG:地主)

NAME_地主 = %CALLNAME:(FLAG:地主)%
PRINTFORML 
PRINTFORML 你成为了为%NAME_地主%种植%STR_NOUMIN(FLAG:地主, "作物")%的佃户
PRINTFORML 你租借的%STR_NOUMIN(FLAG:地主, "田的种类")%是在%STR_NOUMIN(FLAG:地主, "畑の所在地")%的附近
PRINTFORMW 请不要偷懒每天都照料它吧

CALL MODE_NOUMIN
CALL LOAN_NOUMIN

[IF_DEBUG]
;挙動、れいあうとの確認用
;MONEY = 100000
;DO
;	SIF FLAG:農民難易度
;		CALL COST_NOUMIN
;	CALL REPAY_NOUMIN
;	WAIT
;LOOP 1
[ENDIF]

@MODE_NOUMIN
#LOCALSIZE 1
#LOCALSSIZE 1
#DIMS NAME_地主
NAME_地主 = %CALLNAME:(FLAG:地主)%
PRINTFORML 请设定农民生活的难易度
PRINTFORML （根据选择的不同可能会导致角色形象的崩坏）
PRINTFORML 
PRINTFORML [1] - 普通农民　    　　　　%NAME_地主%是个好心的地主
PRINTFORML [2] - 硬核农民　      　　　%NAME_地主%是个贪婪的地主
PRINTFORML [3] - 极限农民          　　%NAME_地主%是个施行暴政的蛮横地主
DO
	INPUT
	IF INRANGE(RESULT, 1, 3)
		FLAG:農民難易度 = RESULT
		BREAK
	ELSE
		REUSELASTLINE 无效输入
		CONTINUE
	ENDIF
LOOP 1
SELECTCASE FLAG:農民難易度
CASE 1
	PRINTFORML 你的地主是个非常善良的好人
	FLAG:畑拡張 = 1
CASE 2
	PRINTFORML 你成为了受地主压迫的农民
	FLAG:畑拡張 = 2
CASE 3
	PRINTFORML 你成为了一个被地主彻底剥削的农民
	FLAG:畑拡張 = 3
ENDSELECT
PRINTFORMW （农民难易度可以在设置里变更）
SETBIT FLAG:1000, 2



;%TOSTR(LOCAL, "#,###")%
;報酬400 * 1000 = 40万円（標準金額）
@LOAN_NOUMIN
#DIMS NAME_地主
NAME_地主 = %CALLNAME:(FLAG:地主)%
LOCAL = INT_NOUMIN(FLAG:地主, "報酬") * 1000
PRINTFORML 开始农业的%CALLNAME:MASTER%收到了初期投资费用和%金额表示(LOCAL)%的借入金
FLAG:地主貸付金 = LOCAL
FLAG:年始貸付金 = LOCAL
SELECTCASE FLAG:農民難易度
CASE 1
	PRINTFORML %NAME_地主%说这是无息借款所以耐心地还就好
	IF !ITEM:園芸用品一式
		PRINTFORML %NAME_地主%还建议先去人间之里的花店买些必要的园艺用品
		PRINTFORML 看来地主不会承担购买农具的费用
	ENDIF
CASE 2, 3
	PRINTFORML 这笔贷款好好像有{INT_NOUMIN(FLAG:地主, "金利")}％的利息
	PRINTFORML %NAME_地主%还提出了借出农具
	PRINTFORML ……好像没有拒绝的权利
	;たぶんこの他に現地に無駄に色々な道具が山ほどある
ENDSELECT
WAIT

@RESET_NOUMIN
FLAG:地主 = 0
FLAG:田地工作的成果 = 0
FLAG:返済延滞回数 = 0
FLAG:収穫納入回数 = 0
FLAG:累積収穫評価 = 0
FLAG:前回収穫評価 = 0
FLAG:農民難易度 = 0
FLAG:畑拡張 = 0
FLAG:地主貸付金 = 0
FLAG:年始貸付金 = 0

@SET_APPLY_NOUMIN
SIF FLAG:農民難易度 >= 2 && !ITEM:園芸用品一式
	ITEM:園芸用品一式 ++

;農民情報表示関数
@SHOW_NOUMIN
#LOCALSIZE 1
#LOCALSSIZE 1
#DIMS NAME_地主
#DIM HATAKE
NAME_地主 = %CALLNAME:(FLAG:地主)%
HATAKE = INT_NOUMIN(FLAG:地主, "場所ID")
SIF GET_MAPID(HATAKE) != MAIN_MAP
	HATAKE = 6000 + (GET_MAP_REPLACEMENT(HATAKE) / 10)

PRINTFORML %NAME_地主%的佃户（在%STR_NOUMIN(FLAG:地主, "説明")%种植%STR_NOUMIN(FLAG:地主, "作物")%）
PRINTFORML %STR_NOUMIN(FLAG:地主, "田的种类")%在%STR_NOUMIN(FLAG:地主, "畑の所在地")%的%NAME_FROM_PLACE(HATAKE)%附近
;いいひとでも作物は全部もらいます
SELECTCASE FLAG:農民難易度
CASE 1
	PRINTFORML 地主是个非常亲切的人
	SIF FLAG:地主貸付金
		PRINTFORML 借了%金额表示(FLAG:地主貸付金)%无息借款
CASE 2
	PRINTFORML 被地主欺压
	SIF FLAG:地主貸付金
		PRINTFORML %金額表示(FLAG:地主貸付金)%借款的利率是{INT_NOUMIN(FLAG:地主, "金利")}％（年利）
CASE 3
	PRINTFORML 被地主彻底压榨
	SIF FLAG:地主貸付金
		PRINTFORML %金額表示(FLAG:地主貸付金)%借款的利率是{INT_NOUMIN(FLAG:地主, "金利")}％（月利）
ENDSELECT
PRINTFORML 


;取り立て関数
;本当は年貢でもなんでもないけどわかりやすさ優先
;口上対応とかご褒美えっちとかは仕様がわからなくて実装できない
@NENGU_NOUMIN
#LOCALSIZE 1
#LOCALSSIZE 1
#DIM 年貢の評価
PRINTFORML %CALLNAME:(FLAG:地主)%来征收本月的成果
PRINTFORMW （今月的收获量：{FLAG:田地工作的成果}）
年貢の評価 = FLAG:田地工作的成果 / 20
LOCAL = (年貢の評価 - 3) * 5
SIF LOCAL < 0
	LOCAL = LOCAL - SQRT(MAX(1, CFLAG:(FLAG:地主):信賴度))
;(天候パッチ)寒気による作物被害が20を超えたら20毎に冷害判定
SIF AMOUNT_COLD_CROP_DAMAGE >= 20
	IS_COLD_CROP_DAMAGE ++
SIF AMOUNT_COLD_CROP_DAMAGE >= 40
	IS_COLD_CROP_DAMAGE ++
SIF AMOUNT_COLD_CROP_DAMAGE >= 60
	IS_COLD_CROP_DAMAGE ++
SELECTCASE 年貢の評価
	;(天候パッチ)冷害・風害発生時は態度が軟化
	CASE IS <= 0
		IF IS_COLD_CROP_DAMAGE > 0 || IS_STORM_CROP_DAMAGE > 0 || IS_FLOOD_CROP_DAMAGE > 0
			PRINTFORML 这个月的%STR_NOUMIN(FLAG:地主, "作物")%的收获量几乎为零
			PRINTFORML %CALLNAME:(FLAG:地主)%看着恶劣天候所造成的破坏惊呆了
		ELSE
			PRINTFORML 这个月的%STR_NOUMIN(FLAG:地主, "作物")%的收获量几乎为零
			PRINTFORML %CALLNAME:(FLAG:地主)%用可怕的表情瞪着这边
		ENDIF
	CASE 1
		IF IS_COLD_CROP_DAMAGE > 0 || IS_STORM_CROP_DAMAGE > 0 || IS_FLOOD_CROP_DAMAGE > 0
			PRINTFORML 这个月的%STR_NOUMIN(FLAG:地主, "作物")%的收获相当少
			PRINTFORML 由于恶劣天气所造成的损害、%CALLNAME:(FLAG:地主)%垂头丧气的样子
		ELSE
			PRINTFORML 这个月的%STR_NOUMIN(FLAG:地主, "作物")%的收获相当少
			PRINTFORML %CALLNAME:(FLAG:地主)%因为收获量太少而明显失望的样子
		ENDIF
	CASE 2
		IF IS_COLD_CROP_DAMAGE > 0 || IS_STORM_CROP_DAMAGE > 0 || IS_FLOOD_CROP_DAMAGE > 0
			PRINTFORML 这个月的%STR_NOUMIN(FLAG:地主, "作物")%的收获比较少
			PRINTFORML 由于恶劣天气所造成的损害、%CALLNAME:(FLAG:地主)%一脸失望的样子
		ELSE
			PRINTFORML 这个月的%STR_NOUMIN(FLAG:地主, "作物")%的收获比较少
			PRINTFORML %CALLNAME:(FLAG:地主)%因为收获量少而不满意的样子
		ENDIF
	CASE 3
		PRINTFORML 这个月的%STR_NOUMIN(FLAG:地主, "作物")%的收获不够多
		PRINTFORML %CALLNAME:(FLAG:地主)%对收获量多多少少感到不满
	CASE 4
		PRINTFORML 这个月的%STR_NOUMIN(FLAG:地主, "作物")%的收获还行
		PRINTFORML %CALLNAME:(FLAG:地主)%对收获量感到满意
	CASE 5
		PRINTFORML 这个月的%STR_NOUMIN(FLAG:地主, "作物")%的收获比较多
		PRINTFORML %CALLNAME:(FLAG:地主)%的多出的收获量很高兴的样子
	CASE 6
		PRINTFORML 这个月的%STR_NOUMIN(FLAG:地主, "作物")%的收获相当多
		PRINTFORML %CALLNAME:(FLAG:地主)%因为预期以上的收获量露出了微笑
	CASEELSE
		PRINTFORML 这个月的%STR_NOUMIN(FLAG:地主, "作物")%的收获多的吓人！
		PRINTFORML %CALLNAME:(FLAG:地主)%因为超乎想象的收获量折服了
ENDSELECT
WAIT

;(天候パッチ)冷害・風害発生時は怒らない
IF LOCAL < 0 && IS_COLD_CROP_DAMAGE <= 0 && IS_STORM_CROP_DAMAGE <= 0 && IS_FLOOD_CROP_DAMAGE <= 0
	PRINTFORMW %CALLNAME:(FLAG:地主)%对你抱着不信任感
	CFLAG:(FLAG:地主):信賴度 = MAX(0, CFLAG:(FLAG:地主):信賴度 + LOCAL)
	IF 年貢の評価 <= 0
		PRINTFORMW ……好像把%CALLNAME:(FLAG:地主)%惹火了
		CFLAG:(FLAG:地主):心情不快 = 1
		CFLAG:(FLAG:地主):ブチギレ = 1
		SIF MARK:(FLAG:地主):反発刻印 < 3
			MARK:(FLAG:地主):反発刻印 ++
	ENDIF
ELSEIF LOCAL > 0
	;(天候パッチ)冷害・風害発生時はより評価される
	IF IS_COLD_CROP_DAMAGE > 0 || IS_STORM_CROP_DAMAGE > 0 || IS_FLOOD_CROP_DAMAGE > 0
		PRINTFORM 尽管天气不顺但仍是丰收
		PRINTFORMW %CALLNAME:(FLAG:地主)%的信赖度大幅上升了
		LOCAL += ((IS_COLD_CROP_DAMAGE + IS_STORM_CROP_DAMAGE + (IS_FLOOD_CROP_DAMAGE * 4)) * 2)
	ELSE
		PRINTFORMW %CALLNAME:(FLAG:地主)%的信赖度上升了
	ENDIF
	CFLAG:(FLAG:地主):信賴度 += LOCAL
	IF !MARK:(FLAG:地主):反発刻印
		LOCAL = 年貢の評価 * INT_NOUMIN(FLAG:地主, "報酬")
		PRINTFORMW %CALLNAME:(FLAG:地主)%给了%金额表示(LOCAL)%的零用钱
		MONEY += LOCAL
	ENDIF
ENDIF
FLAG:田地工作的成果 = 0
FLAG:年貢 = 0
FLAG:収穫納入回数 ++
FLAG:累積収穫評価 += 年貢の評価
FLAG:前回収穫評価 = 年貢の評価
SIF FLAG:野菜娘デイリー == 2
	FLAG:野菜娘デイリー = 1
CALL REPAY_NOUMIN
SIF FLAG:農民難易度 > 1
	CALL COST_NOUMIN

;(天候パッチ)冷害発生回数と風害発生回数をリセット
IS_COLD_CROP_DAMAGE = 0
IS_STORM_CROP_DAMAGE = 0
IS_FLOOD_CROP_DAMAGE = 0
AMOUNT_COLD_CROP_DAMAGE = 0


@REPAY_NOUMIN
#DIM HENSAI
#DIM MIN_HENSAI
#DIMS NAME_地主
NAME_地主 = %CALLNAME:(FLAG:地主)%
SIF !FLAG:地主貸付金
	GOTO KANRYOU
SELECTCASE FLAG:農民難易度
CASE 1
	MIN_HENSAI = 100
CASE 2
	MIN_HENSAI = INT_NOUMIN(FLAG:地主, "報酬") * 5
CASE 3
	MIN_HENSAI = INT_NOUMIN(FLAG:地主, "報酬") * (10 + FLAG:返済延滞回数)
ENDSELECT
MIN_HENSAI = MIN(MIN_HENSAI, FLAG:地主貸付金)

PRINTFORML %NAME_地主%要求偿还债务
PRINTFORM 要还多少钱？（借金额：
PRINTBUTTON 金额表示(FLAG:地主貸付金), FLAG:地主貸付金
PRINTFORM 、所持金：
PRINTBUTTON 金额表示(MONEY), MONEY
PRINTFORM 、返済额在
PRINTBUTTON 金额表示(MIN_HENSAI), MIN_HENSAI
PRINTFORML 以下时延期还款）
DO
	INPUT
	HENSAI = MIN(RESULT, FLAG:地主貸付金)
	SELECTCASE HENSAI
	CASE IS < MIN_HENSAI
		PRINTFORML %CALLNAME:MASTER%拜托%NAME_地主%可以延期还款
		IF FLAG:農民難易度 == 1
			IF MONEY < MIN_HENSAI
			;本気で金が無い時
				PRINTDATAL
					DATAFORM %NAME_地主%对这边的生活情况真心的担忧
					DATAFORM %NAME_地主%对这边的生活情况真心的担忧
					DATAFORM %NAME_地主%鼓励着%CALLNAME:MASTER%鼓励着
					DATAFORM %NAME_地主%安慰着%CALLNAME:MASTER%安慰起来
					DATAFORM %NAME_地主%不知道该说什么的样子
					DATAFORM %NAME_地主%那怜悯的视线扎了过来
					DATAFORM %NAME_地主%的表情阴沉
					DATAFORM %NAME_地主%感到为难
				ENDDATA
			ELSE
			;等一下！
				PRINTDATAL
					DATAFORM %NAME_地主%深深叹了口气
					DATAFORM %NAME_地主%揉着太阳穴
					DATAFORM %NAME_地主%用蔑视的眼神盯着
					DATAFORM %NAME_地主%抱着胳膊绷紧了脸
					DATAFORM %NAME_地主%那怜悯的视线扎了过来
					DATAFORM %NAME_地主%脸色僵硬
					DATAFORM %NAME_地主%皱起了眉头
					DATAFORM %NAME_地主%的表情阴沉
					DATAFORM %NAME_地主%感到为难
				ENDDATA
				PRINTFORML 
				PRINTFORML ……看来总算是同意了
			ENDIF
			FLAG:返済延滞回数 ++
		ELSEIF MONEY >= MIN_HENSAI
			PRINTFORML %NAME_地主%打断了%CALLNAME:MASTER%的话从钱包中强行夺走了%金额表示(MIN_HENSAI)%
			MONEY -= MIN_HENSAI
		ELSE
			PRINTFORML %NAME_地主%砸了砸嘴用冷漠的眼神瞪视这边
			FLAG:返済延滞回数 ++
		ENDIF
	CASE IS > MONEY
		$RETRY
		REUSELASTLINE 无效输入
		CONTINUE
	CASEELSE
		PRINTFORML %CALLNAME:MASTER%偿还了%金额表示(HENSAI)%的借款
		MONEY -= HENSAI
		FLAG:返済延滞回数 = 0
		FLAG:地主貸付金 -= HENSAI
		IF !FLAG:地主貸付金
			IF FLAG:農民難易度 == 1
				PRINTFORML %CALLNAME:MASTER%终于还清了债
				PRINTFORML %NAME_地主%也一起感到高兴
			ELSE
				PRINTFORML %CALLNAME:MASTER%还清了债
				PRINTFORML ……虽然很快就被以其他的理由强行借了钱
			ENDIF
		ELSE
			PRINTFORML 剩下的借款还有%金额表示(FLAG:地主貸付金)%……
		ENDIF
		$KANRYOU
		PRINTFORML 
		IF FLAG:農民難易度 == 1
			PRINTDATAL
				DATAFORM 在%NAME_地主%的关照下越说越投机
				DATAFORM %NAME_地主%津津有味的听着田地工作的报告
				DATAFORM %NAME_地主%有些担心是不是有感到困扰的事
				DATAFORM 一边吃着%NAME_地主%带来的点心一边畅谈
				DATAFORM %NAME_地主%认真倾听着农田改造的请求
				DATAFORM %NAME_地主%用温和的视线看着这边
				DATAFORM %NAME_地主%的笑容很耀眼
			ENDDATA
		ELSE
			PRINTDATAL
				DATAFORM %NAME_地主%只有找到空隙就向着%CALLNAME:MASTER%强行推销
				DATAFORM %NAME_地主%敷衍的听着田地工作的报告
				DATAFORM %NAME_地主%在寻找着有没有为钱所困的人
				DATAFORM %NAME_地主%以改善农田为名建议投资设备……没有那些钱
				DATAFORM %NAME_地主%的视线里可以察觉到不好的气息
				DATAFORM %NAME_地主%的笑容里好像藏着什么
				DATAFORM %NAME_地主%的笑容不知为何看着心里发怂
			ENDDATA
		ENDIF
	ENDSELECT
	BREAK
LOOP 1

IF FLAG:返済延滞回数
	LOCAL = POWER(FLAG:返済延滞回数, FLAG:農民難易度 + 1)
	LOCAL = MIN(LOCAL, CFLAG:(FLAG:地主):信賴度)
	IF LOCAL
		PRINTFORMW %CALLNAME:(FLAG:地主)%对你抱着不信任感
		CFLAG:(FLAG:地主):信賴度 -= LOCAL
	ENDIF
ENDIF



;なんだかんだ理由つけて差っ引かれる
;標準レート400で金利・返済延滞によるペナルティを除くと
;ハードコアで1万、エクストリームで3万の経費が毎月発生する
@COST_NOUMIN
#LOCALSIZE 9
#LOCALSSIZE 9
#DIMS NAME_地主
#DIMS PREV_地主
#DIM COST_RATE
#DIM LCOUNT
NAME_地主 = %CALLNAME:(FLAG:地主)%
COST_RATE = INT_NOUMIN(FLAG:地主, "報酬")
LOCAL:0 = 0
LOCAL:8 = 0
PRINTFORML 根据本月份的如下各种经费追加了借款


;金利計算
;ハードコアなら年始借入金準拠で年利計算
;エクストリームだと年始借入金・今月借入金のうち大きい方で月利計算
IF FLAG:地主貸付金 < 10000000
	SELECTCASE FLAG:農民難易度
	CASE 2
		LOCAL:1 = FLAG:年始貸付金 * INT_NOUMIN(FLAG:地主, "金利") / 1200
	CASE 3
		LOCAL:1 = MAX(FLAG:地主貸付金, FLAG:年始貸付金) * INT_NOUMIN(FLAG:地主, "金利") / 100
	ENDSELECT
ENDIF

SELECTCASE FLAG:農民難易度
CASE 2
	LOCAL:2 = FLAG:返済延滞回数 * COST_RATE
CASE 3
	LOCAL:2 = FLAG:返済延滞回数 * COST_RATE * 10
ENDSELECT

;固定値が大半なんで毎回再計算する必要はあまりない
;地主の切り替わり（と、再起動）のタイミングで判定するように
IF NAME_地主 != PREV_地主
	LOCALS:0 = 经费总额
	LOCALS:1 = 利息
	LOCALS:2 = 欠款滞纳金
	LOCALS:3 = 农具租赁费
	LOCALS:8 = 借款总额
	SELECTCASE FLAG:農民難易度
	CASE 2
		LOCAL:3 = COST_RATE * 5
	CASE 3
		LOCAL:3 = COST_RATE * 10
	ENDSELECT

	LOCALS:4 = 肥料费
	SELECTCASE FLAG:農民難易度
	CASE 2
		LOCAL:4 = COST_RATE * 5
	CASE 3
		LOCAL:4 = COST_RATE * 10
	ENDSELECT

	LOCALS:5 = 作物销售费
	SELECTCASE FLAG:農民難易度
	CASE 2
		LOCAL:5 = COST_RATE * 5
	CASE 3
		LOCAL:5 = COST_RATE * 15
	ENDSELECT

	LOCALS:6 = 灌溉费
	SELECTCASE FLAG:農民難易度
	CASE 2
		LOCAL:6 = COST_RATE * 5
	CASE 3
		LOCAL:6 = COST_RATE * 20
	ENDSELECT

	IF STR_NOUMIN(FLAG:地主, "作物") == "蚕丝"
		LOCALS:7 = 下季度蚕种费
	ELSE
		LOCALS:7 = 下季度种子费
	ENDIF
	
	SELECTCASE FLAG:農民難易度
	CASE 2
		LOCAL:7 = COST_RATE * 5
	CASE 3
		LOCAL:7 = COST_RATE * 20
	ENDSELECT
	PREV_地主 = %NAME_地主%
ENDIF

LOCAL:0 = SUMARRAY(LOCAL)
LOCAL:8 = LIMIT(FLAG:地主貸付金 + LOCAL:0,  0, 10000000)
FLAG:地主貸付金 = LOCAL:8
PRINTFORML ――――――――――――――――――――――――――――――――
PRINTFORML 　诸経费
PRINTFORML ――――――――――――――――――――――――――――――――
FOR LCOUNT, 1, 8
	SIF LOCAL:LCOUNT
		PRINTFORML 　　%LOCALS:LCOUNT, 12%｜　　　%金額表示(LOCAL:LCOUNT),20%
NEXT
PRINTFORML ――――――――｜―――――――――――――――――――――――
PRINTFORML 　　%LOCALS:0, 12%｜　　　%金額表示(LOCAL:0),20%
PRINTFORML ――――――――｜―――――――――――――――――――――――
PRINTFORM 　　%LOCALS:8, 12%｜　　　%金額表示(LOCAL:8),20%
IF FLAG:地主貸付金 == 10000000
	PRINTFORML 　！　上限　！
ELSE
	PRINTFORML 
ENDIF
PRINTFORMW ――――――――――――――――――――――――――――――――


;CONFIGからの農民再設定（試作）
@RESTART_NOUMIN
#DIM LOCAL_地主
#DIM LOCAL_田地工作的成果
#DIM LOCAL_返済延滞回数
#DIM LOCAL_収穫納入回数
#DIM LOCAL_累積収穫評価
#DIM LOCAL_前回収穫評価
#DIM LOCAL_農民難易度
#DIM LOCAL_畑拡張
#DIM LOCAL_地主貸付金
#DIM LOCAL_年始貸付金
#DIMS LOCALS_元地主
#DIMS LOCALS_新地主
#DIMS LOCALS_元難易度
#DIMS LOCALS_新難易度

LOCAL_地主 = FLAG:地主
LOCAL_田地工作的成果 = FLAG:田地工作的成果
LOCAL_返済延滞回数 = FLAG:返済延滞回数
LOCAL_収穫納入回数 = FLAG:収穫納入回数
LOCAL_累積収穫評価 = FLAG:累積収穫評価
LOCAL_前回収穫評価 = FLAG:前回収穫評価
LOCAL_農民難易度 = FLAG:農民難易度
LOCAL_畑拡張 = FLAG:畑拡張
LOCAL_地主貸付金 = FLAG:地主貸付金
LOCAL_年始貸付金 = FLAG:年始貸付金
LOCALS_元地主 = %CALLNAME:(FLAG:地主)%

IF FLAG:地主
	PRINTFORML %CALLNAME:MASTER%是%LOCALS_元地主%手下的打工农民
	SIF FLAG:地主貸付金 > 0
		PRINTFORML 残余的欠款是%金額表示(FLAG:地主貸付金)%
	SELECTCASE FLAG:農民難易度
	CASE 1
		LOCALS_元難易度 = 普通
	CASE 2
		LOCALS_元難易度 = 硬核
	CASE 3
		LOCALS_元難易度 = 极限
	ENDSELECT
ELSE
	PRINTFORML %CALLNAME:MASTER%是无债一身轻的自由人
ENDIF

PRINTFORML 要变更农家的设定吗？
PRINTFORML [1] - 是
PRINTFORML [0] - 不是
DO
	INPUT
	SELECTCASE RESULT
	CASE 0
		PRINTFORML 
		RETURN
	CASE 1
	CASEELSE
		REUSELASTLINE 无效输入
		CONTINUE
	ENDSELECT
	BREAK
LOOP 1

DO
	CALL RESET_NOUMIN
	CALL SET_NOUMIN
	PRINTFORML 

	LOCALS_新地主 = %CALLNAME:(FLAG:地主)%
	IF LOCAL_地主 && FLAG:地主 && LOCAL_地主 != FLAG:地主
		PRINTFORML %CALLNAME:MASTER%从%LOCALS_元地主%手下离开了、今后在%LOCALS_新地主%手下打工
		IF LOCAL_地主貸付金 > 0
			PRINTFORML %LOCALS_元地主%的借款%金額表示(LOCAL_地主貸付金)%被%LOCALS_新地主%接管了
			FLAG:地主貸付金 += LOCAL_地主貸付金
		ENDIF
		PRINTFORML 向%LOCALS_新地主%借款合计为%金額表示(LOCAL_地主貸付金)%

	ELSEIF LOCAL_地主 && FLAG:地主 && LOCAL_地主 == FLAG:地主
		; 雇い主の変更がないので元に戻す
		SETCOLOR C_YELLOW
		PRINTFORML 因为雇主没有变更、上述借款没有发生
		SIF LOCAL_地主貸付金 > 0
			PRINTFORML %LOCALS_新地主%的借款现在仍是%金額表示(LOCAL_地主貸付金)%
		RESETCOLOR
		FLAG:田地工作的成果 = LOCAL_田地工作的成果
		FLAG:返済延滞回数 = LOCAL_返済延滞回数
		FLAG:収穫納入回数 = LOCAL_収穫納入回数
		FLAG:累積収穫評価 = LOCAL_累積収穫評価
		FLAG:前回収穫評価 = LOCAL_前回収穫評価
		FLAG:地主貸付金 = LOCAL_地主貸付金
		FLAG:年始貸付金 = LOCAL_年始貸付金

		; 難易度が変更されたか
		IF LOCAL_農民難易度 != FLAG:農民難易度
			SELECTCASE FLAG:農民難易度
			CASE 1
				LOCALS_新難易度 = 普通
			CASE 2
				LOCALS_新難易度 = 硬核
			CASE 3
				LOCALS_新難易度 = 极限
			ENDSELECT
			PRINTFORML 难易度从[%LOCALS_元難易度%]变更为了[%LOCALS_新難易度%]
		ELSE
			PRINTFORML 难易度的设定没有变更
		ENDIF

	ELSEIF !LOCAL_地主 && !FLAG:地主
		PRINTFORML 设定保持不变
	ELSEIF LOCAL_地主 && !FLAG:地主
		IF LOCAL_地主貸付金 > 0
			PRINTFORML 没有还清债务的时候不能辞掉农民的工作
			CONTINUE
		ENDIF
		PRINTFORML 辞去农民的工作变成了自由人
	ENDIF

	PRINTFORML 
	PRINTFORML 是否更改设定？
	PRINTFORML [1] - 是
	PRINTFORML [2] - 重新决定
	PRINTFORML [0] - 不更改
	DO
		INPUT
		SELECTCASE RESULT
		CASE 0
			FLAG:地主 = LOCAL_地主
			FLAG:田地工作的成果 = LOCAL_田地工作的成果
			FLAG:返済延滞回数 = LOCAL_返済延滞回数
			FLAG:収穫納入回数 = LOCAL_収穫納入回数
			FLAG:累積収穫評価 = LOCAL_累積収穫評価
			FLAG:前回収穫評価 = LOCAL_前回収穫評価
			FLAG:農民難易度 = LOCAL_農民難易度
			FLAG:畑拡張 = LOCAL_畑拡張
			FLAG:地主貸付金 = LOCAL_地主貸付金
			FLAG:年始貸付金 = LOCAL_年始貸付金
			PRINTFORML 取消更改设定
			PRINTFORML 
			RETURN
		CASE 1
			PRINTFORML 設定已更改
			PRINTFORML 
			RETURN
		CASE 2
		CASEELSE
			REUSELASTLINE 无效输入
			CONTINUE
		ENDSELECT
		BREAK
	LOOP 1
LOOP 1

