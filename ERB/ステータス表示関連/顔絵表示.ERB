;CIDが0の時別途定義のMOB角色を表示するように
;例）CALL PRINT_FACE, 0, "通常", "服", "6" でMOB僵尸妖精を表示
@PRINT_FACE,CID,表情="",服装="",差分="",エフェクト="",立绘选择 = -1
#LOCALSIZE 1
#LOCALSSIZE 1
#DIMS DYNAMIC 表情
#DIMS DYNAMIC 服装
#DIMS DYNAMIC 差分
#DIMS DYNAMIC エフェクト
#DIMS DYNAMIC ツールチップ
#DIM DYNAMIC CID
#DIM DYNAMIC 立绘选择
#DIM DYNAMIC PREV_立ち絵種類
CALL PRINT_FIGURE,CID,表情,服装,差分,エフェクト,"顔絵",立绘选择
	RETURN
SIF !顔絵付きメッセージ || !FLAG:画像表示
	RETURN
角色画像表示尺寸比 = 100

SIF 表情 == ""
	表情 = 通常

PREV_立ち絵種類 = FLAG:立ち絵種類

IF CID
	FLAG:立ち絵種類 = 1
	;custom code
	TRYCALLFORM IMAGE_EX_I{NO:CID}_PREリソースチェック(CID, "顔絵", 服装, 表情, "", 差分, "")
	CALL GET_BASE_RESOURCE(CID, "顔絵", 服装, 表情, "", 差分)
	SIF !RESULT
		RESULTS = %GET_BASESTYLE(CID, "顔絵")%_服_通常_{CID}
	ツールチップ '= CALLNAME:CID
ELSE
	RESULTS = 顔絵_%服装%_%表情%_MOB_%差分%
	;SIF !SPRITECREATED(RESULTS) ;debloat custom code
	SIF !RM_リソースチェック(RESULTS)
		CALL 路人店員画像作成(TOINT(差分))
	;SIF !SPRITECREATED(RESULTS) ;debloat custom code
	SIF !RM_リソースチェック(RESULTS)
		RESULTS = 顔絵_服_通常_MOB_%差分%
	;SIF !SPRITECREATED(RESULTS) ;debloat custom code
	SIF !RM_リソースチェック(RESULTS)
		RETURN
	ツールチップ = 路人子
ENDIF

SIF FLAG:多尺寸
	默认角色画像横幅 = RM_リソースの高さ(RESULTS)

;-------------------------
DEBUGPRINTFORML PRINT_FACEで%RESULTS%を表示
;-------------------------

CALL 画像セット(RESULTS, 0, 0, 角色画像表示尺寸比, 0, 1, "100", ツールチップ)

IF エフェクト != ""
	CALL GET_EFFECT_RESOURCE(CID, エフェクト, , , 服装)
	FOR LOCAL, 0, RESULT
		CALL 画像セット(RESULTS:LOCAL, 0, 0, 角色画像表示尺寸比, LOCAL + 1)
	NEXT
ENDIF

CALL 画像一斉表示(0, 0, 1, -1)
FLAG:立ち絵種類 = PREV_立ち絵種類

@SPTALK,CID,表情="",デフォルト=0,MESSAGE,整形=0,CUSTOMFONTCOLOR
;CIDが0の時別途定義のMOB角色を表示するように
;例）CALL SPTALK, 0, "笑顔", 1, @"「この記述で」/「路人が喋ります」",1
#DIM CID
#DIMS 表情
#DIM デフォルト
#DIMS MESSAGE
#DIM 整形
#DIMS 服装
#DIMS MESSAGE_PRINT,50
#DIMS FONTCOLOR
#DIMS ツールチップ
#DIM TEMP_COLOR
#DIM DYNAMIC PREV_立ち絵種類

#DIMS CUSTOMFONTCOLOR

角色画像表示尺寸比 = 100
TEMP_COLOR = GETCOLOR()

PREV_立ち絵種類 = FLAG:立ち絵種類

;口上色取得判定:通常キャラ
IF CID
	CALL SET_KOJO_COLOR(CID)
	IF GETCOLOR() == 12632256 || !FLAG:口上色
		FONTCOLOR = 0xD5DEDF
	ELSE
		FONTCOLOR = 0%TOSTR(GETCOLOR(), "X")% 
	ENDIF
	RESETCOLOR
ENDIF
VARSET MESSAGE_PRINT
IF 顔絵付きメッセージ == 1
	;added support for alternative clothing options (specified with a split in the expression argument)
	;ie 通常:裸, 通常:服, 通常:服_バニー
	服装 '= SPLIT_SINGLE(表情, 1)
	表情 '= SPLIT_SINGLE(表情)
	
	SIF 表情 == ""
	表情 = 通常

	IF CID
		SIF STRLENS(服装)
			GOTO SKIP
		
		IF デフォルト == 1
			服装 = 服
		ELSE
			服装 =
		ENDIF
		$SKIP
		ツールチップ '= CALLNAME:CID
		
		FLAG:立ち絵種類 = 1

		IF TALENT:CID:行きずり && !GCREATED(CFLAG:CID:路人子素材CFLAG開始位置)
			CALL 路人子生成(CID,0)
			CALL 路人子生成(CID,1)
		ENDIF
		CALL GET_BASE_RESOURCE(CID, "顔絵", 服装, 表情)
		SIF !RESULT
			RESULTS = %GET_BASESTYLE(CID, "顔絵")%_服_通常_{CID}
	ELSE
		SIF !STRLENS(服装)
			服装 = 服
		RESULTS = 顔絵_%服装%_%表情%_MOB_{デフォルト}
		;SIF !SPRITECREATED(RESULTS) ;debloat custom code
		SIF !RM_リソースチェック(RESULTS)
			CALL 路人店員画像作成(デフォルト)
		IF !FLAG:口上色
			FONTCOLOR = 0XD5DEDF
		ELSEIF デフォルト >= 90
			SELECTCASE デフォルト
				CASE 90
					FONTCOLOR = 0xC8C9CF
				CASE 95
					FONTCOLOR = 0xD37CD0
			ENDSELECT
		ELSE
			FONTCOLOR = %TOSTR(路人子店員口上色:デフォルト, "X")%
		ENDIF
		ツールチップ = Mob
	ENDIF
	SIF CUSTOMFONTCOLOR != ""
		FONTCOLOR '= CUSTOMFONTCOLOR
	SIF STRLENS(FONTCOLOR) && !STRING_STARTS_IN(FONTCOLOR, "0x", "0X")
		FONTCOLOR '= "0X" + FONTCOLOR
	SIF !FLAG:口上色
		FONTCOLOR = 0XD5DEDF
	
	IF !STRLENS(FONTCOLOR) || FONTCOLOR == "0"
		PRINTFORMW Error: FONTCOLOR was empty!
		FONTCOLOR = 0XD5DEDF
	ENDIF
	

	SIF FLAG:多尺寸
		默认角色画像横幅 = RM_リソースの高さ(RESULTS)
	CALL 画像セット(RESULTS, 0, 0, 角色画像表示尺寸比, 0, 1, "100", ツールチップ)

	;オプション指定で行送りを揃える
	SIF 整形
		MESSAGE = %SPTALK整形(MESSAGE)%

	SPLIT MESSAGE,"/",MESSAGE_PRINT
	FOR LOCAL,0,RESULT
		CALL PRINT_STR, MESSAGE_PRINT:LOCAL
		LOCALS = %HTML_POPPRINTINGSTR()%
		SIF LOCAL != 0
			CALL ダミーセット(0, 0, 角色画像表示尺寸比, LOCAL)
		TEMP_HTML:LOCAL += @"<font color='#%FONTCOLOR%'>%LOCALS%</font>"
	NEXT
	CALL 画像一斉表示(0, 0, 1, -1)
	PRINTL
	
	FLAG:立ち絵種類 = PREV_立ち絵種類
ELSE
	IF !CID
		IF デフォルト >= 90
			SELECTCASE デフォルト
				CASE 90
					FONTCOLOR = 0xC8C9CF
				CASE 95
					FONTCOLOR = 0xD37CD0
			ENDSELECT
		ELSE
			FONTCOLOR = %TOSTR(路人子店員口上色:デフォルト, "X")%
		ENDIF
	ENDIF
	SIF CUSTOMFONTCOLOR != ""
		FONTCOLOR '= CUSTOMFONTCOLOR
	SIF STRLENS(FONTCOLOR) && !STRING_STARTS_IN(FONTCOLOR, "0x", "0X")
		FONTCOLOR '= "0X" + FONTCOLOR
	SIF !FLAG:口上色
		FONTCOLOR = 0XD5DEDF
	SIF STRLENS(FONTCOLOR) && TOINT(FONTCOLOR) != 0
		SETCOLOR TOINT(FONTCOLOR)
	;顔絵メッセージOFF時は通常通りに表示させる
	SIF CID
		PRINTFORML －%CALLNAME:CID%－
	SPLIT MESSAGE,"/",MESSAGE_PRINT
	FOR LOCAL,0,RESULT
		SIF MESSAGE_PRINT:LOCAL != ""
			CALL PRINT_STRL, MESSAGE_PRINT:LOCAL
	NEXT
	RESETCOLOR
ENDIF
SETCOLOR TEMP_COLOR

;SPTALKに渡す文を整形
@SPTALK整形(ARGS,画像行幅 = 0)
#FUNCTIONS
#DIMS 分割後メッセージ, 50
#DIM 画像行幅
#DIMS DYNAMIC 整形メッセージ
#DIM DYNAMIC メッセージ行 = 0
VARSET 分割後メッセージ
SIF 画像行幅 == 0
	画像行幅 = 默认角色画像横幅 / GETCONFIG("一行の高さ")
SPLIT ARGS, "/", 分割後メッセージ

;文が4行以下の時は間隔を開ける。それ以外は詰めて成形
FOR LOCAL, 0, 画像行幅
	IF (RESULT > 4 && (画像行幅-RESULT)/2 - LOCAL > 0) || (RESULT <= 4 && (画像行幅-RESULT*2)/2 - LOCAL > 0)
		整形メッセージ += "/"
	ELSE
		IF RESULT <= 4
			整形メッセージ += "/"
			LOCAL += 1
		ENDIF
		整形メッセージ += "/" + 分割後メッセージ:メッセージ行
		メッセージ行 += 1
	ENDIF
	SIF メッセージ行 == RESULT
		BREAK
NEXT
;SELECTCASE RESULT
;	CASE 1
;		整形メッセージ = 　/　/　/%分割後メッセージ:0%
;	CASE 2
;		整形メッセージ = 　/　/%分割後メッセージ:0%//%分割後メッセージ:1%
;	CASE 3
;		整形メッセージ = %分割後メッセージ:0%//%分割後メッセージ:1%//%分割後メッセージ:2%
;	CASE 4
;		整形メッセージ = 　/　/%分割後メッセージ:0%/%分割後メッセージ:1%/%分割後メッセージ:2%/%分割後メッセージ:3%
;	CASE IS > 4
;		整形メッセージ = %ARGS%
;ENDSELECT
RETURNF 整形メッセージ

;既読・未読で演出変化するPRINT_TEXT関数
;CALL PRINT_TEXT(代入する判定条件あるいは数値,"メッセージ内容/L/100行まで反応できるおー")
;/L/ で改行
;[W]　MODE（未読、あるいは何等かのフラグ）がTRUEの時にWAIT,FALSEの時に待たせず次の行をPRINT
;[WW] で強制WAIT
;例：一行にまとまる書き方
;	CALL PRINT_TEXT(MODE,@"○月×日 /L/今日、 /L/外界から来たあの人に出会ったようです。[W]/L/ ただ●●を探すことでいっぱいになって、 /L/   /L/ ちゃんと…挨拶できなかった…[W]/L/……いや、わたしが…挨拶もできやしないかもしれないです[W]/L/外界の人とはいえ、やはり人間です /L/なんだか怖いですぅ[W]/L/あう、もう二度と合わせないで…[WW]")
;改行できる書き方
;{
;	CALL PRINT_TEXT(MODE,@"○月×日 /L/ 
;	今日、 /L/ 
;	外界から来たあの人に出会ったようです。[W]/L/ 
;	ただ●●を探すことでいっぱいになって、 /L/   /L/ 
;	ちゃんと…挨拶できなかった…[W]/L/ 
;	……いや、わたしが…挨拶もできやしないかもしれないです[W]/L/ 
;	外界の人とはいえ、やはり人間です /L/ 
;	なんだか怖いですぅ[W]/L/ 
;	あう、もう二度と合わせないで…[WW]")
;}
;これを応用して、他の口上に使うこともできます。
@PRINT_TEXT(MODE,MESSAGE)
#DIMS MESSAGE
#DIM MODE

VARSET LOCALS
VARSET RESULTS
VARSET LOCAL

SPLIT MESSAGE,"/L/",LOCALS
FOR LOCAL,0,100
    SIF LOCALS:LOCAL == ""
        CONTINUE
    IF STRCOUNT(LOCALS:LOCAL,ESCAPE("[W]"))
        LOCALS:LOCAL '= REPLACE(LOCALS:LOCAL,ESCAPE("[W]"),"")
        IF MODE
            PRINTFORML %LOCALS:LOCAL%
        ELSE
            PRINTFORMW %LOCALS:LOCAL%
        ENDIF
    ELSEIF STRCOUNT(LOCALS:LOCAL,ESCAPE("[WW]"))
        LOCALS:LOCAL '= REPLACE(LOCALS:LOCAL,ESCAPE("[WW]"),"")
        PRINTFORMW %LOCALS:LOCAL%
    ELSE
        PRINTFORML %LOCALS:LOCAL%
    ENDIF
NEXT

@PRINT_BODY,CID,表情="",服装="",差分="",エフェクト="",立绘选择 = -1
#LOCALSIZE 1
#LOCALSSIZE 1
#DIMS DYNAMIC 表情
#DIMS DYNAMIC 服装
#DIMS DYNAMIC 差分
#DIMS DYNAMIC エフェクト
#DIM DYNAMIC CID
#DIM DYNAMIC 立绘选择
CALL PRINT_FIGURE,CID,表情,服装,差分,エフェクト,"立絵",立绘选择

@PRINT_FIGURE,CID,表情="",服装="",差分="",エフェクト="",立绘种类 = "",立绘选择 = -1
#LOCALSIZE 1
#LOCALSSIZE 1
#DIMS DYNAMIC 表情
#DIMS DYNAMIC 服装
#DIMS DYNAMIC 差分
#DIMS DYNAMIC エフェクト
#DIMS DYNAMIC ツールチップ
#DIM DYNAMIC CID
#DIMS DYNAMIC 立绘种类
#DIM DYNAMIC 立绘选择
#DIM DYNAMIC PREV_立ち絵種類
#DIM DYNAMIC PREV_立绘选择
SIF !顔絵付きメッセージ || !FLAG:画像表示
	RETURN
角色画像表示尺寸比 = 100

SIF 表情 == ""
	表情 = 通常
PREV_立ち絵種類 = FLAG:立ち絵種類
IF 立绘种类 == "顔絵"
	FLAG:立ち絵種類 = 1
ELSEIF 立绘种类 == "立絵"
	FLAG:立ち絵種類 = 0
ELSE
	立绘种类 = %GET_BASESTYLE(CID)%
ENDIF
PREV_立绘选择 = CFLAG:CID:差し替え適用
SIF 立绘选择 != -1
	CFLAG:CID:差し替え適用 = LIMIT(立绘选择,0,CFLAG:CID:別顔有)
IF CID
	;custom code
	TRYCALLFORM IMAGE_EX_I{NO:CID}_PREリソースチェック(CID, 立绘种类, 服装, 表情, "", 差分, "")
	CALL GET_BASE_RESOURCE(CID, 立绘种类, 服装, 表情, "", 差分)
	SIF !RESULT
		RESULTS = %立绘种类%_服_通常_{CID}
	ツールチップ '= CALLNAME:CID
ELSE
	IF FLAG:立ち絵種類
		RESULTS = 顔絵_%服装%_%表情%_MOB_%差分%
	ELSE
		RESULTS = 立絵_%服装%_%表情%_MOB_%差分%
	ENDIF
	;SIF !SPRITECREATED(RESULTS) ;debloat custom code
	SIF !RM_リソースチェック(RESULTS)
		CALL 路人店員画像作成(TOINT(差分))
	;SIF !SPRITECREATED(RESULTS) ;debloat custom code
	SIF !RM_リソースチェック(RESULTS)
		RESULTS = 顔絵_服_通常_MOB_%差分%
	;SIF !SPRITECREATED(RESULTS) ;debloat custom code
	SIF !RM_リソースチェック(RESULTS)
		RETURN
	ツールチップ = 路人子
ENDIF

SIF FLAG:多尺寸
	默认角色画像横幅 = RM_リソースの高さ(RESULTS)

;-------------------------
DEBUGPRINTFORML PRINT_FACEで%RESULTS%を表示
;-------------------------

CALL 画像セット(RESULTS, 0, 0, 角色画像表示尺寸比, 0, 1, "100", ツールチップ)

IF エフェクト != ""
	CALL GET_EFFECT_RESOURCE(CID, エフェクト, , , 服装)
	FOR LOCAL, 0, RESULT
		CALL 画像セット(RESULTS:LOCAL, 0, 0, 角色画像表示尺寸比, LOCAL + 1)
	NEXT
ENDIF
CALL 画像一斉表示(0, 0, 1, -1)
FLAG:立ち絵種類 = PREV_立ち絵種類
CFLAG:CID:差し替え適用 = PREV_立绘选择