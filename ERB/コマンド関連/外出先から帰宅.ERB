@从外面回家
#DIM DIST_TO_HOME
#DIM ReqTSP
#DIM 道迷い時間
#DIMS 低気圧位置

低気圧位置 = %GET_LOCATION_LOW()%

ReqTSP = QOL_RETURN_HOME_TIME_REQUIRED() * 3 ;qol custom code
IF FLAG:デート相手 || FLAG:70 || (GETBIT(FLAG:瞬間移動, AT_HOME(MASTER)) && BASE:MASTER:TSP >= ReqTSP) ;qol custom code
	CALL QoL_DateEndConfirm
	SIF RESULT == -1
		RETURN -1
ENDIF

IF  !FLAG:デート相手 && GETBIT(FLAG:瞬間移動, AT_HOME(MASTER)) && BASE:MASTER:TSP >= ReqTSP
	BASE:MASTER:TSP -= ReqTSP
	GOTO WARP
ENDIF

IF !FLAG:约会的对象 && !FLAG:70
	PRINTL 消费TSP的话可以不消耗时间回家。
	PRINTL 要使用TSP吗？
	CALL ASK_M("不使用", 1, @"使用 (TSP {ReqTSP}/{BASE:MASTER:TSP})", BASE:MASTER:TSP >= ReqTSP, "返回", 1) ;qol custom code
	IF RESULT == 1
		BASE:MASTER:TSP -= ReqTSP
		GOTO WARP
	ENDIF
	SIF RESULT == 2
		RETURN -1
ENDIF
IF !FLAG:70
	;(天候パッチ)連続降水Counter20以上で土砂崩れに巻き込まれる可能性
	IF CONTINUOUS_RAINFALL >= 20 && RAND:(MAX(2, (40 - CONTINUOUS_RAINFALL) / 2)) == 0
		PRINTFORML …走在路上，从山边传来异响
		PRINTFORMW ！！ 是泥石流！！

		PRINTFORML 
		PRINTFORML %CALLNAME:MASTER%虽然被卷入泥石流
		PRINTFORML 但还是设法成功逃脱了…
		PRINTL 
		CALL ASK_DIARY("遭遇了泥石流")
		BASE:MASTER:体力 -= (MAXBASE:MASTER:体力 * 3)
		
		GOTO WARP2
	ENDIF
	;(天候パッチ)雪崩発生フラグが立っているとき雪崩に巻き込まれる可能性
	IF POSSIBLITY_AVALANCHE == 1 && POWER_COLD_AIR >= 3 && RAND:((6 - POWER_COLD_AIR) * 10) == 0
		PRINTFORML …走在路上，从山边有什么东西逼近
		PRINTFORMW ！！ 是雪崩！！

		PRINTFORML 
		PRINTFORML %CALLNAME:MASTER%虽然被卷入雪崩
		PRINTFORML 但还是设法成功逃脱了…
		PRINTL 

		POSSIBLITY_AVALANCHE = 0
		CONTINUOUS_SNOWFALL = 0

		CALL ASK_DIARY("遭遇了雪崩")
		BASE:MASTER:体力 -= (MAXBASE:MASTER:体力 * 3)

		GOTO WARP4
	ELSEIF POSSIBLITY_AVALANCHE == 2 && POWER_WARM_AIR >= 2 && RAND:((6 - POWER_WARM_AIR) * 10) == 0
		PRINTFORML …走在路上，从山边有什么东西逼近
		PRINTFORMW ！！ 是雪崩！！

		PRINTFORML 
		PRINTFORML %CALLNAME:MASTER%虽然被卷入雪崩
		PRINTFORML 但还是设法成功逃脱了…
		PRINTL 

		POSSIBLITY_AVALANCHE = 0
		CONTINUOUS_SNOWFALL = 0
		SNOW_COVERAGE = 0

		CALL ASK_DIARY("遭遇了雪崩")
		BASE:MASTER:体力 -= (MAXBASE:MASTER:体力 * 3)
		
		GOTO WARP5
	ENDIF
	;(天候パッチ)暴風雪発生時にホワイトアウト発生の可能性 颶風時には確率上昇
	IF TIME:5 == 9 && ((WIND_VELOCITY == 2 && RAND:3 == 0) || WIND_VELOCITY >= 3)
		道迷い時間 = 60 + RAND:120
		PRINTFORML 纷纷扬扬的雪被猛烈的风吹打着
		PRINTFORMW 眼前一片雪白！
		PRINTFORML 
		PRINTFORML ……迷路了，多走了{道迷い時間}分钟
		TIME:0 += 道迷い時間
		BASE:MASTER:体力 = MAX(BASE:MASTER:体力 - (20 * 道迷い時間), 0)
		
		GOTO WARP6
	ELSEIF TIME:5 == 8 && ((WIND_VELOCITY == 2 && RAND:10 == 0) || WIND_VELOCITY >= 3)
		道迷い時間 = 30 + RAND:60
		PRINTFORML 纷纷扬扬的雪被猛烈的风吹打着
		PRINTFORMW 眼前一片雪白！
		PRINTFORML 
		PRINTFORML ……迷路了，多走了{道迷い時間}分钟
		TIME:0 += 道迷い時間
		BASE:MASTER:体力 = MAX(BASE:MASTER:体力 - (15 * 道迷い時間), 0)
		
		GOTO WARP7
	ENDIF
	CALL お出かけイベント
	$WARP2
	$WARP4
	$WARP5
	$WARP6
	$WARP7
	CALL 帰宅の時間経過
ELSEIF BASE:MASTER:TSP < ReqTSP
	PRINTW TSP不足，无法在时间停止的时候回去了
	RETURN -1
ELSE
	BASE:MASTER:TSP -= ReqTSP
ENDIF

$WARP
IF CFLAG:败犬 && TALENT:路人 == 1
	PRINTFORML 把%CALLNAME:TARGET%带回去了。
ELSEIF BASE:MASTER:体力 <= 0
	PRINTFORML %CALLNAME:MASTER%用尽最后一点力气、回到了%GET_MAPNAME(MAIN_MAP)%
	PRINTFORML 然而才刚刚到达%NAME_FROM_PLACE(ENTRANCE)%便已经失去了意识…
	PRINTFORMW 
ELSE
	PRINTFORMW 从\@ CFLAG:MASTER:约会中 != CFLAG:TARGET:约会中 || TARGET == MASTER ? 外出 # 约会 \@中回来了
ENDIF
CALL 守矢神簽约会的对象のリセット()
FOR LOCAL,1,CHARANUM
	IF CHK_DATENOW(CFLAG:LOCAL:约会中)
		CALL DATE_EVENT(LOCAL)
		CALL SET_DATE(99,LOCAL)
	ENDIF
NEXT
SIF CHK_DATENOW(CFLAG:MASTER:约会中)
	CALL SET_DATE(99,MASTER)

CFLAG:MASTER:現在位置 = GET_ENTRANCE(MAIN_MAP)

@帰宅の時間経過
;残り時間算出
;qol custom code, replaced calculation method
TIME = TIME + QOL_RETURN_HOME_TIME_REQUIRED()
;IF TFLAG:约会道中
;	;道中の場合
;	TIME += MIN(TIME_REQUIRED(MAIN_MAP, CFLAG:MASTER:约会中) + TFLAG:约会道中, TIME_REQUIRED(MAIN_MAP, CFLAG:MASTER:あなた前ターン位置 / 100) + TIME_REQUIRED(CFLAG:MASTER:约会中, CFLAG:MASTER:あなた前ターン位置 / 100) - TFLAG:约会道中)
;	;TIME = TIME + TIME_REQUIRED(MAIN_MAP, CFLAG:MASTER:约会中) - TFLAG:约会道中
;ELSE
;	;约会先到着済みの場合
;	TIME = TIME + TIME_REQUIRED(MAIN_MAP, CFLAG:MASTER:约会中)
;ENDIF
CALL 守矢神簽约会的对象のリセット()

@外出先から誘えるか(ARG)
LOCAL = FLAG:约会的对象
IF CFLAG:ARG:行動 == 5
	PRINTFORMW %CALLNAME:ARG%现在正在工作中
	RETURN 0
ENDIF
IF LOCAL != ARG && LOCAL != MASTER
	PRINTFORM %CALLNAME:LOCAL%
	;気弱系
	IF CFLAG:LOCAL:性格傾向 == 1
		PRINTFORML 抓紧了裙角朝着%CALLNAME:MASTER%摇了摇头
	;强气系
	ELSEIF CFLAG:LOCAL:性格傾向 == 2
		PRINTFORML 眼光一闪凝视着%CALLNAME:MASTER%
	;坦率系
	ELSEIF CFLAG:LOCAL:性格傾向 == 3
		PRINTFORML 凝视着%CALLNAME:MASTER%诉说到一直想要见面
	;真面目系
	ELSEIF CFLAG:LOCAL:性格傾向 == 4
		PRINTFORML 对着%CALLNAME:MASTER%说道那是不行的
	;其他
	ELSEIF CFLAG:LOCAL:性格傾向 == 5
		PRINTFORML 对着%CALLNAME:MASTER%叹息一声、摇了摇头
	ENDIF
	PRINTFORMW %CALLNAME:MASTER%放弃了邀请%CALLNAME:ARG%
	RETURN 0
ENDIF
IF !VISIT(ARG)
	PRINTFORMW 被告知现在不可以
	RETURN 0
ENDIF
IF ARG == 93 && !ITEM:簡易泳池
	PRINTW 没办法带她出去…
	RETURN 0
ELSEIF BASE:ARG:気力 < 500
	PRINTFORMW %CALLNAME:ARG%好像太累了
	RETURN 0
ELSEIF !CHARA_HOLIDAY(ARG) && BASE:ARG:工作量 && TCVAR:ARG:工作開始 < TIME + 180
	PRINTFORMW %CALLNAME:ARG%之后还有工作
	RETURN 0
ELSE
	CFLAG:ARG:拠点来訪 = 1
	RETURN 1
ENDIF


;FileName_DATE_CMN.ERB ------------------------------- Rev1.00
;约会に誘えるか
;-----------------------------------------------------------
@CAN_DATE(ARG)
#FUNCTION

SIF !CHK_DATENOW(CFLAG:MASTER:约会中)
	RETURNF 0
SIF !SHIRAHU(ARG)
	RETURNF 0
SIF CFLAG:MASTER:現在位置 == OMANEKIBEYA()
	RETURNF 0
IF !CFLAG:ARG:败犬
	SIF !CFLAG:ARG:態度 && ARG != RANDOM_CHARANUM
		RETURNF 0
	SIF CFLAG:ARG:ブチギレ
		RETURNF 0
ENDIF
;潜伏モード中は不可
SIF GETBIT(FLAG:潜伏,0)
	RETURNF 0
SIF CHK_DATENOW(CFLAG:ARG:约会中)
	RETURNF 0
SIF (!TALENT:ARG:思慕 && !TALENT:ARG:恋慕 && !TALENT:ARG:愛欲 && ARG != RANDOM_CHARANUM)
	RETURNF 0
RETURNF 1

