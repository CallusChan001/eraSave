@COOKING_UI(DISH_TYPE_NUM, SELECT_MENU, SELECT_ING, SELECT_DRG, SELECT_DRG_2, 練習モード, HIGHLIGHTED, SEARCH_INPUT, SEARCH_TYPE)
#LOCALSIZE 1
#LOCALSSIZE 1
#DIM DISH_TYPE_NUM
#DIM SELECT_MENU
#DIM 料理結果
#DIM REQUIRED_LV
#DIM SELECT_ING
#DIM SELECT_DRG
#DIM SELECT_DRG_2
#DIMS HIGHLIGHTED
#DIM 練習モード
#DIM MENUCOUNT
#DIM MAXMENU
#DIMS SEARCH_INPUT
#DIM SEARCH_TYPE
#DIMS CONST TENKABUTSU, 8 = "", "无", "栄養強化剤", "莓子酒", "黑朗姆酒", "白兰地VS", "白兰地VSOP", "白兰地XO"
#DIMS CONST SHIKOMI, 7 = "", "无", "媚薬", "利尿剤", "催情薬", "睡眠薬","新鲜精液"
COOKING_LCOUNT = 0
料理結果 = 0
SELECTCASE DISH_TYPE_NUM
CASE 1
	DISH_TYPE = 軽食
	MAXMENU = 100
CASE 2
	DISH_TYPE = 主食
	MAXMENU = 100
CASE 3
	DISH_TYPE = 甜点
	MAXMENU = 50
CASE 4
	練習モード = 1
	MAXMENU = 2
	;練習メニューは記憶しない
	SIF SELECT_MENU - 400 != DISH_ASSI
		SELECT_MENU = 0
ENDSELECT
;メニュー表示
DRAWLINEFORM ＝
IF 練習モード
	;練習用メニュー
	PRINTFORML ＜教学＞　★师傅：%CALLNAME:DISH_ASSI%
	PRINTFORML 可以学习到固有料理
	PRINTL 
	PRINT 　　
	CALL SHOW_DISH_MENU(400 + DISH_ASSI, "練習", SELECT_MENU, 0, 練習モード)
	PRINTL 
	COOKING_LCOUNT += 3
ELSE
	;通常メニュー
	MENUCOUNT = 0
	PRINTFORM ＜MENU＞　
	IF DISH_ASSI
		PRINTFORM ●助手：%CALLNAME:DISH_ASSI%
		SPLIT GET_STR(, "キャラデータ", DISH_ASSI, "料理：助手効果"), "＊", RESULTS
		SIF RESULTS:1 != ""
			PRINTFORM （得意料理：%TASTE_TR(RESULTS:1)%）
		PRINTL 
	ELSE
		PRINTFORML ○助手：无
	ENDIF
	IF SEARCH_TYPE && SEARCH_INPUT != ""
		FOR LOCAL, 101, 400 + CHARANUM
			CALL SHOW_DISH_MENU(LOCAL, DISH_TYPE, SELECT_MENU, MENUCOUNT, 0, HIGHLIGHTED, SEARCH_INPUT, SEARCH_TYPE)
			SIF RESULT == 2
				COOKING_LCOUNT ++
			SIF RESULT >= 1
				MENUCOUNT ++
		NEXT
	ELSE
		FOR LOCAL, 1 + DISH_TYPE_NUM * 100, MAXMENU + DISH_TYPE_NUM * 100
			CALL SHOW_DISH_MENU(LOCAL, DISH_TYPE, SELECT_MENU, MENUCOUNT, 0, HIGHLIGHTED, SEARCH_INPUT, SEARCH_TYPE)
			SIF RESULT == 2
				COOKING_LCOUNT ++
			SIF RESULT >= 1
				MENUCOUNT ++
		NEXT
		;ローターメニュー
		FOR LOCAL, 351, 400 + CHARANUM
		;351~360はローター
		;401~はスペシャル
			SIF LOCAL >= 361 && LOCAL <= 400
				CONTINUE
			CALL SHOW_DISH_MENU(LOCAL, DISH_TYPE, SELECT_MENU, MENUCOUNT,,HIGHLIGHTED, SEARCH_INPUT, SEARCH_TYPE)
			SIF RESULT == 2
				COOKING_LCOUNT ++
			SIF RESULT >= 1
				MENUCOUNT ++
		NEXT
	ENDIF
ENDIF
PRINTL 

;選択中の料理情報取得
IF SELECT_MENU
	CALL DISHDATA(SELECT_MENU, DISH_TYPE, 練習モード)
	料理結果 = RESULT
	REQUIRED_LV = RESULT:1
	;材料選択中ならDISH_TASTEに加える,0番はローター
	SIF SELECT_ING >= 0
		CALL DISH_TASTE_ADD(SELECT_ING)
ENDIF

;DISH_TYPE選択
DRAWLINEFORM ＝
IF HIGHLIGHTED == ""
	PRINTPLAIN 制作：　
	FONTSTYLE 1
	FOR LOCAL, 1, 5
		SELECTCASE LOCAL
		CASE 1
			LOCALS = 軽食
		CASE 2
			LOCALS = 主食
		CASE 3
			LOCALS = 甜点
		CASE 4
			LOCALS = 教学
			SIF LEARNED_MENU:DISH_ASSI >= 100
				LOCALS += "（完成）"
		ENDSELECT
		IF LOCAL == 4 && (!DISH_ASSI || LEARNED_MENU:DISH_ASSI >= 100 || ABL:TARGET:親密 < 5)
			SETCOLOR C_GRAY
		ELSEIF DISH_TYPE_NUM == LOCAL
			SETCOLOR C_AQUA
		ENDIF
		PRINTFORM [{LOCAL, 2}]%TASTE_TR(LOCALS), 16, LEFT%
		RESETCOLOR
	NEXT
	FONTREGULAR
ENDIF
PRINTL 

;材料選択,おまかせ時は空白
IF 料理結果
PRINTPLAIN 使用材料：　
	FOR LOCAL, 1, 11
		IF LOCAL == 6
			PRINTL 
			PRINTPLAIN 　　　　　　
		ENDIF
		SIF DISH_ING:LOCAL < 0
			CONTINUE
		IF SELECT_ING == DISH_ING:LOCAL
			SETCOLOR C_AQUA
		ELSEIF ITEM:(DISH_ING:LOCAL) <= 0
			SETCOLOR C_GRAY
		ENDIF
		LOCALS = [{LOCAL + 10}]%ITEMNAME_TR(DISH_ING:LOCAL)%({ITEM:(DISH_ING:LOCAL), 2})
		PRINTFORM %LOCALS, 22, LEFT%
		RESETCOLOR
	NEXT
ELSE
	PRINTL 
ENDIF
PRINTL 

;仕込み
IF !DISH_ASSI && HIGHLIGHTED == ""
	PRINTPLAIN 　混入物：　
	FOR LOCAL, 21, 27
		LOCALS = %SHIKOMI:(LOCAL - 20)%
		IF LOCAL == 21
			SIF !SELECT_DRG
				SETCOLOR C_AQUA
			PRINTFORM [{LOCAL, 2}]%TASTE_TR(LOCALS), 18, LEFT%
		ELSE
			IF ITEMNAME:SELECT_DRG == LOCALS
				SETCOLOR C_AQUA
			ELSEIF ITEM:GETNUM(ITEM, LOCALS) <= 0
				SETCOLOR C_GRAY
			ENDIF
			LOCALS = [{LOCAL, 2}]%TASTE_TR(LOCALS)%({ITEM:GETNUM(ITEM, LOCALS), 2})
			PRINTFORM %LOCALS, 22, LEFT%
		ENDIF
		RESETCOLOR
	NEXT
	PRINTL 
	COOKING_LCOUNT ++
ENDIF

IF SELECTCOM != 498 ;qol custom code, not during Iron Chef contest 
PRINTPLAIN 　添加物：　
FOR LOCAL, 31, 38
	LOCALS = %TENKABUTSU:(LOCAL - 30)%
	SIF DISH_TYPE_NUM != 3 && GROUPMATCH(LOCAL, 33, 34, 35, 36, 37)
		CONTINUE
	IF LOCAL == 31
		SIF !SELECT_DRG_2
			SETCOLOR C_AQUA
		PRINTFORM [{LOCAL, 2}]%TASTE_TR(LOCALS), 18, LEFT%
	ELSE
		IF ITEMNAME:SELECT_DRG_2 == LOCALS
			SETCOLOR C_AQUA
		ELSEIF ITEM:GETNUM(ITEM, LOCALS) <= 0
			SETCOLOR C_GRAY
		ENDIF
		LOCALS = [{LOCAL, 2}]%TASTE_TR(LOCALS)%({ITEM:GETNUM(ITEM, LOCALS), 2})
		PRINTFORM %LOCALS, 22, LEFT%
		IF LOCAL == 32
			PRINTL 
			PRINTPLAIN 　　　　　　
		ENDIF
	ENDIF
	RESETCOLOR
NEXT
PRINTL 
COOKING_LCOUNT ++
;(酒造パッチ)調整
COOKING_LCOUNT ++
ENDIF ;qol custom code

;addition custom code
;IF !DISH_ASSI && SELECTCOM != 498
;	CALL ADD_LewdFoodAdd(SELECT_LEWD)
;	PRINTL 
;	COOKING_LCOUNT ++
;ENDIF
;--------------------

DRAWLINEFORM －
;料理の情報
IF 料理結果
	PRINTFORML 【%DISHNAME_TR()%】%DISH_COMMENT%
	PRINT 　
	CALL SHOW_TASTE(DISH_TASTE, "DISH_DATA")
ELSE
	PRINTL 
	PRINTL 
ENDIF
;コマンド
DRAWLINE
IF !料理結果
	PRINTL 
ELSEIF 料理結果 < 10
	IF 料理結果 == 1
		IF DISH_ING:1 < 0
			CALL COLORMESSAGE(@"　　※这道料理无法制作", C_ORANGE, 1)
		ELSE
			CALL COLORMESSAGE(@"　　材料不足…", C_ORANGE, 1)
		ENDIF
	ELSEIF 料理結果 == 2
		CALL COLORMESSAGE(@"　　需要等级 {REQUIRED_LV}（现在 {ABL:MASTER:料理技能}）", C_ORANGE, 1)
	ELSE
		CALL COLORMESSAGE(@"　　不知为何无法制作…", C_ORANGE, 1)
	ENDIF
ELSEIF ITEM:SELECT_ING <= 0
	CALL COLORMESSAGE(@"　　材料不足…", C_ORANGE, 1)
ELSE
	PRINTFORML [ 9]制作【%DISHNAME_TR()%】
ENDIF


;-------------------------------------------------
;メニュー表示用の関数
;ARGS     DISH_TYPE
;RESULT   2=改行した,1=表示した,0=表示しない
;-------------------------------------------------
@SHOW_DISH_MENU(DISH_NUM, ARGS, SELECT_MENU, MENUCOUNT, 練習モード, HIGHLIGHTED, SEARCH_INPUT, SEARCH_TYPE)
#LOCALSIZE 1
#LOCALSSIZE 1
#DIM DISH_NUM
#DIM SELECT_MENU
#DIM MENUCOUNT
#DIM 練習モード
#DIMS HIGHLIGHTED
#DIMS DYNAMIC SEARCH_INPUT
#DIM DYNAMIC SEARCH_TYPE
#DIM DYNAMIC RATE
;料理データを呼ぶ
CALL DISHDATA(DISH_NUM, ARGS, 練習モード, SEARCH_TYPE && SEARCH_INPUT != "")
;作れなくてもグレーで表示する,選択中なら赤
IF RESULT && DISH_SPECIAL_FILTER(SEARCH_INPUT, SEARCH_TYPE)
	IF RESULT < 10 && SELECT_MENU == DISH_NUM
		SETCOLOR C_RED
	ELSEIF RESULT < 10
		SETCOLOR C_GRAY
	ELSEIF SELECT_MENU == DISH_NUM
		SETCOLOR C_AQUA
	ELSEIF STRCOUNT(DISH_TASTE, "旬/") || STRCOUNT(DISH_TASTE, "縁起物/") || STRCOUNT(DISH_TASTE, "台風炸丸子/") && HIGHLIGHTED == ""
		SETCOLOR C_CREAM
	ELSEIF STRCOUNT(DISH_TASTE, HIGHLIGHTED) && HIGHLIGHTED != ""
		SETCOLOR C_CREAM
	ENDIF
	IF (DISH_ASSI)
		RATE = REVIEW_DISH_FOR_MENU(DISH_ASSI,DISH_TASTE)
		LOCALS '= @"({RATE})"+ DISHNAME_TR()
		PRINTFORM [{DISH_NUM, 3}]%LOCALS,35, LEFT%
	ELSE
		LOCALS '= DISHNAME_TR()
		PRINTFORM [{DISH_NUM, 3}]%LOCALS,35, LEFT%
	ENDIF
	RESETCOLOR
	;改行したらRESULT = 2,表示したらRESULT = 1
	IF (MENUCOUNT + 1) % 3 == 0
		PRINTL 
		RETURN 2
	ELSE
		RETURN 1
	ENDIF
ENDIF
RETURN 0

;-------------------------------------------------
;味の表示用関数
;味にかぎらず使いまわせそう？
;横幅の限界に注意
;-------------------------------------------------
@SHOW_TASTE(TASTE_STR, PRINT_TYPE, INVISIBLE, SEARCH)
#LOCALSIZE 1
#LOCALSSIZE 1
#DIMS TASTE_STR              	;表示対象の味データ
#DIMS DYNAMIC TASTE_LIST, 100	;味リスト
#DIM  DYNAMIC TASTE_CNT,  100	;味リスト各要素の重複个数
#DIM  DYNAMIC TASTE_NUM			;味の種類の総数
#DIM  DYNAMIC SHOW_NUM			;表示した味の数
#DIM  TASTE_ID               	;ループ用
#DIMS PRINT_TYPE				;好き嫌いで高Lvを赤くしたり青くしたり
#DIM INVISIBLE					;これが真だと？？？？表示にする　親密が足りない時非表示など
#DIM nSpace = 20
#DIM nColumn = 5
#DIM SEARCH

VARSET RESULTS
SPLIT TASTE_STR, "＊", RESULTS
IF RESULTS:1 != "" && PRINT_TYPE != "DISH_DATA"
	LOCALS = [得意料理：%TASTE_TR(RESULTS:1)%]
	SETCOLOR C_CREAM
	PRINTFORM 　%LOCALS, nSpace%
	RESETCOLOR
	SHOW_NUM ++
ENDIF
;味リストの取得
;CALLF CNT_SPLIT(TASTE_STR, TASTE_LIST, TASTE_CNT, TASTE_NUM)
;有序的TASTE标签取得
CALLF GET_SORTED_CNT_SPLIT(TASTE_STR, TASTE_LIST, TASTE_CNT, TASTE_NUM, 1)
;味リストの表示
FOR TASTE_ID, 0, TASTE_NUM
	;PRINTFORML %TASTE_LIST:TASTE_ID%
	SIF !STRLENS(TASTE_LIST:TASTE_ID)
		CONTINUE
	;料理の情報を見る時用
	IF PRINT_TYPE == "DISH_DATA"
		SIF TASTE_CNT:TASTE_ID >= 2
			SETCOLOR C_L_GREEN
		SIF TASTE_CNT:TASTE_ID >= 3
			SETCOLOR 0xffff96
		SIF TASTE_CNT:TASTE_ID >= 5
			SETCOLOR 0xffc8c8
		IF GROUPMATCH(TASTE_LIST:TASTE_ID, "BONUS", "旬", "縁起物", "台風炸丸子")
			SETCOLOR C_YELLOW
			PRINTBUTTON @"[★%TASTE_TR(TASTE_LIST:TASTE_ID)%]", ""
		ELSEIF GROUPMATCH(TASTE_LIST:TASTE_ID, "过季")
			SETCOLOR C_RED
			PRINTBUTTON @"[%TASTE_TR(TASTE_LIST:TASTE_ID)%]", ""
		ELSEIF GROUPMATCH(TASTE_LIST:TASTE_ID, "精力", "薬効", "鬼の魔力", "血の魔力", "魔性", "加護", "奇跡", "幸運", "豊穣")
			SETCOLOR C_GREEN
			PRINTBUTTON @"[%TASTE_TR(TASTE_LIST:TASTE_ID)%]", ""
		;表示しないもの
		ELSEIF STRCOUNT(TASTE_LIST:TASTE_ID, DISH_NAME)
		;	PRINTFORML DISHNAMEなので非表示
		;ELSEIF STRCOUNT(TASTE_LIST:TASTE_ID, DISH_TYPE) && DISH_TYPE != "デザート"
		;	PRINTFORML デザートじゃないので非表示
		ELSEIF STRCOUNT(TASTE_LIST:TASTE_ID, "得意料理")
		;	PRINTFORML 得意料理なので非表示
		ELSEIF STRCOUNT(TASTE_LIST:TASTE_ID, "中立")
		;	PRINTFORML 中立なので非表示
		;伏せる
		ELSEIF (STRCOUNT(TASTE_STR, "心跳味") || STRCOUNT(TASTE_STR, "未知の味")) && GROUPMATCH(TASTE_LIST:TASTE_ID, "甜", "辣", "咸", "酸")
			SETCOLOR C_P_PURPLE
			PRINTBUTTON @"[？？？]", ""
		;通常
		ELSE
			PRINTBUTTON @"[%TASTE_TR(TASTE_LIST:TASTE_ID)%]", ""
		ENDIF
		RESETCOLOR
		SHOW_NUM ++
		;改行はしない
	;助手効果を見る時用
	ELSEIF PRINT_TYPE == "ASSIDATA"
		IF GROUPMATCH(TASTE_LIST:TASTE_ID, "精力", "薬効", "鬼の魔力", "血の魔力", "魔性", "加護", "奇跡", "幸運", "豊穣")
			SETCOLOR C_YELLOW
			LOCALS = [%TASTE_TR(TASTE_LIST:TASTE_ID)%]　　
		;表示しないもの
		ELSEIF STRCOUNT(TASTE_LIST:TASTE_ID, "得意料理")
			CONTINUE
		ELSE
			LOCALS = [%TASTE_TR(TASTE_LIST:TASTE_ID)%]　　
		ENDIF
		PRINTFORM 　%LOCALS, nSpace%
		RESETCOLOR
		SHOW_NUM ++
		SIF SHOW_NUM % nColumn
			CONTINUE
		PRINTFORML 
	ELSEIF PRINT_TYPE == "GIFT" ;qol custom code
		CALL Qol_GiftData(TASTE_LIST:TASTE_ID, TASTE_CNT:TASTE_ID)
	;キャラの好みを見る時用
	ELSE
		IF TASTE_CNT:TASTE_ID >= 2
			IF PRINT_TYPE == "POSITIVE"
				SETCOLOR C_GREEN
			ELSEIF PRINT_TYPE == "NEGATIVE"
				SETCOLOR C_ORANGE
			ENDIF
		ENDIF
		IF TASTE_CNT:TASTE_ID >= 5
			IF PRINT_TYPE == "POSITIVE"
				SETCOLOR C_AQUA
			ELSEIF PRINT_TYPE == "NEGATIVE"
				SETCOLOR C_RED
			ENDIF
		ENDIF
		LOCALS = [%TASTE_TR(TASTE_LIST:TASTE_ID)%]Lv{TASTE_CNT:TASTE_ID, 2}
		SIF INVISIBLE
			LOCALS = [？？？？]Lv{TASTE_CNT:TASTE_ID, 2}
		;システム側で6文字、味名称に14文字分をわりあて
		PRINTFORM 　%LOCALS, nSpace%
		RESETCOLOR
		SHOW_NUM ++
		SIF SHOW_NUM % nColumn
			CONTINUE
		PRINTFORML 
	ENDIF
NEXT
IF PRINT_TYPE == "DISH_DATA"
	PRINTL 
ELSE
	SIF SHOW_NUM % nColumn
		PRINTFORML 
ENDIF

;-------------------------------------------------
;料理の出来栄えを表示する関数
;-------------------------------------------------
@出来栄え(ARG)
#LOCALSIZE 1
#LOCALSSIZE 1
;CHKFONT "ＭＳ Ｐ明朝"
;SIF RESULT
;	SETFONT "ＭＳ Ｐ明朝"
FONTBOLD
SELECTCASE ARG
CASE IS < 400
	SETCOLOR C_RED
	PRINT 【差】
CASE IS < 500
	SETCOLOR C_ORANGE
	PRINT 【贫】
CASE IS < 600
	SETCOLOR C_YELLOW
	PRINT 【良】
CASEELSE
	SETCOLOR C_PINK
	PRINT 【美】
ENDSELECT
SETFONT 
FONTREGULAR
RESETCOLOR

;-------------------------------------------------
;料理情報をリセットする関数
;-------------------------------------------------
@RESET_DISH
#LOCALSIZE 1
#LOCALSSIZE 1
DISH_NAME = 
DISH_TASTE = 
DISH_TYPE = 
DISH_COMMENT = 
VARSET DISH_ING, -1
DISH_TIME = 0
DISH_DRG = 0
DISH_DRG_2 = 0
DISH_ASSI = 0
EX_DISH_NAME = 
DISH_BASEPOINT = 0
DISH_LIMIT = 0
FLAG:料理 = 0
DISHNAME = 
CVARSET TCVAR, GETNUM(TCVAR, "盗み食い抑制")
;-------------------------------------------------
;料理中にたかりに来る関数
;-------------------------------------------------
@COOKING_TAKARI(ARG, 確率 = 0)
#LOCALSIZE 1
#LOCALSSIZE 1
#DIM 確率

;タカらない
IF !CAN_TAKARI(ARG)
	確率 = -1 ;bug fix, could still activate if rand rolls 0 because 0 = 0
;来訪中なら100%
ELSEIF AT_HOME(ARG) && !CFLAG:ARG:神社在住
	確率 = MAX(100,確率)
;同じ拠点なら二分の一
ELSEIF AT_HOME(ARG) && CFLAG:ARG:神社在住
	確率 = 確率 * 1 / 2
ENDIF

;確率
SIF 確率 < RAND:100
	RETURN
IF AT_HOME(ARG)
	PRINTFORMW \@確率>100?感受到自己心心念念的料理的波动#闻到料理的气味\@，%CALLNAME:ARG%串门来了……
ELSE
	PRINTFORMW \@確率>100?感受到自己心心念念的料理的波动#料理刚做好\@，%CALLNAME:ARG%不知从何处笑眯眯地冒了出来……
ENDIF
CFLAG:ARG:拠点来訪 = 1
CFLAG:ARG:現在位置 = CFLAG:MASTER:現在位置
TCVAR:ARG:なるべく動かない = 1


;-------------------------------------------------
;たかれるかの判定関数
;-------------------------------------------------
@CAN_TAKARI(ARG)
#FUNCTION
#LOCALSIZE 1
#LOCALSSIZE 1
SIF FLAG:70 ;com custom code
	RETURNF 0
;活動時間-1h外
SIF !BETWEENTIME(CFLAG:ARG:来訪時間, CFLAG:ARG:帰宅時間 - 60)
	RETURNF 0
;MASTERが外
SIF !AT_HOME(MASTER)
	RETURNF 0
;お招き部屋
SIF CFLAG:MASTER:現在位置 == OMANEKIBEYA()
	RETURNF 0
;addition custom code
SIF (CFLAG:MASTER:現在位置 == CFLAG:MASTER:初期位置 || CFLAG:MASTER:現在位置 == CFLAG:TARGET:初期位置) && FLAG:自室施錠
	RETURNF 0
;MASTERと同じ場所
SIF CFLAG:ARG:現在位置 == CFLAG:MASTER:現在位置
	RETURNF 0
;寝てる
SIF CFLAG:ARG:睡眠
	RETURNF 0
;仕事中
SIF CFLAG:ARG:行動 == 5
	RETURNF 0
;bug fix - if they're currently idling
SIF Activity_Duration:ARG
	RETURNF 0
RETURNF 1

;-------------------------------------------------
;口上等で即席料理を作る関数
;食材は消費せず、DISH_ING:1を使用する
;CHARA１人のみの効果を得る
;道具の効果は得られない
;ARGS 練習用
;-------------------------------------------------
@料理生成(料理名, CHARA, ARGS)
#LOCALSIZE 1
#LOCALSSIZE 1
#DIMS 料理名
#DIM CHARA
#DIM DISH_NUM
#DIM BONUS点
CALL RESET_DISH
CALL 料理番号出力(料理名, "料理生成")
DISH_NUM = RESULT
DISH_TYPE = %RESULTS:1%
DISH_RANK = 食材レベル(DISH_ING:1)
;DISHNAMEは口上でのみ使用してください
DISHNAME = %DISHNAME_TR()%
FLAG:料理 = DISH_NUM

CALL DISH_TASTE_ADD(DISH_ING:1)
IF !CHARA && FLAG:扮演
	CALL DISH_TASTE_FROM_CHARA(FLAG:扮演)
ELSE
	CALL DISH_TASTE_FROM_CHARA(CHARA)
ENDIF
SIF ARGS == "練習"
	DISH_TASTE += "練習/"
DISH_TASTE += "できたて/"
DISH_TASTE += DISH_NAME + "/"
SIF STRCOUNT(DISH_TASTE, "練習/")
	DISH_TASTE '= REPLACE(DISH_TASTE, "过季\/", "")
DISH_LIMIT = DISH_MAXLIMIT
EX_DISH_NAME = %DISH_NAME%
BONUS点 = 0
SIF STRCOUNT(DISH_TASTE, "旬/")
	BONUS点 += 2
SIF STRCOUNT(DISH_TASTE, "縁起物/")
	BONUS点 += 2
SIF STRCOUNT(DISH_TASTE, "台風炸丸子/")
	BONUS点 += 2
SIF STRCOUNT(DISH_TASTE, "玉兎流/") && STRCOUNT(DISH_TASTE, "胡萝卜/")
	BONUS点 += 2
SIF STRCOUNT(DISH_TASTE, "プロの味/")
	BONUS点 += 2
SIF STRCOUNT(DISH_TASTE, "得意料理")
	BONUS点 += 2
SIF STRCOUNT(DISH_TASTE, "闪耀/")
	BONUS点 += 1
SIF STRCOUNT(DISH_TASTE, "切れ味/")
	BONUS点 += 1
SIF STRCOUNT(DISH_TASTE, "美味的水/")
	BONUS点 += 1
SIF STRCOUNT(DISH_TASTE, "咖喱の日/")
	BONUS点 += 1
DISH_BASEPOINT = ABL:CHARA:料理技能 * 2 + DISH_RANK + BONUS点
DISH_BASEPOINT = MIN(300 + (DISH_BASEPOINT * 20), 700)

;-------------------------------------------------
;料理名から料理番号を逆算する関数
;料理生成しないならリセットする
;気軽に使えません。要注意。
;-------------------------------------------------
@料理番号出力(料理名, 生成)
#LOCALSIZE 1
#LOCALSSIZE 1
#DIMS 料理名
#DIMS 生成
#DIM 結果保存
結果保存 = 0
FOR LOCAL, 101, 600
	CALL DISHDATA(LOCAL)
	IF DISH_NAME == 料理名
		結果保存 = LOCAL
		BREAK
	ENDIF
NEXT
SIF 生成 != "料理生成" || !結果保存
	CALL RESET_DISH
VARSET RESULT
RETURN 結果保存

;-------------------------------------------------
;固有料理のラーニング処理関数
;一応料理外で覚えた場合（口上とか）も想定
;LEARNED_MENUには100+DAYが入るので、いつ習得したか判定できる
;ARG      DISH_ASSI
;ARGS     DISH_NAME
;-------------------------------------------------
@LEARNING_SP_DISH(ARG, ARGS)
#LOCALSIZE 1
#LOCALSSIZE 1
LEARNED_MENU:ARG = 100 + DAY
CALL COLORMESSAGE(@"学会了『%DISHNAME_TR(ARGS)%』！", C_YELLOW, 1)
