;-------------------------------------------------
;外出
;日常系指令、レベル1
;-------------------------------------------------
@COM405
#LOCALSIZE 11
#LOCALSSIZE 1
VARSET LOCAL
FOR LOCAL,1,CHARANUM
	IF CFLAG:(LOCAL):同行
		LOCAL:1 ++
		SIF CFLAG:LOCAL:衰弱 || BASE:LOCAL:気力 == 0
			LOCAL:2++
		SIF !CHARA_HOLIDAY(LOCAL) && BASE:LOCAL:工作量
			LOCAL:3++
		SIF CFLAG:LOCAL:態度 < 1 && CAN_IRAI_CHARA_GO_TOGATHER(LOCAL) && !Add_CAN_IRAI_CHARA_GO_OUT_ALWAYS(LOCAL) ;魔改内容，新增可以带出据点的委托人
			LOCAL:4++
		;QOL,新增剧本侧判定，所有同行者均可自定义拒绝邀约
		TFLAG:192 = 0
		CALL KOJO_MESSAGE_SEND("SUCCESS",-1,LOCAL,-1,-1)
		SIF TFLAG:192 < 0
			LOCAL:5++
	ENDIF
NEXT

IF NEMUKE() >= 720
	TFLAG:193 = 1
ELSEIF LOCAL:1 > 2 && !MOREDATE
	TFLAG:193 = 2
ELSEIF LOCAL:2 || BASE:MASTER:気力 == 0
	TFLAG:193 = 3
ELSEIF LOCAL:3 && FESTIVAL() == ""
	TFLAG:193 = 4
ELSEIF LOCAL:4
	TFLAG:193 = 5
;(天候パッチ)悪天候時も外出可能に
;ELSEIF TIME:5 == 9 || TIME:5 == 5 || WIND_VELOCITY >= 2
;	DRAWLINE
;	PRINTL 因为天气恶劣所以无法外出
;	RETURN -1
;	TFLAG:193 = 6
ELSEIF TALENT:MASTER:100 < -4 
	TFLAG:193 = 7
ELSEIF LOCAL:5
	TFLAG:193 = 99
ENDIF
IF TFLAG:193
	FOR LOCAL, 1, GET_TARGETNUM() + 1
		IF CFLAG:(TARGET:LOCAL):同行
			CALL KOJO_MESSAGE_SEND("CUSTOM_EVENT",2,TARGET:LOCAL)
		ENDIF
	NEXT
RETURN -1
ENDIF
IF !CHK_ENTRANCE(CFLAG:MASTER:現在位置, MAIN_MAP)
	PRINTFORML 首先往%NAME_FROM_PLACE(ENTRANCE)%走去
	;-----------------------------
	;移动不可需要弹出
	IF MAP_CAN_MOVE(ENTRANCE) == 0
		CALL CANNOT_MOVE_MESSAGE(ENTRANCE)
	ELSE
		CALL SKIP_MOVE(CFLAG:MASTER:現在位置, ENTRANCE)
	ENDIF
	IF !CHK_ENTRANCE(CFLAG:MASTER:現在位置, MAIN_MAP) ;SKIP_MOVE中玩家选择停下了，无事发生
		TFLAG:193 = 8
		FOR LOCAL, 1, GET_TARGETNUM() + 1
			IF CFLAG:(TARGET:LOCAL):同行
				CALL KOJO_MESSAGE_SEND("CUSTOM_EVENT",2,TARGET:LOCAL)
			ENDIF
		NEXT
	ENDIF
	;qol，允许玩家在地图出入口触发移动时遭遇事件和选择中止外出
	LOCAL:10 = 0
	FOR LOCAL,1,CHARANUM
		IF CFLAG:LOCAL:現在位置 == ENTRANCE
			LOCAL:10 = LOCAL
			BREAK
		ENDIF
	NEXT
	IF LOCAL:10
		CALL KOJO_ACTIVE_INFO(LOCAL:10)
		PRINTFORML 经过%NAME_FROM_PLACE(ENTRANCE)%时遇上了%CALLNAME:(LOCAL:10)%。
		IF FLAG:停下脚步 == 2
			;立ち止まらない
		ELSEIF FLAG:停下脚步 == 1 && !RESULT
			;口上がある場合のみ
		ELSE
			CALL ASK_M("打声招呼后继续移动",!FLAG:70,"无视",1,"停下来",1)
			IF RESULT == 2
				CALL KOJO_MESSAGE_SEND("EVENT",4,LOCAL:10,1,0)
				TFLAG:193 = 8
				FOR LOCAL, 1, GET_TARGETNUM() + 1
					IF CFLAG:(TARGET:LOCAL):同行
						CALL KOJO_MESSAGE_SEND("CUSTOM_EVENT",2,TARGET:LOCAL)
					ENDIF
				NEXT
				RETURN -1
			ELSEIF RESULT == 1
				CALL KOJO_MESSAGE_SEND("EVENT",4,LOCAL:10,3,0)
			ELSE
				CALL KOJO_MESSAGE_SEND("EVENT",4,LOCAL:10,2,0)
			ENDIF
		ENDIF
	ENDIF
ENDIF
DRAWLINE
CALL OTHERREGIONS("外出")
LOCAL:2 = RESULT
LOCAL = 0
IF LOCAL:2 == MAIN_MAP || LOCAL:2 == 100
	FLAG:约会的对象 = 0
	SIF MOREDATE
		CALL CLEAN_GROUP_DATES
	TFLAG:193 = 8
	FOR LOCAL, 1, GET_TARGETNUM() + 1
		IF CFLAG:(TARGET:LOCAL):同行
			CALL KOJO_MESSAGE_SEND("CUSTOM_EVENT",2,TARGET:LOCAL)
		ENDIF
	NEXT
	RETURN -1
ENDIF
RETURN 0

@COM_ABLE405
SIF !TFLAG:100
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(405)
	RETURN RESULT
;携带中は不可
SIF TFLAG:運搬
	RETURN 0
SIF CFLAG:MASTER:陪睡中
	RETURN 0
SIF CFLAG:诶嘿嘿 == 2
	RETURN 0
SIF CFLAG:MASTER:恶作剧
	RETURN 0
;参道設定
;SIF !CHK_ENTRANCE(CFLAG:MASTER:現在位置, MAIN_MAP)
;	RETURN 0
SIF !AT_HOME(MASTER)
	RETURN 0
RETURN 1

;-------------------------------------------------
;触发事件-邀请外出失败
;成功时会进入剧本的对应指令文本函数
;-------------------------------------------------
@CUSTOM_EVENT_MESSAGE_2(ARG,ARG:1,ARG:2,ARG:3)
#LOCALSIZE 1
#LOCALSSIZE 1
SELECTCASE TFLAG:193
	CASE 0
		;外出成功，情景信息在@OTHERREGIONS里
	CASE 1
		PRINTL 在睡意来袭时还是别外出比较好吧
	CASE 2
		PRINTL 不能带三人以上外出约会
	CASE 3
		PRINTFORML \@ BASE:MASTER:気力 == 0 ? 因为身体不适所以无法外出 # 不要带身体不适的人外出比较好吧\@
	CASE 4
		PRINTL 因为同行者处于工作日所以无法外出
	CASE 5
		PRINTL 被说了有时间出门不如早点完成委托…
	CASE 6
		PRINTL 以这样的身体出远门是很危险的
	CASE 99
		PRINTFORML 邀约被拒绝了
ENDSELECT
