;@EVENTLOAD
;#LATER
;;should not be retained across saves
;BREWING_LIST_CHECKED = 0
;
;CALL BREWING_GUIDE_CHECK_EXISTING

@BREWING_GUIDE_CHECK_EXISTING
#DIMS DRINKS_TO_CHECK, OBJ_ID_LAST
#DIM CHECK_INDEX
#DIMS DRINK_NAME
#DIMS PREVSTAGESTR
#DIMS PREVSTAGES, 10
#DIMS MIXINGPARTS, 3

SIF BREWING_KNOWN_DRINKS_CHECKED
    RETURN

BREWING_KNOWN_DRINKS:1 = 1 ;plain water is the default

CALL MAKE_BOOZE_MAP
CALL BREWING_GUIDE_MAKE_PREVSTAGE_MAP
CALL BREWING_GUIDE_MAKE_MIXABLES_MAP

FOR LOCAL, 1, OBJ_ID_LAST
	IF GET_INT(0, "酒データ", LOCAL, "醸造数") > 0 || GET_INT(0, "酒データ", LOCAL, "酒気増加量") > 0
        LOCALS '= GET_STR(0, "酒データ", LOCAL, "名称")
        LOCAL:1 = GETNUM(ITEM, LOCALS)
        SIF LOCAL:1 < 0
            CONTINUE
        IF ITEM:(LOCAL:1)
            ;own the drink, count as known and also consumed if it's drinkable
            SIF GET_INT(0, "酒データ", LOCAL, "酒気増加量") > 0
                BREWING_CONSUMED_DRINKS:LOCAL = 1
            DRINKS_TO_CHECK:(CHECK_INDEX++) '= LOCALS ;check this and preceding drinks later
        ENDIF
	ENDIF
NEXT

LOCAL = 0
;simplified version of the main loop
WHILE LOCAL < CHECK_INDEX
    DRINK_NAME '= DRINKS_TO_CHECK:LOCAL
    LOCAL:1 = ALC_ID(DRINK_NAME)
    BREWING_KNOWN_DRINKS:(LOCAL:1) = 1

    ;check brewing stages
    PREVSTAGESTR '= MAP_GET(BREWING_BOOZE_PREVSTAGE_MAP, DRINK_NAME)
    SPLIT PREVSTAGESTR, "／", PREVSTAGES
    FOR LOCAL:2, 0, RESULT
        SIF PREVSTAGES:(LOCAL:2) != "" && FINDELEMENT(DRINKS_TO_CHECK, PREVSTAGES:(LOCAL:2),,,1) == -1
            DRINKS_TO_CHECK:(CHECK_INDEX++) '= PREVSTAGES:(LOCAL:2) ;add it to the loop
    NEXT

    ;check mixing stages
    PREVSTAGESTR '= MAP_GET(BREWING_BOOZE_MIXABLES_MAP, DRINK_NAME)
    SPLIT PREVSTAGESTR, "／", PREVSTAGES
    FOR LOCAL:2, 0, RESULT
        IF PREVSTAGES:(LOCAL:2) != ""
            SPLIT PREVSTAGES:(LOCAL:2), "：", MIXINGPARTS
            SIF MIXINGPARTS:0 != "" && FINDELEMENT(DRINKS_TO_CHECK, MIXINGPARTS:0,,,1) == -1
                DRINKS_TO_CHECK:(CHECK_INDEX++) '= MIXINGPARTS:0 ;add it to the loop
        ENDIF
    NEXT
    LOCAL++
WEND
BREWING_KNOWN_DRINKS_CHECKED = 1

@BREWING_GUIDE_MAKE_PREVSTAGE_MAP
#DIMS SOURCE_DRINK
#DIMS RESULT_DRINK
#DIMS EXISTING_VALUE

IF !MAP_EXIST(BREWING_BOOZE_PREVSTAGE_MAP)
	MAP_CREATE BREWING_BOOZE_PREVSTAGE_MAP
	FOR LOCAL, 1, OBJ_ID_LAST
		RESULT_DRINK '= GET_STR(0, "酒データ", LOCAL, "変化先")
        SOURCE_DRINK '= GET_STR(0, "酒データ", LOCAL, "名称")

        IF RESULT_DRINK != "" && RESULT_DRINK != "0"
            IF MAP_HAS(BREWING_BOOZE_PREVSTAGE_MAP, RESULT_DRINK)
                EXISTING_VALUE '= MAP_GET(BREWING_BOOZE_PREVSTAGE_MAP, RESULT_DRINK)
                SIF !CONTAINS_NAME(EXISTING_VALUE, SOURCE_DRINK)
                    MAP_SET BREWING_BOOZE_PREVSTAGE_MAP, RESULT_DRINK, EXISTING_VALUE + "／" + SOURCE_DRINK
            ELSE
                MAP_SET BREWING_BOOZE_PREVSTAGE_MAP, RESULT_DRINK, SOURCE_DRINK
            ENDIF
        ENDIF
	NEXT
ENDIF

@BREWING_GUIDE_MAKE_MIXABLES_MAP
#DIMS ADDITION_OPTIONS, 20
#DIMS ADDED_INGREDIENTS
#DIMS SOURCE_DRINK
#DIMS MAP_VALUE
#DIMS RESULT_DRINK
#DIMS EXISTING_VALUE

IF !MAP_EXIST(BREWING_BOOZE_MIXABLES_MAP)
	MAP_CREATE BREWING_BOOZE_MIXABLES_MAP
	FOR LOCAL, 1, OBJ_ID_LAST
		LOCALS '= GET_STR(0, "酒データ", LOCAL, "ADDITIONS")
        IF LOCALS != ""
            SOURCE_DRINK '= GET_STR(0, "酒データ", LOCAL, "名称")
            SPLIT LOCALS, "／", ADDITION_OPTIONS
            FOR LOCAL:1, 0, RESULT
                ADDED_INGREDIENTS '= ADDITION_OPTIONS:(LOCAL:1)
                SIF ADDED_INGREDIENTS == ""
                    CONTINUE

                MAP_VALUE = %SOURCE_DRINK%：%ADDED_INGREDIENTS%
                RESULT_DRINK '= GET_STR(0, "酒データ", LOCAL, @"ITEM：%ADDED_INGREDIENTS%")
                IF MAP_HAS(BREWING_BOOZE_MIXABLES_MAP, RESULT_DRINK)
                    EXISTING_VALUE '= MAP_GET(BREWING_BOOZE_MIXABLES_MAP, RESULT_DRINK)
                    SIF !CONTAINS_NAME(EXISTING_VALUE, MAP_VALUE)
                        MAP_SET BREWING_BOOZE_MIXABLES_MAP, RESULT_DRINK, EXISTING_VALUE + "／" + MAP_VALUE
                ELSE
                    MAP_SET BREWING_BOOZE_MIXABLES_MAP, RESULT_DRINK, MAP_VALUE
                ENDIF
            NEXT
        ENDIF
	NEXT
ENDIF


@BREWING_GUIDE
#DIM INDEX
#DIMS PATHS_WALKED, 10

SIF !TALENT:MASTER:酒造家
    RETURN

CALL BREWING_GUIDE_MAKE_PREVSTAGE_MAP
CALL BREWING_GUIDE_MAKE_MIXABLES_MAP

IF !BREWING_LIST_CHECKED
    INDEX = 0
    VARSET LOCALS
    FOR LOCAL, 1, OBJ_ID_LAST
        ;search for every final element (i.e. can't be brewed further)
        ;all basic sake is also always included
	    IF BREWING_GUIDE_IS_BASIC_SAKE(GET_STR(0, "酒データ", LOCAL, "名称")) || (!GET_INT(0, "酒データ", LOCAL, "醸造日数") && GET_INT(0, "酒データ", LOCAL, "醸造数") && GET_STR(0, "酒データ", LOCAL, "ADDITIONS") == "")
            CALL BREWING_GUIDE_WALK_ALL_PATHS(ALC_NAME(LOCAL), LOCALS)
            INDEX = RESULT
        ENDIF
    NEXT

    BREWING_LIST_CHECKED = 1
ENDIF

;brewing todo translation
PRINTFORMDL 酿造指南:
FOR LOCAL, 0, INDEX
    CALL AUTOMATIC_HTML_LINEBREAK(@"{LOCAL, 2, RIGHT}. %LOCALS:LOCAL%", MAXWIDTH() - 40, "`", "    ")
NEXT
;brewing todo translation
PRINTDL ※(？？？)尚未获得

@BREWING_GUIDE_WALK_ALL_PATHS(DESTINATION_DRINK, BREWING_PATHS)
#DIMS DESTINATION_DRINK
#DIMS REF BREWING_PATHS
#DIMS CURRENT_DRINK
#DIMS DYNAMIC BREW_CHAIN, 15
#DIMS DYNAMIC BREWING_OPTIONS, 15, 5
#DIM BREWING_OPTION_COUNTS, 15
#DIM DYNAMIC BREWING_OPTIONS_CHECKED, 15
#DIM DYNAMIC DEPTH
#DIMS PREVSTAGESTR
#DIMS PREVSTAGES, 5
#DIMS MIXINGSTR
#DIMS INGREDIENTS, 5
#DIMS BREWSTR, 3
#DIMS BREWSTR_OTHER, 3
#DIM ANY_WATER
#DIMS ANY_WATER_INGREDIENTS

CURRENT_DRINK '= DESTINATION_DRINK
BREW_CHAIN:0 = RESULT：%DESTINATION_DRINK%
DO
    ANY_WATER = 0
    IF BREWING_GUIDE_IS_BASIC_SAKE(CURRENT_DRINK) && DEPTH > 0 ;don't go all the way down when derived from finished products
    ELSEIF BREWING_OPTIONS_CHECKED:DEPTH == 0
        BREWING_OPTION_COUNTS:DEPTH = 0

        ;check brewing stages
        PREVSTAGESTR '= MAP_GET(BREWING_BOOZE_PREVSTAGE_MAP, CURRENT_DRINK)
        SPLIT PREVSTAGESTR, "／", PREVSTAGES
        FOR LOCAL, 0, RESULT
            SIF PREVSTAGES:LOCAL != ""
                BREWING_OPTIONS:DEPTH:(BREWING_OPTION_COUNTS:DEPTH++) = FERMENT：%PREVSTAGES:LOCAL%
        NEXT

        ;check mixing stages
        PREVSTAGESTR '= MAP_GET(BREWING_BOOZE_MIXABLES_MAP, CURRENT_DRINK)
        SPLIT PREVSTAGESTR, "／", PREVSTAGES

        ;special exception when all water is valid
        IF RESULT == 4 && BREWING_GUIDE_IS_ALL_WATERS(PREVSTAGES)
            ANY_WATER_INGREDIENTS '= SUBSTRING(PREVSTAGES:1, STRFIND(PREVSTAGES:1, "：")+1) 
            ANY_WATER = 1
        ELSE
            FOR LOCAL, 0, RESULT
                IF PREVSTAGES:LOCAL != ""
                    ;check for repeats
                    SPLIT PREVSTAGES:LOCAL, "：", BREWSTR
                    SIF BREWSTR:0 == CURRENT_DRINK
                        CONTINUE

                    BREWING_OPTIONS:DEPTH:(BREWING_OPTION_COUNTS:DEPTH++) = MIXING：%PREVSTAGES:LOCAL%

                ENDIF
            NEXT
        ENDIF
    ENDIF

    IF ANY_WATER
        ;brewing todo translation (任何净水)
        BREW_CHAIN:(DEPTH+1) = MIXING：任何净水：%ANY_WATER_INGREDIENTS%
    ELSEIF BREWING_OPTIONS_CHECKED:DEPTH < BREWING_OPTION_COUNTS:DEPTH
        ;increase depth
        LOCAL = BREWING_OPTIONS_CHECKED:DEPTH
        LOCALS '= BREWING_OPTIONS:DEPTH:LOCAL

        BREWING_OPTIONS_CHECKED:DEPTH++
        SPLIT LOCALS, "：", BREWSTR
        IF BREWSTR:0 == "FERMENT"
            CURRENT_DRINK '= BREWSTR:1
            BREW_CHAIN:(DEPTH+1) '= LOCALS
        ELSEIF BREWSTR:0 == "MIXING"
            CURRENT_DRINK '= BREWSTR:1
            BREW_CHAIN:(DEPTH+1) '= LOCALS
        ELSE
            THROW %BREWSTR:0% is not a valid option.
        ENDIF
        DEPTH++
        CONTINUE
    ENDIF

    ;no options, end of line
    IF BREWING_OPTION_COUNTS:DEPTH == 0
        LOCAL = FINDELEMENT(BREWING_PATHS, "",,,1)
        BREWING_PATHS:(LOCAL >= 0 ? LOCAL # VARSIZE("BREWING_PATHS") - 1) '= BREWING_GUIDE_WRITE_PATH(BREW_CHAIN)
    ENDIF

    IF DEPTH > 0
        ;decrease depth
        BREW_CHAIN:DEPTH =
        BREWING_OPTIONS_CHECKED:DEPTH = 0
        BREWING_OPTION_COUNTS:DEPTH = 0
        VARSETEX @"BREWING_OPTIONS:{DEPTH}:0", "", 0

        SPLIT BREW_CHAIN:(DEPTH-1), "：", BREWSTR
        CURRENT_DRINK '= BREWSTR:1
    ENDIF
    DEPTH--
LOOP DEPTH >= 0

LOCAL = FINDELEMENT(BREWING_PATHS, "",,,1)
RETURN LOCAL >= 0 ? LOCAL # VARSIZE("BREWING_PATHS") - 1

@BREWING_GUIDE_IS_BASIC_SAKE(DRINK_NAME)
#FUNCTION
#DIMS DRINK_NAME
RETURNF GROUPMATCH(DRINK_NAME, "清酒", "鬼杀酒", "神酒", "浊酒")

@BREWING_GUIDE_IS_WATER(DRINK_NAME)
#FUNCTION
#DIMS DRINK_NAME
RETURNF GROUPMATCH(DRINK_NAME, "普通的水", "雾之湖的水", "地狱温泉的水", "奇迹之水")

@BREWING_GUIDE_IS_ALL_WATERS(PREVSTAGES)
#FUNCTION
#DIMS REF PREVSTAGES
#DIMS CONST WATERS = "普通的水", "雾之湖的水", "地狱温泉的水", "奇迹之水"

FOR LOCAL, 0, 4
    SIF !STRING_STARTS_IN(PREVSTAGES:LOCAL, WATERS:LOCAL)
        RETURNF 0
NEXT
RETURNF 1

@BREWING_GUIDE_WRITE_PATH(BREW_CHAIN)
#FUNCTIONS
#DIMS REF BREW_CHAIN
#DIMS BREWPARTS, 3
#DIM DRINK_ID
#DIM IS_KNOWN
#DIM GRAYED_COLOR = 0x606060
LOCALS =
FOR LOCAL, VARSIZE("BREW_CHAIN") - 1, -1, -1
    SIF BREW_CHAIN:LOCAL == ""
        CONTINUE

    SPLIT BREW_CHAIN:LOCAL, "：", BREWPARTS

    ;brewing todo translation
    IF BREWPARTS:1 == "任何净水"
        DRINK_ID = 1
    ELSE
        DRINK_ID = ALC_ID(BREWPARTS:1)
    ENDIF
    IS_KNOWN = BREWING_KNOWN_DRINKS:DRINK_ID

    SELECTCASE BREWPARTS:0
        CASE "RESULT"
            LOCALS:1 = \@ IS_KNOWN ? %BREWING_GUIDE_COLOR_DRINK(BREWPARTS:1)% # %HTML_TEXT("？？？", GRAYED_COLOR)% \@
        CASE "FERMENT"
            IF IS_KNOWN
                ;brewing todo translation (days)
                LOCALS:1 = %BREWING_GUIDE_COLOR_DRINK(BREWPARTS:1)% (%HTML_TEXT(@"{BREWING_GUIDE_FERMENT_TIME(BREWPARTS:1)}天", C_TEAL)%)-->%" "%
            ELSE
                LOCALS:1 = %HTML_TEXT("？？？", GRAYED_COLOR)% (%HTML_TEXT("？？？", GRAYED_COLOR)%)-->%" "%
            ENDIF
        CASE "MIXING"
            IF IS_KNOWN
                LOCALS:1 = %BREWING_GUIDE_COLOR_DRINK(BREWPARTS:1)% (%HTML_TEXT(@"＋%BREWING_GUIDE_MIXING_INGREDIENTS(DRINK_ID, BREWPARTS:2)%", C_P_YELLOW)%)-->%" "%
            ELSE
                LOCALS:1 = %HTML_TEXT("？？？", GRAYED_COLOR)% (%HTML_TEXT("？？？", GRAYED_COLOR)%)-->%" "%
            ENDIF
    ENDSELECT
    LOCALS += @"<nonbutton>%LOCALS:1%</nonbutton>"
NEXT

RETURNF LOCALS

@BREWING_GUIDE_COLOR_DRINK(DRINK)
#FUNCTIONS
#DIMS DRINK
#DIM COLOR
#DIM DRINKABLE
DRINKABLE = BREWING_GUIDE_CAN_DRINK(DRINK)
;brewing todo translation (任何净水)
IF VAR_FERMENT == DRINK || (BREWING_GUIDE_IS_WATER(VAR_FERMENT) && DRINK == "任何净水")
    COLOR = C_GREEN
ELSEIF !BREWING_GUIDE_CAN_RETRIEVE(DRINK)
    COLOR = C_WHITE
ELSE
    COLOR = 0x339944
ENDIF
;add asterisk if not yet consumed
LOCALS =
LOCAL = ALC_ID(DRINK)
SIF LOCAL > 0 && BREWING_GUIDE_CAN_DRINK(DRINK) && !BREWING_CONSUMED_DRINKS:LOCAL
    LOCALS = *

RETURNF HTML_TEXT(HTML_ESCAPE(@"%DRINK%%LOCALS%"), COLOR, DRINKABLE)

@BREWING_GUIDE_CAN_DRINK(DRINK)
#FUNCTION
#DIMS DRINK
RETURNF GET_INT(0, "酒データ", ALC_ID(DRINK), "酒気増加量")

@BREWING_GUIDE_CAN_RETRIEVE(DRINK)
#FUNCTION
#DIMS DRINK
RETURNF GET_INT(0, "酒データ", ALC_ID(DRINK), "醸造数")

@BREWING_GUIDE_FERMENT_TIME(DRINK)
#FUNCTION
#DIMS DRINK
LOCAL = ALC_ID(DRINK)
LOCAL:1 = GET_INT(0, "酒データ", LOCAL, "醸造日数")

SIF TALENT:MASTER:酒造家 >= 2 && LOCAL:1 > 2
	LOCAL:1--
    
RETURNF LOCAL:1

@BREWING_GUIDE_MIXING_INGREDIENTS(PREV_DRINK, INGREDIENTSSTR)
#FUNCTIONS
#DIM PREV_DRINK
#DIMS INGREDIENTSSTR
#DIMS DYNAMIC PREVSTAGE_AND_INGREDIENTS, 2
#DIMS DYNAMIC INGREDIENTS_LIST, 2
#DIMS DYNAMIC INGREDIENT
#DIMS DYNAMIC INGREDIENTS_STRING

SPLIT INGREDIENTSSTR, "＋", INGREDIENTS_LIST
FOR LOCAL:1, 0, RESULT
    INGREDIENT '= INGREDIENTS_LIST:(LOCAL:1)
    SIF INGREDIENTS_STRING != ""
        INGREDIENTS_STRING += HTML_ESCAPE(" ＆ ")
    INGREDIENTS_STRING += @"{GET_INT(0, "酒データ", PREV_DRINK, @"QUANTITY：%INGREDIENT%")} %ITEMNAME:GETNUM(ITEM, INGREDIENT)%"
NEXT

RETURNF INGREDIENTS_STRING

@BREWING_GUIDE_MAKE_KNOWN(JAR_CONTENTS)
#DIMS JAR_CONTENTS
LOCAL = ALC_ID(JAR_CONTENTS)
IF LOCAL > 0
    IF !BREWING_KNOWN_DRINKS:LOCAL
        BREWING_KNOWN_DRINKS:LOCAL = 1
    ENDIF
    BREWING_LIST_CHECKED = 0
ENDIF

@BREWING_GUIDE_CONSUME(DRINK_NAME)
#DIMS DRINK_NAME
LOCAL = ALC_ID(DRINK_NAME)
SIF LOCAL > 0
    BREWING_CONSUMED_DRINKS:LOCAL = 1

@BREWING_GUIDE_MIX_MENU
#DIMS ADDITIONS
#DIM DRINK_ID
#DIMS DYNAMIC OPTIONS, 15
#DIM DYNAMIC OPTION_VALID, 15
#DIM OPTION_COUNT
#DIMS DYNAMIC INGREDIENTS, 10
#DIMS INGREDIENT
#DIM INGREDIENT_REQ
#DIM INGREDIENT_ID
#DIM SELECTED_OPTION
#DIMS EXTRA_ITEM
#DIM EXTRA_ITEM_QUANTITY
#DIMS SPEED_INCREASE
PRINTBUTTON @"-1[什么也不做]", -1
PRINTL

DRINK_ID = ALC_ID(VAR_FERMENT)
ADDITIONS '= GET_STR(0, "酒データ", DRINK_ID, @"ADDITIONS")
SPLIT ADDITIONS, "／", OPTIONS
OPTION_COUNT = RESULT
SIF ADDITIONS == ""
    GOTO SELECTOPTION
FOR LOCAL, 0, OPTION_COUNT
    LOCALS =

    OPTION_VALID:LOCAL = 1
    ;check if allowed at all
    SIF GROUPMATCH(VAR_FERMENT, "普通的水", "雾之湖的水", "地狱温泉的水", "奇迹之水") && !CAN_FERMENT()
        OPTION_VALID:LOCAL = 0

    SPLIT OPTIONS:LOCAL, "＋", INGREDIENTS
    FOR LOCAL:1, 0, RESULT
        INGREDIENT '= INGREDIENTS:(LOCAL:1)
        INGREDIENT_REQ = GET_INT(0, "酒データ", DRINK_ID, @"QUANTITY：%INGREDIENT%")
        SIF LOCALS != ""
            LOCALS += "+"
        INGREDIENT_ID = GETNUM(ITEM, INGREDIENT)
        LOCALS += @"%ITEMNAME:INGREDIENT_ID%%COND_STR(@"＊{INGREDIENT_REQ}", INGREDIENT_REQ > 1)%"

        SPEED_INCREASE '= GET_STR(0, "酒データ", DRINK_ID, @"SPEED：%INGREDIENT%")
        SIF SPEED_INCREASE != ""
            ;brewing todo translation
            LOCALS += @"（缩短%SPEED_INCREASE%天时间）"

        SIF ITEM:(INGREDIENT_ID) < INGREDIENT_REQ
            OPTION_VALID:LOCAL = 0
    NEXT

    IF OPTION_VALID:LOCAL
        RESETCOLOR
        PRINTBUTTON @"{LOCAL, 2, RIGHT}[%LOCALS%]", LOCAL
    ELSE
        SETCOLOR 0x606060
        PRINTPLAINFORM {LOCAL, 2, RIGHT}[%LOCALS%]
    ENDIF
    PRINTL
NEXT
RESETCOLOR

$SELECTOPTION
INPUT
SELECTED_OPTION = RESULT
IF SELECTED_OPTION == -1
    RETURN -1
ELSEIF INRANGE(SELECTED_OPTION, 0, OPTION_COUNT)
    IF OPTION_VALID:SELECTED_OPTION
        SPLIT OPTIONS:SELECTED_OPTION, "＋", INGREDIENTS
        FOR LOCAL:1, 0, RESULT
            INGREDIENT '= INGREDIENTS:(LOCAL:1)
            INGREDIENT_REQ = GET_INT(0, "酒データ", DRINK_ID, @"QUANTITY：%INGREDIENT%")
            CALL LOST_ITEM(INGREDIENT, INGREDIENT_REQ, 1)
        NEXT

        EXTRA_ITEM '= GET_STR(0, "酒データ", DRINK_ID, @"EXTRA：%OPTIONS:SELECTED_OPTION%")
        EXTRA_ITEM_QUANTITY = GET_INT(0, "酒データ", DRINK_ID, @"EXTRAQUANTITY：%OPTIONS:SELECTED_OPTION%")

        SIF EXTRA_ITEM != ""
            CALL GET_ITEM(EXTRA_ITEM, EXTRA_ITEM_QUANTITY, 1)

        SPEED_INCREASE '= GET_STR(0, "酒データ", DRINK_ID, @"SPEED：%OPTIONS:SELECTED_OPTION%")

        VAR_FERMENT '= GET_STR(0, "酒データ", DRINK_ID, @"ITEM：%OPTIONS:SELECTED_OPTION%")

        SIF SPEED_INCREASE != "" && ISNUMERIC(SPEED_INCREASE)
            DAY:10 += TOINT(SPEED_INCREASE)

		CALL BREWING_GUIDE_MAKE_KNOWN(VAR_FERMENT)
    ELSE
        IF GROUPMATCH(VAR_FERMENT, "普通的水", "雾之湖的水", "地狱温泉的水", "奇迹之水") && !CAN_FERMENT()
            ;brewing todo translation
            REUSELASTLINE 发酵过头了
        ELSE
            ;brewing todo translation
            REUSELASTLINE 材料不足
        ENDIF
        GOTO SELECTOPTION
    ENDIF
ELSE
    ;brewing todo translation
    REUSELASTLINE 无效输入
    GOTO SELECTOPTION
ENDIF
