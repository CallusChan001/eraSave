;伐採
@COM442
#DIM CHARA
#DIM 木材種
#DIM W_ID
#DIM 伐採パワー
#DIM 減少値
#DIM 伐採時間
#DIM 採集パワー
#DIM 採集物
#DIM 探索回数
#DIM nLuck ;logging tweak
nLuck = CFLAG:MASTER:今日の運勢 / 20 - 25
SIF ITEM:チェーンソー && GETBIT(nChainSawMods, ChainSaw_TreeSeeker) ;addition custom code
	nLuck += 20

;原木を抱えてるときは薪割りするか選択　薪割りしたら体力気力消費時間経過させる
IF ITEM:原木
	;依頼の報酬など、伐採以外で原木を入手してしまった場合の退避用
	SIF !FLAG:原木種類
		;とりあえずスギにしておく
		FLAG:原木種類 = 1
	DRAWLINE
	PRINTFORML 必须先处理掉现在手头上的原木
	CALL WOOD_CHOPPING(FLAG:原木種類)
	RETURN RESULT
ENDIF
;青竹を抱えすぎてる時もリターン
IF ITEM:青竹 > 9 && FLAG:最後に材木商に訪れた日 != 0
	DRAWLINE
	PRINTFORML 得先处理一下持有的竹子……
	RETURN -1
ENDIF
;夕方～夜の伐採はやめる
IF TIME:2 >= 5 && !FLAG:70 && !(ITEM:チェーンソー && GETBIT(nChainSawMods, ChainSaw_Flashlight))
	DRAWLINE
	PRINTFORML 这个时间伐木的话太黑了…
	RETURN -1
ENDIF
;大雨、吹雪のときはやめる(天候パッチ)強風時もやめる
IF GROUPMATCH(天候, 5, 9) || GROUPMATCH(FLAG:異常気象, 9, 10) || WIND_VELOCITY >= 1
	DRAWLINE
	PRINTFORML 这个天气下采伐太危险了
	RETURN -1
ENDIF
;あからさまな人の敷地での盗伐はやめる
IF FLAG:催眠強度 <= 50 ;hypnosis patch
	SELECTCASE CFLAG:MASTER:現在位置
		CASE 25, 50
			IF ABL:[[霊夢]]:親密 < 5
				DRAWLINE
				PRINTFORML 在这里擅自伐木的话总觉得不太妙
				RETURN -1
			ENDIF
		CASE 斜角的竹林, 433
			IF ABL:[[妹紅]]:親密 < 5 && ABL:[[帝]]:親密 < 5 && ABL:[[輝夜]]:親密 < 5 && ABL:[[永琳]]:親密 < 5
				DRAWLINE
				PRINTFORML 在这里擅自伐木的话总觉得不太妙
				RETURN -1
			ENDIF
		CASE 絶景之丘, 805
			IF ABL:[[文]]:親密 < 5 && ABL:[[果]]:親密 < 5 && ABL:[[龍]]:親密 < 5
				DRAWLINE
				PRINTFORML 在这里擅自伐木的话总觉得不太妙
				RETURN -1
			ENDIF
	ENDSELECT
ENDIF

TFLAG:情緒上昇抑制 = 1

;先に伐採パワー算出
伐採パワー = 1 + (TALENT:MASTER:伐採Lv * 2) + ABL:MASTER:教養
;参加者判定
IF FLAG:约会的对象 && (!FLAG:70 || CFLAG:TARGET:344 == 1)
	CHARA = FLAG:约会的对象
ELSEIF TARGET && ABL:TARGET:親密 >= 10 && (!FLAG:70 || CFLAG:TARGET:344 == 1)
	CHARA = TARGET
ELSE
	CHARA = 0
ENDIF
;参加者がいる場合は伐採パワー追加
SIF CHARA && (!FLAG:70 || CFLAG:CHARA:344 == 1)
	伐採パワー += (TALENT:CHARA:伐採Lv * 2) + ABL:CHARA:教養

;伐採パワー += Add_Apply_Fairy_Title_Bonus("logging") ;addition custom code

探索回数 = 1 ;moved search counter to always count first tree

$LOOP_SELECT

木材種 = 0
W_ID = 0
減少値 = 0
伐採時間 = 0

REPEAT 5
	REUSELASTLINE  \@ CHARA ? 和%CALLNAME:CHARA%一起 #  \@捜索可以采伐的树木.%"." * COUNT%
	TWAIT 40, 1
REND

;伐採候補の木材種を決定
CALL WOOD_SPECIES(伐採可能(CFLAG:MASTER:現在位置), nLuck) ;logging tweak
;種類判定用に退避する
W_ID = RESULT
;★木材関連のデータを読み出し
CALL WoodData(W_ID)
;大きさを抽選 FLAG:原木種類に流し込む数値を作る
木材種 = WOOD_SIZE(W_ID, nLuck)

;伐採候補をテキスト出力
IF W_ID == 13
	PRINTFORML 折断一根小枝、是有着芳香的树木…
ELSEIF W_ID == 12
	PRINTFORML 这棵竹子又直又匀称…
ELSE
	PRINTFORML 这棵树的话怎么样呢…
ENDIF
DRAWLINE
SETCOLOR C_YELLOW
PRINTFORM 发现了
;logging tweak
PRINTFORM %WOOD_NAME(木材種)%\@ FLAG_TO_LOGSIZE(木材種) > 1 ? !!! # \@
RESETCOLOR

;体力気力の減少値と伐採にかかる時間を算出
;PRINTFORML WOOD_EFFORT(木材種, 大きさ) - {WOOD_EFFORT(木材種, 大きさ)}
;減少値 = WOOD_EFFORT(木材種, 大きさ) - (WOOD_EFFORT(木材種, 大きさ) * (伐採パワー / 70))
減少値 = MULTIPLY(WOOD_EFFORT(木材種), 100-(伐採パワー/3*2)) ;logging tweak
;PRINTFORML 減少値 = {減少値}
SIF ITEM:チェーンソー
	減少値 -= SQRT(MAX(WOOD_EFFORT(木材種), 1)) * (GETBIT(nChainSawMods, ChainSaw_MiniFanDispenser) ? 15 # 10) ;addition custom code
;PRINTFORML Ch: 減少値 = {減少値}
減少値 = MAX(減少値, 20) ;logging tweak
IF CHARA && !FLAG:70
	;bug fix
	伐採時間 = 減少値 / (4 + TALENT:MASTER:伐採Lv + SQRT(TALENT:CHARA:伐採Lv))
ELSE
	伐採時間 = 減少値 / (4 + TALENT:MASTER:伐採Lv)
ENDIF
SIF ITEM:チェーンソー && GETBIT(nChainSawMods, ChainSaw_DualSaw) ;addition custom code, 25% reduction
	伐採時間 = MULTIPLY(伐採時間, 75)

;伐採時間に応じてテキスト出力
SETCOLOR C_L_GRAY
SELECTCASE 伐採時間
	CASE IS <= 60 
		PRINTFORML 　（应该比较简单就可以采伐）
	CASE IS <= 120
		PRINTFORML 　（采伐的话会稍微费点功夫吧）
	CASE IS <= 180
		PRINTFORML 　（有点棘手啊…）
	CASE IS <= 240
		PRINTFORML 　（这个的话就比较辛苦了…）
	CASE IS <= 300
		PRINTFORML 　（这是得站稳了腰慢慢干的等级…）
	CASE IS <= 360
		PRINTFORML 　（会变成一个很大的工程…）
	CASEELSE
		PRINTFORML 　（以现在的水平的话还是太困难了…）
ENDSELECT
RESETCOLOR
PRINTL 
;体力・気力が実施後マイナスになる場合は、木材種選択に戻る
;logging tweak
{
	IF 
	(!FLAG:70 && 
		(BASE:MASTER:体力 <= 減少値 ||
		 BASE:MASTER:気力 <= 減少値 - (ITEM:チェーンソー ? (GETBIT(nChainSawMods, ChainSaw_MiniFanDispenser) ? 75 # 50) # 25) )) ||
	(FLAG:70 && BASE:MASTER:TSP <= (伐採時間 * 10) + (探索回数 * 5))
}
	CALL WOOD_V_RESET
	;探索回数が10回を超えた場合は無限ループ除けのためにリターンする
	IF 探索回数 > 10
		$GIVEUP
		PRINTFORMW ……你失去了兴致，决定暂时停下。
		IF !FLAG:70
			TIME += 探索回数 * 5
		ELSE
			BASE:MASTER:TSP -= 探索回数 * 5
		ENDIF
		RETURN 1 ;tweaked to show time spent
	ELSE
		PRINTFORML 考虑到现在的状态，采伐这棵树会是个坏主意……换一棵吧。
		;moved search counter
		CALL ASK_YN("继续", "停止") ;logging tweak
		SIF RESULT
			GOTO GIVEUP
		PRINTL 
		CALL WOOD_V_RESET
		GOTO LOOP_SELECT
	ENDIF
ENDIF
PRINTFORML 要采伐这个吗？
CALL ASK_M("好", 1, "不", 1, "停下", 1)
IF !RESULT
	CLEARLINE 5
ELSEIF RESULT == 1
	CLEARLINE 9
	探索回数 ++
	CALL WOOD_V_RESET
	;探索回数が10回を超えた場合は無限ループ除けのためにリターンする
	IF 探索回数 > 10
		PRINTFORMW ……你感觉如果太投入的话可能会迷路，所以你决定暂时就到此为止。
		IF !FLAG:70
			TIME += 探索回数 * 5
		ELSE
			BASE:MASTER:TSP -= 探索回数 * 5
		ENDIF
		RETURN 1 ;tweaked to show time spent
	ENDIF
	GOTO LOOP_SELECT
ELSE
	GOTO GIVEUP
ENDIF
CALL DQPRINT("你砍着树ー", 1)
CALL DQPRINT("嘿～嘿～吼～", 1)
CALL DQPRINT("嘿～嘿～吼～", 1)
SIF 伐採時間 >= 540
	CALL DQPRINT("………………………", 1)
SIF 伐採時間 >= 480
	CALL DQPRINT("……………………", 1)
SIF 伐採時間 >= 420
	CALL DQPRINT("…………………", 1)
SIF 伐採時間 >= 360
	CALL DQPRINT("………………", 1)
SIF 伐採時間 >= 300
	CALL DQPRINT("……………", 1)
SIF 伐採時間 >= 240
	CALL DQPRINT("…………", 1)
CALL DQPRINT("………", 1)
CALL DQPRINT("……", 1)

;時間経過と体力消費
IF FLAG:70
	BASE:MASTER:TSP -= (伐採時間 * 10) + (探索回数 * 5)
ELSE
	DOWNBASE:MASTER:体力 = 減少値
	DOWNBASE:MASTER:気力 = 減少値
	IF ITEM:チェーンソー && GETBIT(nChainSawMods, ChainSaw_MiniFanDispenser)
		PRINTFORML 你的饮水机帮助你更快恢复了！
		CALL RECOVER_PERMIL(MASTER, 伐採時間, "体力", 1, "清爽的水") ;water recovery tweak
		CALL RECOVER_PERMIL(MASTER, 伐採時間, "気力", 1, "清爽的水")
	ENDIF
	TIME += 伐採時間 + (探索回数 * 10)
	;logging tweak
	IF PERCENT(nLuck)
		LOCAL:0 = RAND:5 + RAND:5 + RAND:3
		LOCAL:1 = RAND:5 + RAND:5 + RAND:3
		MAXBASE:MASTER:体力 += LOCAL:0
		MAXBASE:MASTER:気力 += LOCAL:1
		SIF LOCAL:0 + LOCAL:1
			PRINTFORMW 砍树锻炼后，你感觉自己变得更强壮了一些……
	ENDIF
ENDIF

;経験追加処理
EXP:MASTER:伐採経験 += MAX((伐採時間 / 20), 1)
;試行回数が少ないだろうから確定
SIF ITEM:木こりの心得
	EXP:MASTER:伐採経験 += MAX((伐採時間 / 20), 1)
;大きさ補正
SIF 木材種 > 100
	EXP:MASTER:伐採経験 += FLAG_TO_LOGSIZE(木材種) * 4
;手伝ってくれたキャラの経験
IF CHARA
	EXP:CHARA:伐採経験 += MAX((伐採時間 / 20), 1)
	SIF 木材種 > 100
		EXP:CHARA:伐採経験 += FLAG_TO_LOGSIZE(木材種) * 4
ENDIF

;約會経験
IF CHK_DATENOW(CFLAG:CHARA:约会中) && CHARA > 0
	EXP:MASTER:約會経験 ++
	EXP:CHARA:約會経験 ++
ENDIF
SIF ITEM:チェーンソー
	PRINTFORML 你的电锯发出尖锐的嗡嗡声，你砍伐着\@ W_ID == 12 ? 竹子 # 树木 \@……
PRINTFORMDL 时间停止能力的应用、瞬 时 干 燥！
;アイテム入手処理
SELECTCASE 木材種
	CASE 12, 13
		CALL GET_ITEM(ITEMNAME:WoodItem, 1, 2)
	CASEELSE
		ITEM:原木 ++
		FLAG:原木種類 = 木材種
		SETCOLOR 0x00FF00
		PRINTFORMW 获得了%WOOD_NAME(FLAG:原木種類)%
		RESETCOLOR
ENDSELECT
DRAWLINE
PRINTFORML 　%WoodIntro1%
PRINTFORML %WoodIntro2%
PRINTW 

;デバッグ用
IF FLAG:原木種類
	DEBUGPRINTFORML 　　确认：%WOOD_NAME(FLAG:原木種類)%
ENDIF
PRINTL 

;薪割り確認 薪割り時間・体力消費はサービス
IF FLAG:原木種類
	CALL WOOD_CHOPPING(FLAG:原木種類, 1)
ENDIF

;オマケ採集物  採集Lvに依存  あくまでおまけなので控えめに
採集パワー = 1 + TALENT:MASTER:採集Lv * 10 + ABL:MASTER:教養 * 5 + TFLAG:幸運補正 * 5 + nLuck ;logging tweak
SIF CHARA && (!FLAG:70 || CFLAG:TARGET:344 == 1)
	採集パワー += (1 + TALENT:CHARA:採集Lv * 10 + ABL:CHARA:教養 * 5) / 2
採集パワー = MAX(0,採集パワー)

CALL WOODMAN_PRESENT(採集パワー)
採集物 = RESULT
IF 採集物
	CALL GET_ITEM(ITEMNAME:採集物, 1, 2)
	EXP:MASTER:採集経験 ++
	SIF CHARA 
		EXP:CHARA:採集経験 ++
ENDIF

;空腹時刻と参加者の疲労度
SIF !FLAG:70 ;bugfix
	TCVAR:MASTER:空腹時刻 -= 25
IF CHARA
	TCVAR:CHARA:空腹時刻 -= 25
	CALL ADD_TIRED(CHARA, POWER((FLAG_TO_LOGSIZE(木材種) + 1) + 1, 3) * 100)
ENDIF

;参加者のソース
IF CHARA > 0
	;固定で獲得するソース
	SOURCE:CHARA:歓楽 = 500
	SOURCE:CHARA:情愛 = 50
	
	TFLAG:信頼度管理 += 3
	
	;ABL:従順をみる
	IF ABL:従順 <= 1
		SOURCE:CHARA:歓楽 += (ABL:従順 * 40)
		SOURCE:CHARA:情愛 += (ABL:親密 * 20)
	ELSEIF ABL:従順 <= 3
		SOURCE:CHARA:歓楽 += 300 + (ABL:従順 * 40)
		SOURCE:CHARA:情愛 += 100 + (ABL:親密 * 30)
	ELSEIF ABL:従順 <= 5
		SOURCE:CHARA:歓楽 += 600 + (ABL:従順 * 40)
		SOURCE:CHARA:情愛 += 300 + (ABL:親密 * 30)
	ELSEIF ABL:従順 <= 8
		SOURCE:CHARA:歓楽 += 850 + (ABL:従順 * 60)
		SOURCE:CHARA:情愛 += 500 + (ABL:親密 * 40)
	ELSEIF ABL:従順 <= 11
		SOURCE:CHARA:歓楽 += 1200 + (ABL:従順 * 60)
		SOURCE:CHARA:情愛 += 700 + (ABL:親密 * 40)
	ELSE
		SOURCE:CHARA:歓楽 += 2500 + (ABL:従順 * 30)
		SOURCE:CHARA:情愛 += 1200 + (ABL:親密 * 20)
	ENDIF
	SIF CHARA != FLAG:デート相手 
		SOURCE:CHARA:情愛 = 0
	
	;好感度はみない
	
	SOURCE:CHARA:受動 = 200 + 100 * ABL:従順
	SOURCE:CHARA:征服 = 200 + 100 * ABL:サドっ気
	SOURCE:CHARA:達成 = 150 * (FLAG_TO_LOGSIZE(木材種) + 1)

	SELECTCASE FLAG_TO_LOGSIZE(木材種)
		CASE 0
			TIMES SOURCE:CHARA:歓楽 , 1.00
			TIMES SOURCE:CHARA:受動 , 1.00
			TIMES SOURCE:CHARA:征服 , 1.00
			SOURCE:CHARA:達成 = 120
		CASE 1
			TIMES SOURCE:CHARA:歓楽 , 2.00
			TIMES SOURCE:CHARA:受動 , 2.00
			TIMES SOURCE:CHARA:征服 , 1.75
			SOURCE:CHARA:達成 = 240
		CASE 2
			TIMES SOURCE:CHARA:歓楽 , 3.00
			TIMES SOURCE:CHARA:受動 , 3.00
			TIMES SOURCE:CHARA:征服 , 2.50
			SOURCE:CHARA:達成 = 360
		;logging tweak
		CASE 3
			TIMES SOURCE:CHARA:歓楽 , 4.00
			TIMES SOURCE:CHARA:受動 , 4.00
			TIMES SOURCE:CHARA:征服 , 3.25
			SOURCE:CHARA:達成 = 480
		CASE 4
			TIMES SOURCE:CHARA:歓楽 , 5.00
			TIMES SOURCE:CHARA:受動 , 5.00
			TIMES SOURCE:CHARA:征服 , 4.00
			SOURCE:CHARA:達成 = 600
	ENDSELECT
ENDIF
;樹木関連の変数をリセット
CALL WOOD_V_RESET

RETURN 1
;-------------------------------------------------
;実行可能判定
;-------------------------------------------------
;伐採する
@COM_ABLE442
SIF !TFLAG:100
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(442)
	RETURN RESULT
SIF CFLAG:诶嘿嘿
	RETURN 0
;伐採可能場所でのみ
SIF !伐採可能(CFLAG:MASTER:現在位置)
	RETURN 0
;约会中は约会的对象とだけ
SIF FLAG:约会的对象 && TARGET != FLAG:约会的对象
	RETURN 0
;睡眠中
SIF CFLAG:MASTER:睡眠 || CFLAG:MASTER:イタズラ ;custom code
	RETURN 0
;体力・気力不足
SIF BASE:MASTER:気力 < 800 || BASE:MASTER:体力 < 800
	RETURN 0
;TSP不足
SIF FLAG:70 && BASE:MASTER:TSP < 700
	RETURN 0
RETURN 1

;-----------------------------------------------------------
;現在位置から伐採スポットを割り出す関数
;-----------------------------------------------------------
@伐採可能(ARG)
#FUNCTION
IF !IN_HOME(ARG)
	SELECTCASE ARG
		CASE 妖精的大樹
			RETURNF 1
		CASE 斜角的竹林
			RETURNF 2
		CASE 森林的入口
			RETURNF 3
		CASE 山麓樹海
			RETURNF 4
		CASE 絶景之丘
			RETURNF 5
	ENDSELECT
ELSE
	SELECTCASE ARG
		CASE 25;鎮守之森
			RETURNF 1
		CASE 433;斜角的竹林
			RETURNF 2
		CASE 507 ;菌菇群生地
			RETURNF 3
		CASE 738
			RETURNF 4
		CASE 805;山頂
			RETURNF 5
	ENDSELECT
ENDIF
RETURNF 0

;-----------------------------------------------------------
;伐採スポットから伐れる木材種を割り出す関数
;-----------------------------------------------------------
@WOOD_SPECIES(伐採可能, nLuck) ;logging tweak
#DIM 伐採可能
#DIM W_ID
#DIM nLuck

$LOOP

W_ID = 1 + RAND:20 ;logging tweak, lower the rand size since there aren't many trees right now (was RAND:49)
SIF PERCENT(nLuck + TFLAG:幸運補正) ;logging tweak
	W_ID = TOINT(SPLIT_G("2:6:7:7:8:9:13:13"))
;変数を読み込み
CALL WoodData(W_ID)

;無名（未登録）ならリターン
SIF WoodName == ""
	GOTO LOOP
SIF WoodValue_Log == 0
	GOTO LOOP
;[希少]は１／２で再抽選に
SIF STRCOUNT(WoodTribe, "希少/") && !PERCENT(nLuck + TFLAG:幸運補正) ;logging tweak
	GOTO LOOP
SELECTCASE 伐採可能
	CASE 1;鎮守の森、妖精の大樹
		SIF STRCOUNT(WoodTribe, "博/")
			RETURN W_ID
	CASE 2;斜角の竹林
		SIF STRCOUNT(WoodTribe, "竹/")
			RETURN W_ID
	CASE 3;魔法の森、キノコ群生地
		SIF STRCOUNT(WoodTribe, "森/")
			RETURN W_ID
	CASE 4;森の深奥
		SIF STRCOUNT(WoodTribe, "深/")
			RETURN W_ID
	CASE 5;絶景の丘、山頂
		SIF STRCOUNT(WoodTribe, "山/")
			RETURN W_ID
	CASEELSE
		;エラーによる無限ループ除け
		RETURN W_ID
ENDSELECT
;樹木関連の変数をリセット
CALL WOOD_V_RESET

GOTO LOOP

;-----------------------------------------------------------
;木材種のサイズを抽選する
;-----------------------------------------------------------
@WOOD_SIZE(W_ID, nLuck) ;logging tweak
#FUNCTION
#DIM W_ID
#DIM SIZE
#DIM nLuck

SIZE = W_ID

SIF STRCOUNT(WoodTribe, "サイズ無視/")
	RETURNF W_ID
;logging tweak
IF PERCENT(20 + nLuck)
	SIZE += 100
	SIF PERCENT(33 + nLuck)
		SIZE += 100
	SIF PERCENT(5 + nLuck)
		SIZE += 100
	SIF PERCENT(1 + nLuck)
		SIZE += 100
ENDIF
RETURNF SIZE

;-----------------------------------------------------------
;原木・青竹の名称を装飾名も含めて返す関数
;FLAG_L …FLAG:原木種類　（木材種番号でも通る）
;TEXT =1 「の大木・の巨木」を文字列に反映しない
;      省略した場合は =0 (巨木・大木表示あり)
;RETURNF 1～ 普通の木 100～ 〇〇の大木 200～ 〇〇の巨木
;-----------------------------------------------------------
@WOOD_NAME(FLAG_L, TEXT = 0)
#FUNCTIONS
#DIM FLAG_L
#DIM TEXT
#DIM 番号
#DIMS 名前

SELECTCASE FLAG_L
	CASE 101 TO 199
		番号 = FLAG_L - 100
	CASE 201 TO 299
		番号 = FLAG_L - 200
	;logging tweak
	CASE 301 TO 399
		番号 = FLAG_L - 300
	CASE 401 TO 499
		番号 = FLAG_L - 400
	CASEELSE
		番号 = FLAG_L
ENDSELECT


SELECTCASE 番号
	CASE 1 TO 11
		名前 = %WoodName%
	CASEELSE
		名前 = %WoodName%
		RETURNF 名前
ENDSELECT

IF TEXT == 0
	SELECTCASE FLAG_L
		CASE 101 TO 199
			名前 += " (大)"
		CASE 201 TO 299
			名前 += " (巨大)"
		CASE 301 TO 399 ;logging tweak
			名前 += " (庞然)"
		CASE 401 TO 499 ;logging tweak
			名前 += " (参天古木)"
	ENDSELECT
ENDIF
RETURNF 名前
;-----------------------------------------------------------
;木材種から大きさを判定する
;-----------------------------------------------------------
@FLAG_TO_LOGSIZE(FLAG_L)
#FUNCTION
#DIM FLAG_L

SELECTCASE FLAG_L
	CASE 101 TO 199
		RETURNF 1
	CASE 201 TO 299
		RETURNF 2
	CASE 301 TO 399 ;logging tweak
		RETURNF 3
	CASE 401 TO 499 ;logging tweak
		RETURNF 4
	CASEELSE
		RETURNF 0
ENDSELECT

;-------------------------------------------------
;FLAG:原木種類から、巨木・大木を無視して木材種番号を取り出す関数
;FLAG_L…FLAG:原木種類
;-------------------------------------------------
@WOOD_NUM(FLAG_L)
#FUNCTION
#DIM FLAG_L

;logging tweak
WHILE FLAG_L > 100
	FLAG_L -= 100
WEND

RETURNF FLAG_L

;-----------------------------------------------------------
;原木・青竹の伐採にかかる体力・気力を返す関数
;-----------------------------------------------------------
@WOOD_EFFORT(木材種, nChainsawBonus) ;logging tweak
#FUNCTION
#DIM 木材種
#DIM 消費
#DIM nChainsawBonus

消費 = WoodEffort
SELECTCASE 木材種
	CASE IS > 100;大木
		消費 += 700
	CASE IS > 200;巨木
		消費 += 1000
	CASE IS > 300 ;logging tweak
		消費 += 1500
	CASE IS > 400 ;logging tweak
		消費 += 1800
ENDSELECT

IF ITEM:チェーンソー && GETBIT(nChainSawMods, ChainSaw_KickBack) && nChainsawBonus ;logging tweak
	消費 = MULTIPLY(WoodEffort, 75)
	SIF 消費 >= 1000
		消費 /= 2
ENDIF

RETURNF 消費

;-----------------------------------------------------------
;樹木関連の変数のリセット
;-----------------------------------------------------------
@WOOD_V_RESET

VARSET WoodName
VARSET WoodValue_Log
VARSET WoodValue_Plank
VARSET WoodEffort
VARSET WoodIntro1
VARSET WoodIntro2
VARSET WoodTribe
VARSET WoodItem

;-----------------------------------------------------------
;薪割りをするかどうかを確認して、木材を入手する
;原木を割る場合は体力・気力消費と時間の経過は別に記述をすること
;FLAG_L FLAG:原木種類　で原木の薪割を行う
;		省略時は=0  一般薪割・原木無しでも可能
;PHASE: 0= 体力気力時間消費あり  1= 消費なし
;-----------------------------------------------------------
@WOOD_CHOPPING(FLAG_L = 0, PHASE = 0)
#DIM FLAG_L
#DIM PHASE
#DIM FireWood
#DIM 伐採パワー
#DIM 減少値
#DIM 伐採時間
#DIM nLuck ;logging tweak
nLuck = CFLAG:MASTER:今日の運勢 / 20 - 25
SIF ITEM:チェーンソー && GETBIT(nChainSawMods, ChainSaw_TreeSeeker) ;addition custom code
	nLuck += 20

IF FLAG_L
	PRINTFORM 现场劈开原木并将其砍成木柴吗？
	;マツは木材数ボーナス
	IF WOOD_NUM(FLAG:原木種類) == 3
		FireWood = 7
	ELSE
		FireWood = 4
	ENDIF
	SELECTCASE FLAG_L
		CASE IS > 200
			FireWood += 8
		CASE IS > 100
			FireWood += 4
	ENDSELECT
ELSE
	;一般薪割りの場合
	PRINTFORM 把木头砍成木柴吗？
	FireWood = 3
ENDIF

SETCOLOR C_GRAY
IF (ITEM:木材 + FireWood) > 99
	CALL COLORMESSAGE(@"　（木柴已经多到拿不下了…）",C_GRAY,2)
ELSE
	PRINTW 
ENDIF
RESETCOLOR
SETCOLOR C_RED
SIF FLAG_L
	CALL COLORMESSAGE(@"　（注意：如果做成木柴的话、就不能加工成木料了）",C_RED,1)
RESETCOLOR
CALL ASK_YN
IF !RESULT
	PRINTFORMW 你劈开了原木
	;原木を割った場合は原木情報を削除
	IF FLAG_L
		ITEM:原木 = 0
		FLAG:原木種類 = 0
	ENDIF
	
	;消費ありの場合
	IF PHASE == 0
		伐採パワー = TALENT:MASTER:伐採Lv * 20
		減少値 = 350 - 伐採パワー
		伐採時間 = 180 - 伐採パワー
		IF ITEM:電鋸
			減少値 /= 2
			伐採時間 /= 2
			;logging tweak, addition custom code
			IF GETBIT(nChainSawMods, ChainSaw_DualSaw)
				減少値 = MULTIPLY(減少値, 75)
				伐採時間 = MULTIPLY(伐採時間, 75)
			ENDIF
		ENDIF
		IF FLAG:70
			BASE:MASTER:TSP -= 減少値
		ELSE
			DOWNBASE:MASTER:体力 += 減少値 ;tweaked to stack with chopping cost
			IF ITEM:チェーンソー
				;added line
				PRINTFORML 你的饮水机帮助你更快恢复了！
				DOWNBASE:MASTER:気力 += 減少値 - (GETBIT(nChainSawMods, ChainSaw_MiniFanDispenser) ? 75 # 50)
			ELSE
				DOWNBASE:MASTER:気力 += 減少値 - 25
			ENDIF
			TIME += 伐採時間
			;logging tweak
			IF PERCENT(nLuck)
				LOCAL:0 = RAND:5 + RAND:5 + RAND:3
				LOCAL:1 = RAND:5 + RAND:5 + RAND:3
				MAXBASE:MASTER:体力 += LOCAL:0
				MAXBASE:MASTER:気力 += LOCAL:1
				SIF LOCAL:0 + LOCAL:1
					PRINTFORMW 劈砍原木一番锻炼后，你感觉自己变得更强壮了一些……
			ENDIF
		ENDIF
	ENDIF
	;木材数の増加処理
	CALL GET_ITEM("木材", FireWood, 2)
	RETURN 1
ELSE
	IF FLAG_L
		CLEARLINE 5
		PRINTFORMW 原木的话要么带去人间之里木材商那里、要么带去山里的制材所那里……
	ENDIF
	RETURN -1
ENDIF
PRINTL 

;-----------------------------------------------------------
;伐採用おまけ抽選
;-----------------------------------------------------------
@WOODMAN_PRESENT(採集パワー)
#DIM 採集パワー
#DIM 採集物

採集物 = 0
採集パワー = MAX(採集パワー, 1) ;logging tweak
SELECTCASE RAND:(1 + 2 * SQRT(採集パワー))
	CASE 0 TO 6
		;失敗
	CASE 7, 9, 11, 13, 15, 17, 19, 21, 23
		IF 伐採可能(CFLAG:MASTER:現在位置) == 2
			;竹林では何も手に入らない
		ELSEIF DAY:2 == 1 || DAY:2 == 2;春夏
			採集物 = RAND:2 ? [[キイチゴ]] # [[はちみつ]] ;キイチゴ or mead ;logging tweak
		ELSEIF DAY:2 == 3;秋
			;logging tweak
			SELECTCASE RAND:3;キイチゴ、アケビ、ヤマブドウ
				CASE 0
					採集物 = [[ヤマブドウ]] ;logging tweak
				CASE 1
					採集物 = [[キイチゴ]] ;logging tweak
				CASEELSE
					採集物 = [[アケビ]] ;logging tweak
			ENDSELECT
		ELSE
			;冬 かびたブドウ
			採集物 = [[かびたブドウ]] ;logging tweak
		ENDIF
		SIF 採集物
			PRINTFORMW 你发现木头上长了些好东西……
	CASEELSE
		IF 伐採可能(CFLAG:MASTER:現在位置) == 2;春の竹林ではタケノコ
			SIF DAY:2 == 1
				採集物 = [[たけのこ]] ;logging tweak
		ELSEIF DAY:2 == 1 || DAY:2 == 3;春秋
			SELECTCASE RAND:3;普通のキノコか山菜
				CASE 0
					採集物 = [[普通のキノコ]] ;logging tweak
				CASE 1
					採集物 = [[草木灰]] ;logging tweak, added
				CASEELSE
					採集物 = [[山菜]] ;logging tweak
			ENDSELECT
		ELSEIF DAY:2 == 2;夏
			採集物 = RAND:2 ? [[普通のキノコ]] # [[草木灰]] ;普通のキノコのみ or ash logging tweak
		ELSE
			SIF RAND:2 == 0;冬は50％の確率で氷の鱗
				採集物 = [[氷の鱗]]
		ENDIF
		SIF 採集物
			PRINTFORMW 你在地上发现了一些好东西……
ENDSELECT
RETURN 採集物
