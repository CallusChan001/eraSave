;-------------------------------------------------
;特殊区域移动
;日常系指令、レベル1
;-------------------------------------------------
@COM464
#DIM FEE
#DIM 目的場所
#DIM 所要時間
VARSET LOCAL
FEE = 0
FOR LOCAL,1,CHARANUM
	IF CFLAG:(LOCAL):同行
		LOCAL:1 ++
		SIF CFLAG:LOCAL:衰弱 || BASE:LOCAL:気力 == 0
			LOCAL:2 ++
		SIF !CHARA_HOLIDAY(LOCAL) && BASE:LOCAL:工作量
			LOCAL:3 ++
	ENDIF
NEXT
IF NEMUKE() >= 720
	PRINTL 在睡意来袭时还是别外出比较好吧
	RETURN -1
ELSEIF LOCAL:1 > 1
	PRINTL 不能带二人以上外出
	RETURN -1
ELSEIF LOCAL:2 || BASE:MASTER:気力 == 0
	PRINTFORML 因为身体不适所以无法外出\@ BASE:MASTER:気力 == 0 ? （气力为零） # （同行者身体不适）\@
	RETURN -1
ELSEIF LOCAL:3 && FESTIVAL() == ""
	PRINTL 因为同行者处于工作日,所以请注意分配时间...
;(天候パッチ)悪天候時も外出可能に
;ELSEIF TIME:5 == 9 || TIME:5 == 5
;	PRINTL 因为天气恶劣所以无法外出
;	RETURN -1
ELSEIF TALENT:MASTER:100 < -4 
	PRINTL 用这样的身体外出是很危险的
	RETURN -1
ENDIF
SELECTCASE CFLAG:MASTER:現在位置
	CASE 205, 210, 803, 820
		IF !INRANGE(TIME, 540, 1020)
			PRINTFORML 已过营业时间
			RETURN -1
		ENDIF
		;(天候パッチ)悪天候時は運行見合わせ
		IF GET_LOCATION_AREA_TYPHOON() == 1
			PRINTFORML 似乎因为台风接近而停止运行了
			RETURN -1
		ENDIF
		IF WIND_VELOCITY >= 1
			PRINTFORML 似乎因为强风而停止运行了
			RETURN -1
		ENDIF
		SELECTCASE TIME:5
			CASE 14, 15
				PRINTFORML 似乎因为大雨而停止运行了
				RETURN -1
			CASE 9
				PRINTFORML 似乎因为大雪而停止运行了
				RETURN -1
		ENDSELECT
		;守矢角色たちと同行時は敷設者割引運賃
		IF CFLAG:31:同行 || CFLAG:32:同行 || CFLAG:33:同行
			FEE = 1
		ELSEIF CFLAG:MASTER:同行
			FEE = 10
		ELSE
			FEE = 5
		ENDIF
		;相手が鬼か天狗だと愛想笑い／敷設者にも敬意を払う
		IF GROUPMATCH(TARGET, 10, 29, 31, 32, 33, 42, 64, 65) && CFLAG:TARGET:同行
			;ガワを山の河童(16)or里の人类(12)に決める
			LOCAL = 12 + ((CFLAG:MASTER:現在位置 / 100 == 8) * 4)
			CALL SPTALK, 0, "笑顔", LOCAL, @"「哎呀哎呀，是%CALLNAME:TARGET%啊，您要使用吗？」",1
			PRINTFORM 认出了%CALLNAME:TARGET%的样子
			;人里側に河童が常駐してるのもアレかなと
			IF CFLAG:MASTER:現在位置 / 100 == 2
				PRINTFORM 受付担当
			ELSE
				PRINTFORM 受付河童
			ENDIF
			PRINTFORML 脸上浮现出亲切的笑容
		ELSE
			;人里側では一応丁寧対応に
			IF CFLAG:MASTER:現在位置 / 100 == 2
				CALL SPTALK, 0, "通常", 12, @"「您要坐索道吗？」/「票价是%金額表示(FEE * 200)%。或者{FEE}筹码也可以」",1
			ELSE
				CALL SPTALK, 0, "通常", 16, @"「…干嘛？要乘坐索道么？」/「{FEE}筹码或%金額表示(FEE * 200)%金也行」",1
			ENDIF
		ENDIF
		CALL PAY_YEN_OR_CM(FEE * 200, FEE)
		SELECTCASE RESULT
			CASE 0
				RETURN -1
			CASE 1
				IF CFLAG:31:同行 || CFLAG:32:同行 || CFLAG:33:同行
					PRINTFORML 「给您优惠哦～」
				ELSE
					PRINTFORML 「多谢惠顾」
				ENDIF
			CASE 2
				IF CFLAG:MASTER:現在位置 / 100 == 2
					PRINTFORML 「用筹码付款吧...嗯、谢谢」
				ELSE
					PRINTFORML 「诶ー、客人明明是人类居然拥有筹码么？真是厉害」
				ENDIF
		ENDSELECT
ENDSELECT
;住宿先からの特殊移動 or 住宿先でない場所への特殊移動は约会扱い
IF AT_HOME(MASTER) || MAIN_MAP != SPECIAL_AREAMOVE() / 100
	LOCAL:2 = 0
	LOCALS = %STR_TR(6000 + SPECIAL_AREAMOVE() / 10)%
	FOR LOCAL,1,CHARANUM
		IF CFLAG:(LOCAL):同行
			CFLAG:LOCAL:约会中 = SPECIAL_AREAMOVE() / 100
			CFLAG:LOCAL:現在位置 = SPECIAL_AREAMOVE()
			LOCAL:2 = LOCAL
			FLAG:约会的对象 = LOCAL
		ENDIF
	NEXT
	CFLAG:MASTER:约会中 = SPECIAL_AREAMOVE() / 100
	CFLAG:MASTER:現在位置 = SPECIAL_AREAMOVE()

	IF CFLAG:MASTER:约会中 == 2
		CALL EX_COSTUME(FLAG:约会的对象, "人里デート")
		SIF RESULT
			PRINTFORMW %CALLNAME:(FLAG:约会的对象)%换了一件适合在人间之里穿的衣服。
	ENDIF
	PRINTFORML 出门去%LOCALS%
	TIME += SPECIAL_AREAMOVE_TIME()

;住宿先でないところから住宿先への移動は约会終了扱い
ELSEIF !AT_HOME(MASTER) && MAIN_MAP == SPECIAL_AREAMOVE() / 100
	IF CFLAG:败犬 && TALENT:路人 == 1
		PRINTFORML 把%CALLNAME:TARGET%带回去了。
	ELSE
		PRINTFORMW 从\@ CFLAG:MASTER:约会中 != CFLAG:TARGET:约会中 || TARGET == MASTER ? 外出 # 约会 \@回来了
	ENDIF
	;约会終了時は強制的にENTRANCEへ飛ばされるため一時保存
	目的場所 = SPECIAL_AREAMOVE()
	所要時間 = SPECIAL_AREAMOVE_TIME()
	CFLAG:MASTER:現在位置 = SPECIAL_AREAMOVE()
	CALL 守矢神簽约会的对象のリセット()
	FOR LOCAL,1,CHARANUM
		IF CHK_DATENOW(CFLAG:LOCAL:约会中)
			CALL DATE_EVENT(LOCAL)
			CFLAG:LOCAL:現在位置 = 目的場所
			CALL SET_DATE(99,LOCAL)
			;约会的对象にも改めて現在位置渡す
			CFLAG:LOCAL:現在位置 = 目的場所
		ENDIF
	NEXT
	CALL SET_DATE(99,MASTER,0)
	;一時保存情報渡す
	CFLAG:MASTER:現在位置 = 目的場所
	TIME += 所要時間
ENDIF
;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE464
;停止中は不可
SIF FLAG:70
	RETURN 0
SIF !TFLAG:100
	RETURN 0
SIF TFLAG:约会道中
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(464)
	RETURN RESULT
;睡眠中
SIF CFLAG:睡眠
	RETURN 0
;陪睡中
SIF CFLAG:陪睡中
	RETURN 0
SIF !SPECIAL_AREAMOVE()
	RETURN 0
SIF CFLAG:诶嘿嘿
	RETURN 0
RETURN 1

;-------------------------------------------------
@SPECIAL_AREAMOVE()
#FUNCTION
SELECTCASE CFLAG:MASTER:現在位置
	;博麗神社守矢分社
	CASE GET_MAP_REPLACEMENT(P21守矢神社分社)
	;守矢角色と同行中
		SIF IS_TSUKIAU(31) || IS_TSUKIAU(32) || IS_TSUKIAU(33)
			RETURNF GET_MAP_REPLACEMENT(P812本殿)
	CASE GET_MAP_REPLACEMENT(P812本殿)
	;守矢角色と同行中
		SIF IS_TSUKIAU(31) || IS_TSUKIAU(32) || IS_TSUKIAU(33)
			RETURNF GET_MAP_REPLACEMENT(P21守矢神社分社)
	;人里北大街<==>索道站互通
	CASE GET_MAP_REPLACEMENT(P205北大街)
		RETURNF GET_MAP_REPLACEMENT(P803索道站)
	CASE GET_MAP_REPLACEMENT(P803索道站)
		RETURNF GET_MAP_REPLACEMENT(P205北大街)
	;无缘冢投胎快速单行道
	CASE GET_MAP_REPLACEMENT(P506無縁塚)
		RETURNF GET_MAP_REPLACEMENT(P601中有之道)
	;地下中枢互通
	CASE GET_MAP_REPLACEMENT(712)
		RETURNF GET_MAP_REPLACEMENT(928)
	CASE GET_MAP_REPLACEMENT(928)
		RETURNF GET_MAP_REPLACEMENT(712)
	;通向山頂的路 <==> 山頂入口
	CASE GET_MAP_REPLACEMENT(P732通向山頂的路)
		RETURNF GET_MAP_REPLACEMENT(P801山頂入口)
	CASE GET_MAP_REPLACEMENT(P801山頂入口)
		RETURNF GET_MAP_REPLACEMENT(P732通向山頂的路)

	;魔界更新
	CASE GET_MAP_REPLACEMENT(P521人偶洋館・愛麗絲的房間)
		SIF IS_TSUKIAU(17)
			RETURNF GET_MAP_REPLACEMENT(P1170万魔殿・闲置的爱丽丝卧室)
	CASE GET_MAP_REPLACEMENT(P1170万魔殿・闲置的爱丽丝卧室)
		SIF IS_TSUKIAU(17)
			RETURNF GET_MAP_REPLACEMENT(P521人偶洋館・愛麗絲的房間)

	CASE GET_MAP_REPLACEMENT(P1113别墅区・闲置的冈崎宅)
		SIF IS_TSUKIAU(8) || IS_TSUKIAU(9)
			RETURNF GET_MAP_REPLACEMENT(P47夢幻遺跡・艦橋)
	CASE GET_MAP_REPLACEMENT(P47夢幻遺跡・艦橋)
		SIF IS_TSUKIAU(8) || IS_TSUKIAU(9)
			RETURNF GET_MAP_REPLACEMENT(P1113别墅区・闲置的冈崎宅)

	CASE GET_MAP_REPLACEMENT(P465大堂)
		SIF IS_TSUKIAU(114) || IS_TSUKIAU(115) || IS_TSUKIAU(116) || IS_TSUKIAU(117) || IS_TSUKIAU(68)
			RETURNF GET_MAP_REPLACEMENT(P1114别墅区・闲置的风见宅)
	CASE GET_MAP_REPLACEMENT(P1114别墅区・闲置的风见宅)
		SIF IS_TSUKIAU(114) || IS_TSUKIAU(115) || IS_TSUKIAU(116) || IS_TSUKIAU(117) || IS_TSUKIAU(68)
			RETURNF GET_MAP_REPLACEMENT(P465大堂)

	CASE GET_MAP_REPLACEMENT(P52別屋)
		SIF IS_TSUKIAU(4)
			RETURNF GET_MAP_REPLACEMENT(P1115别墅区・闲置的怨灵宅)
	CASE GET_MAP_REPLACEMENT(P1115别墅区・闲置的怨灵宅)
		SIF IS_TSUKIAU(4)
			RETURNF GET_MAP_REPLACEMENT(P52別屋)
	;8 命莲寺钟楼↔魔界法界
	CASE GET_MAP_REPLACEMENT(122)
		SIF IS_TSUKIAU(55) || IS_TSUKIAU(86)
			RETURNF GET_MAP_REPLACEMENT(1184)
	CASE GET_MAP_REPLACEMENT(1184)
		SIF IS_TSUKIAU(55) || IS_TSUKIAU(86)
			RETURNF GET_MAP_REPLACEMENT(122)
	;锦上京更新
	CASE GET_MAP_REPLACEMENT(P745月都秘道)
		FOR I, 1, GET_TARGETNUM() + 1
			SIF GET_MAPID(CFLAG:(TARGET:I):自宅位置)== 10 && IS_TSUKIAU(TARGET:I)
				RETURNF GET_MAP_REPLACEMENT(P1003玉兎街)
		NEXT
		SIF ITEM:月の羽衣
			RETURNF GET_MAP_REPLACEMENT(P1003玉兎街)
	CASE GET_MAP_REPLACEMENT(P1003玉兎街)
		FOR I, 1, GET_TARGETNUM() + 1
			SIF GET_MAPID(CFLAG:(TARGET:I):自宅位置)== 10 && IS_TSUKIAU(TARGET:I)
				RETURNF GET_MAP_REPLACEMENT(P745月都秘道)
		NEXT
		SIF ITEM:月の羽衣
			RETURNF GET_MAP_REPLACEMENT(P745月都秘道)
	CASE GET_MAP_REPLACEMENT(P756月海通路)
		SIF IS_TSUKIAU([[丰姬]])
			RETURNF GET_MAP_REPLACEMENT(P1016入海口)
	CASE GET_MAP_REPLACEMENT(P1016入海口)
		SIF IS_TSUKIAU([[丰姬]])
			RETURNF GET_MAP_REPLACEMENT(P756月海通路)
ENDSELECT
RETURNF 0
;-------------------------------------------------
@SPECIAL_AREAMOVE_TIME()
#FUNCTION
SELECTCASE CFLAG:MASTER:現在位置
	;博麗神社守矢分社
	CASE GET_MAP_REPLACEMENT(P21守矢神社分社),GET_MAP_REPLACEMENT(P812本殿)
		RETURNF 1
	;人里北大街<==>索道站互通
	CASE GET_MAP_REPLACEMENT(P205北大街),GET_MAP_REPLACEMENT(P803索道站)
		RETURNF 15
	;投胎
	CASE GET_MAP_REPLACEMENT(P506無縁塚)
		RETURNF 5
	;地下电梯
	CASE GET_MAP_REPLACEMENT(712), GET_MAP_REPLACEMENT(928)
		RETURNF 10
	;通向山頂的路 <==> 山頂入口
	CASE GET_MAP_REPLACEMENT(P732通向山頂的路),GET_MAP_REPLACEMENT(P801山頂入口)
		RETURNF 5
	;爱丽丝宅
	CASE GET_MAP_REPLACEMENT(P521人偶洋館・愛麗絲的房間), GET_MAP_REPLACEMENT(P1170万魔殿・闲置的爱丽丝卧室)
		RETURNF 1
	;魔界都市-别墅区
	CASE GET_MAP_REPLACEMENT(P47夢幻遺跡・艦橋),GET_MAP_REPLACEMENT(P1113别墅区・闲置的冈崎宅),GET_MAP_REPLACEMENT(P1114别墅区・闲置的风见宅),GET_MAP_REPLACEMENT(P465大堂),GET_MAP_REPLACEMENT(P52別屋),GET_MAP_REPLACEMENT(P1115别墅区・闲置的怨灵宅)
		RETURNF 1
	;钟楼 <==> 法界
	CASE GET_MAP_REPLACEMENT(122), GET_MAP_REPLACEMENT(1184)
		RETURNF 1
	;锦上京 月都的密道
	CASE GET_MAP_REPLACEMENT(P745月都秘道),GET_MAP_REPLACEMENT(P1003玉兎街),GET_MAP_REPLACEMENT(P756月海通路),GET_MAP_REPLACEMENT(P1016入海口)
		RETURNF 10
ENDSELECT
