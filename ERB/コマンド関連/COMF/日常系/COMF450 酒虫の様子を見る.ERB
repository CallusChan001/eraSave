;-------------------------------------------------
;酒虫の样子を見る
;日常系コマンド、レベル1
;-------------------------------------------------

@COM450
#DIM 醸造量
#DIM 酒ID
#DIM CAN_ADD_ITEMS
VARSET LOCAL

酒ID = ALC_ID()
醸造量 = GET_INT(0, "酒データ", 酒ID, "醸造数")
LOCALS '= @"替换成%VAR_WATER%了"
LOCAL = 1
IF VAR_WATER == ""
	LOCALS '= "没有带着可以替换的水"
	LOCAL = 0
ENDIF

CALL BREWING_GUIDE

PRINTFORML %CALLNAME:MASTER%往放着酒虫的瓶子里窥去
PRINTFORML 瓶里装满了%VAR_FERMENT%…
DRAWLINE

CAN_ADD_ITEMS = GET_STR(0, "酒データ", ALC_ID(VAR_FERMENT), "ADDITIONS") != ""
{
CALL ASK_M("什么也不做",1,@"%LOCALS%", LOCAL,"添加物品",1,
"操纵时间造酒（消费TSP2400）",BASE:MASTER:8 >= 2400,"『绯红之王』！（消费TSP4000&绯红之王）", BASE:MASTER:8 >= 4000 && ITEM:绯红之王 && GET_STR(0, "酒データ", ALC_ID(VAR_FERMENT), "変化先") != "0")
}
SELECTCASE RESULT
	CASE 0
		RETURN -1
	CASE 1
		PRINTFORML %CALLNAME:MASTER%取出%VAR_FERMENT%后放入了%VAR_WATER%
		IF 醸造量 > 0
			;bonus from brewer
			SIF TALENT:MASTER:酒造家
				醸造量++
			CALL GET_ITEM(@"%VAR_FERMENT%", 醸造量, 2)
			CALL BREWING_GUIDE_CONSUME(VAR_FERMENT)
		ENDIF
		;(酒造パッチ)酒粕入手
		SIF VAR_FERMENT == "吟酿酒" || VAR_FERMENT == "熟成酒" || VAR_FERMENT == "古酒" || VAR_FERMENT == "贵酿酒" || VAR_FERMENT == "贵酿熟成酒" || VAR_FERMENT == "贵酿古酒"
			CALL GET_ITEM("酒粕", 1, 1)
		VAR_FERMENT = %VAR_WATER%
		VAR_WATER =
		DAY:10 = 0
		;入れ替え时にエサをやれる
		IF ITEM:鱼粕 || ITEM:肉骨粉
			PRINTFORML 给酒虫喂食吧…
			{
			CALL ASK_M("喂鱼粕", ITEM:鱼粕,
			"喂肉骨粉", ITEM:肉骨粉,
			"不喂食",1)
			}
			SELECTCASE RESULT
				CASE 0
					CALL LOST_ITEM("鱼粕", 1, 1)
					PRINTFORML 酒虫吃完了鱼粕而变得稍微活跃了一点…
					DAY:10 += 1
				CASE 1
					CALL LOST_ITEM("肉骨粉", 1, 1)
					PRINTFORML 酒虫吃完了肉骨粉而变得活跃起来…
					DAY:10 += 2
				CASE 2
					PRINTFORML 什么都不喂
			ENDSELECT
		ENDIF
		CALL BREWING_GUIDE_MAKE_KNOWN(VAR_FERMENT)
	CASE 2
		CALL COM450_1
	CASE 3
		IF GET_STR(0, "酒データ", 酒ID, "変化先") == "0"
			PRINTFORMW 对%VAR_FERMENT%没有意义
			RETURN -1
		ENDIF
		PRINTFORML %CALLNAME:MASTER%使用时间停止能力强行加速了瓶中的时间
		BASE:MASTER:8 -= 2400
		CALL 酒造
	CASE 4
		IF GET_STR(0, "酒データ", 酒ID, "変化先") == "0"
			PRINTFORMW 对%VAR_FERMENT%没有意义
			RETURN -1
		ENDIF
		PRINTFORML %CALLNAME:MASTER%消除了时间得到了『结果』
		BASE:MASTER:8 -= 4000
		ITEM:绯红之王 --
		DAY:10 += 9
		CALL 酒造
ENDSELECT
TIME += 5
RETURN 1



@COM450_1
CALL BREWING_GUIDE_MIX_MENU
RETURN RESULT

;酒虫の样子を見る
@COM_ABLE450
;停止中は不可
SIF FLAG:70 == 1
	RETURN 0
;移动实行判定
SIF !TFLAG:100
	RETURN 0
;批量管理
SIF GLOBAL_COMABLE(450)
	RETURN RESULT
;廚房
SIF CFLAG:MASTER:現在位置 != 拠点_酒虫
	RETURN 0
SIF CFLAG:诶嘿嘿
	RETURN 0
RETURN 1

@CAN_FERMENT(ARG)
#FUNCTION
SIF DAY:10 >= 3
	RETURNF 0
SIF GROUPMATCH(VAR_FERMENT, "普通的水", "雾之湖的水", "地狱温泉的水", "奇迹之水")
	RETURNF 1
RETURNF 0

