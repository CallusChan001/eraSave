;-------------------------------------------------
;掃除
;日常系指令、レベル1
;-------------------------------------------------
@COM410
#DIM 清掃力
#DIM SWEEPID
#DIM SWEEP_YOGORE
SWEEPID = CFLAG:MASTER:現在位置
TFLAG:情緒上昇抑制 = 1
IF YOGORE:SWEEPID < OSOUJI_LIMIT(MASTER)
	;IF SWEEPGOD == 2
	;	RETURN -1
	;ELSE
		DRAWLINE
		PRINTFORML 也没有那么乱七八糟、就算不扫除可以吧
		RETURN -1
	;ENDIF
ENDIF

SELECTCASE YOGORE:SWEEPID
	CASE IS < 500
		TFLAG:194 = 0
	CASE IS < 2000
		TFLAG:194 = 1
	CASE IS < 5000
		TFLAG:194 = 2
	CASEELSE
		TFLAG:194 = 3
ENDSELECT
;=================================================
LOCAL:4 = 0
SIF BASE:MASTER:TSP > 1500
	LOCAL:4 = 1
SIF BASE:MASTER:TSP > 2000
	LOCAL:4 = 2

IF YOGORE:SWEEPID > 5000 && TALENT:MASTER:汚臭耐性 == -2 && BASE:MASTER:体力 > (1500 - LOCAL:4 * 300) && BASE:MASTER:気力 > (2000 - LOCAL:4 * 100) && BASE:MASTER:TSP > 1000 && !CFLAG:MASTER:大掃除 && !FLAG:時間停止
	PRINTL 即便如此、这也实在脏的令人无法忍受…
	;PRINTL [0]平常心を保つ
	;PRINTL [1]無理
	;INPUT
	;IF RESULT == 0
		GOTO CANCEL_LOOP
	;ELSE
	;	CALL OSOUJI(LOCAL:4)
	;	RETURN 1
	;ENDIF
;=================================================
ELSE
	$ CANCEL_LOOP
	IF (!TARGET || !SHIRAHU(TARGET))
		CALL 胖次発見
	ENDIF
	TFLAG:193 = TARGET && SHIRAHU(TARGET) && (ABL:親密 * 500 + ABL:従順 * 500 + ABL:清掃技能 * 1000 > CFLAG:地位 - CFLAG:MASTER:地位) && (!CFLAG:行動 || (CFLAG:行動 == 5 && GROUPMATCH(CFLAG:職種, JOB_清掃, JOB_宴会の片づけ)));allow characters to join as long as they're not working (or if they are cleaning)顺带排除掉无法帮忙的情况，这里简单使用和胖次一样的SHIRAHU无意识判定即可，现在这个就直接代表着角色帮助了清扫的意思

	清掃力 = SWEEP_POWER_CALC(TFLAG:193 ? TARGET # 0)
	SIF ITEM:特制竹箒
		PRINTFORML %CALLNAME:MASTER%用特制的扫帚发出吼声
	SWEEP_YOGORE = YOGORE:SWEEPID - YOGORE:SWEEPID / (清掃力 * (SWEEPID == P54小人的虫籠 ? 5 # 1))
	YOGORE:SWEEPID -= SWEEP_YOGORE
	;这里加上顺手清扫虫笼的体型限制，小人也可以请求其他人帮忙
	IF GROUPMATCH(SWEEPID, P9居間, P10走廊) && (TALENT:MASTER:体型 > -5 || TFLAG:193 && TALENT:体型 > -5)
		PRINTFORML 顺手把虫笼也清扫干净了
		PRINTFORML 因为只要挪开针妙丸的私人物品再倒过来抖一抖就行了所以很简单
		;这里把虫笼的垃圾乘上体型修正也加入扫除量，不然像是垃圾被虚空吃了一样
		SWEEP_YOGORE += YOGORE:P54小人的虫籠 / 5
		YOGORE:P54小人的虫籠 = 0
	ENDIF
	EX:MASTER:今日的掃除回数 += SWEEP_YOGORE
	IF FLAG:時間停止
		BASE:MASTER:TSP -= 50
		LOCAL:3 = 0
		FOR LOCAL,1,CHARANUM
			SIF CFLAG:LOCAL:同室
				LOCAL:3 ++
		NEXT
		IF LOCAL:3
			EXP:MASTER:異常清掃経験 ++
			IF !GETBIT(CFLAG:MASTER:異常経験,11)
				EXP:MASTER:異常経験 ++
				SETBIT CFLAG:MASTER:異常経験,11
			ENDIF
		ENDIF
	ELSE
		DOWNBASE:MASTER:体力 = 50
		DOWNBASE:MASTER:気力 = 100
		TIME += 20
		;这里只给TARGET加同行对吗？我不知道
		SIF CFLAG:同行
			CFLAG:同行 += 20
	ENDIF

	IF TARGET && SHIRAHU(TARGET) && !CFLAG:诶嘿嘿
		;固定で獲得するソース
		SOURCE:歓楽 = 400
		;信頼
		TFLAG:信賴度管理 = 3

		;ABL:従順をみる
		IF ABL:従順 <= 1
			SOURCE:歓楽 += (ABL:従順 * 60)
		ELSEIF ABL:従順 <= 3
			SOURCE:歓楽 += 200 + (ABL:従順 * 60)
		ELSEIF ABL:従順 <= 5
			SOURCE:歓楽 += 500 + (ABL:従順 * 60)
		ELSEIF ABL:従順 <= 8
			SOURCE:歓楽 += 750 + (ABL:従順 * 75)
		ELSEIF ABL:従順 <= 11
			SOURCE:歓楽 += 1000 + (ABL:従順 * 75)
		ELSE
			SOURCE:歓楽 += 2000 + (ABL:従順 * 30)
		ENDIF

		;好感度をみる
		IF CFLAG:好感度 <= 500
			SOURCE:歓楽 += CFLAG:好感度
		ELSEIF CFLAG:好感度 <= 5000
			SOURCE:歓楽 += 500 + (CFLAG:好感度 - 500) / 3
		ELSE
			SOURCE:歓楽 += 2000 + (CFLAG:好感度 - 5000) / 5
		ENDIF
		SIF SOURCE:歓楽 < 0
			SOURCE:歓楽 = 0
		SOURCE:征服 = 300 + 100 * ABL:施虐属性
	;ELSEIF TARGET > 0 && CFLAG:诶嘿嘿 && !FLAG:70
	ELSEIF TARGET && SHIRAHU(TARGET) && CFLAG:诶嘿嘿
		DOWNBASE:気力 += 100
		SOURCE:反感 += 500
		SOURCE:鬱屈 += 100
		LOCAL:3 = 0
		FOR LOCAL,1,CHARANUM
			SIF CFLAG:LOCAL:同室
				LOCAL:3 ++
		NEXT
		IF LOCAL:3 == 1
			EXP:MASTER:異常清掃経験 ++
			IF !GETBIT(CFLAG:異常経験,11)
				EXP:異常経験 ++
				SETBIT CFLAG:異常経験,11
			ENDIF
		ENDIF
	ENDIF

	;SIF TFLAG:193 && !CFLAG:睡眠 && !FLAG:70 && !CFLAG:诶嘿嘿
	SIF TFLAG:193
		EXP:清掃経験 ++
	EXP:MASTER:清掃経験 ++
ENDIF
RETURN 1

@OSOUJI(ARG)
#LOCALSIZE 1
#LOCALSSIZE 1
VARSET LOCAL
SETCOLOR 0xEE1010
PRINTFORMW %CALLNAME:PLAYER%非常生气。
PRINTFORMW 下定了决心、一定要把这杯盘狼籍的%GET_MAPNAME(MAIN_MAP)%给收拾干净。
PRINTFORMW %CALLNAME:PLAYER%并没有信仰。
PRINTFORMW %CALLNAME:PLAYER%只不过是、超常的强奸魔而已。
PRINTW 将时间停止、过着玩弄少女的生活。
PRINTW 但是对于脏东西、也有着超过常人一倍的敏感。
RESETCOLOR
PRINTFORML 
PRINTFORML %CALLNAME:PLAYER%停止了时间、打扫起了%GET_MAPNAME(MAIN_MAP)%！
PRINTFORML 
PRINTFORMW %CALLNAME:PLAYER%扫除中…
PRINTFORMW %CALLNAME:PLAYER%扫除中……
PRINTFORMW %CALLNAME:PLAYER%扫除中………
FOR LOCAL,MINROOM(), MAXROOM
IF YOGORE:LOCAL > 2000
	YOGORE:LOCAL = YOGORE:LOCAL / (3 + ABL:MASTER:清掃技能)
FLAG:大掃除 += 1
ENDIF
NEXT 
PRINTFORMW %CALLNAME:PLAYER%清扫了%TOKANJIT(FLAG:大掃除)%间房间
CFLAG:MASTER:大掃除 += 4 + RAND:(13 - 2 * ABL:MASTER:清掃技能)
EXP:MASTER:清掃経験 += 1 + RAND:(MAX(2, ARG * 3))
EXP:MASTER:異常清掃経験 += RAND:5
;CFLAG:MASTER:現在位置 = CFLAG:MASTER:初期位置
;FOR LOCAL,1,11
;	CFLAG:LOCAL:現在位置 = 9
;	CFLAG:LOCAL:大掃除 = 1 + RAND:(ABL:MASTER:話術技能)
;NEXT
;原逻辑只有神社那几个，现在改成对应地图的大扫除，虽然现在暂时用不上但这样写拓展性更好些
FOR LOCAL,1,CHARANUM
	IF GET_MAPID(CFLAG:LOCAL:初期位置) == GET_MAPID(CFLAG:MASTER:現在位置) && GET_MAPID(CFLAG:LOCAL:現在位置) == GET_MAPID(CFLAG:MASTER:現在位置)
		CFLAG:LOCAL:大掃除 = 1 + RAND:(ABL:MASTER:話術技能)
		EXP:LOCAL:清掃経験 += 1
	ENDIF
NEXT
FLAG:大掃除 = 1
TIME += (500 - ARG * 100)
DOWNBASE:MASTER:体力 = (1500 - ARG * 300)
DOWNBASE:MASTER:気力 = (1500 - ARG * 100)
BASE:MASTER:TSP = 0

@胖次発見
#LOCALSIZE 3
#LOCALSSIZE 1
;#DIM 自宅人物
VARSET LOCAL
;之前那套CASE里三妖精房间好像是设错的，干脆一道全部废弃吧
[SKIPSTART]
FOR 自宅人物,1,CHARANUM
	IF CFLAG:MASTER:現在位置 == CFLAG:(自宅人物):神社在住
		LOCAL = 自宅人物
	ENDIF
NEXT
LOCAL:1 = 今天穿的内裤(LOCAL)
LOCAL:2 =  RAND:5000
SIF FLAG:祈願内容 == 202
	LOCAL:2 = RAND:4000
IF YOGORE:(CFLAG:MASTER:現在位置) > LOCAL:2 && LOCAL && LOCAL:1 && !FLAG:70
	TSTR:3 = %CALLNAME:LOCAL%{LOCAL:1}/
	TSTR:2 = %PANTSNAME(LOCAL:1, LOCAL)%
	FOUND_PANTS_TYPE = LOCAL:1
	FOUND_PANTS_OWNER = LOCAL
ENDIF
[SKIPEND]
;之前逻辑只能拿到该位置在住最后一个编号的胖次，为什么不能全都拿到呢，顺带凭什么时停扫除不能拿内裤（
FOR LOCAL,1,CHARANUM
	IF CFLAG:LOCAL:初期位置 == CFLAG:MASTER:現在位置
		LOCAL:1 = 今天穿的内裤(LOCAL)
		LOCAL:2 = (FLAG:祈願内容 == 202) ? RAND:4000 # RAND:5000
		IF YOGORE:(CFLAG:MASTER:現在位置) > LOCAL:2 && LOCAL:1
			TSTR:3 = %CALLNAME:LOCAL%{LOCAL:1}/
			TSTR:2 = %PANTSNAME(LOCAL:1, LOCAL)%
			FOUND_PANTS_TYPE = LOCAL:1
			FOUND_PANTS_OWNER = LOCAL
		ENDIF
	ENDIF
NEXT

;清扫力计算
@SWEEP_POWER_CALC(ARG)
#FUNCTION
#LOCALSIZE 1
#LOCALSSIZE 1
#DIM SWEEP_POWER
;基础玩家清扫力
SWEEP_POWER = 2 + ABL:MASTER:清掃技能 + 3 * TALENT:MASTER:清掃中毒
;特制竹箒物品补正
SELECTCASE ITEM:特制竹箒
	CASE 1
		SWEEP_POWER += 2
	CASE 2
		SWEEP_POWER += 3
	CASE 3
		SWEEP_POWER += 4
ENDSELECT
;玩家污臭耐性补正
SWEEP_POWER = SWEEP_POWER * (5 - TALENT:MASTER:汚臭耐性) / 5
;小人体型补正，毕竟小人没有魔法那很难打扫大房间的吧，修正暂时先设成五分之一吧，其实可以调整
SIF TALENT:MASTER:体型 == -5
	SWEEP_POWER = SWEEP_POWER / 5
;角色帮助补正，之前是1，但其实我更想加成ABL:清掃技能，毕竟清扫高的人帮忙清扫力强也很正常啊，所以小人应该和其他人一起打扫会更好
SIF ARG
	SWEEP_POWER += ABL:ARG:清掃技能
RETURNF SWEEP_POWER

;角色がそろそろ掃除しようと思う部屋の散らかり具合
@OSOUJI_LIMIT(ARG)
#FUNCTION
#LOCALSIZE 1
#LOCALSSIZE 1
#DIM O_LIMIT
O_LIMIT = 300
SIF TALENT:ARG:懒散
	O_LIMIT *= 10
O_LIMIT = O_LIMIT * (3 + TALENT:ARG:汚臭耐性) / 3
SIF TALENT:ARG:献身的
	O_LIMIT /= 2
SIF TALENT:ARG:清掃中毒
	O_LIMIT /= 10
RETURNF O_LIMIT

@COM_ABLE410
;掃除実行判定
SIF !TFLAG:100
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(410)
	RETURN RESULT
SIF CHK_DATENOW(CFLAG:MASTER:约会中)
	RETURN 0
;汚れてないとダメ
SIF YOGORE:(CFLAG:MASTER:現在位置) < 100
	RETURN 0
;陪睡中はダメ
SIF CFLAG:陪睡中
	RETURN 0
SIF CFLAG:诶嘿嘿 == 2
	RETURN 0
SIF CFLAG:行動 == 5 && GROUPMATCH(CFLAG:職種, JOB_宴会準備, JOB_宴会参加, JOB_酔いつぶれる) && !FLAG:70
	RETURN 0
;外出中はダメ、待客室は掃除できる
SIF AT_HOME(MASTER) == 0 && CFLAG:MASTER:現在位置 != OMANEKIBEYA()
	RETURN 0
;気力0
SIF BASE:MASTER:気力 <= 0
	RETURN 0
;あなたの酒気が0以上なら偽
SIF GETPERCENT(BASE:MASTER:酒気, MAXBASE:MASTER:酒気) > 15
	RETURN 0
RETURN 1

;自動掃除
@AUTO_SWEEP
#LOCALSIZE 1
#LOCALSSIZE 1
#DIM SWEEPID
#DIM 清掃力
#DIM SWEEP_YOGORE
#DIM 掃除骰子
[IF DEBUG]
PRINTFORML 自動掃除フラグ:{GETBIT(FLAG:自動掃除, 1)}、同行：{CFLAG:MASTER:同行}
[ENDIF]

SWEEPID = CFLAG:MASTER:現在位置
;体力气力为0时无法自动扫除
SIF (BASE:MASTER:体力 == 0 || BASE:MASTER:気力 == 0)
	RETURN -1
;醉酒到一定程度无法自动扫除
SIF GETPERCENT(BASE:MASTER:酒気, MAXBASE:MASTER:酒気) > 15
	RETURN -2
;与人同行无法自动扫除
SIF CFLAG:MASTER:同行
	RETURN -3
;主观觉得不需要扫除
SIF YOGORE:SWEEPID < OSOUJI_LIMIT(MASTER)
	RETURN -4
;掃除処理
清掃力 = SWEEP_POWER_CALC(0)
;成功判定
掃除骰子 = (4 + ABL:MASTER:清掃技能) - RAND:11
[IF DEBUG]
PRINTFORML [ベースの清掃力:{清掃力}][掃除骰子:{掃除骰子}]
[ENDIF]
;掃除骰子が1以上なら成功(清掃0で30%、清掃6で90%)
IF (掃除骰子 > 0)
	;掃除量を計算して半分を戻して反映
	SWEEP_YOGORE = (YOGORE:SWEEPID - YOGORE:SWEEPID / (清掃力 * (SWEEPID == P54小人的虫籠 ? 5 # 1))) / 2
	YOGORE:SWEEPID -= SWEEP_YOGORE
	EX:MASTER:今日的掃除回数 += SWEEP_YOGORE
	SIF SWEEPGOD < 2
		PRINTFORML %CALLNAME:PLAYER%在离开%NAME_FROM_PLACE(SWEEPID)%之前做了扫除
	PRINTFORML
	;清掃経験は1/3上昇
	SIF !RAND:3
		EXP:MASTER:清掃経験 ++
ELSE 
	;掃除失敗
	SELECTCASE IFRAND("0TO10", 1, "D2, D6, D7", FLAG:70)
		CASE 1
			PRINTFORML %CALLNAME:PLAYER%想在%NAME_FROM_PLACE(SWEEPID)%做扫除、却把水桶打翻了
		CASE 2
			PRINTFORML %CALLNAME:PLAYER%手中的扫帚突然开始暴走、飞到了找不到的地方
		CASE 3
			PRINTFORML %CALLNAME:PLAYER%没能很好地扫除%NAME_FROM_PLACE(SWEEPID)%
		CASE 4
			PRINTFORML %CALLNAME:PLAYER%扫除过后的%NAME_FROM_PLACE(SWEEPID)%看起来比之前更脏了
		CASE 5
			PRINTFORML %CALLNAME:PLAYER%在让%NAME_FROM_PLACE(SWEEPID)%变得更漂亮这件事上努力过了
		CASE 6
			PRINTFORML %CALLNAME:PLAYER%正准备打扫房间的时候、面前出现了想要捣乱的妖精集团！
		CASE 7
			PRINTFORML %CALLNAME:PLAYER%在前面扫除的时候、有妖精在后面捣乱...
		CASE 8
			PRINTFORML %CALLNAME:PLAYER%觉得还是明天再整理就好了
		CASEELSE
			PRINTFORML %CALLNAME:PLAYER%感觉现在还不是扫除的时候
	ENDSELECT
	PRINTFORML
	;清掃経験は1/20上昇
	SIF !RAND:20
		EXP:MASTER:清掃経験 ++
ENDIF
;消費TSP/ステータス/消費時間は全て通常の半分
;自動時止め移動、またはTS中ならTSP消費
IF ((GETBIT(FLAG:瞬間移動, 1) || FLAG:70) && BASE:MASTER:TSP > 25)
	IF SWEEPGOD > 1
		BASE:MASTER:TSP -= 10
	;一键扫除凭啥不扣TSP啊
	;ELSEIF SWEEPGOD == 3
	;	BASE:MASTER:TSP -= 0
	ELSE
		BASE:MASTER:TSP -= 25
	ENDIF
ELSE
	DOWNBASE:MASTER:体力 = 25
	DOWNBASE:MASTER:気力 = 50
	IF SWEEPGOD > 1
		TIME += 5
	ELSE
		TIME += 15
	ENDIF
ENDIF
RETURN 1
