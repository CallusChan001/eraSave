;材木商
@COM623
#DIM 売値
#DIM ループ数
#DIM リザルト保管
#DIM 候補番号
#DIM 候補個数
#DIM 売却個数
#DIM 消去行数
ループ数 = 0

;開店時間・閉店時間
IF INRANGE(TIME:0, 540, 1200) == 0 ;logging tweak
	PRINTFORMW 不在营业时间
	RETURN -1
ENDIF


;初回イベント
IF FLAG:最後に木材商に訪れた日 == 0
	;初回イベント前は、原木持ちでなければ店にも入れない
	IF ITEM:原木 == 0
		PRINTFORMW 现在来这里也没什么用…先去伐点原木再来吧
		RETURN -1
	ENDIF
	;依頼の報酬など、伐採以外で原木を入手してしまった場合の退避用
	SIF !FLAG:原木種類
		FLAG:原木種類 = MAX(RAND:12, 1)
	CALL WoodData(WOOD_NUM(FLAG:原木種類))
	PRINTFORMW 「你好，我是这里的老板。你是来卖原木的吗？」
	PRINTFORMW 「好的。现在，我们来看看货吧，好吗？」
	PRINTFORMW 「…………」
	PRINTFORMW 「嗬，我得说，这可真不简单，你引起了我的兴趣……」
	PRINTFORMW 「……嗯？你想的话我可以每天都带过来？而且人里外面就有一整片森林长着这些？」
	PRINTFORMW 「…………」
	PRINTFORMW 「……看来你可不是什么普通人，是吧，客人？」
	PRINTFORMW 「成交。你砍下来的原木我们都收。」
	PRINTFORMW 「不用说，报酬总的来说取决于木材的价值。」
	PRINTFORMW 「此外，如果你需要木材做家具或其他东西，我们可以在这里直接将原木加工成木材。」
	PRINTFORMW 「不过，我会收取加工费。」
	PRINTFORMW 「话虽如此，让我再看看你今天带来的货物。」
	;売値はここで設定、基本的に中途キャンセルはしない
	売値 = WOOD_VALUE(FLAG:原木種類)
	SETCOLOR 0x00FF00
	PRINTFORMD 「这看起来像是
	PRINTFORM %WOOD_NAME(FLAG:原木種類)%
	PRINTFORMD ，我很确定……我想我可以以
	PRINTFORM %金額表示(売値)%
	RESETCOLOR
	PRINTFORMW 的价格向你收购。」
	;PRINTFORMW 　　Money: %金額表示(MONEY)% → %金額表示(MONEY + 売値)%
	PRINTFORMW 「交易完成。好了，我期待你的下一次运货。」
	PRINTFORMW 「嘛，别担心竞争。」
	PRINTFORMW 「没多少伐木工能在妖精和妖怪横行的森林里伐木。」
	PRINTFORMW 「我指望你了，听见了吗？」
	PRINTL
	CALL LOST_ITEM("原木", 1, 1)
	CALL GET_MONEY_YEN(売値, 2)
	FLAG:原木種類 = 0
	CALL WOOD_V_RESET
	ループ数 = 1
ENDIF

gLineCount = LINECOUNT
$LOOP_SELECT
SIF LINECOUNT - gLineCount > 0
	CLEARLINE LINECOUNT - gLineCount
IF ループ数
	PRINTL 
	;取引额超过限制时
	IF AMOUNTPAID_V > 300000
		PRINTFORMW 「好了，我想我们今天的生意就到此为止了。」
	ELSE
		PRINTFORMW 「还有什么我能为你做的吗？」
	ENDIF
ELSE
	;平常来店
	PRINTL 
	IF DAY:0 - FLAG:最後に木材商に訪れた日 > 28
		PRINTFORMW 「喔、是你啊。我还以为你已经从樵夫的工作里金斧洗手了」
	ELSEIF DAY:0 - FLAG:最後に木材商に訪れた日 > 14
		PRINTFORMW 「喔、好久不见了」
	ELSEIF FLAG:最後に木材商に訪れた日 == DAY:0
		PRINTFORMW 「什么、忘了东西？　真是个粗心的家伙啊」
	ELSE
		PRINTFORMW 「喔、今日有何贵干？」
	ENDIF
ENDIF

ループ数 ++
FLAG:最後に木材商に訪れた日 = DAY:0

PRINTFORML 　　所持金: %金額表示(MONEY)%
DRAWLINE

CALL ASK_M("出售原木", ITEM:原木, "出售木柴", ITEM:木材, "出售竹子", ITEM:青竹, "出售木料", 3, "购买木料", 4, "关于交易", 5, "人里木材市场状况", 6, "离开", 7)
リザルト保管 = RESULT

SELECTCASE リザルト保管
	;原木を売る
	CASE 0
		;依頼の報酬など、伐採以外で原木を入手してしまった場合の退避用
		SIF !FLAG:原木種類
			FLAG:原木種類 = 1
		CALL WoodData(WOOD_NUM(FLAG:原木種類))
		売値 = WOOD_VALUE(FLAG:原木種類)
		PRINTFORMW 「那么，让我看看你这次带来的货物。」
		SETCOLOR 0x00FF00

		PRINTFORMD 「
		PRINTFORM %WOOD_NAME(FLAG:原木種類)%
		PRINTFORMD ，是吧……它值
		PRINTFORM %金額表示(売値)%
		RESETCOLOR
		PRINTFORMW ，您意下如何？」
		CALL ASK_YN
		IF !RESULT
			PRINTFORMW 「合作愉快。那么，期待您下次造访。」
			PRINTL 
		ELSE
			PRINTFORMW 「怎么，不满意这个报价？恐怕我不能再给你加价了。」
			PRINTL 
			CALL WOOD_V_RESET
			GOTO LOOP_SELECT
		ENDIF
		PRINTFORMW 金钱：%金額表示(MONEY)% → %金額表示(MONEY + 売値)%
		MONEY += 売値
		;取引額リミッターにも加算
		AMOUNTPAID_V += 売値
		CALL LOST_ITEM("原木", 1, 1)
		;製材の在庫に売却物を追加
		WOODYARD_V:(WOOD_NUM(FLAG:原木種類)) += (1 + FLAG_TO_LOGSIZE(FLAG:原木種類) * 2)
		FLAG:原木種類 = 0
		CALL WOOD_V_RESET
	;木材/青竹を売る
	CASE 1, 2
		IF リザルト保管 == 1
			候補個数 = ITEM:木材
			候補番号 = 610
			売値 = ITEMPRICE:候補番号 / 2
		ELSEIF リザルト保管 == 2
			候補個数 = ITEM:青竹
			候補番号 = 645
			CALL WoodData(12)
			売値 = WOOD_VALUE(12)
		ENDIF
		PRINTFORM 「我愿意以每件%金額表示(売値)%的价格购买%ITEMNAME_TR(候補番号)%，您意下如何？」
		CALL COM623_SELL(候補個数)
		$INPUT_LOOP01
		INPUT
		SELECTCASE RESULT
			CASE 0
				PRINTL 
				GOTO LOOP_SELECT
			CASE 1 TO 候補個数
				CLEARLINE 2
				売却個数 = RESULT
				PRINTFORML 「好的，那么就以%金額表示(売値 * 売却個数)%的价格购买%TOKANJI(売却個数)%件%ITEMNAME_TR(候補番号)%。」
				PRINTFORMW 金钱：%金額表示(MONEY)% → %金額表示(MONEY + (売値 * 売却個数))%
				MONEY += (売値 * 売却個数)
				;取引額リミッターにも加算
				AMOUNTPAID_V += (売値 * 売却個数)
				CALL LOST_ITEM(ITEMNAME:候補番号, 売却個数, 1)
				IF リザルト保管 == 2
					WOODYARD_V:12 += 売却個数
					CALL WOOD_V_RESET
				ENDIF
				PRINTL 
				GOTO LOOP_SELECT
			CASEELSE
				CLEARLINE 1
				GOTO INPUT_LOOP01
		ENDSELECT
	;製材を売る／買う
	CASE 3, 4
		IF リザルト保管 == 3
			;取引額リミッター、暫定30万円 １日の終わりにリセット
			IF AMOUNTPAID_V > 300000
				PRINTFORMW 「哇哦，我今天没法收购任何东西了。明天再来吧！」
				GOTO LOOP_SELECT
			ENDIF
			PRINTFORML 「你要卖我什么木料？」
			CALL PLANK_LIST(1)
		ELSEIF リザルト保管 == 4
			PRINTFORML 「你想要什么木料？」
			CALL PLANK_LIST(0)
		ENDIF
		消去行数 = RESULT
		$INPUT_LOOP02
		INPUT
		候補番号 = RESULT
		CALL WoodData(候補番号)
		IF 候補番号 == 0
			PRINTL 
			CALL WOOD_V_RESET
			GOTO LOOP_SELECT
		;売り時の所有材ゼロ、買い時の在庫ゼロ、登録外、香木の売り買いを弾く
		ELSEIF (リザルト保管 == 3 && LOG_SALES:候補番号 <= 0) || (リザルト保管 == 4 && WOODYARD_V:候補番号 <= 0) || 候補番号 > 50 || WoodName == "" || 候補番号 == 13
			CLEARLINE 1
			CALL WOOD_V_RESET
			GOTO INPUT_LOOP02
		ENDIF
		CLEARLINE 消去行数 + 1
		DRAWLINE
		IF リザルト保管 == 3
			候補個数 = LOG_SALES:候補番号
			PRINTFORML 「我愿意以每件%金額表示(PLANK_VALUE(候補番号, 1, 0, WOODYARD_V:候補番号))%的价格购买%WoodName%木料，您意下如何？」
			CALL COM623_SELL(候補個数, 1)
		ELSEIF リザルト保管 == 4
			候補個数 = WOODYARD_V:候補番号
			PRINTFORML 「%WoodName%木料每件%金額表示(PLANK_VALUE(候補番号, 0, 0, WOODYARD_V:候補番号))%。你想要多少？」
			CALL COM623_SELL(候補個数, 0)
		ENDIF
		
		$INPUT_LOOP03
		INPUT
		SELECTCASE RESULT
			CASE 0
				PRINTL 
				CALL WOOD_V_RESET
				GOTO LOOP_SELECT
			CASE 1 TO 候補個数
				CLEARLINE 2
				売却個数 = RESULT
				IF リザルト保管 == 3
					PRINTFORML 「那么我将以%金額表示(PLANK_BARTERPRICE(候補番号, 売却個数, 1))%的价格购买%TOKANJI(売却個数)%件%WoodName%，可以吗？」
					CALL ASK_YN
					IF !RESULT
						PRINTFORMW 金钱：%金額表示(MONEY)% → %金額表示(MONEY + PLANK_BARTERPRICE(候補番号, 売却個数, 1))%
						MONEY += PLANK_BARTERPRICE(候補番号, 売却個数, 1)
						;取引額リミッターにも加算
						AMOUNTPAID_V += PLANK_BARTERPRICE(候補番号, 売却個数, 1)
						CALL PLANK_BARTER(候補番号, 売却個数, 1, 2)
					ELSE
						CLEARLINE 6
						CALL COM623_SELL(候補個数, 1)
						GOTO INPUT_LOOP03
					ENDIF
				ELSE
					IF MONEY >= PLANK_BARTERPRICE(候補番号, 売却個数, 0)
						PRINTFORML 「那么你想要%TOKANJI(売却個数)%件%WoodName%，是吧？我一共算你%金額表示(PLANK_BARTERPRICE(候補番号, 売却個数, 0))%，可以吗？」
						CALL ASK_YN
						IF !RESULT
							CALL PAY_MONEY_YEN(PLANK_BARTERPRICE(候補番号, 売却個数, 0), 1)
							CALL PLANK_BARTER(候補番号, 売却個数, 0, 2)
						ELSE
							CLEARLINE 6
							CALL COM623_SELL(候補個数, 0)
							GOTO INPUT_LOOP03
						ENDIF
					;お金が足りないよ
					ELSE
						PRINTFORMW 「那么你想要%TOKANJI(売却個数)%件%WoodName%，是吧？总共是%金額表示(PLANK_BARTERPRICE(候補番号, 売却個数, 0))%……嗯，怎么，你钱包里钱不够吗？」
						CLEARLINE 3
						CALL COM623_SELL(候補個数, 0)
						GOTO INPUT_LOOP03
					ENDIF
				ENDIF
				PRINTL 
				CALL WOOD_V_RESET
				GOTO LOOP_SELECT
			CASEELSE
				CLEARLINE 1
				GOTO INPUT_LOOP03
		ENDSELECT

	;取引内容を確認する
	CASE 5
		PRINTFORMW 「你带来的所有原木和竹子我们都收。」
		PRINTFORMW 「嗯，有多余的散料和木柴，我们也会收。不过价格不会太高。」
		PRINTFORMW 「我还张贴了一张关于人里木材贸易现状的告示，随时都可以来看看。」
		PRINTFORMW 「我也卖/买木料，所以如果你需要的话告诉我。根据我们的库存量，也许能给你个好价钱。」
		PRINTFORMW 「……呃，家具呢？不，那不是我的专业领域。你得去找个家具匠。」
		PRINTFORMW 「他就住在这人里，一个脾气古怪的家伙，但他很懂行。」
		GOTO LOOP_SELECT
	CASE 6
		PRINTFORMW 有一张告示显示了人里木材交易的数量……
		CALL WOODYARD_RATE()
		GOTO LOOP_SELECT
	CASE 7
		PRINTL 
		PRINTFORMW 「好嘞，那回见。」
		DEBUGPRINTFORML {AMOUNTPAID_V}
		PRINTL 
		TIME += ループ数 * 5
		RETURN -1
ENDSELECT
GOTO LOOP_SELECT

;-------------------------------------------------
;実行可能判定
;-------------------------------------------------
;木材商
@COM_ABLE623
;一括管理
SIF GLOBAL_COMABLE(623)
	RETURN RESULT

IF !AT_HOME(MASTER)
	SIF CFLAG:MASTER:現在位置 != 商家町
		RETURN 0
ELSE
	;北通り
	SIF CFLAG:MASTER:現在位置 != P205北通り
		RETURN 0
ENDIF
;睡眠中
SIF CFLAG:MASTER:睡眠 || CFLAG:MASTER:イタズラ || CFLAG:うふふ ;custom code
	RETURN 0
;時間停止中
SIF FLAG:70
	RETURN 0
RETURN 1


;-------------------------------------------------
;原木の売り価格を設定する関数
;FLAG_L …FLAG:原木種類
;Plain = 0 口八丁補正込み、省略時 1= 口八丁補正除外
;QTY = 0 在庫数の売却値を返す(省略時) 1～ 外部からの指定数
;-------------------------------------------------
@WOOD_VALUE(FLAG_L, Plain = 0, QTY = 0)
#FUNCTION
#DIM FLAG_L
#DIM Plain
#DIM Num
#DIM Price
#DIM QTY
#DIM 代入

Price = 0
代入 = 0
Num = WOOD_NUM(FLAG_L)

;里
IF WOODYARD_TYPE() == 0
	IF QTY
		代入 = MAX(QTY, 0)
	ELSE
		代入 = WOODYARD_V:Num
	ENDIF
	SELECTCASE 代入
		CASE IS < 0
			Price = WoodValue_Log * 3
		CASE 0, 1, 2
			Price = WoodValue_Log * 3
		;市場調整　(基本価格*16)÷(1 + √(在庫+1)*3)
		CASEELSE
			Price = (WoodValue_Log * 16) / (1 + (SQRT(代入 + 1) * 3))
	ENDSELECT
;山
ELSEIF WOODYARD_TYPE() == 1
	IF QTY
		代入 = MAX(QTY, 0)
	ELSE
		代入 = WOODYARD_M:Num
	ENDIF
	SELECTCASE 代入
		CASE IS < 0
			Price = WoodValue_Plank * 3
		CASE 0, 1, 2
			Price = WoodValue_Plank * 3
		CASEELSE
			Price = (WoodValue_Plank * 16) / (1 + (SQRT(代入 + 1) * 3))
	ENDSELECT
ENDIF

Price = Price - (Price % 100)

SIF Price < 1
	Price = 1

;巨木・大木補正
IF FLAG_L > 200
	TIMES Price, 5.0
ELSEIF FLAG_L > 100
	TIMES Price, 3.0
ENDIF

;口八丁補正
IF Plain == 0
	SELECTCASE (ABL:MASTER:話術技能 * 4 + ABL:MASTER:教養)
		CASE 0 TO 10

		CASE 11 TO 20
			TIMES Price, 1.04
		CASE 21 TO 30
			TIMES Price, 1.07
		CASEELSE
			TIMES Price, 1.10
	ENDSELECT
	Price = Price - (Price % 100)
ENDIF
RETURNF Price

;-------------------------------------------------
;製材の売り／買い価格を設定する関数
;W_ID …木材種
;Sales  =0 製材購入時（省略時） =1 製材売却時
;Plain = 0 口八丁補正込み（省略時） 1= 口八丁補正除外
;QTY = 0 在庫数の売却値を返す(省略時) 1～ 外部からの指定数
;-------------------------------------------------
@PLANK_VALUE(W_ID, Sales = 0, Plain = 0, QTY = 0)
#FUNCTION
#DIM W_ID
#DIM Sales
#DIM Plain
#DIM Price
#DIM QTY

;口八丁補正を省いた原木売買正規額をベースに算出
Price = WOOD_VALUE(W_ID, 1, QTY)

;里…原木売り値の1.5倍端数切捨て
;山…原木売り値の1.6倍端数切捨て
IF WOODYARD_TYPE() == 0
	TIMES Price, 1.50
ELSEIF WOODYARD_TYPE() == 1
	TIMES Price, 1.60
ENDIF
;製材の一本単価
Price = Price - (Price % 100)

;製材買い
IF Sales == 0
	;口八丁補正で減額
	IF Plain == 0
		SELECTCASE (ABL:MASTER:話術技能 * 4 + ABL:MASTER:教養)
			CASE 0 TO 10
				TIMES Price, 0.99
			CASE 11 TO 20
				TIMES Price, 0.97
			CASE 21 TO 30
				TIMES Price, 0.95
			CASEELSE
				TIMES Price, 0.93
		ENDSELECT
		Price = Price - (Price % 100)
	ENDIF
;製材売り
ELSEIF Sales == 1
	;里の売値は製材価格の0.65倍
	IF WOODYARD_TYPE() == 0
		TIMES Price, 0.65
	;山の売値は製材価格の0.6倍
	ELSEIF WOODYARD_TYPE() == 1
		TIMES  Price, 0.6
	ENDIF
	;口八丁補正で増額
	IF Plain == 0
		SELECTCASE (ABL:MASTER:話術技能 * 4 + ABL:MASTER:教養)
			CASE 0 TO 10

			CASE 11 TO 20
				TIMES Price, 1.02
			CASE 21 TO 30
				TIMES Price, 1.04
			CASEELSE
				TIMES Price, 1.06
		ENDSELECT
		Price = Price - (Price % 100)
	ENDIF
ENDIF
RETURNF MAX(Price, 100) ;bugfix custom code, minimum price

;-------------------------------------------------
;材木の取引場所タイプを現在位置から算出
;Type  =1 外部から山の取引場所を参照したいとき 省略時は0
;RETURNF = 0 里 =1 山
;-------------------------------------------------
@WOODYARD_TYPE(Type = 0)
#FUNCTION
#DIM Type

SIF Type == 1
	RETURNF 1
IF !AT_HOME(MASTER)
	SIF CFLAG:MASTER:現在位置 == 商家町
		RETURNF 0
	SIF CFLAG:MASTER:現在位置 == 玄武の沢
		RETURNF 1
ELSE
	SIF CFLAG:MASTER:現在位置 == P205北通り
		RETURNF 0
	SIF CFLAG:MASTER:現在位置 == P703玄武の沢
		RETURNF 1
ENDIF
RETURNF 0

;-------------------------------------------------
;材木の取引額の一覧を出す
;Type = 0 里 =1 山
;-------------------------------------------------
@WOODYARD_RATE()
#DIM 在庫
#DIM 消去行数
#DIM 売値
#DIM 製材

消去行数 = 4
PRINTL 
CALL PRINTLINE,"交易所每日现货原木成交价格"
PRINTFORML 　名称　　　　　　　　状态　　　       原木(买入)　　    木料(卖出)
FOR LOCAL, 1, 50
	;香木をスキップ
	SIF LOCAL == 13
		CONTINUE
	CALL WoodData(LOCAL)
	IF WoodName != ""
		売値 = WOOD_VALUE(LOCAL, 1, 0)
		PRINTFORM 　%WoodName, 20, LEFT%
		SELECTCASE WOODYARD_TYPE()
			CASE 0
				CALL PRINT_COLORBAR, WOODYARD_V:LOCAL,50 ,10, UNICODE(0x2584) , UNICODE(0x2584), 0xC0C0C0, 0x404040
				在庫 = WOODYARD_V:LOCAL
			CASE 1
				CALL PRINT_COLORBAR, WOODYARD_M:LOCAL,50, 10, UNICODE(0x2584), UNICODE(0x2584), 0xC0C0C0, 0x404040 
				在庫 = WOODYARD_M:LOCAL
		ENDSELECT
		PRINTFORM 　%金額表示(売値), 8, RIGHT%
		SELECTCASE 在庫
			CASE 0 TO 10
				PRINTFORM 　库存不足
			CASE IS > 30
				PRINTFORM 　库存积压
			CASEELSE
				PRINTFORM 　　　　　 
		ENDSELECT
		PRINTFORM 　／
		製材 = PLANK_VALUE(LOCAL, 0, 1, 0)
		PRINTFORML 　%金額表示(製材), 8, RIGHT%
		CALL WOOD_V_RESET
		消去行数 ++
	ENDIF
NEXT
WAIT
RETURN 消去行数

;-------------------------------------------------
;製材売買のリスト表記
;ARG =0 購入, =1 売却
;-------------------------------------------------
@PLANK_LIST(ARG)
#DIM Yard_Plank
#DIM My_Plank
#DIM Price
#DIM 消去行数

消去行数 = 5
DRAWLINE
PRINTFORM 　名称　　　　　　　　　  所持　　库存　　　
IF ARG == 0
	PRINTFORML 售出
ELSE
	PRINTFORML 买入
ENDIF
Price = 0
FOR LOCAL, 1, 50
	;香木をスキップ
	SIF LOCAL == 13
		CONTINUE
	
	CALL WoodData(LOCAL)
	IF WoodName != ""
		Price = PLANK_VALUE(LOCAL, ARG, 0, 0)
		My_Plank = LOG_SALES:LOCAL
		IF WOODYARD_TYPE() == 0
			Yard_Plank = WOODYARD_V:LOCAL
		ELSEIF WOODYARD_TYPE() == 1
			Yard_Plank = WOODYARD_M:LOCAL
		ENDIF

		PRINTFORM 　
		SIF LOCAL < 10
				PRINTFORM  
		;購入時、在庫製材が0のときと、売却時、所持製材が0か在庫が50のとき
		IF (Yard_Plank <= 0 && ARG == 0) || (Yard_Plank >= 50 && ARG == 1) || (My_Plank <= 0 && ARG == 1) 
			SETCOLOR 0x606060
			PRINTPLAINFORM [{LOCAL}] %WoodName, 16, LEFT%
		ELSE
			PRINTFORM [{LOCAL}]
			PRINTFORM  %WoodName, 16, LEFT%
		ENDIF
		PRINTFORM 　(
		PRINTFORM {My_Plank, 3, RIGHT})　　(
		PRINTFORM {Yard_Plank, 3, RIGHT})　　
		PRINTFORML %金額表示(Price), 8, LEFT%
		RESETCOLOR
		消去行数 ++
	ENDIF
	CALL WOOD_V_RESET
NEXT
CALL WOOD_V_RESET
PRINTFORM 　 
PRINTBUTTON @"[0] 返回", 0
RETURN 消去行数

;-------------------------------------------------
;売却個数表記用
;Sales =0 購入, =1 売却
;-------------------------------------------------
@COM623_SELL(候補個数, Sales = 1)
#DIM 候補個数
#DIM Sales
#DIM OLD_COLOR ;qol custom code, restore previous color

OLD_COLOR = GETCOLOR()
SETCOLOR 0x777777
IF Sales == 1
	PRINTFORML 　选择要出售的木料数量。（1～{候補個数}，0 返回）
ELSE
	PRINTFORML 　选择要购买的木料数量。（1～{候補個数}，0 返回）
ENDIF
RESETCOLOR
PRINTL 
PRINT 　
PRINTBUTTON "[0] 取消", 0
PRINT     
PRINTBUTTON "[1] 1 件", 1
PRINT     
IF 候補個数 >= 5
	PRINTBUTTON "[5] 5 件", 5
	PRINT     
ENDIF
IF 候補個数 >= 10
	PRINTBUTTON "[10] 10 件", 10
	PRINT    
ENDIF
IF 候補個数 >= 50
	PRINTBUTTON "[50] 50 件", 50; bugfix custom code
	PRINT    
ENDIF
PRINTBUTTON @"[{候補個数}] 全部", 候補個数
PRINT    
SETCOLOR OLD_COLOR ;qol custom code

;-------------------------------------------------
;製材売買の製材の出入り
;Sales =0 購入, =1 売却
;PRINT_TYPE 表示形式　0=通常　1=L　2=W
;-------------------------------------------------
@PLANK_BARTER(W_ID, 個数, Sales = 1, PRINT_TYPE)
#DIM W_ID
#DIM 個数
#DIM Sales
#DIM PRINT_TYPE

SELECTCASE Sales
	CASE 0
		LOG_SALES:W_ID += 個数
		CALL COLORMESSAGE(@"获得了{個数}件%WoodName%木料！({(LOG_SALES:W_ID - 個数)} → {LOG_SALES:W_ID})", C_YELLOW, PRINT_TYPE)
		IF WOODYARD_TYPE() == 0
			WOODYARD_V:W_ID -= 個数
			SIF WOODYARD_V:W_ID < 0
				WOODYARD_V:W_ID = 0
				
		ELSEIF WOODYARD_TYPE() == 1
			WOODYARD_M:W_ID -= 個数
			SIF WOODYARD_M:W_ID < 0
				WOODYARD_M:W_ID = 0
		ENDIF
	CASE 1
		LOG_SALES:W_ID -= 個数
		CALL COLORMESSAGE(@"售出了{個数}件%WoodName%木料！({(LOG_SALES:W_ID + 個数)} → {LOG_SALES:W_ID})", C_YELLOW, PRINT_TYPE)
		SIF LOG_SALES:W_ID < 0
			LOG_SALES:W_ID = 0
		
		IF WOODYARD_TYPE() == 0
			WOODYARD_V:W_ID += 個数
		ELSEIF WOODYARD_TYPE() == 1
			WOODYARD_M:W_ID += 個数
		ENDIF
ENDSELECT


;-------------------------------------------------
;製材購入・売却時の費用・売却値を算出
;単純な売却価格×本数にせず、同一店舗内での製材の大量売買による錬金を抑制する
;候補番号 = W_ID
;Sales = 0 購入時 =1 売却時
;-------------------------------------------------
@PLANK_BARTERPRICE(候補番号, 候補個数, Sales)
#FUNCTION
#DIM 候補番号
#DIM 候補個数
#DIM Sales
#DIM Price
#DIM 限度

Price = 0
IF 候補個数 == 1
	IF WOODYARD_TYPE() == 0
		Price += PLANK_VALUE(候補番号, Sales, 0, WOODYARD_V:候補番号)
	ELSE
		Price += PLANK_VALUE(候補番号, Sales, 0, WOODYARD_M:候補番号)
	ENDIF
	RETURNF Price
ENDIF
;購入時
IF Sales == 0
	;購入後に減少した在庫数を確認
	IF WOODYARD_TYPE() == 0
		限度 = WOODYARD_V:候補番号 - 候補個数 + 1
		;購入後数+1（限度）～現在庫まで回して、費用を合算
		FOR LOCAL, 限度, ((WOODYARD_V:候補番号) + 1)
			Price += PLANK_VALUE(候補番号, Sales, 0, LOCAL)
		NEXT
	ELSE
		限度 = WOODYARD_M:候補番号 - 候補個数 + 1
		FOR LOCAL, 限度, ((WOODYARD_M:候補番号) + 1)
			Price += PLANK_VALUE(候補番号, Sales, 0, LOCAL)
		NEXT
	ENDIF
;売却時
ELSE
	;売却後に増加した在庫数を確認
	IF WOODYARD_TYPE() == 0
		限度 = WOODYARD_V:候補番号 + 候補個数 
		;現在庫～売却後数（限度）-1まで回して、費用を合算
		FOR LOCAL, WOODYARD_V:候補番号, 限度
			Price += PLANK_VALUE(候補番号, Sales, 0, LOCAL)
		NEXT
	ELSE
		限度 = WOODYARD_M:候補番号 + 候補個数 
		FOR LOCAL, WOODYARD_M:候補番号, 限度
			Price += PLANK_VALUE(候補番号, Sales, 0, LOCAL)
		NEXT
	ENDIF
ENDIF
RETURNF Price

;-------------------------------------------------
;材木市場の経時在庫変化
;AFTERTRAで実行
;-------------------------------------------------
@WOODYARD_PROGRESS()
#DIM 差
#DIM 融通市場
#DIM 融通量

;支払額リミッターをリセット
AMOUNTPAID_V = 0
AMOUNTPAID_M = 0

FOR LOCAL, 1, 50
	;香木をスキップ
	SIF LOCAL == 13
		CONTINUE
	CALL WoodData(LOCAL)
	IF WoodName != ""
		;里
		WOODYARD_V:LOCAL -= WOODYARD_SALES(WOODYARD_V:LOCAL, LOCAL)
		WOODYARD_V:LOCAL += WOODYARD_SUPPLY(WOODYARD_V:LOCAL, LOCAL, 0)

		;山
		WOODYARD_M:LOCAL -= WOODYARD_SALES(WOODYARD_M:LOCAL, LOCAL)
		WOODYARD_M:LOCAL += WOODYARD_SUPPLY(WOODYARD_M:LOCAL, LOCAL, 1)

		SIF WOODYARD_V:LOCAL < 0
			WOODYARD_V:LOCAL = 0
		SIF WOODYARD_M:LOCAL < 0
			WOODYARD_M:LOCAL = 0
			
		融通市場 = 0
		;在庫調整 在庫量に差が大きければ調整する 交易稼ぎを(ある程度)抑制
		差 = WOODYARD_M:LOCAL - WOODYARD_V:LOCAL
		IF 差 < 0
			差 = ABS(差)
			融通市場 = 1
		ENDIF
		SELECTCASE 差
			CASE 6 TO 10
				融通量 = MAX(RAND:5, 1)
			CASE 11 TO 20
				融通量 = MAX(RAND:9, 2)
			CASE 21 TO 30
				融通量 = MAX(RAND:13, 3)
			CASE 31 TO 40
				融通量 = MAX(RAND:17, 4)
			CASE 41 TO 50
				融通量 = MAX(RAND:21, 5)
			CASEELSE
				融通量 = 0
		ENDSELECT
		IF 融通市場 == 1
			WOODYARD_V:LOCAL -= 融通量
			WOODYARD_M:LOCAL += 融通量
		ELSE
			WOODYARD_V:LOCAL += 融通量
			WOODYARD_M:LOCAL -= 融通量
		ENDIF
		
		SELECTCASE WOODYARD_V:LOCAL
			CASE IS < 0
				WOODYARD_V:LOCAL = 0
			CASE IS > 50
				WOODYARD_V:LOCAL = 50
		ENDSELECT

		SELECTCASE WOODYARD_M:LOCAL
			CASE IS < 0
				WOODYARD_M:LOCAL = 0
			CASE IS > 50
				WOODYARD_M:LOCAL = 50
		ENDSELECT
		DEBUGPRINTFORML  Village %WoodName% Stock：{WOODYARD_V:LOCAL}
		DEBUGPRINTFORML Mountain %WoodName% Stock：{WOODYARD_M:LOCAL}
	ENDIF
	CALL WOOD_V_RESET
NEXT
CALL WOOD_V_RESET

;-------------------------------------------------
;材木市場の在庫売却数を返す
;market …WOODYARD_V か WOODYARD_M
;W_ID … LOCAL
;-------------------------------------------------
@WOODYARD_SALES(market, W_ID)
#FUNCTION
#DIM market
#DIM W_ID
#DIM SalesNum

SalesNum = 0
SELECTCASE market
	CASE IS < 0
		RETURNF 0
	CASE IS == 0
		RETURNF 0
	CASE IS <= 3
		SalesNum = RAND:2
	CASEELSE
		;台風通過後・二つ玉低気圧通過後・スーパーセル通過後は買い付け増加
		IF GET_LOCATION_LOW() == "台風一過" || GET_LOCATION_LOW() == "二つ玉低気圧通過" || (GET_LOCATION_CUMULUS() == "積乱雲通過" && CAN_CUMULUS:0 == 5)
			SalesNum = RAND:(market * 9 / 10)
		ELSE
			SalesNum = RAND:(market * 2 / 3)
		ENDIF
ENDSELECT
RETURNF SalesNum
;材種ごとに買い付け数が変わる瞬間も作りたい

;-------------------------------------------------
;材木市場の在庫供給数を返す
;market …WOODYARD_V か WOODYARD_M
;W_ID … LOCAL
;Type …WOODYARD_Vなら0  WOODYARD_Mなら1
;-------------------------------------------------
@WOODYARD_SUPPLY(market, W_ID, Type)
#FUNCTION
#DIM market
#DIM W_ID
#DIM Type
#DIM S_Num

S_Num = 0
SELECTCASE market
	CASE IS < 0
		RETURNF 10
	CASE IS == 0
		RETURNF RAND:10
	CASE IS > 49
		RETURNF 0
	CASEELSE
		S_Num = RAND:4
ENDSELECT

SIF STRCOUNT(WoodTribe, "軟質/")
	S_Num += RAND:6
SIF STRCOUNT(WoodTribe, "中硬/")
	S_Num += RAND:4
SIF STRCOUNT(WoodTribe, "硬質/")
	S_Num += RAND:2
;山は供給多めで
SIF Type
	S_Num += RAND:3
RETURNF S_Num
