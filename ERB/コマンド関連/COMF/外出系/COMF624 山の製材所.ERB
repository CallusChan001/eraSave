;山の製材所
@COM624
#DIM 売値
#DIM ループ数
#DIM リザルト保管
#DIM 候補番号
#DIM 候補個数
#DIM 売却個数
#DIM 消去行数

ループ数 = 0

;開店時間・閉店時間
IF INRANGE(TIME:0, 540, 1200) == 0 ;logging tweak
	PRINTFORMW 不在营业时间
	RETURN -1
ENDIF

;初回イベント
IF FLAG:最後に製材所に訪れた日 == 0
	;初回イベント前は、原木持ちでなければ店にも入れない
	IF ITEM:原木 == 0
		PRINTFORMW 现在来这里也没什么用…先去伐点原木再来吧
		RETURN -1
	ENDIF
	;依頼の報酬など、伐採以外で原木を入手してしまった場合の退避用
	SIF !FLAG:原木種類
		FLAG:原木種類 = MAX(RAND:12, 1)
	
	LOCALS '= @"「终于见面了。很高兴见到你，盟友！」/"
	SIF FLAG:なりきり == 0
		LOCALS += @"「毕竟，我听说了你很多事。听说有个奇怪的外来者四处游荡，做着各种各样的事情。」/"
	IF ABL:[[にとり]]:親密 >= 3
		LOCALS += @"「看来也有河童同伴很照顾你。」/"
		LOCALS += @"「我听说你对生意非常有益——……咳咳，我的意思是，你是个又好又值得信赖的人！」/"
	ENDIF
	LOCALS += @"「那么，我能为你做些什么呢？」"
	CALL COM624_KAPPASPEAK(LOCALS, "smile") ;logging tweak
	WAIT
	LOCALS '= @"「嘿嘿，所以你想让我把原木给你锯成木料？没问题，这是我的收费——」"
	CALL COM624_KAPPASPEAK(LOCALS)
	WAIT
	PRINTFORMW 滋滋、河童把显示平板亮了出来…上面说明了交易项目……
	PRINTFORMW 等等，这不比人里制材的行情便宜得多吗？
	PRINTFORMW 最重要的是，他们似乎也接受筹码作为支付方式。
	LOCALS '= @"「没错，那是当然！全自动机械化锯木厂怎么可能输给人工呢？」"
	CALL COM624_KAPPASPEAK(LOCALS, "smile")
	WAIT
	LOCALS '= @"「……话虽如此，现在寺庙建造和神社修复工作已经完成……」/"
	LOCALS += @"「日常来说有点大材小用，接到的订单太少了/"
	LOCALS += @"　我们花了那么多时间制作的机器，大部分时间都闲置着。」"
	CALL COM624_KAPPASPEAK(LOCALS)
	WAIT
	LOCALS '= @"「哦，对了！你说，如果你答应成为我的常客，/"
	LOCALS += @"　今天第一次服务就完全免费！意下如何？」"
	CALL COM624_KAPPASPEAK(LOCALS, "smile")
	WAIT
	PRINTFORML ……那不就是炫耀你的机器吗？差点脱口而出，
	PRINTFORMW 但你还是决定收下这份好意。
	LOCALS '= @"「那就这么定了！现在，请把原木放在那边……」/"
	LOCALS += @"「好嘞，开始了！开机！全速！呼呼呼，我的机器咆哮吧！见识河童的科技实力！」"
	CALL COM624_KAPPASPEAK(LOCALS, "smile")
	WAIT
	PRINTFORMW 姑且不理会这个异常兴奋的河童，机器以极其精确和快速的方式完成了木料的加工。
	PRINTFORMW ………
	PRINTFORMW ……
	CALL WoodData(WOOD_NUM(FLAG:原木種類))
	CALL LogRefine("製材所", FLAG:原木種類)
	PRINTL 
	LOCALS '= @"「是啊，现在都搞定啦！怎么样？！」"
	CALL COM624_KAPPASPEAK(LOCALS, "smile")
	WAIT
	;it's a girl, I don't care what anyone says
	PRINTFORMW 河童摆出一个非常得意和自满的姿势，+++识趣地恭敬赞美她的工作。
	LOCALS '= @"「嘿嘿，很高兴我的工作得到了好评。那么，正如承诺的那样，我将期待你的惠顾！」"
	CALL COM624_KAPPASPEAK(LOCALS, "smile")
	WAIT
	CALL WOOD_V_RESET
	PRINTL 
	ループ数 = 1
ENDIF

gLineCount = LINECOUNT
$LOOP_SELECT
SIF LINECOUNT - gLineCount > 0
	CLEARLINE LINECOUNT - gLineCount

LOCALS:1 = smile
IF ループ数
	PRINTL 
	;取引額リミッターを超えている場合
	IF AMOUNTPAID_M > 300000
		LOCALS '= @"「我得稍微周转一下了…赶明儿再来好吧…」"
		LOCALS:1 =
	ELSE
		LOCALS '= @"「撒撒撒！把下一根原木给我！」"
	ENDIF
ELSE
	;平常来店
	PRINTL 
	IF FLAG:最後に製材所に訪れた日 == DAY:0
		LOCALS '= @"「哦哟？怎么了？」"
		LOCALS:1 =
	ELSE
		LOCALS '= "「嘿，"
		SELECTCASE TIME:0
			CASE 540 TO 600
				LOCALS += @"早上好啊，"
			CASE 601 TO 1020
				LOCALS += @"你好啊，"
			CASEELSE
				LOCALS += @"晚上好啊，"
		ENDSELECT
		LOCALS += @" 我的朋友！」/"
		IF DAY:0 - FLAG:最後に製材所に訪れた日 > 28
			LOCALS '= "「我有一段时间没见到你了。嗐，还有点担心你嘞。」"
			LOCALS:1 =
		ELSEIF DAY:0 - FLAG:最後に製材所に訪れた日 > 14
			LOCALS '= "「好久不见！」"
		ELSE
			LOCALS '= "「今天能为你做些什么呢？」"
		ENDIF
	ENDIF
ENDIF
CALL COM624_KAPPASPEAK(LOCALS, LOCALS:1) ;logging tweak

ループ数 ++
FLAG:最後に製材所に訪れた日 = DAY:0

PRINTFORMDL 　所持金：%金額表示(MONEY)%　筹码：CM%DIGIT_GROUP(MONEY:2)%
RESETCOLOR ;qol custom code
DRAWLINE

CALL ASK_M("木料加工服务", ITEM:原木 || ITEM:青竹, "出售原木", ITEM:原木, "出售木柴", ITEM:木材, "出售竹子", ITEM:青竹, "出售木料", 4, "购买木料", 5, "关于交易", 6, "交易行情", 7, "离开", 8)
リザルト保管 = RESULT

SETCOLOR 路人子店員口上色:7 ;qol custom code
SELECTCASE リザルト保管
	;製材してほしい
	CASE 0
		;依頼の報酬など、伐採以外で原木を入手してしまった場合の退避用
		SIF ITEM:原木 && !FLAG:原木種類
			FLAG:原木種類 = MAX(RAND:12, 1)

		CALL COM624_KAPPASPEAK(, "smile")
		;原木を持っている場合は原木優先で処理
		IF ITEM:原木
			候補番号 = FLAG:原木種類
			CALL WoodData(WOOD_NUM(候補番号))
			候補個数 = 1
		ELSE
			候補番号 = 12
			CALL WoodData(候補番号)
			候補個数 = ITEM:青竹
		ENDIF
		PRINTFORM 「哦哦，加工
        CALL COLORMESSAGE(@"%WOOD_NAME(候補番号)%木料", 0x00FF00, 0)
		PRINTFORM 对吧。那么我给你
        CALL COLORMESSAGE(@"%金額表示(LUMBERING(候補番号, "製材所", 0, 1))%", 0x00FF00, 0)
		SIF 候補番号 == 12 ;bamboo
			PRINTFORM  每件
		PRINTFORML 的价格为你加工。
		PRINTFORM 　哦，如果你更喜欢用筹码支付，那将是
        CALL COLORMESSAGE(@"CM%TOSTR(LUMBERING_CMCOST(候補番号, 1),"#,###")%", 0x00FF00, 0)
		PRINTFORMW 。两种方式都可以！」
		;RESETCOLOR ;qol custom code, disabled
		IF 候補番号 == 12 ;bamboo
			CALL ASK_M("用￥支付", MONEY >= LUMBERING(候補番号, "製材所"), "用筹码支付", MONEY:2 >= LUMBERING_CMCOST(候補番号), "取消", 2, "用￥支付全部", MONEY >= LUMBERING(候補番号, "製材所", 0, 候補個数), "用筹码支付全部", MONEY:2 >= LUMBERING_CMCOST(候補番号, 候補個数))
		ELSE
			CALL ASK_M("用￥支付", MONEY >= LUMBERING(候補番号, "製材所"), "用筹码支付", MONEY:2 >= LUMBERING_CMCOST(候補番号), "取消", 2)
		ENDIF

		リザルト保管 = RESULT
		IF リザルト保管 == 0 || リザルト保管 == 1 || リザルト保管 == 3 || リザルト保管 == 4
			IF リザルト保管 == 0 || リザルト保管 == 3
				;PRINTFORMW 　Money: \\%DIGIT_GROUP(MONEY)% → \\%TOSTR(MONEY - LUMBERING(候補番号, "製材所", 0, 候補個数),"#,###")%
				PRINT 　
				CALL PAY_MONEY_YEN(LUMBERING(候補番号, "製材所", 0, 候補個数), 2)
			ELSE
				;PRINTFORMW 　Charisma: %TOSTR(MONEY:2,"#,###")% → %TOSTR(MONEY:2 - LUMBERING_CMCOST(候補番号, 候補個数),"#,###")%
				PRINT 　
				CALL PAY_MONEY_CM(LUMBERING_CMCOST(候補番号, 候補個数), 2)
			ENDIF
			PRINTL 
			LOCALS '= "「" + SPLIT_G("感谢惠顾～:感谢您一如既往的光顾！:合作愉快～") + "/"
			LOCALS += "　" + SPLIT_G("好嘞，开始了！开机！全速！呼呼呼，我的机器咆哮吧！见识一下河童的技术力吧！:只要把原材料放这里，全自动锯木机就会施展它的魔法！:谢谢惠顾。那么、让你见识一下现代化工厂・自动化生产的神髄！") + "」"
			CALL COM624_KAPPASPEAK(LOCALS, "smile")
			WAIT
			CALL LogRefine("製材所", 候補番号, 候補個数)
			PRINTL 
		ELSEIF リザルト保管 == 2
			LOCALS '= "「呃，我得告诉你，我不接受讨价还价！」"
			CALL COM624_KAPPASPEAK(LOCALS, "anger")
			WAIT
			PRINTL 
			GOTO LOOP_SELECT
		ENDIF
	;出售原木
	CASE 1
		;在委托的报酬等，通过伐木以外方式获得原木时，用于避险
		SIF !FLAG:原木種類
			FLAG:原木種類 = 1
		
		CALL WoodData(WOOD_NUM(FLAG:原木種類))
		売値 = WOOD_VALUE(FLAG:原木種類)
		LOCALS '= "「成交！让我看看你这次带来了什么。」"
		CALL COM624_KAPPASPEAK(LOCALS, "smile")
		WAIT
		;SETCOLOR 0x00FF00 ;qol custom code, disabled

		PRINTFORM 「
        CALL COLORMESSAGE(@"%WOOD_NAME(FLAG:原木種類)%", 0x00FF00, 0)
		PRINTFORM ，是吧……我的报价是
        CALL COLORMESSAGE(@"%金額表示(売値)%", 0x00FF00, 0)
		;RESETCOLOR ;qol custom code, disabled
		PRINTFORMW ，听起来怎么样？」
		CALL ASK_YN
		IF !RESULT
			LOCALS '= "「那我们就成交了！好的，把它装到这边的机器上。」"
			CALL COM624_KAPPASPEAK(LOCALS, "smile")
			WAIT
			PRINTL 
		ELSE
			LOCALS '= "「嗯，我跟你说了，山上的供应远远超过需求。 」/「我想你在人里会卖得高得多。」"
			CALL COM624_KAPPASPEAK(LOCALS)
			WAIT
			CLEARLINE LINECOUNT - gLineCount
			PRINTL 
			CALL WOOD_V_RESET
			GOTO LOOP_SELECT
		ENDIF
		PRINTFORMDW 金钱：%金額表示(MONEY)% → %金額表示(MONEY + 売値)%
		MONEY += 売値
		;取引額リミッターにも加算
		AMOUNTPAID_M += 売値
		CALL LOST_ITEM("原木", 1, 1)
		;库存中增加售出物品
		WOODYARD_M:(WOOD_NUM(FLAG:原木種類)) += (1 + FLAG_TO_LOGSIZE(FLAG:原木種類) * 2)
		FLAG:原木種類 = 0
		CALL WOOD_V_RESET
	;出售木材/青竹
	CASE 2, 3
		IF リザルト保管 == 2
			候補個数 = ITEM:木材
			候補番号 = 610
			売値 = ITEMPRICE:候補番号 / 2
		ELSEIF リザルト保管 == 3
			候補個数 = ITEM:青竹
			候補番号 = 645
			CALL WoodData(12)
			売値 = WOOD_VALUE(12)
		ENDIF
		LOCALS '= @"「你每件%ITEMNAME_TR(候補番号)%我付%金額表示(売値)%喔～！」"
		CALL COM624_KAPPASPEAK(LOCALS, "smile")
		CALL COM623_SELL(候補個数)
		$INPUT_LOOP01
		INPUT
		SELECTCASE RESULT
			CASE 0
				PRINTL 
				GOTO LOOP_SELECT
			CASE 1 TO 候補個数
				CLEARLINE 2
				売却個数 = RESULT
				LOCALS '= @"「好嘞，所以我以%金額表示((売値 * 売却個数))%向你收购%TOKANJI(売却個数)%件%ITEMNAME_TR(候補番号)%！」"
				CALL COM624_KAPPASPEAK(LOCALS, "smile")
				PRINTFORMDW 金钱：%金額表示(MONEY)% → %金額表示(MONEY + (売値 * 売却個数))%
				MONEY += (売値 * 売却個数)
				;取引额限制器也加算
				AMOUNTPAID_M += (売値 * 売却個数)
				CALL LOST_ITEM(ITEMNAME:候補番号, 売却個数, 1)
				IF リザルト保管 == 3
					WOODYARD_M:12 += 売却個数
					CALL WOOD_V_RESET
				ENDIF
				PRINTL 
				GOTO LOOP_SELECT
			CASEELSE
				CLEARLINE 1
				GOTO INPUT_LOOP01
		ENDSELECT
	;出售/购买木料
	CASE 4, 5
		LOCAL:9 = LINECOUNT
		IF リザルト保管 == 4
			;交易额限制器，暂定30万日元，一天结束时重置
			IF AMOUNTPAID_M > 300000
				LOCALS '= @"「哎呀，收购就到此为止了」/「再继续下去会周转不开的，嘿嘿嘿……」"
				CALL COM624_KAPPASPEAK(LOCALS, "smile")
				WAIT
				GOTO LOOP_SELECT
			ENDIF
			LOCALS '= @"「你要卖什么？」"
			CALL COM624_KAPPASPEAK(LOCALS, "smile")
            RESETCOLOR ;qol custom code
			CALL PLANK_LIST(1)
		ELSEIF リザルト保管 == 5
			LOCALS '= @"「你要买什么？」"
			CALL COM624_KAPPASPEAK(LOCALS, "smile")
            RESETCOLOR ;qol custom code
			CALL PLANK_LIST(0)
		ENDIF
		消去行数 = RESULT
        SETCOLOR 路人子店員口上色:7
		$INPUT_LOOP02
		INPUT
		候補番号 = RESULT
		CALL WoodData(候補番号)
		IF 候補番号 == 0
			PRINTL 
			CALL WOOD_V_RESET
			GOTO LOOP_SELECT
		;出售时无持有木材，购买时无库存，未注册，以及拒绝香木的出售/购买
		ELSEIF (リザルト保管 == 4 && LOG_SALES:候補番号 == 0) || (リザルト保管 == 5 && WOODYARD_M:候補番号 <= 0) || 候補番号 > 50 || WoodName == "" || 候補番号 == 13
			CLEARLINE 1
			CALL WOOD_V_RESET
			GOTO INPUT_LOOP02
		ENDIF
		CLEARLINE LINECOUNT - LOCAL:9
        RESETCOLOR ;qol custom code
		DRAWLINE
        SETCOLOR 路人子店員口上色:7 ;qol custom code
		CALL COM624_KAPPASPEAK(, "smile")
		IF リザルト保管 == 4
			候補個数 = LOG_SALES:候補番号
			PRINTFORML 「你每件%WoodName%木料我付%金額表示(PLANK_VALUE(候補番号, 1, 0, WOODYARD_M:候補番号))%！」
			CALL COM623_SELL(候補個数, 1)
		ELSEIF リザルト保管 == 5
			候補個数 = WOODYARD_M:候補番号
			PRINTFORML 「%WoodName%木料每件%金額表示(PLANK_VALUE(候補番号, 0, 0, WOODYARD_M:候補番号))%。要多少？」
			CALL COM623_SELL(候補個数, 0)
		ENDIF
		
		$INPUT_LOOP03
		INPUT
		SELECTCASE RESULT
			CASE 0
				PRINTL 
				CALL WOOD_V_RESET
				GOTO LOOP_SELECT
			CASE 1 TO 候補個数
				CLEARLINE 2
				売却個数 = RESULT
				IF リザルト保管 == 4
					PRINTFORML 「我以%金額表示(PLANK_BARTERPRICE(候補番号, 売却個数, 1))%购买%TOKANJI(売却個数)%件%WoodName%，成交吗？」
					CALL ASK_YN
					IF !RESULT
						PRINTFORMDW 金钱：%金額表示(MONEY)% → %金額表示(MONEY + PLANK_BARTERPRICE(候補番号, 売却個数, 1))%
						MONEY += PLANK_BARTERPRICE(候補番号, 売却個数, 1)
						;交易额限制器也加算
						AMOUNTPAID_M += PLANK_BARTERPRICE(候補番号, 売却個数, 1)
						CALL PLANK_BARTER(候補番号, 売却個数, 1, 2)
					ELSE
						CLEARLINE 6
						CALL COM623_SELL(候補個数, 1)
                        SETCOLOR 路人子店員口上色:7 ;qol custom code
						GOTO INPUT_LOOP03
					ENDIF
				ELSE
					IF MONEY >= PLANK_BARTERPRICE(候補番号, 売却個数, 0)
						PRINTFORML 「是%TOKANJI(売却個数)%件%WoodName%吗？让我算算……总价是%金額表示(PLANK_BARTERPRICE(候補番号, 売却個数, 0))%，成交吗？」
						CALL ASK_YN
						IF !RESULT
							CALL PAY_MONEY_YEN(PLANK_BARTERPRICE(候補番号, 売却個数, 0), 1)
							CALL PLANK_BARTER(候補番号, 売却個数, 0, 2)
						ELSE
							CLEARLINE 6
							CALL COM623_SELL(候補個数, 0)
							GOTO INPUT_LOOP03
						ENDIF
					;钱不够哦
					ELSE
						PRINTFORMW 「是%TOKANJI(売却個数)%件%WoodName%吗？总共是%金額表示(PLANK_BARTERPRICE(候補番号, 売却個数, 0))%……但是看起来你现金不足，对吧，盟友？」
						CLEARLINE 3
						CALL COM623_SELL(候補個数, 0)
						GOTO INPUT_LOOP03
					ENDIF
				ENDIF
				PRINTL 
				CALL WOOD_V_RESET
				GOTO LOOP_SELECT
			CASEELSE
				CLEARLINE 1
				GOTO INPUT_LOOP03
		ENDSELECT
	;确认交易内容
	CASE 6
		LOCALS '= "「我们以合理的价格为你加工原木和竹子！」/"
		LOCALS += "「你说收购原木和木料呢？」/"
		LOCALS += "「你看，山上这种东西供过于求，所以……」/"
		LOCALS += "「当然我们可以收购，但我敢肯定，你到村里去卖可能价格更好。」"
		CALL COM624_KAPPASPEAK(LOCALS, "smile")
		WAIT
		LOCALS '= "「我们可以做家具什么的吗？……不行不行，恐怕不行。那听起来太麻烦了。」/"
		LOCALS += "「我们河童很容易感到无聊，所以不擅长那种事情。」/"
		LOCALS += "「那种复杂的活儿最好由一群专业工匠来做，我想。」/"
		LOCALS += "「如果我们来做的话，很快就会变成典型的河童道具，附带上各种不必要的功能。」"
		CALL COM624_KAPPASPEAK(LOCALS)
		WAIT
		LOCALS '= "「……你会感兴趣吗？嘿嘿，嗯，也许有机会吧……」"
		CALL COM624_KAPPASPEAK(LOCALS, "smile")
		WAIT
		PRINTL 
	CASE 7
        RESETCOLOR ;qol custom code
		PRINTFORMW 这里有一个电子显示屏，显示着交易所的交易行情……
		CALL WOODYARD_RATE()
        SETCOLOR 路人子店員口上色:7 ;qol custom code
		GOTO LOOP_SELECT
	CASE 8
        RESETCOLOR ;qol custom code
		;Custom code, sex with the sawmill kappa.
		RESULT = 0
		SIF CFLAG:51:口上セレクタ == 1
			TRYCALL K51_EGG_SAWMILL_KAPPA_SEX
		SIF !RESULT
		LOCALS '= "「好了，下次再见！」"
		CALL COM624_KAPPASPEAK(LOCALS, "smile")
		WAIT
		DEBUGPRINTFORML {AMOUNTPAID_M}
		PRINTL 
		TIME += ループ数 * 5
		RETURN -1
ENDSELECT
GOTO LOOP_SELECT

;-------------------------------------------------
;実行可能判定
;-------------------------------------------------
;山の製材所
@COM_ABLE624
;一括管理
SIF GLOBAL_COMABLE(624)
	RETURN RESULT

IF !AT_HOME(MASTER)
	SIF CFLAG:MASTER:現在位置 != 玄武之澤
		RETURN 0
ELSE
	;山麓が拠点の場合は、河童のアジト
	SIF CFLAG:MASTER:現在位置 != 703 ;logging custom code, changed to Genbu Ravine because Kappa's Hideout is closed most of the time
		RETURN 0
ENDIF
;睡眠中
SIF CFLAG:MASTER:睡眠 || CFLAG:MASTER:イタズラ || CFLAG:うふふ ;custom code
	RETURN 0
;時間停止中
SIF FLAG:70
	RETURN 0
RETURN 1


;-------------------------------------------------
;製材コストをカリスマに変換する関数
;ARG …FLAG:原木種類
;Num …個数 青竹のまとめて製材時に本数を代入
;-------------------------------------------------
@LUMBERING_CMCOST(FLAG_L, Num = 1)
#FUNCTION
#DIM FLAG_L
#DIM 費用ベース
#DIM Num

費用ベース = LUMBERING(FLAG_L, "製材所", 0, Num)

SELECTCASE 費用ベース
	CASE IS < 100
		費用ベース = 1
	CASE IS < 500
		費用ベース = MAX(費用ベース / 200, 1)
	CASEELSE
		費用ベース = MAX(費用ベース / 300, 2)
ENDSELECT

RETURNF 費用ベース
;-------------------------------------------------
;製材コスト、製材数を割り出す関数
;複数回選べるので、固定値を出す
;FLAG_L	FLAG:原木種類
;Craft	"材木商"、"製材所"
;Type	価格(0)ないし製材数(QTY)
;Num	候補個数、省略時は1個
;-------------------------------------------------
@LUMBERING(FLAG_L, Craft, Type, Num = 1)
#FUNCTION
#DIM FLAG_L
#DIM 費用ベース
#DIM 製材数
#DIMS Craft
#DIMS Type
#DIM Num

IF Type == "QTY"
	;青竹製材は複数製材可
	IF FLAG_L == 12
		製材数 = Num
	ELSE
		製材数 = 1 + FLAG_TO_LOGSIZE(FLAG_L) * 2
	ENDIF
	RETURNF 製材数
ENDIF

SELECTCASE Craft
	CASE "材木商"
		費用ベース = WoodValue_Log / 2
	CASE "製材所"
		費用ベース = WoodValue_Plank / 4
ENDSELECT

費用ベース = (費用ベース - (費用ベース % 100)) * Num

RETURNF 費用ベース

;-------------------------------------------------
;製材結果の表示など
;-------------------------------------------------
@LogRefine(Craft, FLAG_L, Num = 1)
#DIMS Craft
#DIM FLAG_L
#DIM 候補番号
#DIM Num

;青竹製材の場合は12を代入
IF FLAG_L == 12
	候補番号 = 12
ELSE
	候補番号 = FLAG:原木種類
ENDIF
LOCAL = LUMBERING(候補番号, Craft, "QTY", Num)
SETCOLOR 0x00FF00
PRINTFORMW 　%WOOD_NAME(候補番号, 1)%木料: {LOG_SALES:WOOD_NUM(候補番号)} → {LOG_SALES:WOOD_NUM(候補番号) + LOCAL}
LOG_SALES:WOOD_NUM(候補番号) += LOCAL

IF FLAG_L != 12
	ITEM:原木 = 0
	FLAG:原木種類 = 0
ELSE
	IF Num > 1
		ITEM:青竹 = 0
	ELSE
		ITEM:青竹 --
	ENDIF
ENDIF
RESETCOLOR


;logging tweak
;note - needs WAIT for W effect
@COM624_KAPPASPEAK(ARGS, ARGS:1 = "通常")
#LOCALSIZE 1
#LOCALSSIZE 1
SIF GROUPMATCH(ARGS:1, "smile", "笑顔")
	ARGS:1 '= "笑顔"
SIF GROUPMATCH(ARGS:1, "blush", "発情")
	ARGS:1 '= "発情"
SIF GROUPMATCH(ARGS:1, "anger", "憤怒")
	ARGS:1 '= "憤怒"
CALL SPTALK, 0, ARGS:1, 7, ARGS, 1
