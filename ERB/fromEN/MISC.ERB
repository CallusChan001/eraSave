@PRINT_CLIT(ARG = -1)
#LOCALSIZE 1
#LOCALSSIZE 1
#FUNCTIONS
SIF ARG == -1
	ARG = TARGET
SIF HAS_PENIS(ARG)
	RETURNF FSYN_CN("阴茎")
RETURNF SPLIT_G("阴蒂:豆豆")

@PRINT_HASPENIS(ARGS, ARG = -1)
#LOCALSIZE 1
#LOCALSSIZE 1
#FUNCTIONS
SIF ARG == -1
	ARG = TARGET
RETURNF RETURN_CAP(ARGS, HAS_PENIS(ARG) ? SPLIT_SINGLE(TOLOWER(ARGS:0)) # SPLIT_SINGLE(TOLOWER(ARGS:0), 1))

;inside:outside
@PRINT_OUTSIDE(ARGS, ARG = -1)
#LOCALSIZE 1
#LOCALSSIZE 1
#FUNCTIONS
#DIM nRefPoint
;create point of reference
SIF ARG == -1
	ARG = PLAYER
nRefPoint = INROOM(CFLAG:ARG:現在位置)

SIF !STRLENS(ARGS)
	ARGS = floor
IF STRFIND(ARGS, "_") >= 0
	ARGS '= REPLACE(ARGS, "_", "")
	GOTO CUSTOM
ENDIF
	
SELECTCASE SPLIT_SINGLE(TOLOWER(ARGS:0))
	CASE "floor","地板"
		LOCALS '= nRefPoint ? "地板" # "地面"
	CASE "bed","床"
		LOCALS '= nRefPoint ? "床" # "地面"
	CASE "room"
		LOCALS '= nRefPoint ? TOLOWER(ARGS:0) # "place"
	CASE "ceiling","天花板"
		LOCALS '= nRefPoint ? "天花板" # "天空"
CASEELSE
	$CUSTOM
	LOCALS '= nRefPoint ? SPLIT_SINGLE(ARGS:0) # SPLIT_SINGLE(ARGS:0, 1)
ENDSELECT

RETURNF LOCALS


@IS_COM_SEX(ARGS)
#LOCALSIZE 1
#LOCALSSIZE 1
#FUNCTION
IF GROUPMATCH(ARGS, "tflag", "tflagv", "tflaga")
	SIF ARGS == "tflagv" || ARGS == "tflag"
		RETURNF IS_V_INSERT_COM(TFLAG:3)
	SIF ARGS == "tflaga" || ARGS == "tflag"
		RETURNF IS_A_INSERT_COM(TFLAG:3)
ENDIF

SIF ARGS == "v" || ARGS == ""
	;RETURNF IS_SELECTCOM("正常位/後背位/騎乗位/対面座位/背面座位/二穴挿し/挿入Ｇスポット責め/挿入子宮口責め") || GROUPMATCH(TCVAR:カウンター行動, 80, 83, 85)
	RETURNF GROUPMATCH(SELECTCOM, 60, 61, 65, 67, 68, 71, 72, 73) || GROUPMATCH(TCVAR:カウンター行動, 80, 83, 85)
SIF ARGS == "a" || ARGS == ""
	;RETURNF IS_SELECTCOM("正常位アナル/後背位アナル/騎乗位アナル/対面座位アナル/背面座位アナル/Ｓ状結腸責め/膣裏責め") || GROUPMATCH(TCVAR:カウンター行動, 87)
	RETURNF GROUPMATCH(SELECTCOM, 62, 63, 66, 69, 70, 71, 74, 75) || GROUPMATCH(TCVAR:カウンター行動, 87)

@IS_V_INSERT_COM(ARG)
#LOCALSIZE 1
#LOCALSSIZE 1
#FUNCTION
RETURNF GROUPMATCH(ARG, 60, 61, 65, 67, 68, 71, 72, 73)

@IS_A_INSERT_COM(ARG)
#LOCALSIZE 1
#LOCALSSIZE 1
#FUNCTION
RETURNF GROUPMATCH(TFLAG:3, 62, 63, 66, 69, 70, 71, 74, 75)
;title name, takes care of the "the"-part as well
;the titles are unique
;ARG - character ID
;ARG:1 - capital letter at start
;ARG:2 - removes the "the" 
;ARG:3 - genetive case
@TNAME(ARG, ARG:1 = 0, ARG:2 = 0, ARG:3 = 0)
#LOCALSIZE 1
#LOCALSSIZE 2
#FUNCTIONS
;if a dialogue uses custom titles, use those instead
IF STRLENS(CUSTOM_TITLE:ARG:CT_TITLES)
	LOCALS:1 = %SPLIT_G(CUSTOM_TITLE:ARG:CT_TITLES)%
	SIF CUSTOM_TITLE:ARG:CT_EXCLUDED_THE != "" && STRCOUNT(LOCALS:1, CUSTOM_TITLE:ARG:CT_EXCLUDED_THE)
		ARG:2 = 1
ELSE
	;grab the title, check against the list of exceptions to exclude "the" if needed
	LOCALS:1 = %SPLIT_G(TITLES(ARG))%
ENDIF
{
SIF GROUPMATCH(LOCALS:1, "One of the Big Four", "Hell's Traffic Accident", "Shuten-Douji of the Gourd Pillow", "Old Youkai #1", "Shoutoku's Prized Steed"
    , "Ibaraki-Douji", "Nega-Kasen", "Arm-chan", "[No Title]", "The Mighty of the Big Four", "The Seventh", "One of the Eight Great Youths"
	, "Suzunaan's Girl with the Deciphering Eye", "Geidontei's Sake-Serving Poster Girl")
}
	ARG:2 = 1
;----------------------------
LOCALS = 
IF ARG:1
	LOCALS = The 
ELSE
	LOCALS = the 
ENDIF
IF ARG == RANDOM_CHARANUM
	;might be better to use PRINT_GET_RACE to supplement?
	IF TALENT:ARG:水棲
		LOCALS:1 = Kappa
	ELSEIF TALENT:ARG:妖精
		LOCALS:1 = Fairy
	ELSEIF TALENT:ARG:妖怪 == 5
		LOCALS:1 = Tengu
	ELSEIF TALENT:ARG:動物耳 == 2
		LOCALS:1 = Rabbit
	ELSEIF TALENT:ARG:妖怪 == 9
		LOCALS:1 = %SPLIT_G("Demon:Devil")%
	ELSEIF TALENT:ARG:妖怪 == 2
		LOCALS:1 = Oni
	ELSEIF TALENT:ARG:妖狐
		LOCALS:1 = Kitsune
	ELSEIF TALENT:ARG:動物耳 == 4
		LOCALS:1 = Cat
	ELSEIF GROUPMATCH(CSTR:ARG:モブ子種族, "化け狸", "茨たぬき")
		LOCALS:1 = Tanuki
	ELSEIF TALENT:ARG:人妻
		LOCALS:1 = Woman
	ELSE
		LOCALS:1 = Girl
	ENDIF
ENDIF
IF ARG:3 && LOCALS:1 != ""
	IF TOLOWER(SUBSTRINGU(LOCALS:1, STRLENSU(LOCALS:1)-1)) == "s"
		LOCALS:1 += "'"
	ELSE
		LOCALS:1 += "'s"
	ENDIF
ENDIF

SIF ARG:2
	RETURNF LOCALS:1
RETURNF @"%LOCALS% %LOCALS:1%"


;--------------------------------------------------------
;When I want to have BREAKENG applied on the text without moving the entire text into LOCALS first
;First we need to prepare the text by removing line breaks from PRINT instruction
;use this regex PRINT(|FORM)(L|W) (.*) and replace with PRINTFORM $3
;all line breaks will be replaced with <br>, additional line breaks can be inserted the same way
;if there's any kind of WAIT in the text, use that line as the last one and then put WAIT after this function
;colored text is not supported
;--------------------------------------------------------
@PRINT_BREAK(ARGS = "\n", ARGS:1)
#LOCALSIZE 1
#LOCALSSIZE 1
#DIMS DYNAMIC PARTS, 100
#DIMS DYNAMIC strFlag, 100
#DIM DYNAMIC nLength
LOCALS '= HTML_TOPLAINTEXT(HTML_POPPRINTINGSTR())
SPLIT LOCALS, "<br>", PARTS

FOR LOCAL, 0, RESULT
	SIF STRLENS(PARTS:LOCAL) == 0
		CONTINUE
		
	VARSET nLength
	;process the flags
	IF STRCOUNT(PARTS:LOCAL, "<wait>")
		PARTS:LOCAL '= REPLACE(PARTS:LOCAL, "<wait>", "")
		strFlag:LOCAL = wait:
	ENDIF
	IF STRCOUNT(PARTS:LOCAL, "<right>")
		PARTS:LOCAL '= REPLACE(PARTS:LOCAL, "<right>", "")
		ALIGNMENT RIGHT
	ENDIF
	;cut a little shorter if there are double-width characters, add on demand
	SIF STRCOUNT(PARTS:LOCAL, "↓|♪")
		nLength = MAXWIDTH()-10
	SIF SPLIT_CHECK(ARGS:1, "br") && LOCAL == 0 ;break line at the beginning
		PRINTL
	IF SPLIT_CHECK(ARGS:1, "dc")
		PRINTSDL BREAKENG(PARTS:LOCAL, , ARGS, nLength)
	ELSEIF SPLIT_CHECK(ARGS:1, "d")
		CALL PRINT_DIALOGUE(PARTS:LOCAL)
	ELSEIF SPLIT_CHECK(ARGS:1, "noLB")
		PRINTS BREAKENG(PARTS:LOCAL, , ARGS, nLength)
	ELSE
		PRINTSL BREAKENG(PARTS:LOCAL, , ARGS, nLength)
	ENDIF
	ALIGNMENT LEFT
	IF SPLIT_CHECK(strFlag:LOCAL, "wait")
		WAIT
	ENDIF
NEXT

SIF SPLIT_CHECK(ARGS:1, "wait") && STRLENS(PARTS:0)
	WAIT

;shorthand instead of remembering the exact code
;@HAS_PENIS(ARG)
;#FUNCTION
;RETURNF (TALENT:ARG:性別 & 2) == 2

;Like textr but it checks against duplication, registering if needed
@USE_WORDS(ARGS)
#LOCALSIZE 1
#LOCALSSIZE 1
#DIM DYNAMIC FOUND_WORD
#DIMS DYNAMIC STR_ARRAY, 100
#DIM DYNAMIC SHAFFLE_TEMP, 100
#DIMS DYNAMIC CHOSEN_WORD
#DIM LAST_RESULT
#DIM WORD_COUNT
#FUNCTIONS

LAST_RESULT = RESULT ;Store the result from before
SPLIT ARGS, "/", STR_ARRAY ;Split the string, the amount of resulting strings is RESULT
WORD_COUNT = RESULT ;Save the amount of words
CALLF FUNC_FISHER_YATES_SHAFFLE(SHAFFLE_TEMP, WORD_COUNT) ;Sets SHAFFLE_TEMP to be a random enumeration of STR_ARRAY

FOR LOCAL, 0, WORD_COUNT ;For every word
	IF !USED_WORD(STR_ARRAY:(SHAFFLE_TEMP:LOCAL)) ;If it hasn't been used
		CHOSEN_WORD '= @"%USE_WORD(STR_ARRAY:(SHAFFLE_TEMP:LOCAL))%" ;Select it as chosen
		FOUND_WORD = 1
		GOTO DONE
	ENDIF
NEXT
SIF FOUND_WORD == 0
	CHOSEN_WORD '= @"%STR_ARRAY:(SHAFFLE_TEMP:0)%"

$DONE
RESULT = LAST_RESULT
RETURNF CHOSEN_WORD

;Registers a word as being used and returns it
@USE_WORD(ARGS)
#LOCALSIZE 1
#LOCALSSIZE 1
#DIM DYNAMIC FOUND_WORD
#DIM LAST_RESULT
#FUNCTIONS
LAST_RESULT = RESULT ;Store the result from before

FOR LOCAL, 0, USED_WORDS_I
	IF USED_WORDS:LOCAL == BASE_FORM(TOLOWER(ARGS))
		FOUND_WORD = 1
		BREAK
	ENDIF
NEXT
IF !FOUND_WORD
	USED_WORDS:USED_WORDS_I '= BASE_FORM(TOLOWER(ARGS))
	USED_WORDS_I ++
ENDIF
RESULT = LAST_RESULT
RETURNF ARGS

;Returns true if a word has been used
@USED_WORD(ARGS)
#LOCALSIZE 1
#LOCALSSIZE 1
#FUNCTION
FOR LOCAL, 0, USED_WORDS_I
	IF USED_WORDS:LOCAL == BASE_FORM(TOLOWER(ARGS))
		RETURNF 1
	ENDIF
NEXT
RETURNF 0
;Returns the base form of a verb, driving -> drive
;Always returns lower case
@BASE_FORM(ARGS)
#LOCALSIZE 1
#LOCALSSIZE 1
#DIMS BASE_WORD
#FUNCTIONS
BASE_WORD '= ARGS
IF STRCOUNT(ARGS, " ") > 0
	SPLIT ARGS, " ", LOCALS 
	BASE_WORD '= LOCALS:0
ENDIF
;if it ends with -ing
;check special case
;else replace -ing with -e
IF WORD_ENDS_IN(BASE_WORD, "ing")
	SELECTCASE BASE_WORD
		CASE "driving", "forcing"
			BASE_WORD '= REPLACE(BASE_WORD, "ing$", "e")
		CASEELSE
			BASE_WORD '= REPLACE(BASE_WORD, "ing$", "")
	ENDSELECT
ENDIF
RETURNF BASE_WORD

@DATENAME_SPOT_TR()
#LOCALSIZE 1
#LOCALSSIZE 1
#FUNCTIONS
SIF CHK_DATENOW(CFLAG:MASTER:约会中) == 0
	RETURNF ""
SIF TFLAG:约会道中
	RETURNF "途中"
RETURNF AT_IN_ON(CFLAG:MASTER:現在位置)

;Takes a location number, returns a description
@AT_IN_ON(ARG)
#LOCALSIZE 1
#LOCALSSIZE 1
#FUNCTIONS
LOCALS =
;exception: in character's rooms
SIF ARG == OMANEKIBEYA()
	RETURNF "in " + NAME_FROM_PLACE(ARG)

;exception: if player's room / or if a dweller's room with a dweller
IF GET_MAPID(ARG) == MAIN_MAP
	IF CFLAG:MASTER:初期位置 == ARG
		LOCALS += "in "
	ELSEIF ARG == SUMIKOMI_ROOM && FLAG:住宿人物
		LOCALS += "in "
	ENDIF
	SIF STRLENS(LOCALS)
		RETURNF LOCALS + NAME_FROM_PLACE(ARG)
ENDIF

;outside place exceptions (DIM.ERH)
IF GET_MAPID(ARG) != MAIN_MAP
	SELECTCASE ARG
		;"under the" scheme
		CASE 桜花道的鳥居
			LOCALS += "under the "
		;"near the" scheme
		CASE 冰造的小雪屋, 大蛤蟆之池, 九天瀑布
			LOCALS += "near the "
		;"by the " scheme
		CASE 山之湖, 寧靜海
			LOCALS += "by the "
		;"by" scheme
		CASE 古木的大樹
			LOCALS += "by "
		;"on the" scheme
		CASE 神社境内, 神社走廊, 迷失的小道, 無名之丘, 再思之道, 中有之道, 槐安通路, 地獄的深道, 旧地獄街道
			LOCALS += "on the "
		;"on" scheme
		CASE 絶景之丘, 守矢神社境内, 命蓮寺境内
			LOCALS += "on "
		;"at the" scheme
		CASE 大衆浴場, 霧之湖, 正門, 斜角的竹林, 幻想風穴, 情人旅館
			LOCALS += "at the "
		;"at" scheme
		CASE 永遠亭, 永遠亭的内院, 無縁塚, 旧地獄温泉
			LOCALS += "at "
		;"in the" scheme
		CASE 神社本堂, 広場, 月之都, 玉兎之街
			LOCALS += "in the "
		;"in" scheme
		CASE 夢幻館, 香霖堂, 霧雨魔法店, 彼岸, 白玉楼, 地獄, 仙人的宅邸, 守矢神社本殿, 天界, 命蓮寺方丈
			LOCALS += "in "

		;empty case
		CASE 倒垂的柳樹下
			RETURNF NAME_FROM_PLACE(ARG)
		;exceptions
		CASE 呑屋小道
			RETURNF "outside the liquor shop"
		CASE 竹林入口
			RETURNF "at the entrance to the Bamboo Forest"
		CASE 森林的入口
			RETURNF "at the entrance to the Forest of Magic"
		CASE 正邪的隠匿処前
			LOCALS += "in front of "
	ENDSELECT
	SIF STRLENS(LOCALS) ;stop here if exception is found, otherwise proceed to the general list (some cases are covered via GET_MAP_REPLACEMENT and whatnot)
		RETURNF LOCALS + NAME_FROM_PLACE(ARG)
ENDIF
;home in-map
;********************** (PLACE_ERH.ERH)
SELECTCASE ARG
	;"by the" scheme (gates, bodies of water, notable objects and such)
	{
	CASE P1鳥居, P5手水舎,
	P101山門, P123霊廟大門,
	P201人里的門, P207龍神像,
	P301正門, P343湖畔, P347霧之湖,
	P433斜角的竹林, P435井戸,
	P540古木的大樹,
	P620冥界の門,
	P701樹海道入口, P705山道・中腹, P708大蛤蟆之池,
	P945川岸
	}
		LOCALS += "by the "
	;"by" scheme
	CASE P21守矢神社分社, P804守矢神社鳥居, P928灼熱地獄跡, P929地獄的人工太陽
		LOCALS += "by "
	;"on the" scheme (floors, roads, open places, bridges, etc)
	{
	CASE P2境内, P10走廊, P47夢幻遺跡・艦橋,
	P102境内, P110走廊, P140地蔵小路,
	P307東陽台, P308西陽台, P324大露台, P327小露台,
	P409走廊, P434迷失的小道, P436無名之丘, P462陽台, P464露台,
	P509再思之道, P529人偶洋館・露台, P530人偶洋館・二階,
	P601中有之道, P603積石塚, P629西行妖,
	P702山道・通往沼澤之道, P732通向山頂的路,
	P802橋, P805頂上, P811境内, P816走廊, P832有頂天街,
	P915露台, P944街道, P946橋,
	P1070屋上
	}
		LOCALS += "on the "
	;"on" sceme
	CASE P715秋姉妹的家・畑
		LOCALS += "on "
	;"at the" scheme (toilets/baths, etc)
	{
	CASE P3賽銭箱, P22参道, P24温泉, P25鎮守之森, P30三妖精之家・風呂, P36夢幻遺跡前, P40夢幻遺跡・廁所,
	P112風呂, P121墓地, P122梵鐘楼, P124霊廟広場, P133湯殿, P138鐘楼, P139邪仙楼,
	P202中央広場, P203南大街, P204東大街, P205北大街, P206西大街, P223銭湯, P236公用廁所, P237公用水井,
	P316一層廁所, P322二層廁所, P333大浴場, P334廃洋館・入口, P348霧之湖・湖底,
	P406来客用廁所, P420風呂, P441温泉, P446祠, P447竹林入口, P450玄関, P454浴室, P455廁所,
	P501魔法之森入口, P503岔道, P507菌菇群生地, P508魔法之広場, P520人偶洋館前, P526人偶洋館・風呂,
	P620冥界の門, P640異界の関所, P643霊長園の風呂,
	P703玄武之澤, P704河童の宣傳站, P707山道・分岐点, P710猫之里, P711休憩所, P712間欠泉地下中樞, P729仙人的宅邸・玄関, P731帚木, P735游泳場, P739秘天崖, P740山童の宣傳站,
	P801山頂入口, P803索道站, P824廁所, P825風呂,
	P901地霊殿前, P906一層廁所, P917二層廁所, P921風呂, P931大街, P934温泉, P935情人旅館, P938鬼的聚集場所, P939小胡同, P948縦穴底部,
	P1010吃飯処, P1011甘味処, P1012酒屋, P1013八百屋, P1015砂浜, P1016入海口
	}
		LOCALS += "at the "
	;"at" scheme (named toilets/baths, etc)
	{
	CASE P213鈴奈庵,
	P401永遠亭入口, P444夜雀的小攤,
	P502香霖堂, P506無縁塚, P514魔理沙宅・風呂, P537艾伦宅・風呂,
	P636八雲邸風呂, P646八千慧宅の風呂, P652早鬼宅の風呂, P655尤魔宅の風呂,
	P721仙人的宅邸・風呂, P733正邪的隠匿処,
	P807椛宅, P808文宅, P809果宅, P837比那名居邸風呂,
	P1020綿月亭・門, P1021綿月亭・玄関, P1030綿月亭・風呂, P1054哆来咪宅・風呂, P1055哆来咪宅・廁所, P1067純狐邸・風呂
	}
		LOCALS += "at "
	;"in" scheme (mostly personal rooms)
	{
	CASE P32桑尼私室, P33露娜私室, P34斯塔私室, P43夢美私室, P44千百合私室, P56阿吽の棲家,
	P109女苑私室, P113納茲琳私室, P114村紗私室, P115星私室, P116一輪私室, P117鵺私室, P118白蓮私室, P125屠自古私室, P127布都私室, P128神子私室, P136響子私室, P137猯藏の棲家,
	P208雷鼓的房間, P209八橋的房間, P210弁弁的房間, P221稗田邸, P224慧音宅, P226小鈴私室, P227八百屋, P228貸切浴場, P229阿求私室, P231蛮奇的房間, P232蓮子的房間, P233梅莉的房間, P234雪的房間, P235​​舞​的房間, 
	P311帕秋莉私室, P312小悪魔私室, P320咲夜私室, P326蕾米莉亜私室, P338廃洋館・風呂, P340露娜薩私室, P341梅露蘭私室, P342莉莉卡私室, P344蕾蒂的冰屋, P345琪露諾的冰屋, P346大妖精の棲家,
	P410帝私室, P411鈴仙私室, P413永琳私室, P414輝夜私室, P432影狼的家, P437梅蒂欣的家, P439拉尔瓦的家, P443米斯蒂亜的家, P445妹紅的家, P451胡桃私室, P452艾麗私室, P457夢月私室, P459幻月私室, P463幽香私室,
	P510霧雨魔法店, P511魔理沙宅・居間, P512魔理沙宅・寝室, P513魔理沙宅・研究部屋, P515魔理沙宅・倉庫, P521人偶洋館・愛麗絲的房間, P522人偶洋館・神綺的房間, P523人偶洋館・夢子的房間, P534轻飘飘的艾伦魔法商店, P535艾伦宅・寝室, P536艾伦宅・倉庫, P541朱鷺子的棲息処, P542莉格露的棲息処, P543露米娅的棲息処, P544成美の小屋, P545慧ノ子の棲み処,
	P602賽の河原, P604彼岸, P606日狭美の家, P607残無の家, P608潤美的家, P609瓔花的家, P615映姫的房間, P616小町的房間, P623幽幽子私室, P624妖夢私室, P630八雲邸玄関, P631八雲邸廊下, P632八雲邸庭, P633紫私室, P635橙私室, P637八雲邸廚房,  P638藍私室, P639八雲邸居間, P644袿姫的房間, P645磨弓的房間, P647八千慧宅の生活間, P648八千慧宅の寝室, P650早鬼宅の生活間, P651早鬼宅の寝室, P653尤魔宅の生活間, P654尤魔宅の寝室,
	P706厄神的家, P709莉莉們的家, P716秋姉妹的家・土間, P717秋姉妹的家・寝室, P718秋姉妹的家・居間, P719秋姉妹的家・座敷, P720仙人的宅邸・廚房, P722仙人的宅邸・道場, P723仙人的宅邸・二階客間, P724仙人的宅邸・書斎, P725仙人的宅邸・大鷲的房間, P726仙人的宅邸・居間, P727仙人的宅邸・華扇私室, P728仙人的宅邸・空的部屋, P729仙人的宅邸・玄関, P730仙人的宅邸・庭, P737招财猫的家, 
	P817神奈子私室, P818諏訪子私室, P820早苗私室, P834比那名居邸土間, P835比那名居邸會客廳, P836比那名居邸厨房, P838比那名居邸居間, P839比那名居邸和室, P840天子私室, P841衣玖私室, P842比那名居邸庫房, P851久侘歌の小屋, P852駒草の小屋, P853魅須丸の小屋, P854龍宅, P856百百世の住処,
	P911阿空私室, P912阿燐私室, P918恋私室, P919覚私室, P930旧血の池地獄, P936勇儀的家, P940帕露西的家, P941山女的家, P942琪斯美的家, P947ちやりの棲み処,
	P1005探女宅・居間, P1006探女宅・寝室, P1008鈴瑚宅, P1009清蘭宅, P1029綿月亭・脱衣所, P1032鈴仙二號私室, P1033綿月亭・空的部屋, P1041豊姫私室, P1042依姫私室, P1050哆来咪宅・居間, P1051哆来咪宅・寝室, P1052哆来咪宅・倉庫, P1053哆来咪宅・洗面所, P1060純狐邸・玄関, P1063純狐邸・空的部屋, P1064克勞恩皮絲私室, P1066純狐邸・脱衣所, P1068純狐邸・中庭, P1071純狐私室, P1072赫卡提亚私室
	}
		LOCALS += "in "
	;empty scheme
	CASE P20本殿内部, P806瀑布背面
	;**********************
	;exceptions
	CASE P214長屋前
		RETURNF "in front of the Residential Area"
	CASE P831比那名居邸前
		RETURNF "in front of the Hinanawi Residence"
	
	;"in the" scheme
	CASEELSE
		LOCALS += "in the "
ENDSELECT
RETURNF LOCALS + NAME_FROM_PLACE(ARG)


;--------------------------------------------------------
;returns a specified part of a split string
;--------------------------------------------------------
@SPLIT_SINGLE(ARGS:0, ARG:0 = 0, ARGS:1 = ":")
#LOCALSIZE 1
#LOCALSSIZE 1
#FUNCTIONS
#DIMS DYNAMIC strSplit, 30

LOCAL = RESULT

SPLIT ARGS:0, ARGS:1, strSplit

RESULT = LOCAL

RETURNF strSplit:(ARG:0)

;--------------------------------------------------------
;modified the function in STR_BODY to be less restrictive (string:string:string, matching string, matching string, etc)
;works similarly to GROUPMATCH, but with : split strings
;--------------------------------------------------------
@SPLIT_CHECK(ARGS:0, ARGS:1 = "NULL", ARGS:2 = "NULL", ARGS:3 = "NULL", ARGS:4 = "NULL", ARGS:5 = "NULL", ARGS:6 = "NULL", ARGS:7 = "NULL", ARGS:8 = "NULL", ARGS:9 = "NULL")
#LOCALSIZE 1
#LOCALSSIZE 1
#FUNCTION

RETURNF STRCOUNT(ARGS:0, "(^|:)(" + ARGS:1 + "|" + ARGS:2 + "|" + ARGS:3 + "|" + ARGS:4 + "|" + ARGS:5 + "|" + ARGS:6 + "|" + ARGS:7 + "|" + ARGS:8 + "|" + ARGS:9 + ")($|:)")

;customize the splitter
@SPLIT_CHECK_C(ARGS:0, ARGS:1 = ":", ARGS:2 = "NULL", ARGS:3 = "NULL", ARGS:4 = "NULL", ARGS:5 = "NULL", ARGS:6 = "NULL", ARGS:7 = "NULL", ARGS:8 = "NULL", ARGS:9 = "NULL", ARGS:10 = "NULL")
#LOCALSIZE 1
#LOCALSSIZE 1
#FUNCTION

RETURNF STRCOUNT(ARGS:0, "(^|" + "\\" + ARGS:1 + ")(" + ARGS:2 + "|" + ARGS:3 + "|" + ARGS:4 + "|" + ARGS:5 + "|" + ARGS:6 + "|" + ARGS:7 + "|" + ARGS:8 + "|" + ARGS:9 + "|" + ARGS:10 + ")($|" + "\\" + ARGS:1 + ")")


;--------------------------------------------------------
;simply continue last letter of the word
;use ARG to set the size, 0 - random, -1 - disable slur, -2 - slur the vowels, -3 slur last vowel
;--------------------------------------------------------
@SLUR(ARGS, ARG)
#LOCALSIZE 1
#LOCALSSIZE 1
#FUNCTIONS
#DIM nRand
ARGS:0 '= SPLIT_G(ARGS:0, ":")
nRand = RAND(2, 6)

SIF ARG == -1 ;multiply none
	RETURNF ARGS
SIF ARG == -2 ;multiply every vowel
	RETURNF replace(ARGS,"(?i)([aeiouy])?", "$1"*nRand)
SIF ARG == -3 ;multiply only last vowel with negative lookahead
	RETURNF replace(ARGS,"(?i)([aeiouy])(?!.*[aeiouy])", "$1"*nRand)
SIF ARG == -4 ;multiply only first vowel
	RETURNF replace(ARGS,"^(.*?)([aeiouy])", "$1" + "$2"*nRand)
;multiply any last character
RETURNF ARGS + SUBSTRING(ARGS, STRLENS(ARGS)-1, -1) * ( ARG > 0 ? ARG # nRand )
;-------------------------------------------------
;Counting numbers and positions
;this is pretty much copypaste from LT, thanks inno
;Only works for values -99,999 to 99,999
;args:
;num for simple int to string (3 - three, 14 - fourteen, 99 - ninety nine, etc)
;pos for positions (1 - first, 13 - thirteenth, 42 - forty second, etc)
;ARG:1 is the additional capitalization flag, 1 - capitalize only first word, 2 - capitalize every word
;-------------------------------------------------
@NUM_COUNT(ARG, ARGS = "num", ARG:1)
#LOCALSIZE 1
#LOCALSSIZE 1
#FUNCTIONS

LOCALS =

IF ARG < 0
	LOCALS '= "minus "
ENDIF
ARG = ABS(ARG)
SIF ARG >= 100000
	RETURNF LOCALS + "a lot"

IF ARG >= 1000
	IF ARG/1000 < 20
		LOCALS += nCountUnderTwenty:(ARG/1000) + " thousand"
	ELSE
		LOCALS += nCountTens:(ARG/10000) + ( (ARG/1000)%10 != 0 ? "-" + nCountUnderTwenty:((ARG/1000)%10) # "") + " thousand"
	ENDIF
ENDIF

IF ARG >= 100 
	;IF ARG >= 1000 && ARG % 1000 != 0
	;	LOCALS += ", "
	;ENDIF
	IF ARG >= 1000 && ARG % 1000 != 0
		IF ARG*10%10000 % 1000 == 0
			LOCALS += " and "
		ELSEIF ARG*10%10000 > 1000
			LOCALS += ", "
		ENDIF
	ENDIF

	ARG = ARG % 1000
	IF ( !STRLENS(LOCALS) || ARG >= 100) 
		LOCALS += nCountUnderTwenty:(ARG/100) + " hundred"
	ENDIF
	IF ARG % 100 != 0
		LOCALS += " and "
		ARG = ARG % 100
	ELSEIF ARGS != "num"
		LOCALS += "th"
	ENDIF
ENDIF

IF (ARG % 100) < 20
	IF (ARG%100 == 0)
		IF !STRLENS(LOCALS)
			RETURNF "zero"
		ENDIF
	ELSE
		LOCALS += (ARGS == "num" ? nCountUnderTwenty:(ARG%100) # nCountPosUnderTwenty:(ARG%100) )
	ENDIF
ELSEIF ARG % 10 == 0 && ARGS != "num"
	LOCALS += nCountPosTens:((ARG%100)/10)
ELSE
	LOCALS += nCountTens:((ARG%100)/10) + ( (ARG%10!=0) ? "-"+(ARGS == "num" ? nCountUnderTwenty:(ARG%10) # nCountPosUnderTwenty:(ARG%10) )# "")
ENDIF

SELECTCASE ARG:1
	CASE 1
		LOCALS '= CAPITALIZE(LOCALS)
	CASE 2
		LOCALS '= CAP_WORD(LOCALS)
ENDSELECT
		
RETURNF LOCALS


@COUNT_CORRECT(ARG = 0, ARG:1 = 0, ARG:2 = 0, ARG:3 = 0, ARG:4 = 0, ARG:5 = 0, ARG:6 = 0)
#LOCALSIZE 2
#LOCALSSIZE 1
#FUNCTION
LOCAL = 0
FOR LOCAL:1, 0, 7
	SIF ARG:(LOCAL:1)
		LOCAL ++
NEXT
RETURNF LOCAL
;----------------------------------------------------------------------
;Like AddEXP, but unbolded and with the name at the front. Can be spaced out with OutputMode = 1 or hidden with OutputMode = -1.
;----------------------------------------------------------------------
@AddEXPOrdered(VAR_NAME,C_ID,变动数,OutputMode)
#LOCALSIZE 1
#LOCALSSIZE 1
#DIMS VAR_NAME
#DIM VAR_ID
#DIM C_ID
#DIM 变动数
#DIM OutputMode
SIF 变动数 == 0
	RETURN
VAR_ID = FINDELEMENT(EXPNAME, VAR_NAME,,,1)
EXP:C_ID:VAR_ID += 变动数
IF OutputMode == 1
	CALL COLORMESSAGE(@"%CALLNAME:C_ID+"的",14,LEFT%%EXP_TR(VAR_ID),18,LEFT% ＋{变动数}",C_YELLOW,1)
ELSEIF OutputMode == 0
	CALL COLORMESSAGE(@"%CALLNAME:C_ID%的%EXP_TR(VAR_ID)% ＋{变动数}",C_YELLOW,2)
ENDIF

;----------------------------------------------------------------------
;Like JUEL_UP_SPECIAL, but with the name at the front. Can be spaced out with OutputMode = 1 or hidden with OutputMode = -1.
;----------------------------------------------------------------------
@JUEL_UP_SPECIAL_ORDERED(ARG,增加的珠,参照能力,PERCENTAGE,OutputMode,PATTERN)
#LOCALSIZE 1
#LOCALSSIZE 1
#DIMS 增加的珠
#DIMS 参照能力
#DIM UP_JL
#DIM UP_ABL
#DIM PERCENTAGE
#DIM OutputMode
#DIM PATTERN
#DIM 上升量
UP_ABL = FINDELEMENT(ABLNAME, 参照能力,,,1)
UP_JL = FINDELEMENT(PALAMNAME, 增加的珠,,,1)
IF UP_ABL < 0
	PRINTFORMW %参照能力%不存在，请保存日志报错
	RETURN
ENDIF
IF UP_JL < 0
	PRINTFORMW %增加的珠%不存在，请保存日志报错
	RETURN
ENDIF
LOCAL = JUEL_DEMAND(UP_ABL,PATTERN,UP_JL,ARG)
SELECTCASE UP_ABL
	CASE 0
		IF TALENT:ARG:敏感な身体
			LOCAL = LOCAL * 3 / 2
		ELSE
			LOCAL = LOCAL * (2 + TALENT:ARG:Ｃ感度) / 2
		ENDIF
	CASE 1
		IF TALENT:ARG:敏感な身体
			LOCAL = LOCAL * 3 / 2
		ELSE
			LOCAL = LOCAL * (2 + TALENT:ARG:Ｖ感度) / 2
		ENDIF
	CASE 2
		IF TALENT:ARG:敏感な身体
			LOCAL = LOCAL * 3 / 2
		ELSE
			LOCAL = LOCAL * (2 + TALENT:ARG:Ａ感度) / 2
		ENDIF
	CASE 3
		IF TALENT:ARG:敏感な身体
			LOCAL = LOCAL * 3 / 2
		ELSE
			LOCAL = LOCAL * (2 + TALENT:ARG:Ｂ感度) / 2
		ENDIF
	CASE 4
		IF TALENT:ARG:敏感な身体
			LOCAL = LOCAL * 3 / 2
		ELSE
			LOCAL = LOCAL * (2 + TALENT:ARG:Ｍ感度) / 2
		ENDIF
ENDSELECT

上升量 = MAX(LOCAL * PERCENTAGE / 100, PERCENTAGE)
JUEL:ARG:UP_JL += 上升量
IF OutputMode == 1
	CALL COLORMESSAGE(@"%CALLNAME:ARG+"的",14,LEFT%%PALAM_TR(PALAMNAME:UP_JL)+"之珠",18,LEFT% ＋{上升量}",C_PINK,1)
ELSEIF OUTPUTMODE == 0
	CALL COLORMESSAGE(@"%CALLNAME:ARG%的%PALAM_TR(PALAMNAME:UP_JL)%之珠 ＋{上升量}",C_PINK,1)
ENDIF


;FileName_COMMON.ERB ------------------------------- Rev1.01
;TARGET性格強度判定
;CALL		USER
;ARG		ターゲットNO
;RETURN		性格強度（＋強気, －弱気）
;COMMENT	+3以上で強気、-1以下で弱気くらいかな
;-----------------------------------------------------------
@CHARA_DISPOS(T_NO)
#LOCALSIZE 2
#LOCALSSIZE 1
#FUNCTION
#DIM T_NO
LOCAL = 0
FOR LOCAL:1,10,30
	SIF TALENT:T_NO:(LOCAL:1) == 0
		CONTINUE
	SELECTCASE LOCAL:1
		;Skip
		CASE 20,25
			;Do Nothing
		;Boolean_Talent_True
		CASE 26
			LOCAL += TALENT:T_NO:(LOCAL:1)
		;Boolean_Talent_False
		CASE 21,22
			LOCAL -= TALENT:T_NO:(LOCAL:1)
		;Else_Talent
		CASEELSE
			LOCAL += TALENT:T_NO:(LOCAL:1)
	ENDSELECT
NEXT
RETURNF LOCAL

;-------------------------------------------------
;check if character ARG:0 has childish properties
;by default check both mind and body, where mind is just an infantile and body is just a midget (perfectly legal officer I swear on me mum)
;-------------------------------------------------
@IS_CHILD(ARG:0, ARGS:0)
#LOCALSIZE 1
#LOCALSSIZE 1
#FUNCTION
SELECTCASE ARGS
	CASE "age"
		RETURNF TALENT:(ARG:0):年齢 < 0
	CASE "mind"
		RETURNF TALENT:(ARG:0):幼稚
	CASE "body"
		RETURNF GROUPMATCH(TALENT:(ARG:0):体型, -1, -2)
CASEELSE
	RETURNF (TALENT:(ARG:0):年齢 < 0 || (TALENT:(ARG:0):幼稚 && TALENT:(ARG:0):年齢 < 1)) && GROUPMATCH(TALENT:(ARG:0):体型, -1, -2)
ENDSELECT

@IS_SPERMARCHE(ARG)
#LOCALSIZE 1
#LOCALSSIZE 1
#FUNCTION
RETURNF HAS_PENIS(ARG) && ( !IS_CHILD(ARG) || (IS_CHILD(ARG) && !IS_DOUTEI(ARG)) )

;A more complete semen description
;Does not work when the player is completely alone, has to have two characters as input
;ARG - whose semen it is
;ARG:1 - who the "target" is
@SAMEN_DESCRIPTION_FULL(ARG,ARG:1)
#LOCALSIZE 1
#LOCALSSIZE 1
#FUNCTIONS
VARSET LOCALS

@PLURAL_PANTIES(ARGS)
#LOCALSIZE 1
#LOCALSSIZE 1
#FUNCTION
RETURNF STRCOUNT(TOLOWER(ARGS), "bloomers|boxers|trunks|panties|scanties|drawers|spats|underpants|knickers|briefs")

;-------------------------------------------------
;create written set of flags without magic numbers and FIRSTTIME abuse
;original from SQ's CEVENT
;@CEVENTはキャラクターごとの各種イベントの記録を参照する関数。経験済みなら１を、未経験なら０を返す
;セットする時には@SET_CEVENTを用い、消去する際には@DEL_CEVENTを使う
;記録にはcEventを使い、cEvent = イベント１/イベント２/ と"/"を用いて区切る
;特殊なイベント群を作る際には、頭に"イベント名："とするのを推奨
;また、記述と操作の都合により、全角の"："と"＾"、半角の"/"の使用は禁止
;-------------------------------------------------
@CEVENT(ARGS, ARG)
#LOCALSIZE 1
#LOCALSSIZE 1
#FUNCTION
SIF ARG == 0 && TARGET
	ARG = TARGET

;ARGS = %ARGS%/

;既にイベント経験済みかを調べる
RETURNF SPLIT_CHECK_C(cEvent:ARG:0, "/", ARGS)

@SET_CEVENT(ARGS, ARG)
#LOCALSIZE 1
#LOCALSSIZE 1
#FUNCTION
SIF ARG == 0 && TARGET
	ARG = TARGET

;ARGS = %ARGS%/

;経験済みなら追加せずに終了
SIF SPLIT_CHECK_C(cEvent:ARG:0, "/", ARGS)
	RETURNF 1

;経験していないなら追加
cEvent:ARG:0 = %cEvent:ARG:0%%ARGS%/

@DEL_CEVENT(ARGS, ARG)
#LOCALSIZE 1
#LOCALSSIZE 1
#FUNCTION
#DIMS DYNAMIC strTemp, 999 ;increase limit if needed
#DIM nLoop
SIF ARG == 0 && TARGET
	ARG = TARGET

;イベントごとに分解する
SPLIT cEvent:ARG:0, "/", strTemp

;一旦cEventを初期化
cEvent:ARG:0 = 

FOR nLoop, 0, VARSIZE("strTemp")
	SIF strTemp:nLoop == ""
		BREAK
	;経験済みだった
	SIF ARGS == strTemp:nLoop
		CONTINUE

	;経験していないなら戻す
	cEvent:ARG:0 = %cEvent:ARG:0%%strTemp:nLoop%/
NEXT

@CLEAR_CEVENT(ARG)
#LOCALSIZE 1
#LOCALSSIZE 1
#FUNCTION
cEvent:ARG:0 '= ""

;get race/species part of the string from full title (cstr:10 originally)
@PRINT_GET_RACE(ARG)
#LOCALSIZE 1
#LOCALSSIZE 2
#FUNCTIONS
#DIMS STR_LIST, 10
VARSET STR_LIST

LOCALS = %CSVCSTR(ARG,10)%
IF STRFIND(LOCALS, "●") > -1
	SPLIT LOCALS, "●", STR_LIST
	FOR LOCAL, 0, RESULT
		IF STRFIND(STR_LIST:LOCAL, "种族：") > -1
			LOCALS:1 = %REPLACE(STR_LIST:LOCAL, "种族：", "")%
			LOCALS:1 = %REPLACE(LOCALS:1, "　", "")%
			LOCALS = %LOCALS:1%
			BREAK
		ENDIF
	NEXT
;ELSEIF ARG == [[小悪魔]]
;	LOCALS = 恶魔
;ELSEIF TALENT:ARG:行きずり
;	PRINTFORM [人間]
;ELSEIF STRLENS(CSTR:ARG:モブ子種族) ;for mobs
	;LOCALS '= RACE_TR(CSTR:ARG:モブ子種族)
ELSE
	LOCALS =
ENDIF
RETURNF LOCALS

;@Pc_Vig_Recover(ARG) ;add back bonus vig when it was reset, usually for PC
;SIF GETBIT(FLAG:好感度ボーナス取得状況, 7)
;	MAXBASE:ARG:精力 += 100
;SIF GETBIT(FLAG:パンツボーナス取得状況, 7)
;	MAXBASE:ARG:精力 += 100
;SIF GETBIT(FLAG:陥落ボーナス取得状況, 6)
;	MAXBASE:ARG:精力 += 100
;SIF GETBIT(FLAG:陥落ボーナス取得状況, 8)
;	MAXBASE:ARG:精力 += 100
;;MAXBASE:ARG:精力 += nVigIncrease ;addition custom code
;;SIF TALENT:ARG:絶倫
;;	MAXBASE:ARG:精力 += 300 ;addition custom code

@CHARASORT_EX_TR
#LOCALSIZE 1
#LOCALSSIZE 1
#FUNCTION
;the only issue here is that th98 characters start from range 201+, but it doesn't seem to be a big deal here?
;reminder to update this list whenever new characters are added!!!
VARSET RESULT, -1 ;set to -1 to include player character in the list
{
RESULT = MASTER,
[[霊夢]], [[魔理沙]],
[[露米娅]], [[大妖精]], [[琪露諾]], [[美鈴]], [[小悪魔]], [[帕秋莉]], [[咲夜]], [[蕾米莉亜]], [[芙兰]],
[[蕾蒂]], [[橙]], [[愛麗絲]], [[莉莉白]], [[莉莉黑]], [[莉莉卡]], [[梅露蘭]], [[露娜薩]], [[妖夢]], [[幽幽子]], [[藍]], [[紫]],
[[萃香]],
[[莉格露]], [[米斯蒂亜]], [[慧音]], [[帝]], [[鈴仙]], [[永琳]], [[輝夜]], [[妹紅]],
[[文]], [[梅蒂欣]], [[幽香]], [[小町]], [[映姫]],
[[静葉]], [[穣子]], [[雛]], [[荷取]], [[椛]], [[早苗]], [[神奈子]], [[諏訪子]],
[[桑尼]], [[露娜]], [[斯塔]],
[[阿求]],
[[蓮子]], [[梅莉]],
[[衣玖]], [[天子]],
[[豊姫]], [[依姫]], [[鈴仙二號]],
[[琪斯美]], [[山女]], [[帕露西]], [[勇儀]], [[覚]], [[阿燐]], [[阿空]], [[恋]],
[[朱鷺子]],
[[納茲琳]], [[小傘]], [[一輪]], [[村紗]], [[星]], [[白蓮]], [[鵺]],
[[果]],
[[華扇]],
[[響子]], [[芳香]], [[青娥]], [[屠自古]], [[布都]], [[神子]], [[猯藏]],
[[小鈴]], [[心]],
[[若鷺姫]], [[蛮奇]], [[影狼]], [[弁弁]], [[八橋]], [[正邪]], [[針妙丸]], [[雷鼓]],
[[菫子]], [[清蘭]], [[鈴瑚]], [[哆来咪]], [[探女]], [[皮絲]], [[純狐]], [[赫卡提亚]],
[[拉尔瓦]], [[合歡乃]], [[阿吽]], [[成美]], [[舞]], [[里乃]], [[隠岐奈]],
[[女苑]], [[紫苑]],
[[影華扇]],
[[瓔花]], [[潤美]], [[久侘歌]], [[八千慧]], [[磨弓]], [[袿姫]], [[早鬼]], [[美宵]],
[[尤魔]],
[[美天]], [[慧之子]], [[血枪]], [[日狭美]], [[残無]],
[[幽幻魔眼]], [[依莉斯]], [[萨丽爱尔]], [[魅魔]], [[菊理]], [[矜羯罗]],
[[里香]], [[明羅]],
[[艾伦]], [[小兎姫]], [[卡娜]], [[理香子]], [[千百合]], [[夢美]], [[留琴]],
[[奥莲姬]], [[胡桃]], [[艾麗]], [[夢月]], [[幻月]],
[[萨拉]], [[露易茲]], [[雪]], [[​舞​]], [[夢子]], [[神綺]],
[[路人子]]
}

;prints text in a box, similarly to omikuji and moriya blanks, to simulate a letter
;the box is always white, but the text uses the currently set color, allowing it to match a character's text color
;LINES=the text to be uses, with the lines separated by a /
;MINWIDTH=the minimum internal width of the box, in double-width characters
;MAXWIDTH=the maximum width of the box. any lines bigger than this will automatically be split up into separate lines with BREAKENG
;CENTERED=if 0, lines will be aligned left, if 1, lines will be centered
@PRINT_LETTER(LINES, MINWIDTH = 10, MAXWIDTH = -1, CENTERED = 0)
#LOCALSIZE 3
#LOCALSSIZE 1
#DIMS LINES
#DIM  MINWIDTH
#DIM  MAXWIDTH
#DIM  CENTERED
#DIMS LINES_SPLIT, 20
#DIM  LINE_COUNT
#DIM DYNAMIC LINE_WIDTH

SIF !STRLENS(LINES)
    RETURN
SIF MAXWIDTH < 0
	MAXWIDTH = MAXWIDTH() / 2 - 2

SPLIT LINES, "/", LINES_SPLIT
LINE_COUNT = RESULT
FOR LOCAL, 0, LINE_COUNT
    LINE_WIDTH = MAX(LINE_WIDTH, STRLENS(LINES_SPLIT:LOCAL))
NEXT
;adjust to double-width characters
SIF LINE_WIDTH % 2 == 1
    LINE_WIDTH++
LINE_WIDTH /= 2
LINE_WIDTH = LIMIT(LINE_WIDTH, MINWIDTH, MAXWIDTH)

PRINTFORMDL ┌%"─"*LINE_WIDTH%┐

FOR LOCAL, 0, LINE_COUNT
    LOCALS '= BREAKENG(LINES_SPLIT:LOCAL,,,LINE_WIDTH*2)
	DEBUGPRINTFORML CONTENTS = "%LOCALS%"
    LOCAL:1 = 0
    WHILE STRFIND(LOCALS, "\n", LOCAL:1) >= 0
        LOCAL:2 = STRFIND(LOCALS, "\n", LOCAL:1)
        CALL PRINT_LETTER_LINE(SUBSTRING(LOCALS, LOCAL:1, LOCAL:2 - LOCAL:1), LINE_WIDTH, CENTERED)
        LOCAL:1 = LOCAL:2 + 1
    WEND
    CALL PRINT_LETTER_LINE(SUBSTRING(LOCALS, LOCAL:1), LINE_WIDTH, CENTERED)
NEXT

PRINTFORMDL └%"─"*LINE_WIDTH%┘

@PRINT_LETTER_LINE(LINE, LINE_WIDTH, CENTERED)
#LOCALSIZE 1
#LOCALSSIZE 1
#DIMS LINE
#DIM  LINE_WIDTH
#DIM  CENTERED
#DIM  BLANK_SPACES
#DIM  TEXT_LENGTH

TEXT_LENGTH = STRLENS(LINE)
BLANK_SPACES = LINE_WIDTH - ((TEXT_LENGTH + (TEXT_LENGTH % 2)) / 2)
BLANK_SPACES = MAX(BLANK_SPACES, 0)

PRINTFORMD ｜
IF CENTERED
	PRINTFORM %"　"*(BLANK_SPACES / 2)%%COND_STR(" ", TEXT_LENGTH % 2 == 1)%%LINE%%"　"*(BLANK_SPACES - (BLANK_SPACES / 2))%
ELSE
	PRINTFORM %LINE%%COND_STR(" ", TEXT_LENGTH % 2 == 1)%%"　"*BLANK_SPACES%
ENDIF

PRINTFORMDL ｜

;note - needs WAIT for W effect
;modified from COM616_EIRINSPEAK
;匹配原版关键字
@CUTSCENESPEAK(ARGS, ARGS:1 = "通常", ARG, ARG:1 = 1)
#LOCALSIZE 1
#LOCALSSIZE 1
SIF GROUPMATCH(ARGS:1, "smile", "笑顔")
	ARGS:1 '= "笑顔"
SIF GROUPMATCH(ARGS:1, "blush", "発情")
	ARGS:1 '= "発情"
SIF GROUPMATCH(ARGS:1, "anger", "憤怒")
	ARGS:1 '= "憤怒"
SIF GROUPMATCH(ARGS:1, "surprise", "驚き")
	ARGS:1 '= "驚き"
DEBUGPRINTFORML Calling SPTALK with args {ARG}, %ARGS:1%, 1, %ARGS%, 1
CALL SPTALK, ARG, ARGS:1, 1, ARGS, 1
SIF ARG:1 == 1
	WAIT

;some font names need double checking
@CHKFONTNAME(ARGS)
#LOCALSIZE 1
#LOCALSSIZE 1
#FUNCTIONS
#DIMS strProbFontName = "ＭＳ Ｐゴシック", "ＭＳ ゴシック", "メイリオ", "ＭＳ Ｐ明朝"
#DIMS strFixedFontName = "MS PGothic", "MS Gothic", "Meiryo", "MS PMincho"
FOR LOCAL, 0, VARSIZE("strProbFontName")
	IF ARGS == strProbFontName:LOCAL && !CHKFONT(ARGS)
		SIF CHKFONT(strFixedFontName:LOCAL);return fixed name if it works, otherwise just give up and return the original
			RETURNF strFixedFontName:LOCAL
	ENDIF
NEXT

RETURNF ARGS

;show time-TSP passage (configurable) if source processing is skipped or something
;with custom mode you can set arbitrary values
@TIMETSPPASSAGE(ARGS)
#LOCALSIZE 1
#LOCALSSIZE 1
IF SPLIT_SINGLE(ARGS) == "custom"
	IF SPLIT_SINGLE(ARGS, 1) == "TSP"
		LOCAL = TOINT(SPLIT_SINGLE(ARGS, 2))
		CALL COLORMESSAGE(@"TSP-{LOCAL}",0x5090FF, 1, 0)
		BASE:MASTER:TSP -= LOCAL
	ELSE
		LOCAL = TOINT(SPLIT_SINGLE(ARGS, 2))
		CALL COLORMESSAGE(@"+%PRINT_PLUR("Minute", LOCAL)%",0x5090FF, 1, 0)
		TIME += LOCAL
	ENDIF
ELSE
	SELECTCASE SPLIT_SINGLE(ARGS)
		CASE "TSP" ;modded branch only
			SIF nRemberTsp - BASE:MASTER:TSP > 0
				CALL COLORMESSAGE(@"TSP-{nRemberTsp - BASE:MASTER:TSP}",0x5090FF, 1, 0)
	CASEELSE
		CALL COLORMESSAGE(@"+%PRINT_PLUR("Minute", TIME_PROGRESS(TIME:1))%",0x5090FF, 1, 0)
	ENDSELECT
ENDIF

;retrieves the dishdata and stores them in temp variables, not affecting the currently held food
@TEMP_DISHDATA(DISH_NUM)
#LOCALSIZE 1
#LOCALSSIZE 1
#DIM DISH_NUM
#DIMS CURRENT_DISH_NAME
#DIMS CURRENT_DISH_TYPE
#DIMS CURRENT_DISH_TASTE
#DIMS CURRENT_DISH_COMMENT
#DIM CURRENT_DISH_ING,11
#DIM CURRENT_DISH_TIME
#DIM CURRENT_DISH_MAXLIMIT

;temporarily store currently held data so we can use DISHDATA to retrieve the name
CURRENT_DISH_NAME '= DISH_NAME
CURRENT_DISH_TYPE '= DISH_TYPE
CURRENT_DISH_TASTE '= DISH_TASTE
CURRENT_DISH_COMMENT '= DISH_COMMENT
ARRAYCOPY "DISH_ING", "CURRENT_DISH_ING"
CURRENT_DISH_TIME = DISH_TIME
CURRENT_DISH_MAXLIMIT = DISH_MAXLIMIT

CALL DISHDATA(DISH_NUM, "", 1)
TEMP_DISH_NAME '= DISH_NAME
TEMP_DISH_TASTE '= DISH_TASTE
TEMP_DISH_TYPE '= DISH_TYPE
TEMP_DISH_COMMENT '= DISH_COMMENT
ARRAYCOPY "DISH_ING", "TEMP_DISH_ING"
TEMP_DISH_TIME = DISH_TIME
TEMP_DISH_MAXLIMIT = DISH_MAXLIMIT

;reset values to previous
DISH_NAME '= CURRENT_DISH_NAME
DISH_TYPE '= CURRENT_DISH_TYPE
DISH_TASTE '= CURRENT_DISH_TASTE
DISH_COMMENT '= CURRENT_DISH_COMMENT
ARRAYCOPY "CURRENT_DISH_ING", "DISH_ING"
DISH_TIME = CURRENT_DISH_TIME
DISH_MAXLIMIT = CURRENT_DISH_MAXLIMIT

;returns the name for the dish with the specified id without affecting the existing values for held food
@FOOD_NAME(DISH_NUM)
#LOCALSIZE 1
#LOCALSSIZE 1
#DIM DISH_NUM
CALL TEMP_DISHDATA(DISH_NUM)
RESULTS '= TEMP_DISH_NAME 

;Prints the season (year) using the Youkai Mountain calendar
@PRINT_DATE_F_YOUKAI_SEASON()
#LOCALSIZE 1
#LOCALSSIZE 1
#FUNCTION
GETTIME
LOCAL = (RESULT / 10000000000000) - 1885
RETURNF LOCAL

;Prints the date using the Youkai Mountain calendar
@PRINT_DATE_F_YOUKAI(ARG, ARG:1)
#LOCALSIZE 4
#LOCALSSIZE 1
#FUNCTIONS
LOCAL:1 = PRINT_DATE_F_YOUKAI_SEASON()
LOCAL = DAY_TO_DATE(ARG)
LOCAL:2 = LOCAL % 1000 / 100
LOCAL:3 = LOCAL % 1000 % 100
SELECTCASE ARG:1
	CASE 1
		LOCALS = {LOCAL:1}期　%GET_MONTH(LOCAL:2)%　{LOCAL:3}日
CASEELSE
	LOCALS = {LOCAL:1}期　%GET_MONTH(LOCAL:2)%　{LOCAL:3}日
ENDSELECT
RETURNF LOCALS

@TOSTR_BUSTSIZE_ATW(ARG:0)
#LOCALSIZE 1
#LOCALSSIZE 1
#FUNCTIONS
SELECTCASE ARG:0
	CASE -2
		RETURNF "平坦"
	CASE -1
		RETURNF "小巧"
	CASE 0
		RETURNF "普通"
	CASE 1
		RETURNF "丰满"
	CASE 2
		RETURNF "巨大"
	; CASE 3
	; 	RETURNF "Gigantic"
	; CASE 4
	; 	RETURNF "Mystifying"
ENDSELECT


;generic version of K30_FIND_AROUND
;Returns a random person at this location, prioritizing lovers and acquintances if desired 
;ACQUAINTANCES: passes a list of priority characters
;OPTION: 0 to prioritize lovers then acquaintances then everyone else, 1 to not prioritize anyone
;		, 2 to prioritize lovers, but not acquaintances, 3 to prioritize acquaintances but not lovers
;RANDOM_ACQUAINTANCE: if 0, acquaintances are prioritized as they are listed, if 1, a random acquaintance is picked
@FIND_AROUND(ACQUAINTANCES, MODE = 0, RANDOM_ACQUAINTANCE = 0)
#LOCALSSIZE 1
#FUNCTION
#DIM TARGETNUM
#DIM DYNAMIC VALID_CHARA_COUNT
#DIM REF ACQUAINTANCES
#DIM MODE
#DIM DYNAMIC SEARCH_MODE ;0=randoms, 1=acquiantances, 2=lovers
#DIM CHARA
#DIM RANDOM_ACQUAINTANCE
#LOCALSIZE 人物数量上限
VARSET LOCAL
TARGETNUM = GET_TARGETNUM() 
;部屋に映姫様一人なら当然0
SIF TARGETNUM == 1
	RETURNF 0

FOR COUNT,1, TARGETNUM + 1
	CHARA = TARGET:COUNT
	;地獄の断頭台withジェロニモ状態を防ぐため口上主はスキップ
	SIF CHARA == TARGET
		CONTINUE

	IF GROUPMATCH(MODE, 0, 2) ;lovers first
		IF TALENT:CHARA:恋人
			IF SEARCH_MODE < 2 ;discard the old list, increase priority
				VARSET LOCAL
				VALID_CHARA_COUNT = 0
				SEARCH_MODE = 2
			ENDIF
			LOCAL:(VALID_CHARA_COUNT++) = CHARA
			CONTINUE
		ELSEIF SEARCH_MODE == 2 ;only lovers allowed at this point, discard
			CONTINUE
		ENDIF
	ENDIF

	IF GROUPMATCH(MODE, 0, 3) ;acquaintances second
		IF FINDELEMENT(ACQUAINTANCES, CHARA) >= 0
			IF SEARCH_MODE < 1 ;discard the old list, increase priority
				VARSET LOCAL
				VALID_CHARA_COUNT = 0
				SEARCH_MODE = 1
			ENDIF
			LOCAL:(VALID_CHARA_COUNT++) = CHARA
			CONTINUE
		ELSEIF SEARCH_MODE == 1 ;only acquiantances allowed at this point, discard
			CONTINUE
		ENDIF
	ENDIF

	;配列のカウントは別で取りながら代入
	LOCAL:(VALID_CHARA_COUNT++) = CHARA
NEXT

;この条件でここまで来たのはなんでだろう
;ともかくエラー出さないためにヨシ！
SIF VALID_CHARA_COUNT < 1
	RETURNF 0

SIF SEARCH_MODE == 1 && !RANDOM_ACQUAINTANCE ;pick first in list
	RETURNF LOCAL:0

;prioritize date partner amongst lovers, if possible
SIF SEARCH_MODE == 2 && FLAG:デート相手 && TALENT:(FLAG:デート相手):恋人
	RETURNF FLAG:デート相手

;抽選してIDを決定
RETURNF LOCAL:(RAND(VALID_CHARA_COUNT))

@PRINT_BREAK_HTML(MAX_LINE_WIDTH)
#LOCALSIZE 1
#LOCALSSIZE 1
#DIM  MAX_LINE_WIDTH
#DIMS HTML_SOURCE_TEXT
#DIMS DYNAMIC HTML_RESULT_TEXT
#DIM  DYNAMIC CURRENT_POS
#DIM  DYNAMIC LINE_POS
#DIM  TEXT_LENGTH
#DIMS HTML_SEGMENT
#DIMS ACTUAL_SEGMENT
#DIM  HTML_SEGMENT_LENGTH
#DIM  ACTUAL_SEGMENT_LENGTH
#DIM  SPACE_POS
SIF !MAX_LINE_WIDTH
	MAX_LINE_WIDTH = MAXWIDTH()-19

HTML_SOURCE_TEXT '= HTML_POPPRINTINGSTR()
; DEBUGPRINTFORML Raw HTML line:
; DEBUGPRINTFORML %HTML_SOURCE_TEXT%

HTML_SOURCE_TEXT '= REPLACE(HTML_SOURCE_TEXT, "<br>", "") ;remove existing line breaks
;STRFIND does not use REGEX, so we instead replace all target spaces with ␝ and work with that
;this could probably be optimized with good use of REGEXPMATCH
HTML_SOURCE_TEXT '= REPLACE(HTML_SOURCE_TEXT, " +(?![^<]*\>)", "␝")
TEXT_LENGTH = STRLENS(HTML_SOURCE_TEXT)

WHILE CURRENT_POS < TEXT_LENGTH
	SPACE_POS = STRFIND(HTML_SOURCE_TEXT, "␝", CURRENT_POS)
	IF SPACE_POS < 0
		SPACE_POS = TEXT_LENGTH
	ENDIF
	HTML_SEGMENT_LENGTH = SPACE_POS + 1 - CURRENT_POS
	HTML_SEGMENT '= SUBSTRING(HTML_SOURCE_TEXT, CURRENT_POS, HTML_SEGMENT_LENGTH)
	ACTUAL_SEGMENT_LENGTH = STRLENS(HTML_TOPLAINTEXT(HTML_SEGMENT))

	;if we ever get a segment that's too big to fit on one line, just throw it in there and start a new line
	IF ACTUAL_SEGMENT_LENGTH > MAX_LINE_WIDTH
		HTML_RESULT_TEXT += REPLACE(HTML_SEGMENT, "␝", "")
		HTML_RESULT_TEXT += "<br>"
		LINE_POS = 0
		CURRENT_POS += HTML_SEGMENT_LENGTH
		CONTINUE
	ELSEIF LINE_POS + ACTUAL_SEGMENT_LENGTH > MAX_LINE_WIDTH
		;new line
		HTML_RESULT_TEXT += "<br>"
		LINE_POS = 0
	ENDIF
	HTML_RESULT_TEXT += HTML_SEGMENT
	LINE_POS += ACTUAL_SEGMENT_LENGTH
	CURRENT_POS += HTML_SEGMENT_LENGTH
WEND
;restore spaces
HTML_RESULT_TEXT '= REPLACE(HTML_RESULT_TEXT, "␝", " ")
HTML_PRINT HTML_RESULT_TEXT

;push player down, originally K41_PUSHDOWN
@INSTANT_PUSHDOWN
#LOCALSIZE 1
#LOCALSSIZE 1
;挿入中は駄目
TEQUIP:50 = -1
TEQUIP:51 = -1
;シャワー中はダメ
TEQUIP:32 = 0
;乳首吸い中はダメ
TEQUIP:41 = 0
;乳揉み中はダメ
TEQUIP:42 = 0
;継続キス
TEQUIP:継続接吻 = 0
CFLAG:MASTER:うふふ = 2
TFLAG:COMABLE管理 = 3
FOR LOCAL,1,GET_TARGETNUM() + 1
	SIF TARGET:LOCAL <= 0
		CONTINUE
	SIF !SHIRAHU(TARGET:LOCAL)
		CONTINUE
	SIF GETBIT(FLAG:潜伏, 0) && !GETBIT(CFLAG:(TARGET:LOCAL):潜伏状態, 0) ;concealment, only apply to the target
		CONTINUE
	CFLAG:(TARGET:LOCAL):うふふ = 2
NEXT
TIME += 5

;Originally K97_RUNOFF
@RUNOFF(ARG, PASS_TIME = 1)
#LOCALSIZE 1
#LOCALSSIZE 1
#DIM PASS_TIME
CFLAG:ARG:延迟 = 0
CFLAG:ARG:行動 = 0
BASE:ARG:工作量 = 0
CFLAG:ARG:現在位置 = SUKIMA()
CFLAG:ARG:拠点来訪 = 0
CFLAG:ARG:同室 = 0
IF FLAG:デート相手 == ARG
	CFLAG:ARG:デート中 = -1
	FLAG:デート相手 = 0
	SIF MOREDATE
		CALL REMOVE_FROM_GROUP_DATE(ARG)
ENDIF
SIF PASS_TIME
	TIME += 5
CALL TARGETSET_CHECK

; Like GIFT_DATE_EVENT, but without the date-specific text
@GIFT_EVENT(CHARA)
#LOCALSIZE 1
#LOCALSSIZE 1
#DIM CHARA
#DIM GIFT_ID
#DIMS GIFT_NAME
#DIM 渡した回数
#DIM 点数
#DIM 本人の評価点
#DIMS GIFT_SENSE


;点数は必要ないけど一応
点数 = 0

;贈り物生成
CALL CREATE_GIFT_FROM_CHARA(CHARA, 701, 1000)
GIFT_ID = RESULT
GIFT_NAME = %RESULTS%
GIFT_SENSE = %RESULTS:1%
渡した回数 = GET_GIFTDATA(CFLAG:CHARA:送出的礼物, "回数") + 1
本人の評価点 = RESULT:1
;IDの設定
GIFT_ID = 渡した回数 * 10000000000000000 + 点数 * 10000000000000 + DAY * 1000000000 + CFLAG:MASTER:デート中 * 10000000 + GIFT_ID
GIFT_GAVE:CHARA:(渡した回数 % 100) = GIFT_ID
CFLAG:CHARA:送出的礼物 = GIFT_ID
CALL KOJO_MESSAGE_SEND("GIFT", 6, CHARA, GIFT_ID, 本人の評価点, GIFT_NAME, GIFT_SENSE)
PRINTFORMW 从%CALLNAME:CHARA%那儿收到了[%GIFT_NAME%]！
@SET_ALTERNATIVE_DEFAULTS
#LOCALSIZE 1
#LOCALSSIZE 1
;Sets certain dialogues to preferred alternatives on new game start.
;CFLAG:15:口上セレクタ = 2
;CFLAG:50:口上セレクタ = 2
;CFLAG:51:口上セレクタ = 1
CFLAG:139:口上セレクタ = 4

;major.minor.patch
@FORMAT_VERSION(ARG)
#LOCALSIZE 1
#LOCALSSIZE 1
#FUNCTIONS
RETURNF @"{ARG / 100}.{ARG % 100 / 10}.{ARG % 10}"

@RAND_SCALING(Roll,Value,Threshold,Cap=1000000,Division=2)
#LOCALSIZE 1
#LOCALSSIZE 1
	;Random roll with a boolean return that becomes more likely to return true if Value is equal to or above Threshold.
	;If Value is equal to or greater than Threshold, Division is increased by the difference between Value and Threshold, and Roll is divided by Division.
	;If Value is Cap or above, Roll is set to 1.
	;Used to make random outcomes more likely when compared against variables such as character abilities.
#FUNCTION
#DIM DYNAMIC Roll
#DIM DYNAMIC Value
#DIM DYNAMIC Threshold
#DIM DYNAMIC Cap
#DIM DYNAMIC Division
IF Value >= Threshold && Value < Cap
	Division += Value-Threshold
	Roll = Roll/Division
ELSEIF Value >= Cap
	Roll = 1
ENDIF
SIF !RAND:Roll
	RETURNF 1
RETURNF 0

;----------------------------------------------------------------------
;Exponent function because era doesn't have one
;Also doesn't work on degress less than 0. 
;Doesn't really matter because Era doesn't have floats.
;ARGS: ARG (your number), nDegree (how many times ARG will be multiplied by itself.)
;----------------------------------------------------------------------
@NUM_EXPONENT(ARG,nDegree)
#LOCALSIZE 1
#LOCALSSIZE 1
#FUNCTION
#DIM nDegree
#DIM nNum
nNum = ARG
SIF nDegree == 0
	RETURNF 1

REPEAT nDegree-1
	nNum *= ARG
REND
RETURNF nNum



;----------------------------------------------------------------------
;Checks if the strength of orgasm on one part is as strong or stronger than the threshold
;ARG: Character to check
;strengthNeed: Needed orgasm strength on any body part
;----------------------------------------------------------------------
@MIN_ORGASM_STRENGTH(ARG, strengthNeed)
#LOCALSIZE 1
#LOCALSSIZE 1
#FUNCTION
#DIM strengthNeed
#DIM isUnconscious
;check all orgasm checks

FOR LOCAL, 0, 4
	SIF NOWEX:ARG:LOCAL >= strengthNeed
		RETURNF 1
NEXT
RETURNF 0


;==================================================
;Counts how many orgasms the given character had in the last sex session
;Stolen from Suwako's dialogue
;==================================================
@ORGASM_COUNT(ARG = -1)
#LOCALSIZE 1
#LOCALSSIZE 1
#FUNCTION
SIF ARG == -1
	ARG = TARGET
RETURNF SUMARRAY(EX:ARG:0, 0, 5)

;==================================================
;Returns the visitor map substitute for a given in-game location, regardless of whether that map is the home map.
;The location given must still be relative to the current game state, e.g. a home map location for the home map, or a visitor location for a visitor map.
;==================================================
@VISITOR_LOCATION(LocationMatch)
#FUNCTION
#LOCALSIZE 1
#LOCALSSIZE 1
#DIM DYNAMIC LocationMatch
LOCAL = -1
IF MAIN_MAP == LocationMatch / 100
		;TRYCALLFORM won't return anything, and I'd rather not add a RESULT assignment to a core function, so these have to be called manually.
	SELECTCASE LocationMatch / 100
		CASE 0
			LOCAL = MAP_SEARCH_REPLACEMENT_0(LocationMatch)
		CASE 1
			LOCAL = MAP_SEARCH_REPLACEMENT_1(LocationMatch)
		CASE 2
			LOCAL = MAP_SEARCH_REPLACEMENT_2(LocationMatch)
		CASE 3
			LOCAL = MAP_SEARCH_REPLACEMENT_3(LocationMatch)
		CASE 4
			LOCAL = MAP_SEARCH_REPLACEMENT_4(LocationMatch)
		CASE 5
			LOCAL = MAP_SEARCH_REPLACEMENT_5(LocationMatch)
		CASE 6
			LOCAL = MAP_SEARCH_REPLACEMENT_6(LocationMatch)
		CASE 7
			LOCAL = MAP_SEARCH_REPLACEMENT_7(LocationMatch)
		CASE 8
			LOCAL = MAP_SEARCH_REPLACEMENT_8(LocationMatch)
		CASE 9
			LOCAL = MAP_SEARCH_REPLACEMENT_9(LocationMatch)
		CASE 10
			LOCAL = MAP_SEARCH_REPLACEMENT_10(LocationMatch)
		CASE 11
			LOCAL = MAP_SEARCH_REPLACEMENT_11(LocationMatch)
	ENDSELECT
ELSE
	LOCAL = LocationMatch
ENDIF
RETURNF LOCAL

@INPUT_RANGE(MINVALUE, MAXVALUE)
#LOCALSIZE 1
#LOCALSSIZE 1
#DIM MINVALUE
#DIM MAXVALUE
INPUT
LOCAL = RESULT
IF !INRANGE(LOCAL, MINVALUE, MAXVALUE)
	PRINTFORMDW 必须介于{MINVALUE}和{MAXVALUE}之间
	CLEARLINE 2
	RESTART
ENDIF
RETURN LOCAL

;西式房间默认为床，其他的都是蒲团
@BED_FUTON(LOCATION)
#LOCALSIZE 1
#LOCALSSIZE 1
#FUNCTIONS
#DIM LOCATION
SIF !LOCATION
	LOCATION = CFLAG:MASTER:現在位置

SIF LOCATION == CFLAG:MASTER:初期位置 && ITEM:キリのベッド
	RETURNF "床"

IF INROOM(LOCATION) == 2
	RETURNF "床"
ELSE
	RETURNF "床铺"
ENDIF

@TIME_TO_GREETING()
#LOCALSIZE 1
#LOCALSSIZE 1
#FUNCTIONS
SELECTCASE TIME:2
	CASE 1 TO 2
		RETURNF "早上"
	CASE 3 TO 5
		RETURNF "下午"
	CASE 6
		RETURNF "傍晚"
	CASEELSE
		RETURNF "晚上"
ENDSELECT
RETURNF ""

;表示是否存在池塘/湖泊或河流
;1=湖泊, 2=河流
@WATERAREA(ARG)
#LOCALSIZE 1
#LOCALSSIZE 1
#FUNCTION

IF IN_HOME(ARG)
	IF ROOMDATA:(ARG % 100) & VAR_OR_DEFAULT("場所_LAKE")
		RETURNF 1
	ELSEIF ROOMDATA:(ARG % 100) & VAR_OR_DEFAULT("場所_RIVER")
		RETURNF 2
	ENDIF
ELSE
	IF ODEKAKEMAP_SETTING(ARG, "LAKE")
		RETURNF 1
	ELSEIF ODEKAKEMAP_SETTING(ARG, "RIVER")
		RETURNF 2
	ENDIF
ENDIF
RETURNF 0

@PREVIOUS_LINES_HAVE_TEXT(LINES)
#LOCALSIZE 1
#LOCALSSIZE 1
#FUNCTION
#DIM LINES

FOR LOCAL, 0, LINES
	SIF STRCOUNT(HTML_TOPLAINTEXT(HTML_GETPRINTEDSTR(LOCAL)), "\\S")
		RETURNF 1
NEXT
RETURNF 0

;sets ABL to the specified value and the minimum amount of exp required to reach it
@SET_ABL_AND_XP(CHARA, ABL_NAME, ABL_LEVEL, IGNORE_PREREQUISITES)
#LOCALSIZE 1
#LOCALSSIZE 1
#DIM CHARA
#DIMS ABL_NAME
#DIM ABL_LEVEL
#DIM ABL_INDEX
#DIM IGNORE_PREREQUISITES

IF ABL_LEVEL <= 0
	RETURN
ELSE
	;handle potential prerequisites
	IF !IGNORE_PREREQUISITES
		SELECTCASE ABL_NAME
			CASE "膣射中毒", "肛射中毒"
				CALL SET_ABL_AND_XP_PART(CHARA, "従順", ABL_LEVEL)
				CALL SET_ABL_AND_XP_PART(CHARA, "奉仕精神", ABL_LEVEL)
				CALL SET_ABL_AND_XP_PART(CHARA, "精液中毒", ABL_LEVEL)
			CASE "断袖中毒"
				CALL SET_ABL_AND_XP_PART(CHARA, "断袖属性", ABL_LEVEL)
			CASE "百合中毒"
				CALL SET_ABL_AND_XP_PART(CHARA, "百合属性", ABL_LEVEL)
			CASE "精液中毒"
				CALL SET_ABL_AND_XP_PART(CHARA, "従順", ABL_LEVEL)
				CALL SET_ABL_AND_XP_PART(CHARA, "奉仕精神", ABL_LEVEL)
			CASE "自慰中毒"
				CALL SET_ABL_AND_XP_PART(CHARA, "Ｃ感覚", ABL_LEVEL+1)
				CALL SET_ABL_AND_XP_PART(CHARA, "露出癖", ABL_LEVEL+1)
			CASE "施虐属性", "受虐属性"
				CALL SET_ABL_AND_XP_PART(CHARA, "欲望", ABL_LEVEL)
			CASE "奉仕精神"
				CALL SET_ABL_AND_XP_PART(CHARA, "従順", ABL_LEVEL)
		ENDSELECT
	ENDIF
	CALL SET_ABL_AND_XP_PART(CHARA, ABL_NAME, ABL_LEVEL)
ENDIF

@SET_ABL_AND_XP_PART(CHARA, ABL_NAME, ABL_LEVEL)
#LOCALSIZE 8
#LOCALSSIZE 1
#DIM CHARA
#DIMS ABL_NAME
#DIM ABL_LEVEL
#DIM ABL_INDEX
ABL_INDEX = GETNUM(ABL, ABL_NAME)
SIF ABL_INDEX < 0
	THROW %ABL_NAME% is not a valid ABL.
VARSET LOCAL

ABL:CHARA:ABL_INDEX = ABL_LEVEL - 1 ;set it to -1 to use the EXP_DEMAND functions
SELECTCASE ABL_NAME
	CASE "戦闘能力"
		IF ABL_LEVEL == 1
			EXP:CHARA:戦闘経験 = MAX(EXP:CHARA:戦闘経験, 5)
		ELSE
			EXP:CHARA:戦闘経験 = MAX(EXP:CHARA:戦闘経験, EXPLV:(ABL:CHARA:戦闘能力 + 3))
		ENDIF
	CASE "話術技能"
		EXP:CHARA:会話経験 = MAX(EXP:CHARA:会話経験, EXPLV:(ABL:CHARA:話術技能 + 3))
	CASE "清掃技能"
		EXP:CHARA:清掃経験 = MAX(EXP:CHARA:清掃経験, EXPLV:(ABL:CHARA:清掃技能 + 3))
	CASE "教養"
		IF ABL_LEVEL == 1
			EXP:CHARA:学習経験 = MAX(EXP:CHARA:学習経験, 5)
		ELSE
			EXP:CHARA:学習経験 = MAX(EXP:CHARA:学習経験, EXPLV:(ABL:CHARA:教養 + 3))
		ENDIF
	CASE "料理技能"
		IF ABL_LEVEL == 1
			EXP:CHARA:料理経験 = MAX(EXP:CHARA:料理経験, 5)
		ELSE
			EXP:CHARA:料理経験 = MAX(EXP:CHARA:料理経験, EXPLV:(ABL:CHARA:料理技能 + 3))
		ENDIF
	CASE "音楽技能"
		IF ABL_LEVEL == 1
			LOCAL = 5
		ELSE
			LOCAL = EXPLV:(ABL:CHARA:音楽技能 + 3)
		ENDIF
		SELECTCASE TALENT:CHARA:音楽知識
			CASE 1
				EXP:CHARA:演奏経験 = MAX(EXP:CHARA:演奏経験, LOCAL)
			CASE 2
				EXP:CHARA:歌唱経験 = MAX(EXP:CHARA:歌唱経験, LOCAL)
			CASE 3
				EXP:CHARA:舞踏経験 = MAX(EXP:CHARA:舞踏経験, LOCAL)
			CASEELSE ;no focus, do everything
				EXP:CHARA:演奏経験 = MAX(EXP:CHARA:演奏経験, LOCAL)
				EXP:CHARA:歌唱経験 = MAX(EXP:CHARA:歌唱経験, LOCAL)
				EXP:CHARA:舞踏経験 = MAX(EXP:CHARA:舞踏経験, LOCAL)
		ENDSELECT
	CASE "指"
		LOCAL = (EXPLV:(ABL:CHARA:指 + 3) / 2)
		LOCAL:1 = 10 + ABL:CHARA:技巧 / 2 + TALENT:CHARA:器用な指 * 2 + TALENT:CHARA:習得速度

		LOCAL:2 = (LOCAL * 10 / LOCAL:1) + 1

		IF EXP:CHARA:手淫経験 < LOCAL:2
			EXP:CHARA:手淫経験 = LOCAL:2
			TCVAR:CHARA:手淫経験 = EXP:CHARA:手淫経験
		ENDIF
	CASE "舌"
		LOCAL = EXPLV:(ABL:CHARA:舌 + 3); + 1
		LOCAL:1 = 10 + ABL:CHARA:技巧 / 2 + TALENT:CHARA:舌使い * 2 + TALENT:CHARA:習得速度

		LOCAL:2 = (LOCAL * 10 / LOCAL:1) + 1

		IF EXP:CHARA:口淫経験 < LOCAL:2
			EXP:CHARA:口淫経験 = LOCAL:2
			TCVAR:CHARA:口淫経験 = EXP:CHARA:口淫経験
		ENDIF
	CASE "胸"
		LOCAL = EXPLV:(ABL:CHARA:胸 + 3)
		LOCAL:1 = 10 + ABL:CHARA:技巧 / 2 + TALENT:CHARA:バストサイズ + TALENT:CHARA:習得速度
		SIF GETBIT (CFLAG:CHARA:引き継ぎ, 5)
			LOCAL:1 *= 4

		LOCAL:2 = (LOCAL * 10 / LOCAL:1) + 1

		IF EXP:CHARA:パイズリ経験 + EXP:CHARA:噴乳経験 / 5 < EXPLV:(ABL:CHARA:胸 + 1)
			LOCAL:6 = EXPLV:(ABL:CHARA:胸 + 1) - (EXP:CHARA:パイズリ経験 + EXP:CHARA:噴乳経験 / 5)
			IF EXP:CHARA:噴乳経験 || TALENT:CHARA:母乳体質
				LOCAL:7 = LOCAL:6 / 2
				EXP:CHARA:噴乳経験 += LOCAL:7 * 5
				LOCAL:6 -= LOCAL:7
			ENDIF
			EXP:CHARA:パイズリ経験 += LOCAL:6
		ENDIF

		IF EXP:CHARA:パイズリ経験 * 2 +  EXP:CHARA:Ｂ経験 / 5 + EXP:CHARA:噴乳経験 / 4 < LOCAL:2
			LOCAL:3 = LOCAL:2 - (EXP:CHARA:パイズリ経験 * 2 +  EXP:CHARA:Ｂ経験 / 5 + EXP:CHARA:噴乳経験 / 4)
			LOCAL:4 = 0
			SIF EXP:CHARA:噴乳経験 || TALENT:CHARA:母乳体質
				LOCAL:4 ++
			;パイズリ経験 and Ｂ経験 are always valid
			LOCAL:4 += 2
			
			IF EXP:CHARA:噴乳経験 || TALENT:CHARA:母乳体質
				LOCAL:5 = LOCAL:3 / LOCAL:4
				EXP:CHARA:噴乳経験 += LOCAL:5 * 4
				LOCAL:3 -= LOCAL:5
				LOCAL:4--
			ENDIF

			LOCAL:5 = LOCAL:3 / LOCAL:4
			EXP:CHARA:Ｂ経験 += LOCAL:5 * 5
			LOCAL:3 -= LOCAL:5

			EXP:CHARA:パイズリ経験 += LOCAL:3 / 2 + 1

			TCVAR:CHARA:パイズリ経験 = EXP:CHARA:パイズリ経験
			TCVAR:CHARA:Ｂ経験 = EXP:CHARA:Ｂ経験
			TCVAR:CHARA:噴乳経験 = EXP:CHARA:噴乳経験
		ENDIF
	CASE "腰"
		LOCAL = EXPLV:(ABL:CHARA:腰 + 3)
		LOCAL:1 = 10 + ABL:CHARA:技巧 / 2 + TALENT:CHARA:習得速度
		SIF GETBIT (CFLAG:CHARA:引き継ぎ, 3) || GETBIT (CFLAG:CHARA:引き継ぎ, 4)
			LOCAL:1 *= 4

		LOCAL:2 = (LOCAL * 10 / LOCAL:1) + 1

		IF EXP:CHARA:Ｖ性交経験 + EXP:CHARA:Ａ性交経験 + EXP:CHARA:挿入経験 < LOCAL:2
			LOCAL:3 = LOCAL:2 - EXP:CHARA:Ｖ性交経験 - EXP:CHARA:Ａ性交経験 - EXP:CHARA:挿入経験
			LOCAL:4 = 0
			SIF EXP:CHARA:Ｖ性交経験
				LOCAL:4 ++
			SIF EXP:CHARA:Ａ性交経験
				LOCAL:4 ++
			;挿入経験 is always valid
			LOCAL:4 ++
			
			IF EXP:CHARA:Ｖ性交経験
				LOCAL:5 = LOCAL:3 / LOCAL:4
				EXP:CHARA:Ｖ性交経験 += LOCAL:5
				LOCAL:3 -= LOCAL:5
				LOCAL:4--
			ENDIF
			IF EXP:CHARA:Ａ性交経験
				LOCAL:5 = LOCAL:3 / LOCAL:4
				EXP:CHARA:Ａ性交経験 += LOCAL:5
				LOCAL:3 -= LOCAL:5
			ENDIF
			EXP:CHARA:挿入経験 += LOCAL:3

			TCVAR:CHARA:Ｖ性交経験 = EXP:CHARA:Ｖ性交経験
			TCVAR:CHARA:Ａ性交経験 = EXP:CHARA:Ａ性交経験
			TCVAR:CHARA:挿入経験 = EXP:CHARA:挿入経験
		ENDIF
	CASE "膣"
		LOCAL = EXPLV:(ABL:CHARA:膣 + 3)
		LOCAL:1 = 10 + ABL:CHARA:技巧 / 2 + TALENT:CHARA:習得速度
		SIF GETBIT (CFLAG:CHARA:引き継ぎ, 3)
			LOCAL:1 *= 4
		LOCAL:2 = (LOCAL * 10 / LOCAL:1) + 1

		IF EXP:CHARA:Ｖ性交経験 < EXPLV:(ABL:CHARA:膣 + 1)
			LOCAL:6 = EXPLV:(ABL:CHARA:膣 + 1) - EXP:CHARA:Ｖ性交経験
			EXP:CHARA:Ｖ性交経験 += LOCAL:6
		ENDIF

		IF EXP:CHARA:Ｖ性交経験  * 2 +  EXP:CHARA:Ｖ経験 / 5 + (EXP:CHARA:Ｖ拡張経験 <= 10 ? EXP:CHARA:Ｖ拡張経験 * 5 # 50 + EXP:CHARA:Ｖ拡張経験) < LOCAL:2
			LOCAL:3 = LOCAL:2 - (EXP:CHARA:Ｖ性交経験  * 2 +  EXP:CHARA:Ｖ経験 / 5 + (EXP:CHARA:Ｖ拡張経験 <= 10 ? EXP:CHARA:Ｖ拡張経験 * 5 # 50 + EXP:CHARA:Ｖ拡張経験))
			LOCAL:4 = 0
			SIF EXP:CHARA:Ｖ拡張経験
				LOCAL:4 ++
			;Ｖ性交経験 and Ｖ経験 are always valid
			LOCAL:4 += 2
			
			IF EXP:CHARA:Ｖ拡張経験
				LOCAL:5 = LOCAL:3 / LOCAL:4
				LOCAL:6 = MIN(10 - EXP:CHARA:Ｖ拡張経験, LOCAL:5 / 5)
				IF LOCAL:6 > 0
					EXP:CHARA:Ｖ拡張経験 += LOCAL:6
					LOCAL:3 -= LOCAL:6 * 5
					LOCAL:5 -= LOCAL:6 * 5
				ENDIF
				
				EXP:CHARA:Ｖ拡張経験 += LOCAL:5
				LOCAL:3 -= LOCAL:5
				LOCAL:4--
			ENDIF

			LOCAL:5 = LOCAL:3 / LOCAL:4
			EXP:CHARA:Ｖ経験 += LOCAL:5 * 5
			LOCAL:3 -= LOCAL:5

			EXP:CHARA:Ｖ性交経験 += LOCAL:3 / 2 + 1

			TCVAR:CHARA:Ｖ性交経験 = EXP:CHARA:Ｖ性交経験
			TCVAR:CHARA:Ｖ経験 = EXP:CHARA:Ｖ経験
			TCVAR:CHARA:Ｖ拡張経験 = EXP:CHARA:Ｖ拡張経験
		ENDIF
	CASE "肛門"
		LOCAL = EXPLV:(ABL:CHARA:アナル + 3)
		LOCAL:1 = 10 + ABL:CHARA:技巧 / 2 + TALENT:CHARA:習得速度
		SIF GETBIT (CFLAG:CHARA:引き継ぎ, 3)
			LOCAL:1 *= 4
		LOCAL:2 = (LOCAL * 10 / LOCAL:1) + 1

		IF EXP:CHARA:Ａ性交経験 < EXPLV:(ABL:CHARA:アナル + 1)
			LOCAL:6 = EXPLV:(ABL:CHARA:アナル + 1) - EXP:CHARA:Ａ性交経験
			EXP:CHARA:Ａ性交経験 += LOCAL:6
		ENDIF

		IF EXP:CHARA:Ａ性交経験 * 2 +  EXP:CHARA:Ａ経験 / 5 + (EXP:CHARA:Ａ拡張経験 <= 25 ? EXP:CHARA:Ａ拡張経験 * 2 # 50 + EXP:CHARA:Ａ拡張経験) < LOCAL:2
			LOCAL:3 = LOCAL:2 - (EXP:CHARA:Ａ性交経験 * 2 +  EXP:CHARA:Ａ経験 / 5 + (EXP:CHARA:Ａ拡張経験 <= 25 ? EXP:CHARA:Ａ拡張経験 * 2 # 50 + EXP:CHARA:Ａ拡張経験))
			LOCAL:4 = 0
			SIF EXP:CHARA:Ａ拡張経験
				LOCAL:4 ++
			;Ａ性交経験 and Ａ経験 are always valid
			LOCAL:4 += 2
			
			IF EXP:CHARA:Ａ拡張経験
				LOCAL:5 = LOCAL:3 / LOCAL:4
				LOCAL:6 = MIN(25 - EXP:CHARA:Ａ拡張経験, LOCAL:5 / 2)
				IF LOCAL:6 > 0
					EXP:CHARA:Ａ拡張経験 += LOCAL:6
					LOCAL:3 -= LOCAL:6 * 2
					LOCAL:5 -= LOCAL:6 * 2
				ENDIF
				
				EXP:CHARA:Ａ拡張経験 += LOCAL:5
				LOCAL:3 -= LOCAL:5
				LOCAL:4--
			ENDIF

			LOCAL:5 = LOCAL:3 / LOCAL:4
			EXP:CHARA:Ａ経験 += LOCAL:5 * 5
			LOCAL:3 -= LOCAL:5

			EXP:CHARA:Ａ性交経験 += LOCAL:3 / 2 + 1

			TCVAR:CHARA:Ａ性交経験 = EXP:CHARA:Ａ性交経験
			TCVAR:CHARA:Ａ経験 = EXP:CHARA:Ａ経験
			TCVAR:CHARA:Ａ拡張経験 = EXP:CHARA:Ａ拡張経験
		ENDIF
	CASEELSE
		FOR LOCAL,0,4
			FOR LOCAL:1,0,100
				SIF JUEL_DEMAND(ABL_INDEX,LOCAL,LOCAL:1,CHARA) < 0
					CONTINUE
				LOCAL:2 = EXP_DEMAND(ABL_INDEX,LOCAL,LOCAL:1,CHARA)
				IF EXP:CHARA:(LOCAL:1) < LOCAL:2
					EXP:CHARA:(LOCAL:1) = LOCAL:2
					TCVAR:CHARA:(400 + LOCAL:1) = EXP:CHARA:(LOCAL:1) ;don't show the increase
				ENDIF
			NEXT
		NEXT
ENDSELECT

ABL:CHARA:ABL_INDEX = ABL_LEVEL

@TRIM_TEXT(TEXT)
#LOCALSIZE 1
#LOCALSSIZE 1
#FUNCTIONS
#DIMS TEXT
RETURNF REPLACE(TEXT, "^\\s+|\\s+$|\\s+(?=\\s)", "")
