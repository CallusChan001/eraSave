@QOL_CHECK_DANGEROUS_TRAVEL(TSP)
#DIM TSP
#DIM LUCK
#DIM ROLL
PRINTFORMDL 为了节省时间，你抄近路穿过地底的隧道。

LUCK = 300
LUCK += CFLAG:MASTER:今日の運勢
LUCK += TFLAG:幸運補正 * 100 ;daily fortune
LUCK += TFLAG:DangerousTravelBonus

SIF DAY:3 == 13 && YOUBI_MATCH(DAY, "金")
    LUCK -= 750
LUCK += ABL:MASTER:教養 * 50
LUCK = MAX(LUCK, 1)

ROLL = RAND:LUCK

IF ROLL < (20 * MAX(TFLAG:DangerousTravelDisaster, 1)) ;very bad result, odds increase with a specific daily travel fortune
    CALL QOL_TRAVEL_MAJOR_ACCIDENT(TSP)
ELSEIF ROLL < 100 ;minor incident
    CALL QOL_TRAVEL_MINOR_ACCIDENT(TSP)
ENDIF

@QOL_TRAVEL_MINOR_ACCIDENT(TSP)
#DIM TSP
SELECTCASE RAND:5
    CASE 0
        PRINTFORMDL 当+++穿行于地下狭窄、黑暗的小径时，不小心把小脚趾踢到了石头上。
        CALL RECOVER(MASTER, -MAX(100, QOL_UNBUFFED_MAXBASE(BASE_体力) / 20), "体力", "唉哟!")
        WAIT
    CASE 1
        PRINTFORMDL 当+++穿过地下光线昏暗的隧道时，不小心被什么东西绊倒，摔了个狗吃屎。
        CALL RECOVER(MASTER, -MAX(150, QOL_UNBUFFED_MAXBASE(BASE_体力) / 15), "体力", "唉哟!")
        CALL RECOVER(MASTER, -MAX(150, QOL_UNBUFFED_MAXBASE(BASE_気力) / 15), "気力", "唉哟!")
        IF TSP
            CALL RECOVER(MASTER, -30, "TSP", "唉哟!")
        ELSE
            TIME += 10
        ENDIF
        WAIT
    CASE 2
        PRINTFORMDL 当+++越过鲜有人迹的地下隧道时，不小心掉了什么东西。
        SELECTCASE RAND:3
            CASE 0
                CALL LOST_MONEY_YEN(2000, 2)
            CASE 1
                CALL LOST_MONEY_CM(20, 2)
            CASE 2
                IF TSP
                    CALL RECOVER(MASTER, -45, "TSP", "在黑暗中摸索")
                ELSE
                    TIME += 15
                ENDIF
                PRINTFORMDW 因为这东西很重要，+++花了一些时间找回来了。
        ENDSELECT
    CASE 3
        IF TSP
            CALL RECOVER(MASTER, -60, "TSP", "迷路了")
        ELSE
            TIME += 20
        ENDIF
        PRINTFORMDW +++在穿过洞穴时迷路了，花了一些时间才得以脱身。
    CASE 4
        PRINTFORMDL 当+++穿行于深处时，洞穴中回荡着响亮的哭泣声。
        PRINTFORMDL +++想去调查一下吗？
        CALL ASK_YN
        IF RESULT
            PRINTFORMDW +++忽略了这个明显的陷阱，径直前往目的地。
        ELSE
            PRINTFORMDL 循着声音来到源头，+++发现永琳手下的一个因幡跪坐在地上，嚎啕大哭。
            PRINTFORMDL +++设法让她平静下来后，她解释说永琳派她去妖怪之山送药。
            PRINTFORMDL 因为她耽误了时间，所以决定走地下通道抄近路，结果却迷路了。
            PRINTFORMDL +++没法放着不管，决定先送她回去。
            PRINTFORMDL 两人回到地面后，因幡给了+++一个温暖的拥抱，然后匆匆告辞了。
            PRINTFORMDL 当+++再次前往自己的目的地时，+++发现因幡在离开前偷偷塞了什么东西到+++的口袋里。

            SELECTCASE RAND:4
                CASE 0
                    CALL GET_ITEM("栄養飲料", 1, 2)
                CASE 1
                    CALL GET_ITEM("精力剤", 1, 2)
                CASE 2
                    CALL GET_ITEM("大回復薬", 1, 2)
                CASE 3
                    CALL GET_ITEM("流星群Ｘ", 1, 2)
            ENDSELECT

            CALL CHANGE_CFLAG(4,[[永琳]],10)
            CALL CHANGE_CFLAG(4,[[輝夜]],10)
            CALL CHANGE_CFLAG(4,[[てゐ]],10)
            CALL CHANGE_CFLAG(4,[[鈴仙]],10)
            WAIT
            TIME += 20
        ENDIF
ENDSELECT

@QOL_TRAVEL_MAJOR_ACCIDENT(TSP)
#DIM TSP

SELECTCASE RAND:3
    CASE 0
        CALL QOL_TRAVEL_MEET_ONI(TSP)
    CASE 1
        IF TSP
            CALL RECOVER(MASTER, -540, "TSP", "迷路了")
        ELSE
            TIME += 180
        ENDIF
        PRINTFORMDL 穿行于洞穴时，+++彻底迷路了。
        PRINTFORMDW 损失了大量时间才得以脱身。
    CASE 2
        PRINTFORML ……洞穴里发生了什么。
        CALL COLORMESSAGE("！！！塌方了！！！",C_RED,2,1)
        
        PRINTFORML 
        PRINTFORMW 经过五个漫长的小时，+++终于爬了出来，但剧痛无比……
        TIME += 300
        CALL RECOVER(MASTER, -MAX(10000, MULTIPLY(QOL_UNBUFFED_MAXBASE(BASE_体力), 90)), "体力", "唉哟!")
        CALL RECOVER(MASTER, -MAX(10000, MULTIPLY(QOL_UNBUFFED_MAXBASE(BASE_気力), 90)), "気力", "唉哟!")
ENDSELECT

@QOL_TRAVEL_MEET_ONI(TSP)
#DIM TSP
#DIM DYNAMIC PROTECTORS = [[萃香]], [[勇儀]], [[さとり]]
#DIM DYNAMIC PROTECTORS_WEIGHTS, 3
#DIM CHARA

FOR LOCAL, 0, 3
    CHARA = PROTECTORS:LOCAL
    PROTECTORS_WEIGHTS:LOCAL += CBRT(CFLAG:CHARA:好感度)
    SIF TALENT:CHARA:恋人
        PROTECTORS_WEIGHTS:LOCAL += 20
    PROTECTORS_WEIGHTS:LOCAL += 5 * TALENT:CHARA:恋慕
    PROTECTORS_WEIGHTS:LOCAL += 5 * TALENT:CHARA:愛欲
NEXT

CALL MAKE_YUKIZURI(RANDOM_CHARANUM, "鬼", 1)
CFLAG:RANDOM_CHARANUM:デート中 = -1 ;bug fix, disable dating flag

IF TSP
    PRINTFORMDL 穿行于迷宫般的通道时，+++不小心失去平衡，摔了个狗吃屎。
    PRINTFORMDW 这一震让+++失去了对停止时间的控制。
    SIF FLAG:70
        RESETBGCOLOR
    PRINTFORMDL 当+++重新站起来时，一个高大的身影出现在面前。
    CALL SPTALK, RANDOM_CHARANUM, "通常", 1, @"「你是谁啊，怎么跑到这儿来了？」「这里可不是人类该来的地方，你懂吧？」", 1
ELSE
    PRINTFORMDW 当+++穿过隧道时，突然一个高大威严的身影出现在面前。
    CALL SPTALK, RANDOM_CHARANUM, "通常", 1, @"「一个人类，来这儿？」「你是不是迷路了啊？」", 1
ENDIF
CALL SPTALK, RANDOM_CHARANUM, "笑顔", 1, @"「嗯，俺也不太在乎你在这儿干啥，但这里是鬼族的地盘。」/「要是想过去，就得交过路费，咕呀。」", 1
WAIT

IF RAND:300 < MIN(SUMARRAY(PROTECTORS_WEIGHTS), 100)
    ;friends in low places
    CHARA = PROTECTORS:ARRAY_HIT(PROTECTORS_WEIGHTS, 3)

    SELECTCASE CHARA
        CASE [[さとり]]
            CALL SPTALK, RANDOM_CHARANUM, "通常", 1, @"「等等，你不是觉大姐的那个人类宠物吗？」", 1
            CALL SPTALK, RANDOM_CHARANUM, "憤怒", 1, @"「啧。俺可不想跟她打交道。」", 1
            CALL SPTALK, RANDOM_CHARANUM, "通常", 1, @"「那就快走吧。俺什么都没看见。」", 1
            WAIT
            PRINTFORMDL 没等鬼改变主意，+++再次停止时间，朝目的地进发。
        CASEELSE
            CALL SPTALK, RANDOM_CHARANUM, "通常", 1, @"「等等，你不是%CALLNAME:CHARA%的朋友吗？」「俺要是动了她的朋友，她肯定会很生气吧。」/「那你走运了。你可以通过了。」", 1
            IF FLAG:70
                CALL SPTALK, RANDOM_CHARANUM, "笑顔", 1, @"「哎，咱还能帮你一把，你小子要去哪儿-」", 1
                CALL SPTALK, RANDOM_CHARANUM, "通常", 1, @"「哎，他去哪儿了？」", 1
                PRINTFORMDL 鬼还没说完，+++再次停止时间，朝目的地进发。
            ELSE
                ;get escorted out
                CALL SPTALK, RANDOM_CHARANUM, "笑顔", 1, @"「哎，咱还能帮你一把，你小子要去哪儿？」", 1
                PRINTFORMDL 鬼带着+++穿过地下隧道，在她的帮助下，+++比预想的更快到达了目的地。
                TIME -= 10
            ENDIF
    ENDSELECT
    PRINTFORMDW 看来在这里有朋友还是有好处的。
ELSE
    SELECTCASE RAND:5
        CASE 0
            ;money
            LOCAL = QOL_GET_YEAR(DAY) * 10000 ;increases every year
            CALL SPTALK, RANDOM_CHARANUM, "通常", 1, @"「俺需要些酒钱，所以你给俺{LOCAL}日元就让你过去。」", 1
            CALL ASK_YN("接受", "拒绝")
            IF RESULT
                CALL SPTALK, RANDOM_CHARANUM, "憤怒", 1, @"「你觉得自己很强硬是吧？看来俺得让你尝尝鬼族的厉害，然后从你尸体上拿钱了。」", 1
                CALL QOL_TRAVEL_ONI_FIGHT(3)
            ELSE
                IF MONEY < LOCAL
                    CALL SPTALK, RANDOM_CHARANUM, "憤怒", 1, @"「你连这点钱都没有？」「啧。真是气死俺了。」/「看来你需要好好回想一下鬼族有多可怕了。」", 1
                    CALL QOL_TRAVEL_ONI_FIGHT(2)
                ELSE
                    CALL SPTALK, RANDOM_CHARANUM, "笑顔", 1, @"「很高兴你这么懂事。快走吧。」", 1
                    CALL LOST_MONEY_YEN(LOCAL, 2)
                ENDIF
            ENDIF
        CASE 1
            ;charisma
            LOCAL = MIN(QOL_GET_YEAR(DAY) * 30, 2000) ;increases every year
            CALL SPTALK, RANDOM_CHARANUM, "通常", 1, @"「俺有点想去赌博了，所以你给俺{LOCAL}枚筹码就让你过去。」", 1
            CALL ASK_YN("接受", "拒绝")
            IF RESULT
                CALL SPTALK, RANDOM_CHARANUM, "憤怒", 1, @"「你觉得自己很强硬是吧？看来俺得让你尝尝鬼族的厉害，然后从你尸体上拿走了。」", 1
                CALL QOL_TRAVEL_ONI_FIGHT(3)
            ELSE
                IF MONEY:2 < LOCAL
                    CALL SPTALK, RANDOM_CHARANUM, "憤怒", 1, @"「你连这点都没有？」「啧。真是气死俺了。」/「看来你需要好好回想一下鬼族有多可怕了。」", 1
                    CALL QOL_TRAVEL_ONI_FIGHT(3)
                ELSE
                    CALL SPTALK, RANDOM_CHARANUM, "笑顔", 1, @"「很高兴你这么懂事。快走吧。」", 1
                    CALL LOST_MONEY_CM(LOCAL, 2)
                ENDIF
            ENDIF
        CASE 2
            ;booze
            LOCAL = QOL_GET_ITEMID_FROM_ALCOHOL_ID(QOL_GET_RANDOM_DRINK())
            CALL SPTALK, RANDOM_CHARANUM, "通常", 1, @"「俺渴得很，你要是给俺一瓶%ITEMNAME_TR(LOCAL)%就让你过去。」", 1
            CALL ASK_YN("接受", "拒绝")
            IF RESULT
                $REFUSE_BOOZE
                CALL SPTALK, RANDOM_CHARANUM, "憤怒", 1, @"「你觉得自己很强硬是吧？看来俺得让你尝尝鬼族的厉害，然后从你尸体上拿走东西了。」", 1
                CALL QOL_TRAVEL_ONI_FIGHT(4, LOCAL)
            ELSE
                IF ITEM:LOCAL >= 1
                    $ACCEPT_BOOZE
                    CALL SPTALK, RANDOM_CHARANUM, "笑顔", 1, @"「你做出了明智的选择。」/「那俺就收下了。」", 1
                    CALL LOST_ITEM(ITEMNAME:LOCAL, 1, 2)
                ELSE
                    LOCAL:1 = LOCAL
                    WHILE LOCAL:1 == LOCAL
                        LOCAL:1 = QOL_GET_ITEMID_FROM_ALCOHOL_ID(QOL_GET_RANDOM_DRINK())
                    WEND
                    CALL SPTALK, RANDOM_CHARANUM, "通常", 1, @"「你没有那酒？」/「那好吧。俺就要一瓶%ITEMNAME_TR(LOCAL:1)%好了。」", 1
                    CALL ASK_YN("接受", "拒绝")
                    IF RESULT
                        LOCAL = LOCAL:1
                        GOTO REFUSE_BOOZE
                    ELSE
                        IF ITEM:(LOCAL:1) >= 1
                            LOCAL = LOCAL:1
                            GOTO ACCEPT_BOOZE
                        ELSE
                            LOCAL:2 = LOCAL:1
                            WHILE LOCAL:2 == LOCAL:1 || LOCAL:2 == LOCAL
                                LOCAL:2 = QOL_GET_ITEMID_FROM_ALCOHOL_ID(QOL_GET_RANDOM_DRINK())
                            WEND
                            CALL SPTALK, RANDOM_CHARANUM, "憤怒", 1, @"「那些你也没有吗？」/「行吧。那俺就要一瓶%ITEMNAME_TR(LOCAL:2)%好了。」", 1
                            CALL ASK_YN("接受", "拒绝")
                            IF RESULT
                                LOCAL = LOCAL:2
                                GOTO REFUSE_BOOZE
                            ELSE
                                IF ITEM:(LOCAL:2) >= 1
                                    LOCAL = LOCAL:2
                                    GOTO ACCEPT_BOOZE
                                ELSE
                                    CALL SPTALK, RANDOM_CHARANUM, "憤怒", 1, @"「你连那个都没有？！」/「啧。看来姐们得给你好好上一课，让你知道消遣俺们鬼族的下场！」", 1
                                    CALL QOL_TRAVEL_ONI_FIGHT(4, 52)
                                ENDIF
                            ENDIF
                        ENDIF
                    ENDIF
                ENDIF
            ENDIF
        CASE 3
            ;sex
            CALL SPTALK, RANDOM_CHARANUM, "通常", 1, @"「俺好久没做了，上次打架后那澡堂就不让俺进了。」/「你要是帮俺解解痒，俺就让你过去。」", 1
            CALL ASK_YN("接受", "拒绝")
            IF RESULT
                CALL SPTALK, RANDOM_CHARANUM, "憤怒", 1, @"「你觉得自己很清高是吧？那俺就把你打一顿，然后强行拿走俺想要的。」", 1
                CALL QOL_TRAVEL_ONI_FIGHT(1)
            ELSE
                ;accept
                CALL SPTALK, RANDOM_CHARANUM, "笑顔", 1, @"「你这家伙还蛮懂事。来吧，俺们开始吧。」", 1
                CALL SPTALK, RANDOM_CHARANUM, "通常", 1, @"「啊，你可以叫俺%CALLNAME:RANDOM_CHARANUM%。」", 1
                PRINTFORMDL 鬼把你带到隧道里的一小块平地。
                PRINTFORMDW %CALLNAME:RANDOM_CHARANUM%迅速脱下衣服，坐在地上。
                CALL SPTALK, RANDOM_CHARANUM, "笑顔:裸", 1, @"「快点，快点，赶紧开始吧！」", 1

                SELECTCASE RAND:1
                    CASE 0
                        PRINTFORMDL %CALLNAME:RANDOM_CHARANUM%张开双腿，向你展示她的%SLIT(RANDOM_CHARANUM)%。
                        CALL SPTALK, RANDOM_CHARANUM, "通常:裸", 1, @"「你还在等什么？快用你的舌头干活。」", 1
                        PRINTFORMDW 在鬼的催促下，+++开始舔弄她。
                        CALL SPTALK, RANDOM_CHARANUM, "笑顔:裸", 1, @"「%MOAN()%……嗯，这正是俺需要的。」", 1
                        PRINTFORMDL +++继续取悦她，%CALLNAME:RANDOM_CHARANUM%的呻吟声在整个洞穴系统中大声回荡。
                        IF ABL:MASTER:舌 > 3
                            CALL SPTALK, RANDOM_CHARANUM, "笑顔:裸", 1, @"「%MOAN("m")%……你这方面挺行的啊。」/「那些娘们儿肯定很喜欢你。」", 1
                        ELSE
                            CALL SPTALK, RANDOM_CHARANUM, "通常:裸", 1, @"「来吧，再用力点。」/「俺知道你比这做得更好。」", 1
                            CALL SPTALK, RANDOM_CHARANUM, "笑顔:裸", 1, @"「%MOAN("m")%……这样好多了。继续这样。」", 1
                        ENDIF
                        WAIT

                        CALL SPTALK, RANDOM_CHARANUM, "通常:裸", 1, @"「也让俺让你爽爽。」/「要是俺一个人爽，那可不公平，对吧？」", 1
                        WAIT
                        PRINTFORMDL %CALLNAME:RANDOM_CHARANUM%突然抓住+++的腰，把+++双腿朝上地翻了个个。
                        IF HAS_PENIS(MASTER)
                            CALL SPTALK, RANDOM_CHARANUM, "笑顔:裸", 1, @"「你已经又硬又棒了。」/「\@ TALENT:MASTER:形状 >= 3 ? 形状也很好看。 # 真是个可爱的小小家伙。 \@」", 1
                            PRINTFORMDW 鬼把%TINKO()%含进嘴里，开始用惊人的力量吸吮。
                        ELSE
                            CALL SPTALK, RANDOM_CHARANUM, "笑顔:裸", 1, @"「你已经这么湿了。」/「让俺尝尝你。」", 1
                            PRINTFORMDW 鬼也开始舔弄你的%SLIT(MASTER)%。显然她在这方面经验丰富。
                        ENDIF
                        PRINTFORMDL +++和%CALLNAME:RANDOM_CHARANUM%继续互相取悦，互相摩擦和舔舐秘处。

                        IF HAS_PENIS(MASTER)
                            PRINTFORMDL 鬼熟练的吸吮很快把你推向极限；她迫不及待地吞下你的%SAMEN_DESCRIPTION(MASTER, 1)%。
                            PRINTFORMDW 这也把她推向高潮，她猛烈地朝你的脸上潮喷。
                            CALL SPTALK, RANDOM_CHARANUM, "笑顔:裸", 1, @"「%MOAN("r",,3)%。 」", 1
                        ELSE
                            PRINTFORMDL 鬼很快达到极限，潮喷了你一脸。
                            CALL SPTALK, RANDOM_CHARANUM, "笑顔:裸", 1, @"「%MOAN("r",,3)%」", 1
                            PRINTFORMDL 然而，她依然没有放松，她持续的爱抚迫使两人同时达到了高潮。
                        ENDIF
                        PRINTFORMDW 完事后，两人互相躺着，大口喘气，从高潮中恢复过来。
                ENDSELECT
                PRINTL
                PRINTFORMDL 两人的互通有无结束后，+++和%CALLNAME:RANDOM_CHARANUM%放开彼此站起身，穿回衣服。
                CALL SPTALK, RANDOM_CHARANUM, "笑顔", 1, @"「跟你玩得很开心啊。」/「出去的时候小心点，俺可不想失去像你这样有趣的%PRINT_MALE("小子:姑娘", MASTER)%」", 1
                PRINTFORMDL 满意之后，鬼终于放+++走了。
                TIME += 30
            ENDIF
        CASE 4
            ;fight
            CALL SPTALK, RANDOM_CHARANUM, "通常", 1, @"「俺在这儿待得挺无聊的，俺们干嘛不比试一下？」", 1
            CALL ASK_YN("接受", "拒绝")
            IF RESULT
                CALL SPTALK, RANDOM_CHARANUM, "憤怒", 1, @"「你搞错了，这不是个问题。」/「想办法取悦俺吧。」", 1
            ELSE
                CALL SPTALK, RANDOM_CHARANUM, "笑顔", 1, @"「你这家伙还蛮懂事。那俺们开始吧。」", 1
            ENDIF
            CALL QOL_TRAVEL_ONI_FIGHT(0)
    ENDSELECT
    SIF TSP
        PRINTFORMDL 鬼终于离开了，你再次停止时间，继续前往你最初的目的地。
ENDIF
IF FLAG:70
    SETBGCOLOR C_THE_WORLD
ENDIF

CALL Add_HideMob()

@QOL_TRAVEL_ONI_FIGHT(DESIRED_REWARD, DESIRED_TYPE)
#DIM モブ残機
#DIM DESIRED_REWARD
#DIM DESIRED_TYPE
TARGET = RANDOM_CHARANUM
TIME += 60
CFLAG:面識 = 1
HANDICAP_FIXED = RAND:(ABL:戦闘能力 * 8 + 1) + 10
HANDICAP_RAND = ABL:戦闘能力 * 50

CALL KOJO_MESSAGE_SEND("DANMAKU", 0, TARGET, 1, -1, "戦闘前")

DRAWLINE
PRINTFORML 
CALL DANMAKU_OPPONENT_STATUS(TARGET)
IF HANDICAP_FIXED
	CALL COLORMESSAGE(@"地形效果：%CALLNAME:MASTER%的骰子点数减少{HANDICAP_FIXED}！",C_YELLOW,1)
	;【三粒の天滴】的效果
	IF ITEM:三粒の天滴
		HANDICAP_FIXED /= 2
		CALL COLORMESSAGE(@"但是土著神的恩赐抵消了一半，变为{HANDICAP_FIXED}！",C_YELLOW,1)
	ENDIF
ENDIF
CALL COLORMESSAGE(@"在这些狭窄的隧道中战斗使你处于严重劣势！",C_YELLOW,1)

PRINTFORMW 对决开始！！！
CALL DANMAKU_BATTLE(TARGET, 1)

モブ残機 = RESULT
CALL RECOVER(MASTER, -500, "体力")
CALL RECOVER(MASTER, -500, "気力")
CALL KOJO_MESSAGE_SEND("DANMAKU", モブ残機, TARGET, 1, -1, "戦闘後")
SIF モブ残機 < 3
	EXP:MASTER:戦闘経験 += ABL:戦闘能力 * 3
SIF モブ残機 < 1
	EXP:MASTER:戦闘経験 += ABL:戦闘能力
IF モブ残機 > 0
	Qol_Info_Count_Array:MASTER:Qol_Count_EncountLost++

    SELECTCASE DESIRED_REWARD
        CASE 0
            CALL SPTALK, RANDOM_CHARANUM, "通常", 1, @"「也许俺有点玩过火了。」/「啊，算了。就这样吧。」", 1
            PRINTFORMDW 鬼玩得很开心，随意地走开了，留下+++独自疗伤。
        CASE 1
            PRINTFORMDW 动弹不得的+++被%CALLNAME:TARGET%压制住。
            PRINTFORMDW +++感觉到一股温暖，几乎是湿热的气息，呼在了+++毫无防备的喉咙上……
            ;CALL Add_LOST_GYAKURE_EX(TARGET, 0)
            ;SIF RESULT
            ;    GOTO GYAKURE_END

            PRINTFORMDW 正当+++以为一切都完蛋的时候时，一股非常灼热而舒适的感觉包裹住了+++的下体。
            PRINTFORMDW +++小心翼翼地瞥了一眼自己的下半身，只见%CALLNAME:TARGET%正得意地冲这边笑着，%FSYN("penis")%牢牢地陷入她的%FSYN("vagina",,TARGET)%中，%FSYN("base")%。
            PRINTFORMDW 
            PRINTFORMD %CALLNAME:TARGET%尽情地蹂躏、欺负、侵犯可怜的+++，以满足她野兽般的欲望……
            CALL PRINT_BREAK,,"wait"
            CALL TRIPLEDOT("reverse:timed")
            PRINTFORMDW 在%CALLNAME:TARGET%心满意足地一番蹂躏之后，+++发现自己被丢出了地底，很接近目的地了。
            
            $GYAKURE_END
            TIME += 60
            FLAG:気絶中断 = 100 + CFLAG:MASTER:現在位置 / 100
            CFLAG:MASTER:デート中 = CFLAG:MASTER:現在位置 / 100
            CALL LOST_GYAKURE(TARGET)
        CASE 2
            CALL SPTALK, RANDOM_CHARANUM, "通常", 1, @"「你输了，所以俺要拿走俺的报酬。」", 1
		    CALL LOST_MONEY_YEN(QOL_GET_YEAR(DAY) * 50000, 2) ;increases every year
        CASE 3
            CALL SPTALK, RANDOM_CHARANUM, "通常", 1, @"「你输了，所以俺要拿走俺的报酬。」", 1
		    CALL LOST_MONEY_CM(QOL_GET_YEAR(DAY) * 150, 2) ;increases every year
        CASE 4
            CALL SPTALK, RANDOM_CHARANUM, "通常", 1, @"「你输了，所以俺要拿走俺的报酬。」", 1
            IF !ITEM:DESIRED_TYPE
                CALL SPTALK, RANDOM_CHARANUM, "憤怒", 1, @"「你身上连值得喝的东西都没有？」「你真是个可悲的%PRINT_MALE("家伙:妹子", MASTER)%啊。」/「给俺滚出去！」", 1
            ELSE
                CALL LOST_ITEM(ITEMNAME:DESIRED_TYPE, ITEM:DESIRED_TYPE, 2)
            ENDIF
    ENDSELECT
	RETURN 1
ELSE
    IF DESIRED_REWARD == 0
        CALL SPTALK, RANDOM_CHARANUM, "通常", 1, @"「你很强啊。很适合俺们鬼族。」/「如果你想找乐子，就来地下吧。」", 1
        PRINTFORMDW 尽管战败，鬼脸上却带着大大的笑容站了起来。
    ELSE
        CALL SPTALK, RANDOM_CHARANUM, "憤怒", 1, @"「咕呀。你比俺想象的要硬气。」/「俺走也。」", 1
        PRINTFORMDW 鬼迅速窜进地下众多隧道中的一条逃之夭夭。
    ENDIF
ENDIF

@Add_HideMob(ARG = -1) ;casts the mob character into the void to prevent movement error messages
SIF ARG == -1
	ARG = RANDOM_CHARANUM
CFLAG:ARG:現在位置 = SUKIMA()
CFLAG:ARG:出禁 = 2
