@Qol_SHOW_RELATION(nChara)
#DIM nChara
#DIM SORT_VALUE,  VARSIZE("RELATION")
#DIM CHARA_TABLE, VARSIZE("RELATION")
#DIM 表示項目数

VARSET CHARA_TABLE, -1
VARSET SORT_VALUE, -1
表示項目数 = 0

;method from tohoK
FOR LOCAL, 0, CHARANUM
	SORT_VALUE:LOCAL = RELATION:nChara:LOCAL
	CHARA_TABLE:LOCAL = LOCAL
NEXT

FOR LOCAL,0,CHARANUM
	SIF CHARA_TABLE:LOCAL == -1
		BREAK
	FOR LOCAL:11,LOCAL,0,-1
		LOCAL:12 = CHARA_TABLE:(LOCAL:11-1)
		LOCAL:13 = CHARA_TABLE:(LOCAL:11)
		IF (qol_Relation_AscendingOrder && SORT_VALUE:(LOCAL:12) < SORT_VALUE:(LOCAL:13)) || (!qol_Relation_AscendingOrder && SORT_VALUE:(LOCAL:12) > SORT_VALUE:(LOCAL:13))
			SWAP CHARA_TABLE:(LOCAL:11),CHARA_TABLE:(LOCAL:11 -1)
		ELSE
			BREAK
		ENDIF
	NEXT
NEXT

PRINTL
CALL PRINTLINE, "人际关系"
PRINTBUTTON @"[\@ qol_Relation_AscendingOrder ? 升序 # 降序 \@]", 9999
PRINTL 
FOR LOCAL, 0, CHARANUM
	SIF !RELATION:nChara:(CHARA_TABLE:LOCAL)
		CONTINUE
	PRINTFORM %CALLNAME:(CHARA_TABLE:LOCAL), 14, RIGHT%
	PRINT × 
	;色の選択
	SELECTCASE RELATION:nChara:(CHARA_TABLE:LOCAL)
	CASE IS < 60
		SETCOLOR C_RED
	CASE IS < 100
		SETCOLOR 0xE9967A
	CASE 100
		SETCOLOR C_YELLOW
	CASE IS < 130
		SETCOLOR C_P_GREEN
	CASE IS < 200
		SETCOLOR C_GREEN
	CASEELSE
		SETCOLOR C_AQUA
	ENDSELECT
	PRINTFORM {RELATION:nChara:(CHARA_TABLE:LOCAL), 4 ,RIGHT}\%
	RESETCOLOR
	表示項目数 ++
	SIF 表示項目数 % 5 == 0 ;custom code
		PRINTL 
NEXT
SIF 表示項目数 % 5 > 0 ;custom code
	PRINTL 
PRINTL 