;by AK Spectre (modified)
@Qol_MC_CHILD
PRINTL
CALL PRINTLINE, "儿童"
PRINTL
PRINTBUTTON "[查看儿童列表]", 9998
PRINTL

@QOL_MC_CHILD_SORT(SORT_TYPE, MC_TH_BELONG, MC_TH_SEQ, MC_TH_BIRTH, PAGE_START, MC_CHILD_PAGES, TOTAL_LINES)
#DIM SORT_TYPE
#DIM REF MC_TH_BELONG
#DIM REF MC_TH_SEQ
#DIM REF MC_TH_BIRTH
#DIM REF PAGE_START
#DIM REF MC_CHILD_PAGES
#DIM TOTAL_LINES
#DIM DYNAMIC CURRENT_LINECOUNT
#DIM MC_TH_TOTAL
#DIM DYNAMIC PAGE
#DIM LASTCHILD
#DIM CHILDNUM

VARSET PAGE_START, 0
PAGE_START:0 = 1

IF SORT_TYPE
	ARRAYMSORT MC_TH_BIRTH, MC_TH_BELONG, MC_TH_SEQ
	LOCAL:1 = TOTAL_LINES / 3
	SIF TOTAL_LINES % 3 == 0
		LOCAL:1 --
	FOR LOCAL, 0, VARSIZE("PAGE_START")
		PAGE_START:LOCAL = (LOCAL:1 * LOCAL) + 1
	NEXT
	MC_CHILD_PAGES = (MC_TH_TOTAL / LOCAL:1) + 1
ELSE
	;this sort is too complicated to do automatically, so we just do it manually every time
	MC_TH_BIRTH:0 = -10000 ;ensure these always remain at index 0, even after sort
	MC_TH_TOTAL = 0
	CURRENT_LINECOUNT = 2
	FOR LOCAL, 1, CHARANUM - 1
		IF CFLAG:LOCAL:子供人数 > 0
			CURRENT_LINECOUNT += 2
			LASTCHILD = MIN(CFLAG:LOCAL:子供人数 + 1, MAXCHILD+1)
			
			FOR COUNT, MAX(CFLAG:LOCAL:子供人数 - MAXCHILD + 1, 1), CFLAG:LOCAL:子供人数 + 1
				CHILDNUM = CHILD_INDEX(COUNT)
				MC_TH_TOTAL += 1
				MC_TH_BELONG:MC_TH_TOTAL = LOCAL
				MC_TH_SEQ:MC_TH_TOTAL = CHILDNUM
				MC_TH_BIRTH:MC_TH_TOTAL = CHILD_BIRTHDAY:LOCAL:CHILDNUM
				CURRENT_LINECOUNT += 2

				IF CURRENT_LINECOUNT >= TOTAL_LINES
					PAGE++
					PAGE_START:PAGE = MC_TH_TOTAL
					CURRENT_LINECOUNT = 4
				ENDIF
			NEXT
		ENDIF
	NEXT
	IF PAGE_START:PAGE <= MC_TH_TOTAL
		PAGE++
		PAGE_START:PAGE = MC_TH_TOTAL + 1
	ENDIF
	MC_CHILD_PAGES = PAGE
ENDIF

@Qol_MC_CHILD_LIST
#LOCALSIZE 2
#LOCALSSIZE 2
#DIM MC_CHILD_PAGES
;holds the mother's id
#DIM DYNAMIC MC_TH_BELONG, 人物数量上限 * MAXCHILD
;holds the index of the child
#DIM DYNAMIC MC_TH_SEQ, 人物数量上限 * MAXCHILD
;holds the day of birth of the child
#DIM DYNAMIC MC_TH_BIRTH, 人物数量上限 * MAXCHILD
;the start index for each page
#DIM DYNAMIC PAGE_START, 人物数量上限 * MAXCHILD / 10
#DIM MC_TH_UPPER
#DIM MC_TH_LOWER
#DIM DYNAMIC MC_TH_LL
#DIM DYNAMIC MC_TH_IN

#DIM nLineCnt_Head
#DIM nPage
#DIM DYNAMIC nSort
#DIM nPageSize
#DIM DYNAMIC LAST_MOM

FOR LOCAL, 1, CHARANUM - 1
	MC_TH_LL += MIN(CFLAG:LOCAL:子供人数, MAXCHILD)
NEXT

PRINTFORML
LOCAL = CHILD_SUPPORT_COST()
LOCALS = 子女总数:{CFLAG:MASTER:兒童人数},育儿人数:{CFLAG:MASTER:育児人数},预计的抚养费: %金額表示(LOCAL)%; Current Day: {DAY}

nPageSize = MAX(CLIENTHEIGHT() / GETCONFIG("フォントサイズ")-9, 30)
CALL QOL_MC_CHILD_SORT(0, MC_TH_BELONG, MC_TH_SEQ, MC_TH_BIRTH, PAGE_START, MC_CHILD_PAGES, nPageSize)

DRAWLINE
nLineCnt_Head = LINECOUNT
REDRAW 0
DO
	SIF LINECOUNT - nLineCnt_Head > 0
		CLEARLINE LINECOUNT - nLineCnt_Head

	IF nPage < 0
		nPage = MC_CHILD_PAGES - 1
	ELSEIF nPage >= MC_CHILD_PAGES
		nPage = 0
	ENDIF	

	MC_TH_UPPER = PAGE_START:(nPage+1)
	MC_TH_LOWER = PAGE_START:nPage 
		
	IF nSort
	;display by date
		PRINTFORML  %LOCALS%　　  按出生日期排序
		PRINTFORML
		FOR LOCAL, MC_TH_LOWER, MC_TH_UPPER
			IF LOCAL <= MC_TH_LL
				PRINTFORM    - #{LOCAL,2} - %CHILDNAME:(MC_TH_BELONG:LOCAL):(MC_TH_SEQ:LOCAL), 15, LEFT% \@ CHILD_SEX:(MC_TH_BELONG:LOCAL):(MC_TH_SEQ:LOCAL) == 1 ? ♀ # ♂ \@  母亲: %CALLNAME:(MC_TH_BELONG:LOCAL)%  %PRINT_DATE_F(CHILD_BIRTHDAY:(MC_TH_BELONG:LOCAL):(MC_TH_SEQ:LOCAL))%(第{CHILD_BIRTHDAY:(MC_TH_BELONG:LOCAL):(MC_TH_SEQ:LOCAL)}日)  出生于%CHILD_WEATHER_CN(CHILD_WEATHER:(MC_TH_BELONG:LOCAL):(MC_TH_SEQ:LOCAL))%星期%GET_DAY_CN(CHILD_BIRTHDAY:(MC_TH_BELONG:LOCAL):(MC_TH_SEQ:LOCAL))%　
				PRINTFORM %BL(31)%
				CALL Qol_Info_Child(MC_TH_BELONG:LOCAL, MC_TH_SEQ:LOCAL)
			ENDIF
		NEXT
		PRINTFORML

	;start by touhou
	;if birth date is smaller or same, put in list first
	;repeat till number of kids added
	;print list
	ELSE
		;display by Touhou
		PRINTFORML  %LOCALS%　　  按孩子母亲排序

		FOR LOCAL, MC_TH_LOWER, MC_TH_UPPER
			IF LOCAL <= MC_TH_LL
				IF LAST_MOM != MC_TH_BELONG:LOCAL || LOCAL == MC_TH_LOWER
					LAST_MOM = MC_TH_BELONG:LOCAL
					;line break here
					PRINTFORML
					PRINTFORM    - 
					PRINTBUTTON @"[%CALLNAME:(MC_TH_BELONG:LOCAL)% (子女总数:{CFLAG:(MC_TH_BELONG:LOCAL):子供人数})]", 10000 + MC_TH_BELONG:LOCAL
					PRINTFORML
				ENDIF
				PRINTFORM    - #{CHILD_NUMBER(MC_TH_BELONG:LOCAL, MC_TH_SEQ:LOCAL),2} - %CHILDNAME:(MC_TH_BELONG:LOCAL):(MC_TH_SEQ:LOCAL), 15, LEFT% \@ CHILD_SEX:(MC_TH_BELONG:LOCAL):(MC_TH_SEQ:LOCAL) == 1 ? ♀ # ♂ \@  %PRINT_DATE_F(CHILD_BIRTHDAY:(MC_TH_BELONG:LOCAL):(MC_TH_SEQ:LOCAL))%(第{CHILD_BIRTHDAY:(MC_TH_BELONG:LOCAL):(MC_TH_SEQ:LOCAL)}日)  出生于%CHILD_WEATHER_CN(CHILD_WEATHER:(MC_TH_BELONG:LOCAL):(MC_TH_SEQ:LOCAL))%星期%GET_DAY_CN(CHILD_BIRTHDAY:(MC_TH_BELONG:LOCAL):(MC_TH_SEQ:LOCAL))%　
				PRINTFORM %BL(31)%
				CALL Qol_Info_Child(MC_TH_BELONG:LOCAL, MC_TH_SEQ:LOCAL)
			ENDIF
		NEXT
		PRINTFORML
	;start by touhou
	;print touhou by touhou
	ENDIF
	SIF MC_CHILD_PAGES > 1 ;spacing
		CALL EQUATE_LINE, nLineCnt_Head + nPageSize + 1
	DRAWLINE
	PRINTFORM    
	PRINTBUTTON "[改变排序方式]", 600
	PRINTFORML
	PRINTFORM   	 
	
	IF MC_CHILD_PAGES == 1
		SETCOLOR 0x606060
		PRINTFORM %"[上一页]",15,LEFT%
		RESETCOLOR 
	ELSE
		PRINTBUTTON @"%"[上一页]",15,LEFT%", 888
	ENDIF
	PRINTFORM ({nPage+1}/{MC_CHILD_PAGES})%BL(6)%
	PRINTFORM 
	IF MC_CHILD_PAGES == 1
		SETCOLOR 0x606060
		PRINTFORM %"[下一页]",15,LEFT%
		RESETCOLOR 
	ELSE
		PRINTBUTTON @"%"[下一页]",15,LEFT%", 777
	ENDIF
	
	PRINTFORML
	PRINTFORM    
	PRINTBUTTON "[返回]", 900
	PRINTFORML
	
	INPUT
	SELECTCASE RESULT
		CASE 900
			RETURN
		CASE 600
			nSort = !nSort
			nPage = 0
			CALL QOL_MC_CHILD_SORT(nSort, MC_TH_BELONG, MC_TH_SEQ, MC_TH_BIRTH, PAGE_START, MC_CHILD_PAGES, nPageSize)
		CASE 777, 888
			SIF MC_CHILD_PAGES == 1
				CONTINUE
			nPage = PAGE_HANDLE(RESULT, 888, nPage, 0, MC_CHILD_PAGES-1)
		CASE 10000 TO 11000
			SIF nSort
				CONTINUE
			RETURN RESULT
	ENDSELECT
LOOP 1

;the child's proper number, even when going past the maximum slots for children
@CHILD_NUMBER(CHARA, CHILDNUM)
#FUNCTION
#DIM CHARA
#DIM CHILDNUM
LOCAL = CHILDNUM + (CFLAG:CHARA:子供人数 / MAXCHILD * MAXCHILD)
SIF LOCAL > CFLAG:CHARA:子供人数 ;not yet past the switchover point
	LOCAL -= MAXCHILD
RETURNF LOCAL

@CHILD_INDEX(CHILD_NUM)
#FUNCTION
#DIM CHILD_NUM
#DIM INDEX
INDEX = CHILD_NUM % MAXCHILD
SIF !INDEX
	INDEX = MAXCHILD
RETURNF INDEX
