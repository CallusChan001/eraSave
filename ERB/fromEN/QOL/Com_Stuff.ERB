@Com_Size_Check(ARGS, ARG = -1)
#LOCALSIZE 1
#LOCALSSIZE 1
#FUNCTION
SIF ARG == -1
	ARG = TARGET
;for normal actions
SELECTCASE ARGS 
	CASE "strict" ;at this stage, need either exp or forbidden knowledge
		RETURNF 挿入不可(ARG) == 1 && EXP:ARG:Ｖ経験 < 50 && !(TALENT:PLAYER:禁断の知識 || TALENT:ARG:禁断の知識)
	CASE "strictA"
		RETURNF 挿入不可(ARG) == 1 && EXP:ARG:Ａ経験 < 50 && !(TALENT:PLAYER:禁断の知識 || TALENT:ARG:禁断の知識)
	CASE "reg" ;need both exp and forbidden knowledge if difference is too big
		RETURNF 挿入不可(ARG) == 2 && ( !(TALENT:PLAYER:禁断の知識 || TALENT:ARG:禁断の知識) || EXP:ARG:Ｖ経験 < 50)
	CASE "regA"
		RETURNF 挿入不可(ARG) == 2 && ( !(TALENT:PLAYER:禁断の知識 || TALENT:ARG:禁断の知識) || EXP:ARG:Ａ経験 < 50)
CASEELSE
;for counter
	RETURNF 挿入不可(ARG) >= 2 && ( !(TALENT:PLAYER:禁断の知識 || TALENT:ARG:禁断の知識) || ( ARGS == "anal" ? EXP:ARG:Ａ経験 # EXP:ARG:Ｖ経験) < 50)
ENDSELECT
[SKIPSTART]
@Com_Size_Player_Check(ARGS)
#FUNCTION
RETURNF ABS(TALENT:体型 - TALENT:PLAYER:体型) > 2 && !(TALENT:PLAYER:禁断の知識 || TALENT:禁断の知識)

@Com_MobRaceSelect(YUKIZURI_RACE, ARG)
#DIMS nRace
#DIMS REF YUKIZURI_RACE
;qol custom code
SELECTCASE CAN_NANPA(CFLAG:MASTER:現在位置)
	CASE 1
		nRace = :村姑:人妻
	CASE 4
		nRace = :販売員:イナバ
	CASE 6
		nRace = :化け狐:化け猫
	CASE 8
		nRace = :白狼天狗:鴉天狗
	CASE 9
		nRace = :鬼:ゾンビフェアリー
	CASE 11
		nRace = :化け狸:茨たぬき
CASEELSE
	nRace =
ENDSELECT
IF !STRLENS(nRace)
	RESULTS = none
	RETURN 
ENDIF
IF ARG == 0
	LOCALS = Leave To Random
ELSEIF ARG > 0
	LOCALS '= RACE_TR( SPLIT_SINGLE(nRace, ARG) )
	YUKIZURI_RACE '= SPLIT_SINGLE(nRace, ARG)
ENDIF
RESULTS '= LOCALS
RETURN 

@Com_Mast
#DIM AMT
SIF !CFLAG:睡眠
	RETURN 0
	
PRINTFORMW %BREAKENG(@"Having the completely defenseless %CALLNAME:TARGET% in %HIS_HER(TARGET)% sleep before you as your personal fap-fuel, you start masturbating furiously.")%
IF CFLAG:イタズラ < 2
	PRINTFORMW %BREAKENG(@"Lightly dressed in %HIS_HER(TARGET)% pajamas at the moment, %HIS_HER(TARGET)% lustrous skin and appearance look even more alluring than ever...")%
ELSE
	PRINTFORMW Now that %HE_SHE(TARGET)%'s undressed, you get a full view of %HIS_HER(TARGET)% naughty bits for your own enjoyment...
ENDIF
PRINTFORMW It doesn't take terribly long, and soon enough, you're already shooting your load out!
IF CFLAG:イタズラ < 2
	PRINTFORMW Your %SEMEN(MASTER,1)% lands on %CALLNAME:TARGET%'s clothes, staining them...
	SOURCE:反感 = 100
	SOURCE:不潔 = 250
	TCVAR:睡眠深度 -= RAND(50, 151)
ELSE
	PRINTFORMW Your %SEMEN(MASTER,1)% lands on %CALLNAME:TARGET%'s exposed chest and face, with some of it trickling onto %HIS_HER(TARGET)% lips...
	PRINTFORMW You worry a bit whether she'll wake up from that.
	STAIN:口 |= 汚れ_精液
	EXP:精液経験 += 1
	SOURCE:反感 = 200
	SOURCE:不潔 = 300
	TCVAR:睡眠深度 -= RAND(100, 201)
ENDIF
	
AMT = 100 + RAND:100
;custom code for color
SETCOLOR C_CREAM
IF TALENT:MASTER:濃厚精液
	AMT *= 2
	PRINTFORMW ...%PARSE("You release")% {AMT}ml of %PARSE("your",1)% extra thick stuff.
ELSE
	PRINTFORMW ...%PARSE("You release")% {AMT}ml.
ENDIF
RESETCOLOR

BASE:MASTER:精力 -= AMT
DOWNBASE:MASTER:気力 = 100
;custom code for exp and message
EXP:MASTER:自慰経験 += 1
SIF TALENT:MASTER:濃厚精液
	PRINTFORMW %PARSE("You feel")% relieved.
;-----------

TIME += 20
RETURN 1

@Com_DateRando(ARG)
PRINTFORMW Invite %CALLNAME:ARG% on a date here, or bring %HIM_HER(ARG)% home?
CALL ASK_YN("Date here", "Bring home")
IF RESULT ;home
	CALL 帰宅の時間経過
	CALL SET_DATE(99,ARG)
	CFLAG:ARG:現在位置 = ENTRANCE
	CFLAG:MASTER:現在位置 = ENTRANCE
	CFLAG:ARG:デート中 = MAIN_MAP
	CFLAG:MASTER:デート中 = MAIN_MAP
	CFLAG:ARG:同行 = 180
	CFLAG:MASTER:同行 = 180
	PRINTFORMW Accompanied by %CALLNAME:ARG%, %PARSE("you return")% back home.
ELSE
	CFLAG:ARG:同行 = 180
	CFLAG:MASTER:同行 = 180
	CFLAG:ARG:デート中 = CFLAG:MASTER:現在位置 / 100
	FLAG:デート相手 = ARG
	TFLAG:デート前好感度 = CFLAG:ARG:好感度
	TIME += 1
	PRINTFORMW %PARSE("You decide")% to go on a date with %CALLNAME:ARG%, who has some spare time.
ENDIF
FLAG:70 = 0

@Com_Date_Meet(ARG) ;specifically on meeting characters that are assigned date flag with you to trigger meeting event
IF !FLAG:70 && CFLAG:ARG:現在位置 == CFLAG:MASTER:現在位置
	IF CFLAG:(NO:ARG):面識 == 0
		CALL SYOTAIMEN(ARG)
	ELSEIF CFLAG:ARG:情事目撃 && CFLAG:(CFLAG:ARG:情事目撃):現在位置 != CFLAG:ARG:現在位置
		PRINTFORMW %CALLNAME:ARG% confronts %PARSE("you")% about what %PARSE("you",1)% did with %CALLNAME:(CFLAG:ARG:情事目撃)% earlier.
		;CALL KOJO_EVENT(0, ARG, 2)
		CALL KOJO_MESSAGE_SEND("EVENT",8,ARG,1,0)
		CFLAG:ARG:情事目撃 = 0
	ELSEIF CFLAG:ARG:情事目撃 && CFLAG:(CFLAG:ARG:情事目撃):現在位置 == CFLAG:ARG:現在位置
		PRINTFORMW %CALLNAME:ARG% scolds %PARSE("you")% and %CALLNAME:(CFLAG:ARG:情事目撃)% for what %PARSE("you",2)% did earlier...
		CALL KOJO_MESSAGE_SEND("EVENT",8,ARG,2,0)
		CFLAG:ARG:情事目撃 = 0
	ELSE
		IF !TCVAR:ARG:今日会ったか && CFLAG:ARG:同室
			CALL KOJO_MESSAGE_SEND("EVENT",1,ARG,CHK_DATENOW(CFLAG:MASTER:デート中) ? 9 # 8,0)
		ENDIF
		;通常遭遇口上
		CALL KOJO_MESSAGE_SEND("EVENT",1,ARG,2,0)
	ENDIF
	CALL FirstMeetToday(ARG)
ENDIF

@Com_Eat
#DIM EAT_TIME
IF FLAG:70 ;in ts
	IF DISH_TYPE == "主食"
		EAT_TIME = 200
	ELSE
		EAT_TIME = 100
	ENDIF
	SIF TALENT:MASTER:健啖 ;custom
		EAT_TIME /= 2
	IF BASE:MASTER:TSP < EAT_TIME
		PRINTFORML You don't have enough TSP to eat your dish ({EAT_TIME} needed).
		RETURN -1
	ELSE
		BASE:MASTER:TSP -= EAT_TIME
		RETURN 1
	ENDIF
ELSE
	PRINTFORML Would you like to eat in private?
	CALL ASK_YN
	IF !RESULT
		PRINTFORML Finding a convenient spot, you begin to consume your %DISHNAME_TR(DISH_NAME)% away from the prying eyes...
		SIF TARGET
			PRINTFORML Although you can see %CALLNAME:TARGET% glancing at you, %HE_SHE(TARGET)% doesn't seem to mind too much.
		TFLAG:194 = 99
		RETURN 1
	ELSE
		RETURN 0
	ENDIF
ENDIF
[SKIPEND]

@Com_Food_Check(ARG, ARG:1)
#LOCALSIZE 1
#LOCALSSIZE 1
SIF !Com_Food_Quality_Check && !ARG:1
	RETURN 0
SIF TCVAR:ARG:料理評価値 == 0
	CALL REVIEW_DISH(ARG, DISH_TASTE) ;get food score
IF TCVAR:ARG:料理評価値 < 500
	IF ARG:1
		PRINTFORMW ……但随后%CALLNAME:ARG%鼻子一扭，发现这菜不合自己口味，放弃了偷吃
	ELSE
		PRINTFORMW %CALLNAME:ARG%决定婉拒，因为这菜不合自己口味……
	ENDIF
	RETURN 1
ENDIF
[SKIPSTART]
@Com_Cook_Tsp_Check
#DIM TSP_TIME
SIF !FLAG:70
	RETURN 0
IF DISH_TIME <= 0
	IF DISH_TYPE == "主食"
		TSP_TIME = 400
	ELSE
		TSP_TIME = 200
	ENDIF
ELSE
	TSP_TIME = DISH_TIME*10
ENDIF
IF BASE:MASTER:TSP < TSP_TIME
	PRINTFORMW You don't have enough TSP to cook ({TSP_TIME} needed).
	RETURN -1
ELSE
	BASE:MASTER:TSP -= TSP_TIME
	RETURN 1
ENDIF

@Com_Put_On_Condom
#DIM DYNAMIC nCondom
;com custom code - put condom on target
IF HAS_PENIS(TARGET) && HAS_PENIS(PLAYER)
	PRINTFORML Who should be putting the condom on? ({ITEM:45} left)
	CALL ASK_M(@"You\@ TEQUIP:PLAYER:コンドーム ? %" "%(Take Off)#\@", 1, @"%CALLNAME:TARGET%\@ TEQUIP:コンドーム ? %" "%(Take Off)#\@", 1, "Both", 1, "Cancel", 1)
	SELECTCASE RESULT
		CASE 0
			SETBIT nCondom, 0
		CASE 1
			TFLAG:194 = 1
			SETBIT nCondom, 1
		CASE 2
			TFLAG:194 = 2
			SETBIT nCondom, 0
			SETBIT nCondom, 1
		CASE 3
			RETURN -1
	ENDSELECT
ELSEIF HAS_PENIS(TARGET)
	TFLAG:194 = 1
	SETBIT nCondom, 1
ELSEIF HAS_PENIS(PLAYER)
	SETBIT nCondom, 0
ENDIF

IF GETBIT(nCondom, 0)
	IF !TEQUIP:PLAYER:コンドーム
		ITEM:45 -= 1
		TEQUIP:PLAYER:コンドーム = 1
		PRINTFORML %PARSE("You put")% on a condom.
		PRINTFORMW {ITEM:45} left.
	ELSE
		TEQUIP:PLAYER:コンドーム = 0
		PRINTFORMW %PARSE("You take")% off the condom.
	ENDIF
ENDIF
IF GETBIT(nCondom, 1)
	IF !TEQUIP:コンドーム
		ITEM:45 -= 1
		TEQUIP:コンドーム = 1
		PRINTFORML %PARSE("You put")% on a condom on %CALLNAME:TARGET%'s %TINKO(TARGET)%.
		PRINTFORMW {ITEM:45} left.
	ELSE
		TEQUIP:コンドーム = 0
		PRINTFORMW %PARSE("You take")% off the condom from %CALLNAME:TARGET%'s %TINKO(TARGET)%.
	ENDIF
ENDIF

;-------------------------------------------------
;COM別FLAGの設定
;-------------------------------------------------
;行為者
TCVAR:116 = PLAYER

@Com_Target_Condom
SIF !TCVAR:コンドーム
	RETURN 0
IF FLAG:70 == 1
	PRINTFORML %CALLNAME:(TARGET)%'s %TINKO(TARGET)% fills the condom with \@ NOWEX:TARGET:射精 >1 ? large quantities of%" "% # \@%SAMEN_DESCRIPTION(TARGET)%...
	PRINTFORM %PARSE("You relieve",1)% it of the 
	SELECTCASE NOWEX:TARGET:射精
		CASE IS > 2
			PRINT condom bursting with semen
		CASE 2
			PRINT rather full condom
		;Custom code
		CASEELSE
			PRINT condom
	ENDSELECT
	PRINTFORML  and %PARSE("put",1)% it aside...
ELSEIF CFLAG:MASTER:イタズラ == 2
	PRINTFORML %CALLNAME:(TARGET)%'s %TINKO(TARGET)% fills the condom with \@ NOWEX:TARGET:射精 >1 ? large quantities of%" "% # \@%SAMEN_DESCRIPTION(TARGET)%...
	PRINTFORM %PARSE("You relieve",1)% it of the 
	SELECTCASE NOWEX:TARGET:射精
		CASE IS > 2
			PRINT condom bursting with semen
		CASE 2
			PRINT rather full condom
		;Custom code
		CASEELSE
			PRINT condom
	ENDSELECT
	PRINTFORML  and %PARSE("place",1)% it on the side of the futon...
ELSE
	PRINTFORML %CALLNAME:(TARGET)%'s %TINKO_TR(TARGET)% shoots out \@ NOWEX:TARGET:射精 >1 ? massive amounts of%" "%# \@%SAMEN_DESCRIPTION(TARGET)% into the condom.
	PRINTFORM %HE_SHE(TARGET,1)% pulls out %HIS_HER(TARGET)% penis and shows %PARSE("you",1)% the 
	SELECTCASE NOWEX:TARGET:射精
		CASE IS > 2
			PRINT condom bursting with semen
		CASE 2
			PRINT rather full condom
		;Custom code
		CASEELSE
			PRINT condom
	ENDSELECT
	PRINTFORML ...
	;custom code, during reverse rape
	IF CFLAG:うふふ == 2 && CFLAG:溜まってる度 >= 500
		PRINTFORML %CALLNAME:(TARGET)% smiles at %PARSE("you")% lasciviously as %HE_SHE(TARGET)% takes the filled condom off %HIS_HER(TARGET)% %TINKO(TARGET)%...
	;膣内射精
	ELSE
		SELECTCASE NOWEX:TARGET:射精
			CASE IS > 2
				PRINTFORML %PARSE("You look",1)% at it in amazement.
			CASE 2
				PRINTFORML %PARSE("You seem",1)% impressed by the yield.
			;Custom code
			CASEELSE
				PRINTFORML %PARSE("You look",1)% at it for a moment, pleased that %CALLNAME:TARGET% found it enjoyable.
		ENDSELECT
	ENDIF
	
	TRYCALL QoL_Info_Write(TARGET, 7, "condom") ;qol custom code Qol_Dates_Semen
ENDIF
;one or the other
LOCALS =
SIF TCVAR:12 == 1 && ABL:PLAYER:膣射中毒
	LOCALS = %PARSE("your")% vagina aches with frustration as it expected to be filled...
SIF TCVAR:12 == 2 && ABL:PLAYER:肛射中毒
	LOCALS = %PARSE("your")% anus aches with frustration as it expected to be filled...
SIF STRLENS(LOCALS)
	PRINTFORML Meanwhile, %LOCALS%
IF CFLAG:TARGET:自動ゴム
	IF TEQUIP:TARGET:コンドーム
		PRINTFORML %PARSE("You put")% on another condom on %CALLNAME:TARGET%'s %TINKO(TARGET)%. ({ITEM:コンドーム} Left)
	ELSE
		PRINTFORML %PARSE("You try")% putting on another condom but %PARSE("you've",1)% run out.
	ENDIF
ENDIF
WAIT
RETURN 1

@Com_Semen_Drink
;based on sqn/akuma maid

PRINTFORML Drink semen yourself, or make %CALLNAME:TARGET% do it?
CALL ASK_M("Yourself", 1, CALLNAME:TARGET, 1, "Cancel", 1)
SIF RESULT == 2
	RETURN -1
IF RESULT == 0 ;drinking semen yourself
	TFLAG:194 = 1 ;flag for self
	
	STAIN:MASTER:口 |= 4
	CFLAG:MASTER:口に精液 = 1
	
	IF FLAG:70 == 1
		BASE:MASTER:TSP -= 50
	ELSE
		DOWNBASE:MASTER:気力 += 100
	ENDIF
	
	;ABL:精液中毒をみる
	SELECTCASE ABL:MASTER:精液中毒
	CASE 0
		PRINTFORML Not being able to handle semen too well, you spill most of it out...
		TFLAG:ゴム内 = MULTIPLY(TFLAG:ゴム内, 25)
	CASE 1
		PRINTFORML Not being able to handle semen too well, you spill half of it out...
		TFLAG:ゴム内 = MULTIPLY(TFLAG:ゴム内, 50)
	CASE 2
		PRINTFORML Not being able to handle semen too well, you spill some out...
		TFLAG:ゴム内 = MULTIPLY(TFLAG:ゴム内, 75)
	ENDSELECT
	SELECTCASE EXP:MASTER:精液経験
	CASE IS < 5
		TIMES DOWNBASE:MASTER:気力, 4.00
	CASE IS < 20
		TIMES DOWNBASE:MASTER:気力, 2.00
	CASE IS < 50
		TIMES DOWNBASE:MASTER:気力, 1.50
	CASE IS < 100
		TIMES DOWNBASE:MASTER:気力, 1.20
	CASE IS < 300
	CASEELSE
		TIMES DOWNBASE:MASTER:気力, 0.50
	ENDSELECT
	;loves semen
	SIF TALENT:MASTER:精愛味覚
		SOURCE:MASTER:快Ｍ += (TFLAG:ゴム内 * 10) / 2
	
	;target's reaction if conscious
	IF SHIRAHU(TARGET)
		SIF ABL:精液中毒 >= 3
			SOURCE:欲情 = (TFLAG:ゴム内 + 4)/5 * 100 + 150 * ABL:サドっ気
		SOURCE:逸脱 = (TFLAG:ゴム内 + 4)/5 * 100
		SOURCE:不潔 = (TFLAG:ゴム内 + 4)/5 * 100

		IF ABL:サドっ気 < 3
			;精液経験をみる
			SELECTCASE EXP:精液経験
			CASE IS < 5
				TIMES SOURCE:逸脱, 2.00
				TIMES SOURCE:不潔, 3.00
			CASE IS < 20
				TIMES SOURCE:逸脱, 1.50
				TIMES SOURCE:不潔, 2.00
			CASE IS < 50
				TIMES SOURCE:逸脱, 1.25
				TIMES SOURCE:不潔, 1.50
			CASE IS < 100
				TIMES SOURCE:逸脱, 1.00
				TIMES SOURCE:不潔, 1.20
			CASE IS < 300
				TIMES SOURCE:逸脱, 0.50
				TIMES SOURCE:不潔, 1.00
			CASEELSE
				TIMES SOURCE:逸脱, 0.50
				TIMES SOURCE:不潔, 0.50
			ENDSELECT
		ELSE
			TIMES SOURCE:逸脱, 0.25
			SOURCE:強要 = (TFLAG:ゴム内 + 4)/5 * 500
			SOURCE:加虐 = (TFLAG:ゴム内 + 4)/5 * 500
		ENDIF
		SELECTCASE ABL:精液中毒
		CASE 0
			TIMES SOURCE:欲情, 0.10
		CASE 1
			TIMES SOURCE:欲情, 1.00
		CASE 2
			TIMES SOURCE:欲情, 1.20
		CASE 3
			TIMES SOURCE:欲情, 1.40
		CASE 4
			TIMES SOURCE:欲情, 1.80
		CASEELSE
			TIMES SOURCE:欲情, 2.00
		ENDSELECT
	ENDIF
	SIF SOURCE:欲情 > 20000
		SOURCE:欲情 = 20000
	;-------------------------
	
	CFLAG:MASTER:累計精飲 += TFLAG:ゴム内
	SELECTCASE TFLAG:ゴム内
		CASE IS > 300
			EXP:MASTER:精液経験 += 8
		CASE IS > 150
			EXP:MASTER:精液経験 += 5
		CASEELSE
			EXP:MASTER:精液経験 += 3
	ENDSELECT
	RETURN 1
ENDIF

;-------------------------------------------------
;実行できるかの判定
;-------------------------------------------------
IF SHIRAHU(TARGET)
	CALL Com_Semen_Drink_COMORDER
	SIF RESULT < 0
		RETURN -1
ENDIF

;-------------------------------------------------
;COM別FLAGの設定
;-------------------------------------------------
;行為者
TCVAR:116 = PLAYER
;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
SELECTCASE TFLAG:ゴム内
	CASE IS > 300
		EXP:精液経験 += 8
	CASE IS > 150
		EXP:精液経験 += 5
	CASEELSE
		EXP:精液経験 += 3
ENDSELECT
STAIN:口 |= 4
IF FLAG:時間停止 == 1
	CFLAG:口に精液 = 2
ELSE
	CFLAG:口に精液 = 1
ENDIF
CFLAG:累計精飲 += TFLAG:ゴム内


;消費体力、気力
IF FLAG:70 == 1
	BASE:MASTER:TSP -= 50
ELSE
	DOWNBASE:気力 += 100
	
	SOURCE:達成 = MULTIPLY(ABL:マゾっ気*200, 100 + ABL:PLAYER:サドっ気*20)
	SOURCE:情愛 = MULTIPLY(ABL:マゾっ気*200, 100 + ABL:PLAYER:サドっ気*20)

	SOURCE:情愛 += TFLAG:ゴム内 / 2
	SOURCE:性行動 = TFLAG:ゴム内 / 2
	SOURCE:達成 += TFLAG:ゴム内 / 2
	SOURCE:屈従 = TFLAG:ゴム内 * 2
	SOURCE:不潔 = (TFLAG:ゴム内 + 4)/5 * 100
	SOURCE:反感 = TFLAG:ゴム内
		
	SIF ABL:精液中毒 >= 3
		SOURCE:欲情 = (TFLAG:ゴム内 + 4)/5 * 100 + 150 * ABL:PLAYER:サドっ気
	IF SHIRAHU(TARGET)
		SOURCE:逸脱 = (TFLAG:ゴム内 + 4)/5 * 100

		;精液経験をみる
		SELECTCASE EXP:精液経験
		CASE IS < 5
			TIMES SOURCE:逸脱, 2.00
			TIMES SOURCE:不潔, 3.00
			TIMES DOWNBASE:気力, 4.00
		CASE IS < 20
			TIMES SOURCE:逸脱, 1.50
			TIMES SOURCE:不潔, 2.00
			TIMES DOWNBASE:気力, 2.00
		CASE IS < 50
			TIMES SOURCE:逸脱, 1.25
			TIMES SOURCE:不潔, 1.50
			TIMES DOWNBASE:気力, 1.50
		CASE IS < 100
			TIMES SOURCE:逸脱, 1.00
			TIMES SOURCE:不潔, 1.20
			TIMES DOWNBASE:気力, 1.20
		CASE IS < 300
			TIMES SOURCE:逸脱, 0.50
			TIMES SOURCE:不潔, 1.00
		CASEELSE
			TIMES SOURCE:逸脱, 0.50
			TIMES SOURCE:不潔, 0.50
			TIMES DOWNBASE:気力, 0.50
		ENDSELECT
	ENDIF
	
	;ABL:精液中毒をみる
	SELECTCASE ABL:精液中毒
	CASE 0
		TIMES SOURCE:欲情, 0.10
		TIMES SOURCE:屈従, 0.50
	CASE 1
		TIMES SOURCE:欲情, 1.00
		TIMES SOURCE:屈従, 1.00
	CASE 2
		TIMES SOURCE:欲情, 1.20
		TIMES SOURCE:屈従, 1.30
	CASE 3
		TIMES SOURCE:欲情, 1.40
		TIMES SOURCE:屈従, 1.60
	CASE 4
		TIMES SOURCE:欲情, 1.80
		TIMES SOURCE:屈従, 2.00
	CASEELSE
		TIMES SOURCE:欲情, 2.00
		TIMES SOURCE:屈従, 3.00
	ENDSELECT
	;loves semen
	IF TALENT:精愛味覚
		SOURCE:快Ｍ += (TFLAG:ゴム内 * 10) / 2
		SOURCE:不潔 = 0
		TIMES SOURCE:達成 , 2.00
		TIMES SOURCE:屈従 , 1.50
		TIMES SOURCE:逸脱 , 0.10
	ENDIF
	SIF SOURCE:屈従 > 10000
		SOURCE:屈従 = 10000
	SIF SOURCE:欲情 > 10000
		SOURCE:欲情 = 10000

	SIF SHIRAHU(TARGET)
		TRYCALL QoL_Info_Write(TARGET, 7) ;qol custom code Qol_Dates_Semen
ENDIF
;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:自己愛 < 0 || TALENT:抵抗
	SOURCE:鬱屈 += 400
RETURN 1

@Com_Semen_Drink_COMORDER(ARG)
SKIPDISP ARG
PRINTL Semen Drinking
LOCAL:99 = 0
$START
A = 0
S = 0

;すべての命令に共通の要素を考慮
;(従順が高いと命令に従いやすいなど)
CALL COM_ORDER

;ABL:欲望
IF ABL:欲望
	SIF S
		PRINT  + 
	A += ABL:欲望 * 1
	PRINT Desire
	PRINTV 'LV,ABL:欲望,'(,ABL:欲望 * 1,')
	S = 1
ENDIF
;ABL:奉仕精神
IF ABL:奉仕精神
	SIF S
		PRINT  + 
	A += MIN(ABL:奉仕精神, 10) * 2
	PRINT Servitude
	PRINTV 'LV,ABL:奉仕精神,'(,MIN(ABL:奉仕精神, 10) * 2,')
	S = 1
ENDIF
;ABL:精液中毒
IF ABL:精液中毒
	SIF S
		PRINT  + 
	A += MIN(ABL:精液中毒, 5) * 5
	PRINT Semen Addict
	PRINTV 'LV,ABL:精液中毒,'(,MIN(ABL:精液中毒, 5) * 5,')
	S = 1
ENDIF

IF MARK:快楽刻印
	SIF S
		PRINT  + 
	A += MARK:快楽刻印 * 1
	PRINT Pleasure Mark
	PRINTV 'LV,MARK:快楽刻印,'(,MARK:快楽刻印 * 1,')
	S = 1
ENDIF
;PALAM:欲情
IF GETPALAMLV(PALAM:欲情,5)
	IF PALAM:欲情 < 3000
		SIF S
			PRINT  - 
		A -= 50
		PRINT Insufficient Desire
		PRINTV '(,50,')
		S = 1
	ELSE
		SIF S
			PRINT  + 
		A += GETPALAMLV(PALAM:欲情,5) * 1
		PRINT Desire
		PRINTV 'LV,GETPALAMLV(PALAM:欲情,5),'(,GETPALAMLV(PALAM:欲情,5) * 1,')
		S = 1
	ENDIF
ENDIF
;恥じらい
IF TALENT:羞恥心 > 0
	PRINT  - 
	A -= 3
	PRINT Shy
	PRINTV '(,3,')
	S = 1
ENDIF
;汚臭鈍感
IF TALENT:汚臭耐性 > 0
	SIF S
		PRINT  + 
	A += 3
	PRINT Smell Resistant
	PRINTV '(,3,')
	S = 1
ENDIF
;献身的
IF TALENT:献身的
	SIF S
		PRINT  + 
	A += 6
	PRINT Devoted
	PRINTV '(,6,')
	S = 1
ENDIF
;快感の否定（淫乱、娼婦で無効）
IF TALENT:快感応答 < 0 && !TALENT:淫乱
	PRINT  - 
	A -= 1
	PRINT Denies Pleasure
	PRINTV '(,1,')
	S = 1
ENDIF
;男嫌い（恋慕、親愛で無効）
IF OTOKOGIRAI(TARGET)
	PRINT  - 
	A -= 12
	PRINT Man Hater
	PRINTV '(,12,')
	S = 1
ENDIF
;恋慕
IF TALENT:恋慕 && ASSIPLAY == 0
	SIF S
		PRINT  + 
	A += 5
	PRINT Love
	PRINTV '(,5,')
	S = 5
ENDIF
;親愛
IF TALENT:恋慕 > 1 && ASSIPLAY == 0
	SIF S
		PRINT  + 
	A += 5
	PRINT Deep Love
	PRINTV '(,5,')
	S = 5
ENDIF
IF CFLAG:負け犬
	SIF S
		PRINT  + 
	A += 20
	PRINT Loser
	PRINTV '(,20,')
	S = 1
ENDIF

Y = 10
Y = Y * (2 - TALENT:汚臭耐性) / 2
;汚れあり
IF Y
	PRINT  - 
	A -= Y
	PRINT Yucky
	;汚臭鈍感
	IF TALENT:汚臭耐性 > 0
		PRINT , Smell Resistant
	ELSEIF TALENT:汚臭耐性 < 0
		PRINT , Smell Sensitive
	ENDIF
	PRINTV '(,Y,')
	S = 1
ENDIF

;amount
IF ABL:精液中毒 < 3 && !TALENT:精愛味覚
	PRINT  - 
	SELECTCASE TFLAG:ゴム内
		CASE IS > 300
			Y = 20
			PRINT Lots of semen
		CASE IS > 150
			Y = 10
			PRINT Load of semen
		CASEELSE
			Y = 3
			PRINT Semen
	ENDSELECT
	A -= Y
	PRINTV '(,Y,')
	S = 1
ENDIF

;合計を表示(24以上で実行)
PRINT  = 
PRINTV A

V = 50
SIF A < V
	PRINT  < 
SIF A == V
	PRINT  = 
SIF A > V
	PRINT  > 
PRINT Required Value 
PRINTV V

WAIT
SKIPDISP 0
;実行できない
IF A < V
	IF ABL:精液中毒 < 3 && !TALENT:精愛味覚
		PRINTFORML
		DRAWLINE
		IF TFLAG:ゴム内 > 300
			PRINTFORML Looks like this amount is a little too much for %CALLNAME:TARGET%...
			PRINTFORML Spill out some and try again?
			CALL ASK_YN
			IF !RESULT
				TFLAG:ゴム内 = 300
				GOTO START
			ENDIF
			LOCAL:99 = 1
		ELSEIF TFLAG:ゴム内 > 150
			PRINTFORML Looks like this amount is still a bit too much for %CALLNAME:TARGET%...
			PRINTFORML Spill out some and try again?
			CALL ASK_YN
			IF !RESULT
				TFLAG:ゴム内 = 150
				GOTO START
			ENDIF
			LOCAL:99 = 1
		ELSE
			IF LOCAL:99 == 1
				PRINTFORML You spill out so much, there's nothing left anymore, and %CALLNAME:TARGET% still doesn't want to drink it...
			ELSE
				PRINTFORML You offer just a little bit, and %CALLNAME:TARGET% still doesn't want to drink it...
			ENDIF
			PRINTFORML You give up for now.
		ENDIF
	ENDIF
	RETURN -1
ENDIF
RETURN 1

@Com_DrinkSemenDescr
#DIM DYNAMIC nContainer
IF TFLAG:194 ;you drink
	SELECTCASE ABL:MASTER:精液中毒
	CASE 0
		PRINTFORML You brace yourself and drink what little you had left.
		PRINTFORML Even then, it tastes terrible, getting stuck in your throat and almost making you gag...
		PRINTFORML You start regretting your decision.
	CASE 1
		PRINTFORML The taste is as terrible as you'd expect, but somehow, you don't feel like vomiting all over yourself this time.
	CASE 2
		PRINTFORML Not being able to handle semen too well, you spill some out...
	CASE 3
		PRINTFORML Something is odd. You didn't have to waste time mentally preparing as much.
		PRINTFORML Perhaps after doing it so many times, it's starting to affect you.
		PRINTFORML It feels scary, but at the same time... exciting.
	CASE 4
		PRINTFORML You can get used to anything, and this is the proof.
		PRINTFORML As a result of swallowing and smelling semen as much as you did, drinking it didn't seem like such a big deal anymore.
		PRINTFORML You almost... start to enjoy the taste somewhat.
	CASEELSE
		PRINTFORML What you're doing right now may seem disgusting or odd for others, but you care not for their opinion.
		PRINTFORML It's an acquired taste, and those people are missing out, you're sure of it.
		PRINTFORML You drink all of the %SEMEN()% like it's nothing, almost wishing for seconds...
	ENDSELECT
	IF SHIRAHU(TARGET)
		IF ABL:サドっ気 >= 3
			PRINTFORML %CALLNAME:TARGET% watches you with a %SPLIT_G(@"glint in %HIS_HER(TARGET)% eyes:sadistic glee")%, enjoying the show you're putting up for %HIM_HER(TARGET)%...
		ELSE
			SELECTCASE ABL:精液中毒
			CASE 0
				PRINTFORML %CALLNAME:TARGET% %SPLIT_G("watches you with horror in her eyes:with disgust written all over her face:almost gags from imagining herself in your position")%, turning %HIS_HER(TARGET)% face away...
			CASE 1
				PRINTFORML %CALLNAME:TARGET% seems speechless and would rather prefer that you didn't do it in front of %HIM_HER(TARGET)%...
			CASE 2
				PRINTFORML %CALLNAME:TARGET% seems strangely attracted to it, unable to avert %HIS_HER(TARGET)% gaze...
			CASE 3
				PRINTFORML %CALLNAME:TARGET% gasps and almost reaches out for it, but stops at the last second...
			CASE 4
				PRINTFORML Something tells you that %CALLNAME:TARGET% is wishing she were in your position right now...
			CASEELSE
				PRINTFORML %CALLNAME:TARGET% watches sadly as %HIS_HER(TARGET)% beloved %SEMEN()% disappears in your mouth, asking you to share with a pout...
			ENDSELECT
		ENDIF
	ENDIF
;part of the original
ELSEIF FLAG:70
	PRINTFORM %PARSE("You put")% the used condom between %CALLNAME:TARGET%'s teeth and %PARSE("pull")% it out, depositing \@ TFLAG:ゴム内 > 150 ? a large serving of%" "% # \@%SAMEN_DESCRIPTION(PLAYER)% into %HIS_HER(TARGET)% mouth.<br>
	PRINTFORM In order to make %TNAME(TARGET)% remember the taste well, %PARSE("you turn",1)% the condom inside out and %PARSE("rub")% it all over %HIS_HER(TARGET)% gums, making sure every part of %HIS_HER(TARGET)% mouth tastes %PARSE("your",1)% %SAMEN_DESCRIPTION(PLAYER)%...
	CALL PRINT_BREAK
ELSEIF CFLAG:MASTER:イタズラ == 2
	PRINTFORM %PARSE("You put")% the used condom between the sleeping %CALLNAME:TARGET%'s teeth and %PARSE("pull")% it out, depositing \@ TFLAG:ゴム内 > 150 ? a large serving of%" "% # \@%SAMEN_DESCRIPTION(PLAYER)% into %HIS_HER(TARGET)% mouth.<br>
	PRINTFORM In order to make %TNAME(TARGET)% remember the taste well, %PARSE("you turn",1)% the condom inside out and %PARSE("rub")% it all over %HIS_HER(TARGET)% gums, making sure every part of %HIS_HER(TARGET)% mouth tastes %PARSE("your",1)% %SAMEN_DESCRIPTION(PLAYER)%...
	CALL PRINT_BREAK
ELSE
;added from sqn/akuma
	LOCAL = 3
	PRINTFORML What kind of container would you like to use?
	CALL ASK_M("Just straight out of condom", 1, "Masu Wooden Box", 1, "Wine Glass", 1, "Tea Cup", 1, "Beer Mug", 1, "Bowl", 1, "Plastic Bottle", 1)
	SELECTCASE RESULT
		CASE 0 ;condom
		CASE 1
			RESULTS = a Japanese square measuring wooden box called masu, which still gives off the fresh fragrance of Japanese hinoki cypress
		CASE 2
			RESULTS = a wine glass
		CASE 3
			RESULTS = a small tea cup
		CASE 4
			RESULTS = a big beer mug
		CASE 5
			RESULTS = a moderately sized bowl
		CASE 6
			RESULTS = a plastic bottle
	ENDSELECT
	SELECTCASE RESULT
	CASE 0
		;condom original part
		PRINTFORM %PARSE("You push")% the 
		SELECTCASE TFLAG:ゴム内
			CASE IS > 300
				PRINTFORM condom almost bursting with semen
			CASE IS > 150
				PRINTFORM condom full of semen
			CASEELSE
				PRINTFORM used condom
		ENDSELECT
		PRINTFORML  against %CALLNAME:TARGET%'s cheek...
		SIF ABL:精液中毒
			PRINTFORM After taking the condom from your hands, %CALLNAME:TARGET% 
		IF ABL:精液中毒 >= 7
			PRINTFORM opens %HIS_HER(TARGET)% mouth ecstatically as %PARSE("you empty")% the condom on %HIS_HER(TARGET)% tongue, holding it in %HIS_HER(TARGET)% mouth and chewing on it as if savoring it
		ELSEIF ABL:精液中毒 >= 5 && RAND:2
			PRINTFORM turns it inside-out and eagerly accepts the thick liquid slowly oozing from it with %SPLIT_G(@"a stretched out tongue:with %HIS_HER(TARGET)% mouth open wide")%
		ELSEIF ABL:精液中毒 >= 3
			PRINTFORM puts the opening in %HIS_HER(TARGET)% mouth and %FSYN("start:s")% %SPLIT_G("vigorously ::")%%SPLIT_G("slurping:sucking")% the so desired liquid contained within
		ELSEIF ABL:精液中毒
			PRINTFORM shows some hesitation at first, but then sticks out %HIS_HER(TARGET)% tongue as the %SEMEN(,1)% slowly oozes on %HIS_HER(TARGET)% lips
		ELSE
			PRINTFORM Timidly opening %HIS_HER(TARGET)% mouth, %TNAME(TARGET)% shudders in revulsion as %PARSE("you empty")% the semen onto %HIS_HER(TARGET)% tongue
		ENDIF
		PRINT .<br>

		PRINTFORM %CALLNAME:TARGET% 
		SELECTCASE ABL:精液中毒
		CASE IS < 3
			PRINTFORM %SPLIT_G(@"winces from:cringes at:frowns from:scowls from:seems like %HE_SHE(TARGET)%'s not used to")% %SPLIT_G(@"the condom smell of semen:the act itself:the fact that %HE_SHE(TARGET)%'s slurping down used condom")%, 
			IF ABL:精液中毒 && RAND:4
				PRINTFORM however %SPLIT_G(@"the desire of %HIS_HER(TARGET)% own body and:%HE_SHE(TARGET)%'s actually not all that concerned by it, and:despite looking a little irritated by doing something so outrageous, %HE_SHE(TARGET)%:%HE_SHE(TARGET)% only looks mildly inconvenienced by it, as %HE_SHE(TARGET)%")% swallows it down %HIS_HER(TARGET)% throat
			ELSE
				PRINTFORM trying to use lots of %SPLIT_G("drool:saliva")% to force it down %HIS_HER(TARGET)% throat as fast as possible
			ENDIF
		CASEELSE
			LOCAL = 0
			IF RAND:2
				LOCALS = the %SPLIT_G("fresh:still warm:freshly ejaculated")% semen coming from 
			ELSE
				LOCALS = the semen inside
				LOCAL = 1
			ENDIF
			
			IF ABL:精液中毒 >= 5 && RAND:2
				PRINTFORM %SPLIT_G(@"looks like it's %SPLIT_G("something out of this world:totally delicious")%:%SPLIT_G("relishes:savors")% it deliciously:%SPLIT_G("licks:smacks")% %HIS_HER(TARGET)% lips")% 
				PRINTFORM while swallowing every drop of %LOCALS%
			ELSE
				PRINTFORM %SPLIT_G(@"casually drinks %LOCALS%:swallows %LOCALS% nonchalantly:gulps %LOCALS% down:drinks %LOCALS% while blushing")%
			ENDIF
			SIF LOCAL
				PRINTFORM %SPLIT_G(" without being bothered by its rubbery smell in the slightest:, seemingly eager to clean up the used condom: as if not bothered by its condom smell whatsoever")%
		ENDSELECT
		SIF ABL:精液中毒 >= 7
			PRINTFORM .<br>After drinking the semen, %CALLNAME:TARGET% continues sucking on the now empty condom, only parting with it reluctantly
		PRINT ...
	CASEELSE
		PRINTFORM You pour out 
		SELECTCASE TFLAG:ゴム内
			CASE IS > 300
				PRINTFORM %SPLIT_G("an obscene:an overflowing:a ridiculous")% amount of 
			CASE IS > 150
				PRINTFORM a %SPLIT_G("big:large:sizable")% portion of 
			CASEELSE
				PRINTFORM some 
		ENDSELECT
		PRINTFORM %SEMEN(,1)% into %RESULTS% and then hand it over to %CALLNAME:TARGET%.<br><wait>
		PRINTFORM %CALLNAME:TARGET% 
		SELECTCASE ABL:精液中毒
		CASE 0
			PRINTFORM seems to be quite %SPLIT_G("disgusted:repulsed:creeped out")% by such request, 
			IF TALENT:性別嗜好 == 1
				PRINTFORM not wanting to drink \@ HAS_PENIS(TARGET) ? %SEMEN()% out of disgust for anything related to men # some guy's %SEMEN()%\@
			ELSEIF IS_FEMALE(TARGET) && EXP:Ｖ経験 && RAND:2
				PRINTFORM saying that it's %SPLIT_G("intended for the other hole:not how you should use it")%
			ELSE
				PRINTFORM complaining about the %SPLIT_G("taste:smell:whole thing")%
			ENDIF
			PRINT .<br>
			PRINTFORM Nevertheless, after some convincing, %HE_SHE(TARGET)% somehow manages to %SPLIT_G("swallow:drink:gulp")% all of the %SEMEN()%, albeit %SPLIT_G("timidly:reluctantly:hesitantly")%, using lots of %SPLIT_G("drool:saliva")% %SPLIT_G(@"to overcome its terrible taste:so %HE_SHE(TARGET)% could swallow it more easily")%
		CASE 1, 2
			PRINTFORM pours the %SEMEN()% into %HIS_HER(TARGET)% mouth at once, %SPLIT_G("chugging it down:swallowing it:gulping it down")%%SPLIT_G(@", trying to ignore its taste as much as possible:, trying to get it out of %HIS_HER(TARGET)% throat as much as possible: while taking a sip of water and gargling to make it easier:::")%
		CASE 3
			PRINTFORM takes %HIS_HER(TARGET)% time carefully licking the %SEMEN()% out, tasting it while savoring %HIS_HER(TARGET)% lips deliciously%SPLIT_G(@" without any complaints: as if %HE_SHE(TARGET)%'s already used to it: in an experienced fashion::")%
		CASE IS < 5
			PRINTFORM %SPLIT_G("gulps:sucks:slurps:guzzles:chugs")% it down\@ RAND:3 ? %SPLIT_G(" casually: nonchalantly")% # with a resounding gulp\@, %SPLIT_G(@"barely holding %HIMSELF_HERSELF(TARGET)% from asking for seconds:making it look very tasty:almost looking like %HE_SHE(TARGET)%'d love to have some more")%
		CASEELSE
			PRINTFORM sniffs it very deeply at first, enjoying its rich fragrance as %HE_SHE(TARGET)% sticks out %HIS_HER(TARGET)% tongue and %FSYN("start:s")% slurping the %SEMEN()% very carefully as to not spill a single drop 
			IF RAND:4
				PRINTFORM as though it's some sort of ambrosia for %HIM_HER(TARGET)%
			ELSEIF RAND:3
				PRINTFORM as if it's the %SPLIT_G("most delicious:tastiest")% thing in the world
			ELSE
				PRINTFORM while %SPLIT_G("tasting:savoring:enjoying")% it thoroughly
			ENDIF
			PRINTFORM , looking really sad when it's over
		ENDSELECT
		IF ABL:精液中毒 < 2
			SELECTCASE MARK:反発刻印
				CASE 0
					IF MARK:不埒刻印 < 2
						PRINTFORM  with a %SPLIT_G("pale:disgusted")% %SPLIT_G("expression:face:look")%
					ELSEIF MARK:不埒刻印 >= 3
						PRINTFORM , forcing %HIMSELF_HERSELF(TARGET)% to make an ecstatic %SPLIT_G("expression:face:look")%
					ENDIF
				CASE 2
					PRINTFORM  \@ RAND:2 ? while barely holding %HIMSELF_HERSELF(TARGET)% from %SPLIT_G("gagging:vomiting")% # with %ARTICLE(SPLIT_G("uneasy:troubled:anxious"))% %SPLIT_G("expression:face:look")%\@
				CASE 3
					PRINTFORM  while %SPLIT_G("glaring:scowling")% at you
			ENDSELECT
		ENDIF
		PRINTFORM ...<br>
		SIF ABL:精液中毒 > 1 && RAND:3
			PRINTFORM After the deed is done, %CALLNAME:TARGET% sticks %HIS_HER(TARGET)% tongue out, showing the proof that she did indeed just swallow everything properly...
	ENDSELECT
	CALL PRINT_BREAK
ENDIF
PRINTFORML Drank {TFLAG:ゴム内}ml.
TFLAG:ゴム内 = 0

@Com_PregPrayer(ARG, ARG:1)
SELECTCASE CFLAG:ARG:子宝補正
	CASE 1 TO 2
		CALL COLORMESSAGE(@"The power of prayer is still weak...",C_YELLOW, 0, 1)
	CASE 3 TO 4
		CALL COLORMESSAGE(@"The power of prayer is getting a bit stronger...",C_YELLOW, 0, 1)
	CASE 5 TO 7
		CALL COLORMESSAGE(@"The power of prayer is starting to form into something promising!",C_YELLOW, 0, 1)
	CASE 8 TO 9
		CALL COLORMESSAGE(@"The power of prayer is growing in strength!",C_YELLOW, 0, 1)
	CASE IS >= 10
		CALL COLORMESSAGE(@"The power of prayer is very potent!",C_YELLOW, 0, 1)
ENDSELECT
SIF TALENT:ARG:妊娠願望 && ARG:1
	CALL COLORMESSAGE(@" Her desire to bear %PARSE("your")% children makes the prayer all the more effective!",C_YELLOW, 0, 1)
SIF TALENT:ARG:恋人 && ARG:1
	CALL COLORMESSAGE(@" The fact that %PARSE("you're", 2)% lovers seems to help %PARSE("your")% cause quite a bit!",C_YELLOW, 0, 1)
WAIT

;cap the penalty from desire for stone stacking, if clear-minded
@COM_STONE_STACKING_LUST_PENALTY(ARG)
#FUNCTION
IF ARG == MASTER
	SIF IS_WISEMAN() || !HAS_PENIS(ARG)
		RETURNF LIMIT(ABL:ARG:欲望, 1, 25)
ELSEIF CFLAG:ARG:溜まってる度 == 0
	RETURNF LIMIT(ABL:ARG:欲望, 1, 25)
ENDIF
RETURNF MAX(ABL:ARG:欲望, 1)

;===========================================
;Added from Korean version, modify ejaculation size
;===========================================
@TWKR_ORIG_SAMEN(RECEIVER, SENDER)
#DIM RECEIVER
#DIM SENDER
#DIM AblBonus
#DIM DecrementAmount
#DIM AbsorbCouple = -1, -1
#DIM DYNAMIC OriginalShoot	;개인 임의수정, 精力의 감소량을 射精量과 일치하게

; 기술에 따른 射精量 배율
; 하단에도 같은 케이스문이 있는데, 개발 편의를 위해 걍 여기서 한번 더 돌리는거야!
; 방중술보다 먼저 처리되게 설계되어있어. 생명력(?) 소진 알고리즘이 방중술로 인해서 고장나는 버그가 있어서야!
; By Subroutineuser
; AblBonus = BASE:SENDER:精力 / 50
AblBonus = MAXBASE:SENDER:精力 / 150 ;BASE 대신 MAXBASE로 처리하도록, 대신 배율감소. 3000 cap 기준 30%증가
DEBUGPRINTFORML "-SAMEN_SHOOT2.AblBonus: " + {AblBonus} + " (BEFORE HANDLE)"
; 만약 사정자가 절륜 소膣을 가지고 있다면 25% 추가
IF TALENT:SENDER:絶倫 ;unmatched
	AblBonus += 25
ENDIF
;TCVAR:12 射精箇所フラグ（1=膣内 2=アナル 3=手淫 4=口淫 5=パイズリ 6=素股 7=足コキ 8=体表 9=アナル奉仕 10=道具 11=触手）
SELECTCASE TCVAR:SENDER:射精した箇所フラグ
	CASE 1
		 LOCAL = ((ABL:SENDER:腰 + ABL:RECEIVER:膣) * 2) + ((ABL:RECEIVER:技巧 + ABL:SENDER:技巧) / 2)
		 SIF NOWEX:RECEIVER:Ｖ絶頂 ;doubled when orgasm
		 	LOCAL *= 2
		 AblBonus += LOCAL
	CASE 2
		LOCAL = ((ABL:SENDER:腰 + ABL:RECEIVER:アナル) * 2) + ((ABL:RECEIVER:技巧 + ABL:SENDER:技巧) / 2)
		SIF NOWEX:RECEIVER:Ａ絶頂 ;doubled when orgasm
		 	LOCAL *= 2
		AblBonus += LOCAL
	CASE 3, 9, 10 ;include stimulation with toys and anal
		AblBonus += ((ABL:RECEIVER:指) + (ABL:RECEIVER:技巧)) * 2
	CASE 4
		AblBonus += (ABL:RECEIVER:舌 * 2) + ABL:RECEIVER:技巧
	CASE 5
		AblBonus += ABL:RECEIVER:技巧
	CASE 6, 8 ;include whole body caress
		AblBonus += (ABL:RECEIVER:腰 * 2) + ABL:RECEIVER:技巧
	CASE 7
		AblBonus += ABL:RECEIVER:技巧
	CASE 50 ;custom: vaginal+anal
		LOCAL = ((ABL:SENDER:腰 + ABL:RECEIVER:膣) * 2) + ((ABL:RECEIVER:技巧 + ABL:SENDER:技巧) / 2)
		SIF NOWEX:RECEIVER:Ｖ絶頂 ;doubled when orgasm
			LOCAL *= 2
		AblBonus += LOCAL
		LOCAL = ((ABL:SENDER:腰 + ABL:RECEIVER:アナル) * 2) + ((ABL:RECEIVER:技巧 + ABL:SENDER:技巧) / 2)
		SIF NOWEX:RECEIVER:Ａ絶頂 ;doubled when orgasm
		 	LOCAL *= 2
		AblBonus += LOCAL
ENDSELECT
;squeezing strength from target
AblBonus += MIN(ABL:RECEIVER:欲望, 15) + ABL:RECEIVER:精液中毒

AblBonus = LIMIT(AblBonus, 0, 100)
OriginalShoot = NOWEX:SENDER:射精量
NOWEX:SENDER:射精量 += (NOWEX:SENDER:射精量 * AblBonus) / 100
SIF TFLAG:MalletSemen ;addition custom code, miracle mallet
	NOWEX:SENDER:射精量 *= 3

IF BASE:SENDER:精力 <= 0
	SETCOLOR 150, 50, 50
	; 네 精力이 아니라 생명을 짜내고 있는데?
	;see if you're having your life out of you squeezed
	IF BASE:SENDER:気力 > 0
		DecrementAmount = LIMIT((NOWEX:SENDER:射精量 * 125) * EX:SENDER:射精 / 100, 0, MAXBASE:SENDER:気力/3)
		BASE:SENDER:気力 -= DecrementAmount
		IF SENDER == MASTER
			PRINTFORML You feel like you've already reached your limit...! (ENE -{DecrementAmount})
		ELSE
			PRINTFORML %CALLNAME:SENDER% feels like %HE_SHE(SENDER)%'s already reached %HIS_HER(SENDER)% limit...! (ENE -{DecrementAmount})
		ENDIF
	ELSE
		DecrementAmount = LIMIT(NOWEX:SENDER:射精量 * EX:SENDER:射精, 0, MAXBASE:SENDER:体力/3)
		BASE:SENDER:体力 -= DecrementAmount
		IF SENDER == MASTER
			PRINTFORML You can't possibly take this anymore...! (STA -{DecrementAmount})
		ELSE
			PRINTFORML %CALLNAME:SENDER% can't take it anymore...! (STA -{DecrementAmount})
		ENDIF
	ENDIF	
	RESETCOLOR
	SIF SENDER == MASTER
		nBorrowedVig += NOWEX:SENDER:射精量 ;track the amount of vig that went over the limit, apply it after the day is over since we can't let your vig go negative during the play
ENDIF
; BASE:SENDER:精力 = MAX(0, BASE:SENDER:精力 - NOWEX:SENDER:射精量)
BASE:SENDER:精力 = MAX(0, BASE:SENDER:精力 - (NOWEX:SENDER:射精量 - OriginalShoot))
DEBUGPRINTFORML "-SAMEN_SHOOT2.AblBonus: " + {AblBonus} + " (AFTER HANDLE)"
; End
;;방중술 및 채양/채음 처리
IF TALENT:SENDER:房中術
	MAXBASE:SENDER:気力++
	MAXBASE:RECEIVER:気力++
	;BASE:SENDER:気力 = LIMIT(BASE:SENDER:気力 + 100, 0, MAXBASE:SENDER:気力)
	;BASE:RECEIVER:気力 = LIMIT(BASE:RECEIVER:気力 + 100, 0, MAXBASE:RECEIVER:気力)
	CALL RECOVER(SENDER,100, "気力", "Art Of Lovemaking")
	CALL RECOVER(RECEIVER,100, "気力", "Art Of Lovemaking")
ENDIF
IF GETBIT(TALENT:SENDER:採陰補阳／採阳補陰, 0) || GETBIT(TALENT:SENDER:採陰補阳／採阳補陰, 1)
	IF GETBIT(TALENT:SENDER:採陰補阳／採阳補陰,0)&&GETBIT(TALENT:SENDER:採陰補阳／採阳補陰,2) ;yang
		AbsorbCouple:0 = SENDER, RECEIVER
	ELSEIF GETBIT(TALENT:SENDER:採陰補阳／採阳補陰,1)&&GETBIT(TALENT:SENDER:採陰補阳／採阳補陰,3) ;yin
		AbsorbCouple:0 = RECEIVER, SENDER
	ELSE
		DEBUGPRINTFORML ERROR:잘못된 채음보양/채양보음 값 {TALENT:SENDER:採陰補阳／採阳補陰}
		CLEARBIT TALENT:SENDER:採陰補阳／採阳補陰,0
		CLEARBIT TALENT:SENDER:採陰補阳／採阳補陰,1
		DEBUGPRINTFORML 수정된 채음보양/채양보음 값 {TALENT:SENDER:採陰補阳／採阳補陰}
	ENDIF
	IF AbsorbCouple:0 != -1 || AbsorbCouple:1 != -1
		MAXBASE:(AbsorbCouple:0):体力 += 2
		BASE:(AbsorbCouple:0):体力 = LIMIT(BASE:(AbsorbCouple:0):体力 + 100, 0, MAXBASE:(AbsorbCouple:0):体力)
		BASE:(AbsorbCouple:1):体力 = LIMIT(BASE:(AbsorbCouple:1):体力 - 20, 0, MAXBASE:(AbsorbCouple:1):体力)
	ENDIF
ENDIF

;set up the flag
@EVENTTRAIN
#PRI
nBorrowedVig = 0

@EVENTTURNEND
#LATER
BASE:MASTER:精力 -= nBorrowedVig
[SKIPEND]

@IS_COM_REVSERVICE(ID_COM)
#LOCALSIZE 1
#LOCALSSIZE 1
#FUNCTION
#DIM ID_COM
SIF GROUPMATCH(ID_COM, 2, 96, 123, 124)
	RETURNF 1
RETURNF 0

@SET_FILTER_NG_REVSERVICE
#LOCALSIZE 1
#LOCALSSIZE 1
#DIM ID_COM
FOR ID_COM, 0, 600
	;V
	SIF !IS_COM_REVSERVICE(ID_COM)
		CONTINUE
	CALL ADD_COM_FILTER(ID_COM)
NEXT

@IS_COM_REVSEX(ID_COM)
#LOCALSIZE 1
#LOCALSSIZE 1
#FUNCTION
#DIM ID_COM
SIF GROUPMATCH(ID_COM, 92, 93, 94, 130, 131, 132, 133, 64)
	RETURNF 1
RETURNF 0

@SET_FILTER_NG_REVSEX
#LOCALSIZE 1
#LOCALSSIZE 1
#DIM ID_COM
FOR ID_COM, 0, 600
	;V
	SIF !IS_COM_REVSEX(ID_COM)
		CONTINUE
	CALL ADD_COM_FILTER(ID_COM)
NEXT

;-------------------------------
;Custom config section
;-------------------------------
;@CUSTOM_OPTION_3
;PRINTFORML [  3] - 角色只吃喜欢的食物：\@ Com_Food_Quality_Check ? 是 # 否 \@
;
;@CUSTOM_OPTION_RESULT_3
;Com_Food_Quality_Check = !Com_Food_Quality_Check
;IF Com_Food_Quality_Check
;	PRINTFORML 角色前来用餐时，只有在第一眼就觉得合胃口（50点以上）的情况下才会吃。
;	PRINTFORMW 你当前[招待用餐]的目标无论如何都会吃。
;ENDIF

@COM_SEND_ASSISTANT_KOJO
#LOCALSIZE 1
#LOCALSSIZE 1
IF TFLAG:50
    IF TFLAG:50 == ADD_CUSTOM_SCOM
        LOCAL = ADD_SELECTED_SCOM
        LOCALS = CUSTOM_SCOM
    ELSE
        LOCAL = TFLAG:50
        LOCALS = SCOM
    ENDIF
ELSE
    IF SELECTCOM == ADD_CUSTOM_COM
        LOCAL = ADD_SELECTCUSTOMCOM
        LOCALS = CUSTOM_COM
    ELSE
        LOCAL = SELECTCOM
        LOCALS = COM
    ENDIF
ENDIF

TRYCALLFORM ASSISTANT_KOJO_%LOCALS%{LOCAL}
