@SHOW_FISHING_LIST
#DIM PLACE
#DIM PADDING
#DIM CURRENTLINE
PADDING = 47
DRAWLINE
PRINTFORML 钓鱼地点列表 (季节: %GET_MONTH()%)
CALL COLORMESSAGE(@"极品鱼类    ",C_YELLOW)
CALL COLORMESSAGE(@"本季可钓    ",C_P_GREEN)
CALL COLORMESSAGE(@"超级稀有    ",C_RED)
CALL COLORMESSAGE(@"非当季",0x606060)
PRINTFORML 
DRAWLINE
CURRENTLINE = LINECOUNT
DO
	SIF LINECOUNT - CURRENTLINE > 0
		CLEARLINE LINECOUNT - CURRENTLINE
		
	LOCAL:1 = 0
	FOR LOCAL,1, 9
		IF FishingPlaceName(LOCAL) != ""
			PRINTFORM [{LOCAL, 2}] %FishingPlaceName(LOCAL), PADDING, LEFT%
			SIF (++LOCAL:1) % 2 == 0
				PRINTL 
		ENDIF
	NEXT
	;PRINTFORML 
	DRAWLINE
	PRINTFORML [99] 返回
	INPUT
	
	PLACE = RESULT
	IF FishingPlaceName(PLACE) != ""
		CALL SHOW_FISHING_ITEM(PLACE)
		PRINTFORML
		PRINTFORMW 
		CLEARLINE 1
		CONTINUE
	ENDIF
	RETURN -1
LOOP 1

@FishingPlaceName(ARG)
#FUNCTIONS
#DIM nLoc = 0, P347霧の湖, P602賽の河原, P703玄武の沢, P708大蝦蟇の池, P806滝の裏, P814湖畔, P945川岸, P1068純狐邸・中庭
SIF !INRANGE(ARG, 1, VARSIZE("nLoc")-1)
	RETURNF

RETURNF NAME_FROM_PLACE(8000 + nLoc:ARG) + @" (%GET_MAPNAME(nLoc:ARG/100)%)"

@SHOW_FISHING_LIST_SELECT(PLACE)
#DIM PLACE
DRAWLINE
PRINTFORML 钓鱼地点列表 (季节: %GET_MONTH()%)
CALL COLORMESSAGE(@"极品鱼类    ",C_YELLOW)
CALL COLORMESSAGE(@"本季可钓    ",C_P_GREEN)
CALL COLORMESSAGE(@"超级稀有    ",C_RED)
CALL COLORMESSAGE(@"非当季",0x606060)
PRINTFORML 
DRAWLINE
SELECTCASE PLACE
	CASE 99
		RETURN -1
	CASEELSE
		CALL SHOW_FISHING_ITEM(PLACE)
		DRAWLINE
		PRINTFORMW
ENDSELECT

@SHOW_FISHING_ITEM(PLACE)
#DIM PLACE

FOR LOCAL,1,61
	SIF LOCAL == 21 ;跳过转子
		CONTINUE
	SIF !STRLENS(FISH_NAME(LOCAL))
		BREAK
	IF FISHING_PRODUCT(LOCAL, PLACE)
		SIF INRANGE(LOCAL, 11, 20) || INRANGE(LOCAL, 24, 60)
			SETCOLOR C_YELLOW
		SIF FISHING_SEASON(LOCAL) >= 2
			SETCOLOR C_P_GREEN
		SIF FISH_RATE(LOCAL) >= 6
			SETCOLOR C_RED
		;季节条件を満たしていない場合は灰色表記に
		;如果未达到季节条件，则以灰色显示
		SIF !FISHING_SEASON(LOCAL)
			SETCOLOR 0x606060
		IF (ABL:MASTER:教養+2) - FISH_RATE(LOCAL) < 0
			PRINTFORML ？？？
		ELSE	
			PRINTFORML %FISH_NAME(LOCAL)% - \@ FISH_RATE(LOCAL) > 5 ? Ex! # %"★"*FISH_RATE(LOCAL)%\@
		ENDIF
	ENDIF
	RESETCOLOR
NEXT
IF ABL:MASTER:教養 >= 3
	;can get some materials too sometimes
	;有时也可以得到一些材料
	SELECTCASE PLACE
		CASE 1, 6 ;龍涎香or氷の鱗（冬）
			;龙涎香or冰之鳞（冬）
			SETCOLOR C_RED
			IF DAY:2 == 4
				PRINTFORML 冰之鳞
			ELSE
				PRINTFORML 龙涎香
			ENDIF
		CASE 2, 3, 5, 7 ;龍の爪or氷の鱗（冬）
			;龙之爪or冰之鳞（冬）
			SETCOLOR C_RED
			IF DAY:2 == 4
				PRINTFORML 冰之鳞
			ELSE
				PRINTFORML 龙之爪
			ENDIF
		CASE 4 ;カエル
			;青蛙
			SETCOLOR C_GREEN
			PRINTFORML 青蛙
		CASE 8
			SETCOLOR C_RED
			IF DAY:2 == 4
				PRINTFORM 冰之鳞　
			ELSE
				PRINTFORM 龙涎香　
			ENDIF
			SETCOLOR C_GREEN
			PRINTFORML 青蛙
	ENDSELECT
	RESETCOLOR
ENDIF

@FISHING_PRODUCT(ARG:0, ARG:1)
#FUNCTION

SELECTCASE ARG:1
	;霧の湖
	CASE 1, 霧の湖
		SIF GROUPMATCH(ARG:0, 5, 7, 10, 12, 16, 18, 20, 21, 25, 31)
			RETURNF 1
	;三途の川
	CASE 2, 三途の川
		SIF GROUPMATCH(ARG:0, 6, 7, 16, 17, 19, 22, 23, 26)
			RETURNF 1
	;玄武の沢
	CASE 3, 玄武の沢
		SIF GROUPMATCH(ARG:0, 1, 2, 3, 4, 11, 13, 14, 15)
			RETURNF 1
	;大蝦蟇の池
	CASE 4, 大蝦蟇の池
		SIF GROUPMATCH(ARG:0, 5, 12, 18, 22, 27)
			RETURNF 1
	;九天の滝
	CASE 5, 九天の滝
		SIF GROUPMATCH(ARG:0, 1, 2, 3, 4, 11, 13, 14, 28)
			RETURNF 1
	;山の湖
	CASE 6, 山の湖
		SIF GROUPMATCH(ARG:0, 1, 7, 10, 12, 16, 18, 22, 29)
			RETURNF 1
	;地獄の深道
	CASE 7, 地獄の深道
		SIF GROUPMATCH(ARG:0, 6, 8, 9, 17, 19, 24, 30)
			RETURNF 1
	;純狐邸中庭
	CASE 8, 1068
		SIF GROUPMATCH(ARG:0, 6, 8, 17, 19, 22, 32)
			RETURNF 1
ENDSELECT
RETURNF 0

@FISHING_SEASON(ARG:0)
#FUNCTION

SELECTCASE ARG:0
	CASE 6
		SIF DAY:2 >= 3
			RETURNF 2
	CASE 11
		SIF DAY:2 == 3
			RETURNF 2
	CASE 12
		SIF DAY:2 == 4
			RETURNF 2
	CASE 13
		SIF DAY:2 < 3
			RETURNF 2
	CASE 17
		SIF DAY:2 < 3
			RETURNF 2
	CASE 18
		SIF DAY:2 != 4
			RETURNF 2
	CASE 31
		SIF MOON_WATCH(DAY:3) == 1
			RETURNF 2
	CASEELSE
		RETURNF 1
ENDSELECT
RETURNF 0

@FISH_RATE(ARG:0)
#FUNCTION

SELECTCASE ARG:0
	CASE 1, 5, 6
		RETURNF 1
	CASE 2, 7, 8
		RETURNF 2
	CASE 3, 4, 9, 10, 16, 17
		RETURNF 3
	CASE 11 TO 14, 18 TO 22, 24
		RETURNF 4
	CASE 15, 23, 25 TO 30, 32
		RETURNF 5
	CASE 31
		RETURNF 6
ENDSELECT
RETURNF 1
