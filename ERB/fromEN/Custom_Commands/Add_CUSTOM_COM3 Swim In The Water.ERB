@ADD_CUSTOM_COM3
#DIM DYNAMIC PARTNER
TFLAG:194 = ADD_WEATHER_HARSHNESS(CFLAG:MASTER:現在位置)

IF TARGET && !CFLAG:睡眠 && TEQUIP:SWIMMING && !(MARK:反発刻印 || CFLAG:ブチギレ) && BASE:気力 > 0 && ADD_SWIMMING_CONSTITUTION(TARGET)
	PARTNER = TARGET
ENDIF
TFLAG:ComPartner = PARTNER

SELECTCASE TFLAG:194
	CASE 0
		DOWNBASE:MASTER:体力 = 200
		DOWNBASE:MASTER:気力 = 200
		IF PARTNER
			;固定で獲得するソース
			SOURCE:歓楽 = 400
			MAXBASE:TARGET:体力 += RAND:7
			DOWNBASE:TARGET:体力 = 200
			DOWNBASE:TARGET:気力 = 200
			;信頼
			TFLAG:98 += 3
			IF ABL:親密 <= 5
				SOURCE:歓楽 += 500 + (ABL:親密 * 60)
				TFLAG:98 ++ 
			ELSEIF ABL:親密 <= 8
				SOURCE:歓楽 += 750 + (ABL:親密 * 75)
			ELSEIF ABL:親密 <= 11
				SOURCE:歓楽 += 1000 + (ABL:親密 * 75)
			ELSE
				SOURCE:歓楽 += 2000 + (ABL:親密 * 30)
			ENDIF
			;好感度をみる
			IF CFLAG:2 <= 500
				SOURCE:歓楽 += CFLAG:2
			ELSEIF CFLAG:2 <= 5000
				SOURCE:歓楽 += 500 + (CFLAG:2 - 500) / 3
			ELSE
				SOURCE:歓楽 += 2000 + (CFLAG:2 - 5000) / 5
			ENDIF
			SIF SOURCE:歓楽 < 0
				SOURCE:歓楽 = 0
			EXP:MASTER:戦闘経験 ++
			EXP:戦闘経験 ++
		ENDIF
	CASE 1
		;悪天候
		DOWNBASE:MASTER:体力 = 300
		DOWNBASE:MASTER:気力 = 300
		IF PARTNER
			;固定で獲得するソース
			SOURCE:歓楽 = 450
			MAXBASE:TARGET:体力  += RAND:10
			MAXBASE:TARGET:気力  += RAND:7
			DOWNBASE:TARGET:体力 = 300
			DOWNBASE:TARGET:気力 = 300
			;信頼
			TFLAG:98 += 3
			;ABL:親密をみる
			IF ABL:親密 <= 5
				SOURCE:歓楽 += 520 + (ABL:親密 * 60)
				TFLAG:98 ++ 
			ELSEIF ABL:親密 <= 8
				SOURCE:歓楽 += 750 + (ABL:親密 * 75)
			ELSEIF ABL:親密 <= 11
				SOURCE:歓楽 += 1100 + (ABL:親密 * 75)
			ELSE
				SOURCE:歓楽 += 2150 + (ABL:親密 * 30)
			ENDIF

			;好感度をみる
			IF CFLAG:2 <= 500
				SOURCE:歓楽 += CFLAG:2
			ELSEIF CFLAG:2 <= 5000
				SOURCE:歓楽 += 500 + (CFLAG:2 - 500) / 3
			ELSE
				SOURCE:歓楽 += 2000 + (CFLAG:2 - 5000) / 5
			ENDIF
			SIF SOURCE:歓楽 < 0
				SOURCE:歓楽 = 0
			EXP:MASTER:戦闘経験 += 2
			EXP:戦闘経験 += 2
		ENDIF
	CASE 2,3
	;ヤバい天気
		DOWNBASE:MASTER:体力 = 800
		DOWNBASE:MASTER:気力 = 800
		LOCAL:0 = RAND:5 + RAND:5 + RAND:3
		LOCAL:1 = RAND:5 + RAND:5 + RAND:3
		MAXBASE:MASTER:体力 += LOCAL:0
		MAXBASE:MASTER:気力 += LOCAL:1
		SIF LOCAL:0 || LOCAL:1
			PRINTFORMW 轻率地在如此极端的天气条件下游泳，+++感觉自己变得更强悍了一些……
		;-----------------------
		IF PARTNER
			;固定で獲得するソース
			SOURCE:歓楽 = 500
			MAXBASE:TARGET:体力 += RAND:30
			MAXBASE:TARGET:気力 += RAND:30
			DOWNBASE:TARGET:体力 = 800
			DOWNBASE:TARGET:気力 = 800
			;信頼
			TFLAG:98 += 3
			;ABL:親密をみる
			IF ABL:親密 <= 5
				TFLAG:98 ++ 
				SOURCE:歓楽 += 550 + (ABL:親密 * 60)
			ELSEIF ABL:親密 <= 8
				SOURCE:歓楽 += 800 + (ABL:親密 * 75)
			ELSEIF ABL:親密 <= 11
				SOURCE:歓楽 += 1200 + (ABL:親密 * 75)
			ELSE
				SOURCE:歓楽 += 2300 + (ABL:親密 * 30)
			ENDIF
			;好感度をみる
			IF CFLAG:2 <= 500
				SOURCE:歓楽 += CFLAG:2
			ELSEIF CFLAG:2 <= 5000
				SOURCE:歓楽 += 500 + (CFLAG:2 - 500) / 3
			ELSE
				SOURCE:歓楽 += 2000 + (CFLAG:2 - 5000) / 5
			ENDIF
			SIF SOURCE:歓楽 < 0
				SOURCE:歓楽 = 0
			EXP:MASTER:戦闘経験 += 3
			EXP:戦闘経験 += 3
		ENDIF
ENDSELECT
TIME += 30
SIF CFLAG:同行
	CFLAG:同行 += 30
EXP:MASTER:戦闘経験 ++
TCVAR:MASTER:空腹時刻 -= 15
IF PARTNER
	TCVAR:TARGET:空腹時刻 -= 15
	CALL ADD_TIRED(TARGET, POWER(TFLAG:194 + 1, 3) * 100)
ENDIF
IF ITEM:251;运动饮料
	IF TFLAG:194 == 3
		DOWNBASE:MASTER:体力 -= 200
		DOWNBASE:MASTER:気力 -= 300
	ELSE
		DOWNBASE:MASTER:体力 -= 50
		DOWNBASE:MASTER:気力 -= 50
	ENDIF
	SIF PARTNER
		SOURCE:歓楽 += 300
	TFLAG:193 = 1
ENDIF

IF ADD_SWIMMING_DAMAGE(MASTER)
	DOWNBASE:MASTER:体力 += 200 * ADD_SWIMMING_DAMAGE(MASTER)
	DOWNBASE:MASTER:気力 += 100 * ADD_SWIMMING_DAMAGE(MASTER)
ENDIF

IF PARTNER && ADD_SWIMMING_DAMAGE(TARGET)
	DOWNBASE:TARGET:体力 += 200 * ADD_SWIMMING_DAMAGE(TARGET)
	DOWNBASE:TARGET:気力 += 100 * ADD_SWIMMING_DAMAGE(TARGET)
ENDIF

RETURN 1

@ADD_CUSTOM_COM_ABLE3
SIF !TFLAG:100
	RETURN 0
;一括管理
SIF CFLAG:うふふ
	RETURN 0
SIF !TEQUIP:MASTER:SWIMMING
	RETURN 0
SIF !ADD_SWIMMING_AREA(CFLAG:MASTER:現在位置)
	RETURN 0
SIF BASE:MASTER:気力 <= 0 ;is tired
	RETURN 0
;停止中は不可
SIF FLAG:70
	RETURN 0
SIF TFLAG:デート道中
	RETURN 0
RETURN 1

@ADD_CUSTOM_MESSAGE_COM3
PRINTFORML 在%ADD_SWIMMING_DESCRIPTION(CFLAG:MASTER:現在位置)%里%COND_STR(@"和%CALLNAME:(TFLAG:ComPartner)%", TFLAG:ComPartner)%游了一会儿。
SELECTCASE TFLAG:194
	CASE 0
		PRINTFORML 游了一段时间后，%CALLNAME:MASTER%觉得身心愉悦，而且很放松。
	CASE 1
		PRINTFORML 水波相当汹涌，%CALLNAME:MASTER%运动后觉得非常疲惫。
	CASE 2
		PRINTFORML 天气有些恶劣，不久后%CALLNAME:MASTER%便感到筋疲力尽。
	CASE 3
		PRINTFORML 极其危险的天气将%CALLNAME:MASTER%的体力逼到了极限。
ENDSELECT

IF ADD_SWIMMING_DAMAGE(MASTER)
	IF (TALENT:MASTER:妖怪 == 3 || TALENT:MASTER:吸血鬼) && ADD_RUNNING_WATER(CFLAG:MASTER:現在位置)
		PRINTFORML 呆在流水中的+++感到自己的身躯传来阵阵剧痛。
	ELSEIF !ADD_SWIMMING_SEASON() && !TALENT:MASTER:水棲 && !TALENT:MASTER:氷精
		PRINTFORML 冰冷的流水反倒是进一步锻炼了+++的体格。
	ENDIF
ENDIF

IF TFLAG:ComPartner && ADD_SWIMMING_DAMAGE(TFLAG:ComPartner)
	IF (TALENT:(TFLAG:ComPartner):妖怪 == 3 || TALENT:(TFLAG:ComPartner):吸血鬼) && ADD_RUNNING_WATER(CFLAG:(TFLAG:ComPartner):現在位置)
		PRINTFORML 尽管流动的水会造成伤害，但对于%CALLNAME:(TFLAG:ComPartner)%而言似乎不成问题。
	ELSEIF TALENT:(TFLAG:ComPartner):143 == 4
		PRINTFORML 尽管生性有些怕水，但对于%CALLNAME:(TFLAG:ComPartner)%而言似乎不成问题。
	ELSEIF !ADD_SWIMMING_SEASON() && !TALENT:(TFLAG:ComPartner):水棲 && !TALENT:(TFLAG:ComPartner):氷精
		PRINTFORML 水温虽冷，但对%CALLNAME:(TFLAG:ComPartner)%来说似乎还是在可承受范围内。
	ENDIF
ENDIF