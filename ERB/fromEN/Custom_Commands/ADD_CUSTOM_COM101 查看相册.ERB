;摄影相关的函数，全部都放在这些文件里了
;这是静叶口上的一个大功能，也许以后被改造成通用补丁也说不定？
;现决定将它作为自定义添加指令而非口上独占指令。——25.10.7

;摄影简介：
;添加了自定义指令「摄影」和「查看相册」以及「购买胶卷」

;「查看相册」允许玩家在自宅和其他特定地点（如秋姐妹私室）查看自己所拍摄的照片
;（别问我怎么互通，你就按MC的末影箱理解就好，不然照片分别放在好几个地方又要嫌麻烦）
;相册存放了所有已经拍摄的照片，可以查看具体信息以及留下备注
;所有刚被拍摄下来且被保留的照片都会存入默认相册
;可以自己创建一个相册以用于存储一部分的照片
;当删除相册时，该相册内的所有照片会被转移至默认相册（当然也提供了同时删除全部照片的按钮）
;所有照片的描述都是文本化的（不要觉得我真的能做出啥图片化的东西）（暂未实装）
;可以挑选一张照片摆放在房间内（H相关的不行）（暂未实装）
;照片可以随时丢弃，且一旦丢弃就无法找回！

@ADD_CUSTOM_COM101
CALL SHOW_PHOTOALBUM_MENU
;查看相片导致的退出
IF RESULT == 998
	TFLAG:193 = 1
	TIME += 5
ELSE
	TFLAG:193 = 0
	RETURN 0
ENDIF
RETURN 1

@ADD_CUSTOM_COM_ABLE101
;実行判定
SIF !TFLAG:100
	RETURN 0
;一括管理
SIF TFLAG:102 != 1
	RETURN 0
;约会道中
SIF TFLAG:约会道中
	RETURN 0
;诶嘿嘿或在浴室中
SIF CFLAG:317 || BATHCHECK(CFLAG:MASTER:300)
    RETURN 0
RETURN 1

;CHECK_PHOTO:0 地点
;CHECK_PHOTO:1 日期
;CHECK_PHOTO:2 时间
;CHECK_PHOTO:3 品质
;CHECK_PHOTO:4 角色（集合）
;CHECK_PHOTO:5 基本描述（目前为空）
;CHECK_PHOTO:6 备注
;其中差分大多是从CHECK_PHOTO:4中来的，例如“顔絵_服_通常_80◇顔絵_照れ_80◇/”或者“顔絵_裸_通常_52◇顔絵_照れ_52◇顔絵_吐息_52◇/”
@ADD_CUSTOM_MESSAGE_COM101
#DIMS TEMP_INFO
#DIMS CHARANAME
;基本上只有查看相片这一块才会为口上提供接口
IF TFLAG:193 == 1
	IF TARGET != 0
		;TARGET看到了自己
		IF STRFIND(CHECK_PHOTO:4, @"_%TOSTR(TARGET)%◇") != -1
			IF STRFIND(CHECK_PHOTO:4, "裸") != -1
				IF STRFIND(CHECK_PHOTO:4, "発情") != -1
					PRINTFORMDL %CALLNAME:TARGET%望着自己淫乱的样子……
				ELSEIF STRFIND(CHECK_PHOTO:4, "性交") != -1 || STRFIND(CHECK_PHOTO:4, "トロ顔") != -1 || STRFIND(CHECK_PHOTO:4, "イキ顔") != -1
					PRINTFORMDL %CALLNAME:TARGET%望着脱光衣服的自己被侵犯的样子，有些站不住了……
				ELSEIF STRFIND(CHECK_PHOTO:4, "通常") != -1 || STRFIND(CHECK_PHOTO:4, "笑顔") != -1
					PRINTFORMDL %CALLNAME:TARGET%问你为什么要拍她的裸照……
				ELSEIF STRFIND(CHECK_PHOTO:4, "憤怒") != -1
					PRINTFORMDL %CALLNAME:TARGET%望着这样的自己，感到哭笑不得……
				ENDIF
			ELSE
				IF STRFIND(CHECK_PHOTO:4, "発情") != -1
					PRINTFORMDL %CALLNAME:TARGET%望着自己失态的样子，不禁羞红了脸……
				ELSEIF STRFIND(CHECK_PHOTO:4, "性交") != -1 || STRFIND(CHECK_PHOTO:4, "トロ顔") != -1 || STRFIND(CHECK_PHOTO:4, "イキ顔") != -1
					PRINTFORMDL %CALLNAME:TARGET%望着自己被侵犯的样子，有些站不住了……
				ELSEIF STRFIND(CHECK_PHOTO:4, "睡眠") != -1
					PRINTFORMDL %CALLNAME:TARGET%不理解你为什么要拍下她的睡颜……
				ELSEIF STRFIND(CHECK_PHOTO:4, "通常") != -1 || STRFIND(CHECK_PHOTO:4, "笑顔") != -1
					IF STRFIND(CHECK_PHOTO:4, "照れ") != -1
						PRINTFORMDL %CALLNAME:TARGET%看着相片中羞涩的自己，也不由得脸红起来……
					ELSE
						PRINTFORMDL %CALLNAME:TARGET%在相片中看到了自己的模样……
					ENDIF
				ELSEIF STRFIND(CHECK_PHOTO:4, "憤怒") != -1
					PRINTFORMDL %CALLNAME:TARGET%不明白生气的自己有什么值得拍下来的……
				ENDIF
			ENDIF
			SELECTCASE CHECK_PHOTO:3
				CASE "0", "1"
					PRINTFORMDL %CALLNAME:TARGET%感觉你拍出来的自己不是很好看……
				CASE "2", "3"
					PRINTFORMDL %CALLNAME:TARGET%审视着相片中的自己……
				CASE "4", "5"
					PRINTFORMDL %CALLNAME:TARGET%感觉你拍出来的自己很好看……
				CASEELSE
					PRINTFORMDL %CALLNAME:TARGET%对你拍出来的自己感到惊讶……
			ENDSELECT
		;自机
		ELSEIF CHECK_PHOTO:4 == "0/"
			SELECTCASE CHECK_PHOTO:3
				CASE "0", "1"
					PRINTFORMDL %CALLNAME:TARGET%感觉这根本不像你本人……
				CASE "2", "3"
					PRINTFORMDL %CALLNAME:TARGET%感觉相片中的你和平常别无二致……
				CASE "4", "5"
					PRINTFORMDL %CALLNAME:TARGET%问相片里的你是不是有些和平常不一样……
				CASEELSE
					PRINTFORMDL %CALLNAME:TARGET%有些不敢相信你把自己拍得这样有魅力……
			ENDSELECT
		;风景
		ELSEIF RESUlTS:5 == ""
			SELECTCASE CHECK_PHOTO:3
				CASE "0", "1"
					PRINTFORMDL %CALLNAME:TARGET%感觉你拍的风景似乎不怎么样……
				CASE "2", "3"
					PRINTFORMDL %CALLNAME:TARGET%说你拍风景的水平还有待提高……
				CASE "4", "5"
					PRINTFORMDL %CALLNAME:TARGET%就你拍的风景简单说了几句……
				CASEELSE
					PRINTFORMDL %CALLNAME:TARGET%感觉你拍的风景很美丽……
			ENDSELECT
		;不知道暂时应该写什么好
		ELSE
			PRINTFORMDL %CALLNAME:TARGET%望着你的相片……
		ENDIF
	ELSE
		SELECTCASE RAND:3
			CASE 0
				PRINTFORMDL %CALLNAME:MASTER%对着自己拍下的相片欣赏了一会……
			CASE 1
				PRINTFORMDL %CALLNAME:MASTER%饶有兴致地看着自己拍的相片……
			CASE 2
				IF CHECK_PHOTO:4 == ""
					PRINTFORMDL %CALLNAME:MASTER%对着相片中的风景琢磨着……
				ELSEIF CHECK_PHOTO:4 == "0/"
					PRINTFORMDL %CALLNAME:MASTER%望着相片中的自己……
				ELSE
					REGEXPMATCH STRINGSET_RANDGET(CHECK_PHOTO:4), "\d+", RESULT, TEMP_INFO
					CHARANAME = %CALLNAME:(TOINT(TEMP_INFO:0))%
					PRINTFORMDL %CALLNAME:MASTER%望着相片中的%CHARANAME%\@ STRINGSET_LEN(CHECK_PHOTO:4) == 1 ?  # 等人\@……
				ENDIF
		ENDSELECT
	ENDIF
ENDIF
RETURN 1

;显示相册
@SHOW_PHOTOALBUM_MENU
#DIM PA_ID
#DIM PA_PHOTONUM
#DIM ALBUM_NO
#DIM FIRSTTIME_FLAG = 0
#DIM LIST_TYPE = 0
#DIM SORT_TYPE = 0
#DIM ALBUMMENU_NOWPAGE = 1
#DIM ALBUMMENU_PAGECOUNT
#DIM ALBUMMENU_PAGECAPACITY = 10
#DIMS INPUT_STR
#DIM INPUT_INT
VARSET LOCALS
VARSET LOCAL
;初始化相册，创建一个默认相册，并将之前的相片全部加入其中
IF !FIRSTTIME_FLAG
	ALBUM_NUM = 1
	PA_ID = 0
	ALBUMMENU_PAGECOUNT = 1
	ALBUMINFO:PA_ID = %DIC_SET(ALBUMINFO:PA_ID, "A_ID", TOSTR(PA_ID))%
	ALBUMINFO:PA_ID = %DIC_SET(ALBUMINFO:PA_ID, "NAME", "默认相册")%
	FOR LOCAL, 0, PHOTO_NUM
		SIF PA_ID == TOINT(DIC_GET(PHOTOINFO:LOCAL, "A_ID"))
			LOCAL:1 += 1
	NEXT
	ALBUMINFO:PA_ID = %DIC_SET(ALBUMINFO:PA_ID, "PHOTONUM", TOSTR(LOCAL:1))%
	ALBUMINFO:PA_ID = %DIC_SET(ALBUMINFO:PA_ID, "NOTE", "（空）")%
	FIRSTTIME_FLAG = 1
	VARSET LOCAL
ENDIF

DRAWLINE
;排序按钮
PRINTS " " * 6
IF LIST_TYPE == 0
	PRINTBUTTON @"\@ SORT_TYPE == 0 ? ▲ # ▼\@ID", "1000"
ELSE
	PRINTS " "
	PRINTBUTTON "ID", "1000"
ENDIF
PRINTS " " * 34
IF LIST_TYPE == 1
	PRINTBUTTON @"\@ SORT_TYPE == 0 ? ▲ # ▼\@相片数量", "1001"
ELSE
	PRINTS " "
	PRINTBUTTON "相片数量", "1001"
ENDIF
PRINTL 
DRAWLINE

FOR ALBUM_NO, (ALBUMMENU_NOWPAGE - 1) * ALBUMMENU_PAGECAPACITY, ALBUMMENU_NOWPAGE * ALBUMMENU_PAGECAPACITY
	IF INRANGE(ALBUM_NO, 0, ALBUM_NUM - 1)
		LOCAL:1 = TOINT(DIC_GET(ALBUMINFO:ALBUM_NO, "A_ID"))
		PA_PHOTONUM = GET_ALBUM_PHOTONUM(LOCAL:1)
		ALBUMINFO:ALBUM_NO = %DIC_SET(ALBUMINFO:ALBUM_NO, "PHOTONUM", TOSTR(PA_PHOTONUM))%
		PRINTBUTTON @"[{ALBUM_NO, 2}] ({LOCAL:1, 4}) %DIC_GET(ALBUMINFO:ALBUM_NO, "NAME"), 36, LEFT% %DIC_GET(ALBUMINFO:ALBUM_NO, "PHOTONUM"), 4%", @"{LOCAL:1}"
		PRINTL 
	ELSE
		PRINTL 
	ENDIF
NEXT
DRAWLINE
;计算页数
ALBUMMENU_PAGECOUNT = 1 + (ALBUM_NUM - 1) / ALBUMMENU_PAGECAPACITY

IF ALBUMMENU_NOWPAGE > 1
	PRINTBUTTON "◀◁◁ 　翻至上一页　", "-2"
ELSE
	SETCOLOR C_L_GRAY
	PRINTPLAINFORM ◀◁◁ 　翻至上一页　 
	RESETCOLOR
ENDIF

PRINTFORMD ▉▉第{ALBUMMENU_NOWPAGE}/{ALBUMMENU_PAGECOUNT}页▉▉

IF ALBUMMENU_NOWPAGE < ALBUMMENU_PAGECOUNT
	PRINTBUTTON "　翻至下一页　 ▷▷▶", "-1"
ELSE
	SETCOLOR C_L_GRAY
	PRINTPLAINFORM 　翻至下一页　 ▷▷▶ 
	RESETCOLOR
ENDIF
PRINTL 
PRINTL 
PRINTBUTTON "[退出相册]▉▉▉", "9999"
PRINTBUTTON "▉▉▉[跳转页码]▉▉▉", "5555"
PRINTBUTTON "▉▉▉[创建相册]", "1111"

VARSET INPUT_STR
VARSET INPUT_INT
DO
	INPUTS
	INPUT_STR '= RESULTS
	SIF !STRLENS(INPUT_STR)
		GOTO RETRY
	IF ISNUMERIC(INPUT_STR)
		INPUT_INT = TOINT(INPUT_STR)
		SELECTCASE INPUT_INT
			;翻至上一页
			CASE -2
				;在第一页时无效
				SIF ALBUMMENU_NOWPAGE <= 1
					GOTO RETRY
				ALBUMMENU_NOWPAGE --
				RESTART
			;翻至下一页
			CASE -1
				;在最后一页时无效
				SIF ALBUMMENU_NOWPAGE >= ALBUMMENU_PAGECOUNT
					GOTO RETRY
				ALBUMMENU_NOWPAGE ++
				RESTART
			;退出
			CASE 9999
				$QUIT
				;归位至第一页
				ALBUMMENU_NOWPAGE = 1
				;恢复默认排序
				LIST_TYPE = 0
				SORT_TYPE = 0
				SIF RESULT == 999
					LOCAL = 998
				CALL SORT_PHOTOALBUM(LIST_TYPE, SORT_TYPE)
				SIF LOCAL == 998
					RETURN 998
				RETURN 999
			;跳转页码
			CASE 5555
				PRINTFORMD 请输入要跳转的页码：
				INPUT
				IF !INRANGE(RESULT, 1, ALBUMMENU_PAGECOUNT)
					PRINTFORMDL 页码不存在
				ELSE
					ALBUMMENU_NOWPAGE = RESULT
				ENDIF
				RESTART
			;创建相册
			CASE 1111
				IF ALBUM_NUM >= ALBUM_MAXNUM
					PRINTFORMDW 相册数已达到上限
					RESTART
				ENDIF
				CALL CREATE_PHOTOALBUM(PA_ID)
				;由于做了包装，故要传入一个参数以保持PA_ID自增
				PA_ID = RESULT
				RESTART
			;按照ID排序
			CASE 1000
				SIF LIST_TYPE == 0
					SORT_TYPE = !SORT_TYPE
				LIST_TYPE = 0
				CALL SORT_PHOTOALBUM(LIST_TYPE, SORT_TYPE)
				RESTART
			;按照相片数量排序
			CASE 1001
				SIF LIST_TYPE == 1
					SORT_TYPE = !SORT_TYPE
				LIST_TYPE = 1
				CALL SORT_PHOTOALBUM(LIST_TYPE, SORT_TYPE)
				RESTART
			CASEELSE
				;打开相册
				SIF INRANGE(GET_ALBUM_INDEX(INPUT_INT), (ALBUMMENU_NOWPAGE - 1) * ALBUMMENU_PAGECAPACITY, ALBUMMENU_NOWPAGE * ALBUMMENU_PAGECAPACITY)
					CALL SHOW_PHOTOALBUM(INPUT_INT)
				SIF RESULT == 999
					GOTO QUIT
				RESTART
				$RETRY
				REUSELASTLINE 无效输入
				CONTINUE
		ENDSELECT
	ELSE

	ENDIF
LOOP 1

;相册排序函数（默认为按照ID升序排列）
;会改变ALBUMINFO中各元素的位置，但是不会改变元素自身的A_ID，因此查询相册时请务必使用A_ID
;LIST_TYPE：0-按照ID，1-按照相片数量
;SORT_TYPE：0-升序，1-降序
@SORT_PHOTOALBUM(LIST_TYPE = 0, SORT_TYPE = 0)
#DIM LIST_TYPE
#DIM SORT_TYPE
#DIM TEMP_LIST, 500
#DIMS array_name = "ALBUMINFO"
FOR LOCAL, 0, ALBUM_NUM
	SIF LIST_TYPE == 0
		TEMP_LIST:LOCAL = TOINT(DIC_GET(ALBUMINFO:LOCAL, "A_ID"))
	SIF LIST_TYPE == 1
		TEMP_LIST:LOCAL = TOINT(DIC_GET(ALBUMINFO:LOCAL, "PHOTONUM"))
NEXT
ARRAYMSORTEX TEMP_LIST, array_name, !SORT_TYPE, ALBUM_NUM

;创建相册
@CREATE_PHOTOALBUM(PA_ID)
#DIM PA_ID
PRINTFORMDL 确定新建相册吗？
CALL ASK_YN("是", "否")
IF !RESULT
	$INPUT_ALBUMNAME
	PRINTFORMD 请输入新建相册名（空格会被去除）：
	INPUTS
	RESULTS = %REPLACE(RESULTS, " ", "")%
	IF !STRLENS(RESULTS)
		PRINTFORMDL 相册名不能为空
		GOTO INPUT_ALBUMNAME
	ENDIF
	FOR LOCAL, 0, ALBUM_NUM
		IF RESULTS == DIC_GET(ALBUMINFO:LOCAL, "NAME")
			PRINTFORMDL 不能使用已经存在的相册名
			GOTO INPUT_ALBUMNAME
		ENDIF
	NEXT
	PA_ID ++
	ALBUM_NUM ++
	ALBUMINFO:(ALBUM_NUM - 1) = %DIC_SET(ALBUMINFO:(ALBUM_NUM - 1), "A_ID", TOSTR(PA_ID))%
	ALBUMINFO:(ALBUM_NUM - 1) = %DIC_SET(ALBUMINFO:(ALBUM_NUM - 1), "NAME", RESULTS)%
	ALBUMINFO:(ALBUM_NUM - 1) = %DIC_SET(ALBUMINFO:(ALBUM_NUM - 1), "PHOTONUM", "0")%
	ALBUMINFO:(ALBUM_NUM - 1) = %DIC_SET(ALBUMINFO:(ALBUM_NUM - 1), "NOTE", "（空）")%
	PRINTFORMDL 成功创建相册 相册ID：{PA_ID}
	PRINTW 
ELSE
	PRINTFORMDL 取消操作
ENDIF
RETURN PA_ID

;显示相册信息
@SHOW_PHOTOALBUM(A_ID)
#DIM A_ID
#DIM A_INDEX
A_INDEX = GET_ALBUM_INDEX(A_ID)
$INPUT_SHOW_ALBUMINFO
PRINTFORMDL 相册名：<%GET_ALBUM_NAME(A_ID)%>  相册ID：{A_ID}  相片数量：{GET_ALBUM_PHOTONUM(A_ID)}
PRINTFORMDL 相册备注：
HTML_PRINT @"%GET_ALBUM_NOTE(A_ID)%"
PRINTL 
PRINTFORMDL [0] -		查看相片
PRINTFORMDL [1] -		修改相册名
PRINTFORMDL [2] -		修改相册备注
IF A_ID == 0
	SETCOLOR C_L_GRAY
	PRINTPLAINFORM [3] -		删除相册
	PRINTL 
	RESETCOLOR
ELSE
	PRINTFORMDL [3] -		删除相册
ENDIF
PRINTFORMDL [999] -		退出
$INPUT_SHOW_ALBUM
INPUT
SELECTCASE RESULT
	CASE 0
		CALL SHOW_PHOTO_MENU(A_ID)
		SIF RESULT == 998
			RETURN 999
		GOTO INPUT_SHOW_ALBUMINFO
	CASE 1
		IF A_ID == 0
			PRINTFORMDL <默认相册>不可更改相册名！
			GOTO INPUT_SHOW_ALBUM
		ENDIF
		CALL SET_PHOTOALBUM_NAME(A_ID)
		GOTO INPUT_SHOW_ALBUMINFO
	CASE 2
		CALL SET_PHOTOALBUM_NOTE(A_ID)
		GOTO INPUT_SHOW_ALBUMINFO
	CASE 3
		IF A_ID == 0
			PRINTFORMDL <默认相册>不可被删除！
			GOTO INPUT_SHOW_ALBUM
		ENDIF
		CALL DELETE_PHOTOALBUM(A_ID)
		SIF RESULT > 0
			RETURN 0
		GOTO INPUT_SHOW_ALBUMINFO
	CASE 999
		RETURN 0
	CASEELSE
		PRINTFORMDL 无效输入
		GOTO INPUT_SHOW_ALBUM
ENDSELECT

;设置相册名
@SET_PHOTOALBUM_NAME(A_ID)
#DIM A_ID
#DIM A_INDEX
A_INDEX = GET_ALBUM_PHOTONUM(A_ID)
PRINTFORMDL 确定修改相册名吗？
CALL ASK_YN("是", "否")
IF !RESULT
	$INPUT_SETALBUMNAME
	PRINTFORMD 请输入修改后的相册名（空格会被去除）：
	INPUTS
	RESULTS = %REPLACE(RESULTS, " ", "")%
	IF !STRLENS(RESULTS)
		PRINTFORMDL 相册名不能为空
		GOTO INPUT_SETALBUMNAME
	ENDIF
	FOR LOCAL, 0, ALBUM_NUM
		IF RESULTS == DIC_GET(ALBUMINFO:LOCAL, "NAME") && LOCAL != A_INDEX
			PRINTFORMDL 不能使用其他已经存在的相册名
			GOTO INPUT_SETALBUMNAME
		ENDIF
	NEXT
	IF RESULTS == DIC_GET(ALBUMINFO:A_INDEX, "NAME")
		PRINTFORMDL 维持原名不变
	ELSE
		ALBUMINFO:A_INDEX = %DIC_SET(ALBUMINFO:A_INDEX, "NAME", RESULTS)%
		PRINTFORMDL 成功修改相册名
	ENDIF
	PRINTW 
ELSE
	PRINTFORMDL 取消操作
ENDIF

;从相册ID返回索引
@GET_ALBUM_INDEX(A_ID)
#FUNCTION
#DIM A_ID
FOR LOCAL, 0, ALBUM_NUM
	SIF A_ID == TOINT(DIC_GET(ALBUMINFO:LOCAL, "A_ID"))
		RETURNF LOCAL
NEXT
RETURNF -1

;从相册ID返回相册名
@GET_ALBUM_NAME(A_ID)
#FUNCTIONS
#DIM A_ID
#DIM A_INDEX
A_INDEX = GET_ALBUM_INDEX(A_ID)
SIF A_INDEX == -1
	RETURNF ""
RETURNF DIC_GET(ALBUMINFO:A_INDEX, "NAME")

;获取相册内的相片数量
@GET_ALBUM_PHOTONUM(A_ID)
#FUNCTION
#DIM A_ID
#DIM A_INDEX
A_INDEX = GET_ALBUM_INDEX(A_ID)
SIF A_INDEX == -1
	RETURNF -1
RETURNF TOINT(DIC_GET(ALBUMINFO:A_INDEX, "PHOTONUM"))

;从相册ID返回备注
@GET_ALBUM_NOTE(A_ID)
#FUNCTIONS
#DIM A_ID
#DIM A_INDEX
A_INDEX = GET_ALBUM_INDEX(A_ID)
SIF A_INDEX == -1
	RETURNF ""
RETURNF DIC_GET(ALBUMINFO:A_INDEX, "NOTE")

;设置相册备注（这一块基本就是挪用了日记那里的代码）
@SET_PHOTOALBUM_NOTE(A_ID)
#DIM A_ID
#DIM A_INDEX
#DIMS note_contentline, 40
#DIM note_linecount
VARSET note_contentline

A_INDEX = GET_ALBUM_INDEX(A_ID)
PRINTFORMDL 确定修改相册备注吗？
CALL ASK_YN("是", "否")
IF !RESULT
	note_linecount = 0
	$INPUT_LOOP_ALBUMNOTE
	PRINTL
	PRINTFORMDL 请输入备注，内容会被显示在下方
	PRINTFORMDL
	FOR LOCAL, 0, note_linecount
		PRINTFORMDL %note_contentline:LOCAL%
	NEXT
	RESETCOLOR
	PRINTL
	PRINTBUTTON "[98] - 删除上一行", "98"
	PRINT 　　
	PRINTBUTTON "[99] - 完成备注", "99"
	PRINTL
	INPUTS
	IF RESULTS == "98"
		IF note_linecount > 0
			note_contentline:note_linecount = 
			note_linecount -= 1
			CLEARLINE 1
		ENDIF
		CLEARLINE (note_linecount + 5)
		GOTO INPUT_LOOP_ALBUMNOTE
	ELSEIF RESULTS == "99"
		LOCALS = 
		FOR LOCAL, 0, note_linecount - 1
			LOCALS += @"%note_contentline:LOCAL%<br>"
		NEXT
		SIF note_linecount > 0
			LOCALS += @"%note_contentline:(note_linecount - 1)%"
		RESULTS = %LOCALS%
	ELSEIF note_linecount == 39
		SETCOLOR C_YELLOW
		PRINTFORMW 行数已达到上限
		RESETCOLOR
		SIF RESULTS != ""
			CLEARLINE 1
		CLEARLINE (note_linecount + 5)
		GOTO INPUT_LOOP_ALBUMNOTE
	ELSE
		note_contentline:note_linecount = %RESULTS%
		SIF RESULTS != ""
			CLEARLINE 1
		CLEARLINE (note_linecount + 4)
		note_linecount += 1
		GOTO INPUT_LOOP_ALBUMNOTE
	ENDIF
	ALBUMINFO:A_INDEX = %DIC_SET(ALBUMINFO:A_INDEX, "NOTE", RESULTS)%
	PRINTFORMDL 修改成功
ELSE
	PRINTFORMDL 取消操作
ENDIF

;删除相册（默认相册不可删除）
@DELETE_PHOTOALBUM(A_ID)
#DIM A_ID
#DIM A_INDEX
#DIM A_END
SIF A_ID == 0
	RETURN -1
A_INDEX = GET_ALBUM_INDEX(A_ID)
PRINTFORMDL 请选择删除相册的方式：
PRINTFORMDL [300] -		仅移除相册（相册内所有相片会被转移至默认相册）
PRINTFORMDL [301] -		完整删除相册（相册内所有相片会被删除且无法寻回）
PRINTFORMDL [302] -		退出
$INPUT_DELETE_ALBUM
INPUT
SELECTCASE RESULT
	CASE 300
		FOR LOCAL, 0, PHOTO_NUM
			IF A_ID == TOINT(DIC_GET(PHOTOINFO:LOCAL, "A_ID"))
				PHOTOINFO:LOCAL = %DIC_SET(PHOTOINFO:LOCAL, "A_ID", "0")%
			ENDIF
		NEXT
		A_END = GET_ALBUM_INDEX(0)
		LOCAL = GET_ALBUM_PHOTONUM(0)
		ALBUMINFO:A_END = %DIC_SET(ALBUMINFO:A_END, "PHOTONUM", TOSTR(LOCAL + 1))%
		FOR LOCAL, A_INDEX + 1, ALBUM_NUM + 1
			ALBUMINFO:(LOCAL - 1) = %ALBUMINFO:LOCAL%
		NEXT
		ALBUM_NUM --
		RETURN 1
	CASE 301
		FOR LOCAL, 0, PHOTO_NUM
			IF A_ID == TOINT(DIC_GET(PHOTOINFO:LOCAL, "A_ID"))
				CALL DISCARD_PHOTO(TOINT(DIC_GET(PHOTOINFO:LOCAL, "P_ID")))
			ENDIF
		NEXT
		FOR LOCAL, A_INDEX + 1, ALBUM_NUM + 1
			ALBUMINFO:(LOCAL - 1) = %ALBUMINFO:LOCAL%
		NEXT
		ALBUM_NUM --
		RETURN 2
	CASE 302
		RETURN 0
	CASEELSE
		PRINTFORMDL 无效输入
		GOTO INPUT_DELETE_ALBUM
ENDSELECT

;显示相册内的相片
@SHOW_PHOTO_MENU(A_ID)
#DIM A_ID
#DIM ALBUM_PHOTONUM
#DIMS A_PHOTOINFO, 500
#DIM PHOTO_NO
#DIM LIST_TYPE = 0
#DIM SORT_TYPE = 0
#DIM PHOTOMENU_NOWPAGE = 1
#DIM PHOTOMENU_PAGECOUNT
#DIM PHOTOMENU_PAGECAPACITY = 20
#DIMS INPUT_STR
#DIM INPUT_INT
VARSET LOCALS
VARSET LOCAL
VARSET A_PHOTOINFO
ALBUM_PHOTONUM = GET_ALBUM_PHOTONUM(A_ID)
IF ALBUM_PHOTONUM == 0
	PRINTFORMDW 当前相册为空
	RETURN 0
ENDIF
DRAWLINE
PRINTFORMDL 相册名：<%GET_ALBUM_NAME(A_ID)%>  相册ID：{A_ID}  相片数量：{ALBUM_PHOTONUM}
;排序按钮
PRINTS " " * 7
IF LIST_TYPE == 0
	PRINTBUTTON @"\@ SORT_TYPE == 0 ? ▲ # ▼\@ID", "1000"
ELSE
	PRINTS " "
	PRINTBUTTON "ID", "1000"
ENDIF
PRINTS " " * 16
IF LIST_TYPE == 1
	PRINTBUTTON @"\@ SORT_TYPE == 0 ? ▲ # ▼\@日期", "1001"
ELSE
	PRINTS " "
	PRINTBUTTON "日期", "1001"
ENDIF
PRINTS " " * 3
IF LIST_TYPE == 2
	PRINTBUTTON @"\@ SORT_TYPE == 0 ? ▲ # ▼\@在场角色数", "1002"
ELSE
	PRINTS " "
	PRINTBUTTON "在场角色数", "1002"
ENDIF
PRINTS " " * 2
IF LIST_TYPE == 3
	PRINTBUTTON @"\@ SORT_TYPE == 0 ? ▲ # ▼\@品质", "1003"
ELSE
	PRINTS " "
	PRINTBUTTON "品质", "1003"
ENDIF
PRINTL 
DRAWLINE
PHOTO_NO = (PHOTOMENU_NOWPAGE - 1) * PHOTOMENU_PAGECAPACITY

FOR LOCAL, 0, PHOTO_NUM
	IF A_ID != TOINT(DIC_GET(PHOTOINFO:LOCAL, "A_ID"))
		CONTINUE
	ELSE
		LOCAL:1 += 1
		IF LOCAL:1 == PHOTO_NO + 1 && PHOTO_NO < PHOTOMENU_NOWPAGE * PHOTOMENU_PAGECAPACITY
			LOCAL:2 = TOINT(DIC_GET(PHOTOINFO:LOCAL, "P_ID"))
			A_PHOTOINFO:PHOTO_NO = %PHOTOINFO:LOCAL%
			PRINTBUTTON @"[{PHOTO_NO, 2}] ({LOCAL:2, 4}) %PRINT_DATE_F(TOINT(DIC_GET(A_PHOTOINFO:PHOTO_NO, "DATE"))), 20% {STRINGSET_LEN(DIC_GET(A_PHOTOINFO:PHOTO_NO, "CHARA")), 12} %DIC_GET(A_PHOTOINFO:PHOTO_NO, "QUALITY"), 6%", @"{LOCAL:2}"
			PHOTO_NO += 1
			PRINTL
		ENDIF
		SIF LOCAL:1 == ALBUM_PHOTONUM
			BREAK
	ENDIF
	SIF PHOTO_NO == PHOTOMENU_NOWPAGE * PHOTOMENU_PAGECAPACITY
		BREAK
NEXT
FOR LOCAL, LOCAL:1, PHOTOMENU_NOWPAGE * PHOTOMENU_PAGECAPACITY
	PRINTL 
NEXT
VARSET LOCAL

DRAWLINE
;计算页数
PHOTOMENU_PAGECOUNT = 1 + (ALBUM_PHOTONUM - 1) / PHOTOMENU_PAGECAPACITY

IF PHOTOMENU_NOWPAGE > 1
	PRINTBUTTON "◀◁◁ 　翻至上一页　", "-2"
ELSE
	SETCOLOR C_L_GRAY
	PRINTPLAINFORM ◀◁◁ 　翻至上一页　 
	RESETCOLOR
ENDIF

PRINTFORMD ▉▉第{PHOTOMENU_NOWPAGE}/{PHOTOMENU_PAGECOUNT}页▉▉

IF PHOTOMENU_NOWPAGE < PHOTOMENU_PAGECOUNT
	PRINTBUTTON "　翻至下一页　 ▷▷▶", "-1"
ELSE
	SETCOLOR C_L_GRAY
	PRINTPLAINFORM 　翻至下一页　 ▷▷▶ 
	RESETCOLOR
ENDIF
PRINTL 
PRINTL 
PRINTBUTTON "▉▉▉▉▉▉[退出相册]▉▉▉", "9999"
PRINTBUTTON "▉▉▉[跳转页码]▉▉▉▉▉▉", "5555"

VARSET INPUT_STR
VARSET INPUT_INT
DO
	INPUTS
	INPUT_STR '= RESULTS
	SIF !STRLENS(INPUT_STR)
		GOTO RETRY
	IF ISNUMERIC(INPUT_STR)
		INPUT_INT = TOINT(INPUT_STR)
		SELECTCASE INPUT_INT
			;翻至上一页
			CASE -2
				;在第一页时无效
				SIF PHOTOMENU_NOWPAGE <= 1
					GOTO RETRY
				PHOTOMENU_NOWPAGE --
				RESTART
			;翻至下一页
			CASE -1
				;在最后一页时无效
				SIF PHOTOMENU_NOWPAGE >= PHOTOMENU_PAGECOUNT
					GOTO RETRY
				PHOTOMENU_NOWPAGE ++
				RESTART
			;退出
			CASE 9999
				$QUIT
				;归位至第一页
				PHOTOMENU_NOWPAGE = 1
				;恢复默认排序
				LIST_TYPE = 0
				SORT_TYPE = 0
				SIF RESULT == 999
					LOCAL = 998
				CALL SORT_PHOTO(A_ID, LIST_TYPE, SORT_TYPE)
				SIF LOCAL == 998
					RETURN 998
				RETURN 999
			;跳转页码
			CASE 5555
				PRINTFORMD 请输入要跳转的页码：
				INPUT
				IF !INRANGE(RESULT, 1, PHOTOMENU_PAGECOUNT)
					PRINTFORMDL 页码不存在
				ELSE
					PHOTOMENU_NOWPAGE = RESULT
				ENDIF
				RESTART
			;按照ID排序
			CASE 1000
				SIF LIST_TYPE == 0
					SORT_TYPE = !SORT_TYPE
				LIST_TYPE = 0
				CALL SORT_PHOTO(A_ID, LIST_TYPE, SORT_TYPE)
				RESTART
			;按照时间排序
			CASE 1001
				SIF LIST_TYPE == 1
					SORT_TYPE = !SORT_TYPE
				LIST_TYPE = 1
				CALL SORT_PHOTO(A_ID, LIST_TYPE, SORT_TYPE)
				RESTART
			;按照在场角色数量排序
			CASE 1002
				SIF LIST_TYPE == 2
					SORT_TYPE = !SORT_TYPE
				LIST_TYPE = 2
				CALL SORT_PHOTO(A_ID, LIST_TYPE, SORT_TYPE)
				RESTART
			;按照品质
			CASE 1003
				SIF LIST_TYPE == 3
					SORT_TYPE = !SORT_TYPE
				LIST_TYPE = 3
				CALL SORT_PHOTO(A_ID, LIST_TYPE, SORT_TYPE)
				RESTART
			CASEELSE
				;查看相片
				LOCAL:2 = -1
				FOR LOCAL:1, (PHOTOMENU_NOWPAGE - 1) * PHOTOMENU_PAGECAPACITY, PHOTO_NO
					SIF INPUT_INT == TOINT(DIC_GET(A_PHOTOINFO:(LOCAL:1), "P_ID"))
						LOCAL:2 = LOCAL:1
				NEXT
				SIF INRANGE(LOCAL:2, (PHOTOMENU_NOWPAGE - 1) * PHOTOMENU_PAGECAPACITY, PHOTOMENU_NOWPAGE * PHOTOMENU_PAGECAPACITY)
					CALL SHOW_PHOTO(INPUT_INT)
				SIF RESULT == 999
					GOTO QUIT
				RESTART
				$RETRY
				REUSELASTLINE 无效输入
				CONTINUE
		ENDSELECT
	ELSE
		GOTO RETRY
	ENDIF
LOOP 1

;相片排序函数（默认为按照ID升序排列）
;会改变PHOTOINFO中各元素的位置，但是不会改变元素自身的P_ID，因此查询相册时请务必使用P_ID
;LIST_TYPE：0-按照ID，1-按照时间戳，2-按照在场角色数量，3-按照品质
;SORT_TYPE：0-升序，1-降序
;排序方式是：
;1.先依次取出所有相册ID为A_ID（-1表示直接对所有相片排序，不论是哪个相册里的）的PHOTOINFO
;2.用OLDINDEX_LIST保存它们的旧索引
;3.按照条件对TEMP_LIST排序，同时PHOTOINFO_LIST也会进行同步排序
;4.最后按照OLDINDEX_LIST的顺序将PHOTOINFO_LIST的元素依次替换回去
@SORT_PHOTO(A_ID = -1, LIST_TYPE = 0, SORT_TYPE = 0)
#DIM A_ID
#DIM LIST_TYPE
#DIM SORT_TYPE
#DIM OLDINDEX_LIST, 500
#DIMS PHOTOINFO_LIST, 500
#DIM TEMP_LIST, 500
#DIMS arrays_name1 = "PHOTOINFO_LIST"
#DIMS arrays_name2 = "PHOTOINFO"
VARSET LOCAL
IF A_ID != -1
	FOR LOCAL, 0, PHOTO_NUM
		IF A_ID == TOINT(DIC_GET(PHOTOINFO:LOCAL, "A_ID"))
			OLDINDEX_LIST:(LOCAL:1) = LOCAL
			PHOTOINFO_LIST:(LOCAL:1) = %PHOTOINFO:LOCAL%
			LOCAL:1 ++
		ENDIF
	NEXT
	FOR LOCAL, 0, GET_ALBUM_PHOTONUM(A_ID)
		IF LIST_TYPE == 0
			TEMP_LIST:LOCAL = TOINT(DIC_GET(PHOTOINFO_LIST:LOCAL, "P_ID"))
		ELSEIF LIST_TYPE == 1
			TEMP_LIST:LOCAL = TOINT(DIC_GET(PHOTOINFO_LIST:LOCAL, "DATE")) + TOINT(DIC_GET(PHOTOINFO_LIST:LOCAL, "TIME"))
		ELSEIF LIST_TYPE == 2
			TEMP_LIST:LOCAL = STRINGSET_LEN(DIC_GET(PHOTOINFO_LIST:LOCAL, "CHARA"))
		ELSEIF LIST_TYPE == 3
			TEMP_LIST:LOCAL = TOINT(DIC_GET(PHOTOINFO_LIST:LOCAL, "QUALITY"))
		ENDIF
	NEXT
	ARRAYMSORTEX TEMP_LIST, arrays_name1, !SORT_TYPE, GET_ALBUM_PHOTONUM(A_ID)
	FOR LOCAL, 0, GET_ALBUM_PHOTONUM(A_ID)
		PHOTOINFO:(OLDINDEX_LIST:LOCAL) = %PHOTOINFO_LIST:LOCAL%
	NEXT
ELSE
	FOR LOCAL, 0, PHOTO_NUM
		IF LIST_TYPE == 0
			TEMP_LIST:LOCAL = TOINT(DIC_GET(PHOTOINFO:LOCAL, "P_ID"))
		ELSEIF LIST_TYPE == 1
			TEMP_LIST:LOCAL = TOINT(DIC_GET(PHOTOINFO:LOCAL, "DATE")) + TOINT(DIC_GET(PHOTOINFO:LOCAL, "TIME"))
		ELSEIF LIST_TYPE == 2
			TEMP_LIST:LOCAL = STRINGSET_LEN(DIC_GET(PHOTOINFO:LOCAL, "CHARA"))
		ELSEIF LIST_TYPE == 3
			TEMP_LIST:LOCAL = TOINT(DIC_GET(PHOTOINFO:LOCAL, "QUALITY"))
		ENDIF
	NEXT
	ARRAYMSORTEX TEMP_LIST, arrays_name2, !SORT_TYPE, PHOTO_NUM
ENDIF


;显示相片信息
@SHOW_PHOTO(P_ID)
#DIM P_ID
$INPUT_SHOW_PHOTOINFO
PRINTFORMDL 相片ID：{P_ID}  拍摄地点：%GET_PHOTO_SPOT(P_ID)%  拍摄日期：%PRINT_DATE_F(GET_PHOTO_DATE(P_ID))%  拍摄时间：%时刻表示(GET_PHOTO_TIME(P_ID))%
PRINTFORMDL 品质：
CALL REVIEW_PHOTO_QUALITY(P_ID)
PRINTL 
PRINTL 
CALL REVIEW_PHOTO_CHARAFACE(P_ID)
PRINTL 
PRINTFORMDL %GET_PHOTO_DESC(P_ID)%
PRINTL 
;这里还需要加一个打印立绘的函数
;CALL 
PRINTL 
PRINTFORMDL 备注：
HTML_PRINT @"%GET_PHOTO_NOTE(P_ID)%"
PRINTL 
PRINTFORMDL [0] -		\@ TARGET == 0 || FLAG:70 || !SHIRAHU(TARGET) ? 欣赏一会 # 和%CALLNAME:TARGET%一起欣赏\@
PRINTFORMDL [1] -		修改相片备注
PRINTFORMDL [2] -		移动相片至其他相册
PRINTFORMDL [3] -		删除相片
PRINTFORMDL [999] - 	退出
$INPUT_SHOW_PHOTO
INPUT
SELECTCASE RESULT
	CASE 0
		CHECK_PHOTO:0 = %GET_PHOTO_SPOT(P_ID)%
		CHECK_PHOTO:1 = %PRINT_DATE_F(GET_PHOTO_DATE(P_ID))%
		CHECK_PHOTO:2 = %时刻表示(GET_PHOTO_TIME(P_ID))%
		CHECK_PHOTO:3 = %GET_PHOTO_QUALITY(P_ID)%
		CHECK_PHOTO:4 = %GET_PHOTO_CHARA(P_ID)%
		CHECK_PHOTO:5 = %GET_PHOTO_DESC(P_ID)%
		CHECK_PHOTO:6 = %GET_PHOTO_NOTE(P_ID)%
		RETURN 999
	CASE 1
		CALL SET_PHOTO_NOTE(P_ID)
		GOTO INPUT_SHOW_PHOTOINFO
	CASE 2
		CALL MOVE_PHOTO(P_ID)
		SIF RESULT > 0
			RETURN 0
		GOTO INPUT_SHOW_PHOTOINFO
	CASE 3
		CALL DELETE_PHOTO(P_ID)
		SIF RESULT > 0
			RETURN 0
		GOTO INPUT_SHOW_PHOTOINFO
	CASE 999
		RETURN 0
	CASEELSE
		PRINTFORMDL 无效输入
		GOTO INPUT_SHOW_PHOTO
ENDSELECT

;设置相片备注（为什么要直接抄前面的代码啊喂）
@SET_PHOTO_NOTE(P_ID)
#DIM P_ID
#DIM P_INDEX
#DIMS note_contentline, 40
#DIM note_linecount
VARSET note_contentline

P_INDEX = GET_PHOTO_INDEX(P_ID)
PRINTFORMDL 确定修改相片备注吗？
CALL ASK_YN("是", "否")
IF !RESULT
	note_linecount = 0
	$INPUT_LOOP_PHOTONOTE
	PRINTL
	PRINTFORMDL 请输入备注，内容会被显示在下方
	PRINTFORMDL
	FOR LOCAL, 0, note_linecount
		PRINTFORMDL %note_contentline:LOCAL%
	NEXT
	RESETCOLOR
	PRINTL
	PRINTBUTTON "[98] - 删除上一行", "98"
	PRINT 　　
	PRINTBUTTON "[99] - 完成备注", "99"
	PRINTL
	INPUTS
	IF RESULTS == "98"
		IF note_linecount > 0
			note_contentline:note_linecount = 
			note_linecount -= 1
			CLEARLINE 1
		ENDIF
		CLEARLINE (note_linecount + 5)
		GOTO INPUT_LOOP_PHOTONOTE
	ELSEIF RESULTS == "99"
		LOCALS = 
		FOR LOCAL, 0, note_linecount - 1
			LOCALS += @"%note_contentline:LOCAL%<br>"
		NEXT
		SIF note_linecount > 0
			LOCALS += @"%note_contentline:(note_linecount - 1)%"
		RESULTS = %LOCALS%
	ELSEIF note_linecount == 39
		SETCOLOR C_YELLOW
		PRINTFORMW 行数已达到上限
		RESETCOLOR
		SIF RESULTS != ""
			CLEARLINE 1
		CLEARLINE (note_linecount + 5)
		GOTO INPUT_LOOP_PHOTONOTE
	ELSE
		note_contentline:note_linecount = %RESULTS%
		SIF RESULTS != ""
			CLEARLINE 1
		CLEARLINE (note_linecount + 4)
		note_linecount += 1
		GOTO INPUT_LOOP_PHOTONOTE
	ENDIF
	PHOTOINFO:P_INDEX = %DIC_SET(PHOTOINFO:P_INDEX, "NOTE", RESULTS)%
	PRINTFORMDL 修改成功
ELSE
	PRINTFORMDL 取消操作
ENDIF

;移动相片
@MOVE_PHOTO(P_ID)
#DIM P_ID
#DIM P_INDEX
P_INDEX = GET_PHOTO_INDEX(P_ID)
PRINTFORMDL 请选择要移动的相册：
DRAWLINE
FOR LOCAL, 0, ALBUM_NUM
	PRINTFORMDL [{LOCAL, 2}] - %DIC_GET(ALBUMINFO:LOCAL, "NAME")%
NEXT
DRAWLINE
$INPUT_CHOOSEALBUM
INPUT
IF INRANGE(RESULT, 0, ALBUM_NUM - 1)
	CALL TRANSFER_PHOTO(P_ID, RESULT)
	SIF RESULT == 1
		RETURN 1
ELSE
	PRINTFORMDL 不存在该索引下的相册
	GOTO INPUT_CHOOSEALBUM
ENDIF

;转移相册内的相片
@TRANSFER_PHOTO(P_ID, A_ENDINDEX)
#DIM P_ID
#DIM P_INDEX
#DIMS A_ID_STR
#DIM A_STARTINDEX
#DIM A_ENDINDEX
P_INDEX = GET_PHOTO_INDEX(P_ID)
A_STARTINDEX = GET_ALBUM_INDEX(TOINT(DIC_GET(PHOTOINFO:P_INDEX, "A_ID")))
A_ID_STR = %DIC_GET(ALBUMINFO:A_ENDINDEX, "A_ID")%
IF A_STARTINDEX == A_ENDINDEX
	PRINTFORMDL 保持原位
	RETURN 0
ELSE
	PHOTOINFO:P_INDEX = %DIC_SET(PHOTOINFO:P_INDEX, "A_ID", A_ID_STR)%
	LOCAL = TOINT(DIC_GET(ALBUMINFO:A_STARTINDEX, "PHOTONUM"))
	LOCAL:1 = TOINT(DIC_GET(ALBUMINFO:A_ENDINDEX, "PHOTONUM"))
	ALBUMINFO:A_STARTINDEX = %DIC_SET(ALBUMINFO:A_STARTINDEX, "PHOTONUM", TOSTR(LOCAL - 1))%
	ALBUMINFO:A_ENDINDEX = %DIC_SET(ALBUMINFO:A_ENDINDEX, "PHOTONUM", TOSTR(LOCAL:1 + 1))%
	PRINTFORMDL 转移成功
	RETURN 1
ENDIF

;删除相片
@DELETE_PHOTO(P_ID)
#DIM P_ID
#DIM P_INDEX
#DIM A_ID
#DIM A_INDEX
P_INDEX = GET_PHOTO_INDEX(P_ID)
PRINTFORMDL 确定删除该相片？（删除后无法找回！！！）
CALL ASK_YN("是", "否")
IF !RESULT
	A_ID = TOINT(DIC_GET(PHOTOINFO:P_INDEX, "A_ID"))
	A_INDEX = GET_ALBUM_INDEX(A_ID)
	LOCAL = TOINT(DIC_GET(ALBUMINFO:A_INDEX, "PHOTONUM"))
	ALBUMINFO:A_INDEX = %DIC_SET(ALBUMINFO:A_INDEX, "PHOTONUM", TOSTR(LOCAL - 1))%
	CALL DISCARD_PHOTO(P_ID)
	RETURN 1
ELSE
	PRINTFORMDL 取消操作
	RETURN 0
ENDIF

;显示相片品质（基本就是拿查看礼物评价函数改的）
@REVIEW_PHOTO_QUALITY(P_ID)
#DIM P_ID
#DIM P_INDEX
#DIM STAR
P_INDEX = GET_PHOTO_INDEX(P_ID)
STAR = TOINT(DIC_GET(PHOTOINFO:P_INDEX, "QUALITY"))
FOR LOCAL, 1, 7
	IF LOCAL <= STAR
		IF STAR == 6
			SETCOLOR C_H_PINK		
		ELSEIF STAR >= 4
			SETCOLOR C_ORANGE
		ELSEIF STAR <= 1
			SETCOLOR C_AQUA
		ELSE
			SETCOLOR C_YELLOW
		ENDIF
		PRINT ★
	ELSE
		SETCOLOR C_GRAY
		PRINT ☆
	ENDIF
NEXT
RESETCOLOR

;显示相片拍摄的人物
@REVIEW_PHOTO_CHARAFACE(P_ID)
#DIM P_ID
#DIM P_INDEX
#DIMS CHARASET
#DIMS CHARAELEM_LIST, 200
#DIMS INFO_LIST, 6
#DIM TEMP_IMAGESIZE, 1
#DIM MAX_IMAGESHOWCOUNT, 1
P_INDEX = GET_PHOTO_INDEX(P_ID)
CHARASET = %DIC_GET(PHOTOINFO:P_INDEX, "CHARA")%
SIF CHARASET == "0/" || CHARASET == ""
	RETURN 0
SPLIT CHARASET, "/", CHARAELEM_LIST
TEMP_IMAGESIZE = 默认角色画像横幅
默认角色画像横幅 = 180
MAX_IMAGESHOWCOUNT = GETCONFIG("ウィンドウ幅") / 默认角色画像横幅
LOCAL:1 = 0
FOR LOCAL, 0, STRINGSET_LEN(CHARASET)
	SPLIT CHARAELEM_LIST:LOCAL, "◇", INFO_LIST
	[IF_DEBUG]
		PRINTFORMDL %INFO_LIST:0%
	[ENDIF]
	LOCAL:1 += 1
	IF LOCAL:1 <= MAX_IMAGESHOWCOUNT
		FOR LOCAL:2, 0, STRCOUNT(CHARAELEM_LIST:LOCAL, "◇")
			CALL 画像セット(INFO_LIST:(LOCAL:2), 0, 0, 100, LOCAL:2)
		NEXT
	ELSE
		CALL 画像一斉表示(0, 0, 1, -1)
		PRINTL 
		FOR LOCAL:2, 0, STRCOUNT(CHARAELEM_LIST:LOCAL, "◇")
			CALL 画像セット(INFO_LIST:(LOCAL:2), 0, 0, 100, LOCAL:2)
		NEXT
		LOCAL:1 = 0
	ENDIF
NEXT
CALL 画像一斉表示(0, 0, 1, -1)
默认角色画像横幅 = TEMP_IMAGESIZE

;从相片ID返回索引
@GET_PHOTO_INDEX(P_ID)
#FUNCTION
#DIM P_ID
FOR LOCAL, 0, PHOTO_NUM
	SIF P_ID == TOINT(DIC_GET(PHOTOINFO:LOCAL, "P_ID"))
		RETURNF LOCAL
NEXT
RETURNF -1

;从相片ID返回拍摄时间
;返回的是一个数，需要转化为年月日请使用PRINT_DATE_F(ARG)函数
@GET_PHOTO_DATE(P_ID)
#FUNCTION
#DIM P_ID
#DIM P_INDEX
P_INDEX = GET_PHOTO_INDEX(P_ID)
SIF P_INDEX == -1
	RETURNF -1
RETURNF TOINT(DIC_GET(PHOTOINFO:P_INDEX, "DATE"))

;从相片ID返回拍摄时间
;返回的是一个数，需要转化为时分请使用时刻表示(ARG)函数
@GET_PHOTO_TIME(P_ID)
#FUNCTION
#DIM P_ID
#DIM P_INDEX
P_INDEX = GET_PHOTO_INDEX(P_ID)
SIF P_INDEX == -1
	RETURNF -1
RETURNF TOINT(DIC_GET(PHOTOINFO:P_INDEX, "TIME"))

;从相片ID返回地点
@GET_PHOTO_SPOT(P_ID)
#FUNCTIONS
#DIM P_ID
#DIM P_INDEX
P_INDEX = GET_PHOTO_INDEX(P_ID)
SIF P_INDEX == -1
	RETURNF ""
RETURNF DIC_GET(PHOTOINFO:P_INDEX, "SPOT")

;从相片ID返回品质
@GET_PHOTO_QUALITY(P_ID)
#FUNCTIONS
#DIM P_ID
#DIM P_INDEX
P_INDEX = GET_PHOTO_INDEX(P_ID)
SIF P_INDEX == -1
	RETURNF ""
RETURNF DIC_GET(PHOTOINFO:P_INDEX, "QUALITY")

;从相片ID返回角色信息
@GET_PHOTO_CHARA(P_ID)
#FUNCTIONS
#DIM P_ID
#DIM P_INDEX
P_INDEX = GET_PHOTO_INDEX(P_ID)
SIF P_INDEX == -1
	RETURNF ""
RETURNF DIC_GET(PHOTOINFO:P_INDEX, "CHARA")

;从相册ID返回基本描述
@GET_PHOTO_DESC(P_ID)
#FUNCTIONS
#DIM P_ID
#DIM P_INDEX
P_INDEX = GET_PHOTO_INDEX(P_ID)
SIF P_INDEX == -1
	RETURNF ""
RETURNF DIC_GET(PHOTOINFO:P_INDEX, "DESC")

;从相册ID返回备注
@GET_PHOTO_NOTE(P_ID)
#FUNCTIONS
#DIM P_ID
#DIM P_INDEX
P_INDEX = GET_PHOTO_INDEX(P_ID)
SIF P_INDEX == -1
	RETURNF ""
RETURNF DIC_GET(PHOTOINFO:P_INDEX, "NOTE")

;移除相片（从数组PHOTOINFO里移除数据）
@DISCARD_PHOTO(P_ID)
#DIM P_ID
#DIM P_INDEX
P_INDEX = GET_PHOTO_INDEX(P_ID)
FOR LOCAL, P_INDEX + 1, PHOTO_NUM + 1
	PHOTOINFO:(LOCAL - 1) = %PHOTOINFO:LOCAL%
NEXT
PHOTO_NUM --