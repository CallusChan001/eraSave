;fellatio
@ADD_SEX_ACTION_1_4(ARG, MULTIPLIER)
#DIM MULTIPLIER
;-------------------------------------------------
;COM別FLAGの設定
;-------------------------------------------------
;行為者
TCVAR:ARG:116 = ARG
;奉仕快楽
TCVAR:ARG:108 = 1

CSTR:PLAYER:10 += @"{ARG}/"
;TCVAR:2 射精箇所フラグ（-1=コンドーム 1=膣内 2=アナル 3=手淫 4=口淫 5=パイズリ 6=素股 7=足コキ 8=体表 9=アナル奉仕 10=道具 11=触手 12=Ａ責め 13=Ｖ責め）
TCVAR:PLAYER:2 = 4
;強度
TCVAR:PLAYER:4 = MULTIPLY(1000, MULTIPLIER)

CALL QoL_Info_Write(ARG, Qol_Dates_Bj) ;qol custom code

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
IF FLAG:70 == 1
	BASE:MASTER:TSP -= 50
	SOURCE:ARG:快Ｍ = MULTIPLY(50, MULTIPLIER)

	SOURCE:PLAYER:快Ｃ = MULTIPLY(110, MULTIPLIER)
	;性技補正
	SOURCE:PLAYER:快Ｃ += MULTIPLY(MIN(ABL:ARG:舌 * 25,100), MULTIPLIER)

	;調教者のTALENT:猫舌をみる
	IF TALENT:ARG:猫舌
		TIMES SOURCE:PLAYER:快Ｃ , 0.75
		;調教者が猫舌持ちの場合は、調教者に嗜虐快楽経験を追加
		EXP:ARG:嗜虐快楽経験 += 1
	ENDIF

ELSE
	DOWNBASE:ARG:体力 += 10
	DOWNBASE:ARG:気力 += 80
	SOURCE:PLAYER:快Ｃ = MULTIPLY(110, MULTIPLIER)
	SOURCE:ARG:快Ｍ = MULTIPLY(50, MULTIPLIER)
	SOURCE:ARG:情愛 = MULTIPLY(100, MULTIPLIER)
	SOURCE:ARG:性行動 = MULTIPLY(300, MULTIPLIER)
	SOURCE:ARG:達成 = MULTIPLY(150, MULTIPLIER)
	SOURCE:ARG:屈従 = MULTIPLY(700, MULTIPLIER)
	SOURCE:ARG:反感 = 400
	SOURCE:ARG:逸脱 = 100

	;上のほうで計算した汚れデータ
	SOURCE:ARG:不潔 = Y*10 + 60

	;-------------------------------------------------
	;特殊ソース処理
	;-------------------------------------------------
	;抑圧、抵抗持ちには、抑鬱ソース追加
	SIF TALENT:ARG:自己愛 < 0 || TALENT:ARG:抵抗
		SOURCE:ARG:鬱屈 += 400

	;性技補正
	IF ABL:舌 >= 4
		SOURCE:PLAYER:快Ｃ += MULTIPLY(100, MULTIPLIER)
	ELSE
		SOURCE:PLAYER:快Ｃ += MULTIPLY(ABL:ARG:舌 * 25, MULTIPLIER)
	ENDIF

	;調教者のTALENT:舌使いをみる
	SIF TALENT:ARG:舌使い
		TIMES SOURCE:PLAYER:快Ｃ , 1.50

	;調教者のTALENT:猫舌をみる
	SIF TALENT:ARG:猫舌
		TIMES SOURCE:PLAYER:快Ｃ , 0.75

ENDIF

;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
;射精に関係なく行われる処理はここから
;奴隷の指⇔調教者のＰの汚れが移動
STAIN:ARG:0 |= STAIN:PLAYER:2
STAIN:PLAYER:2 |= STAIN:ARG:0

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;フェラ経験
;とりあえずM経験追加
;SOURCEに快Ｍがあると自動で経験追加されるので、実質、経験＋２扱いは変ではないかと
;EXP:Ｍ経験 += 1

IF !FLAG:70 && !CFLAG:ARG:イタズラ
	EXP:ARG:口淫経験 += 3
	;奴隷が猫舌持ちの場合は、調教者に嗜虐快楽経験を追加
	SIF TALENT:TARGET:猫舌
		EXP:ARG:嗜虐快楽経験 += 1
	;愛情経験
	SIF TALENT:ARG:恋慕 && ASSIPLAY == 0
		EXP:ARG:愛情経験 += 1
ELSE
	SIF !CFLAG:ARG:イタズラ
		EXP:PLAYER:時姦経験 += 1
ENDIF
IF CFLAG:PLAYER:イタズラ == 2
	EXP:PLAYER:睡眠姦経験 += 1
	EXP:ARG:睡眠姦経験 += 1
ENDIF

;精液汚れ
SIF TCVAR:PLAYER:112 == 1 || TCVAR:PLAYER:112 == 2
	EXP:ARG:精飲経験 ++
TCVAR:ARG:ＦＫ喪失 = 2
Qol_Info_Count_Array:ARG:Qol_Count_GiveMSex += EXP_UP(GETNUM(EXP, "口淫経験"), ARG) ;qol custom code
RETURN 1

@ADD_SEX_ABLE_1_4(ARG)
;フェラチオ実行判定
SIF !TFLAG:100
	RETURN 0
SIF GLOBAL_COMABLE(81)
	RETURN RESULT
;調教者がオトコか半人半妖かふたなりじゃないとダメ
SIF !(TALENT:PLAYER:2 & 2)
	RETURN 0
;時間停止中は許可
IF FLAG:70 != 1
	;猫舌の奴隷の場合は技巧Lv2以上必要
	SIF TALENT:ARG:猫舌 && ABL:ARG:技巧 < 2
		RETURN 0
	;小人サイズだと口の中に入らない
	SIF 挿入不可(ARG) == 3
		RETURN 0
ENDIF
;ボールギャグ使用中は不可
SIF TEQUIP:ARG:ボールギャグ
	RETURN 0
RETURN 1

@ADD_SEX_CAN_1_4()
ADD_SEX_MODULE_DIFFICULTY_TEMP += 24
ADD_SEX_MODULE_COM_ORDER_TEMP |= QOL_COM_ORDER_SERVICE_CUM_FULL | QOL_COM_ORDER_DIRT_SOURCE_PENIS
