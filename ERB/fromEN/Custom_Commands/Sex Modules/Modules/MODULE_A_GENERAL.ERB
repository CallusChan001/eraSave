;general
@ADD_SEX_ACTION_1_7_0(ARG, MULTIPLIER)
#DIM MULTIPLIER
CSTR:PLAYER:10 += @"{ARG}/"
TCVAR:ARG:116 = PLAYER
TCVAR:MASTER:2 = 1
TCVAR:MASTER:4 = MULTIPLY(2500, MULTIPLIER)

DOWNBASE:PLAYER:体力 += 40
DOWNBASE:PLAYER:気力 += 40
IF FLAG:70 == 1
	BASE:MASTER:TSP -= 120
	SOURCE:PLAYER:快Ｃ += MULTIPLY(400, MULTIPLIER)
	SOURCE:ARG:快Ａ = MULTIPLY(400, MULTIPLIER)
	
	SOURCE:ARG:苦痛 = 400
ELSE
	DOWNBASE:ARG:体力 += 80
	DOWNBASE:ARG:気力 += 80

	SOURCE:PLAYER:快Ｃ = MULTIPLY(400, MULTIPLIER)
	SOURCE:ARG:快Ａ = MULTIPLY(400, MULTIPLIER)
	SOURCE:ARG:情愛 = MULTIPLY(100, MULTIPLIER)
	SOURCE:ARG:苦痛 = 800
	SOURCE:ARG:露出 = MULTIPLY(60, MULTIPLIER)
	SOURCE:ARG:反感 = 500
	
	LOCAL = 1 + (ABL:PLAYER:腰 - 2) / 10
	SOURCE:ARG:快Ａ *= LOCAL
	SOURCE:ARG:情愛 *= LOCAL
ENDIF
;Vへの苦痛は先に処理しておく
CALL PAIN_CHECK_A(ARG)
;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
;射精に関係なく行われる処理はここから
CALL ADD_TARGET_WRAPPED(ARG, "STAIN_アナルＰ挿入")

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
EXP:PLAYER:挿入経験 ++
EXP:ARG:Ａ経験 ++
;同性愛　レズなら戻り値1ゲイなら戻り値2
IF FLAG:70 == 1 || CFLAG:ARG:イタズラ
	IF CFLAG:PLAYER:イタズラ == 2
		EXP:PLAYER:睡眠姦経験 += 1
		EXP:ARG:睡眠姦経験 += 1
	ENDIF
	SIF !CFLAG:ARG:イタズラ
	    EXP:PLAYER:時姦経験 += 1
ELSE
	EXP:ARG:Ｖ性交経験 ++
	IF !ASSIPLAY
		EXP:ARG:愛情経験 += TALENT:ARG:恋慕 * 2
		;処女
		SIF TALENT:ARG:処女 == 1 && TALENT:ARG:恋慕
			EXP:ARG:愛情経験 += 10
	ENDIF
ENDIF
Qol_Info_Count_Array:ARG:Qol_Count_ASex ++ ;qol custom code
SIF FLAG:70 == 1 || CFLAG:ARG:イタズラ
	Qol_Info_Count_Array:ARG:Qol_Count_UncASex ++ ;qol custom code

@ADD_SEX_ABLE_1_7_0(ARG)
;正常位実行判定
SIF !TFLAG:100
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(62)
	RETURN RESULT
SIF IS_STANDING_SEX(62)
	RETURN 0
;調教者がオトコ、ふたなりの場合はある程度勃起してないとダメ
SIF (TALENT:PLAYER:2 & 2) && BASE:PLAYER:勃起 < 1000
	RETURN 0
;バイブ挿入中はダメ
SIF TEQUIP:ARG:13
	RETURN 0
;オトコだとダメ
SIF IS_MALE(ARG)
	RETURN 0
;毒人形だと性器がついてないのでダメ
SIF TALENT:ARG:196 == 99
	RETURN 0
;体格差がある場合は経験がないとダメ
SIF Com_Size_Check("strictA", ARG) ;com custom code
	RETURN 0
;さらに体格差があると禁断の知識が必要
SIF Com_Size_Check("regA", ARG) ;com custom code
	RETURN 0
SIF 挿入不可(ARG) == 3
	RETURN 0
;調教者がオトコ、半人半妖、ふたなり、もしくはペニスバンドがないとダメ
SIF !(TALENT:PLAYER:2 & 2) && ITEM:PBAND == 0
	RETURN 0
;処女で、従順4以下・レズっ気4以下のASSIの場合はできない（サドならＯＫ）
IF TALENT:ARG:処女 && ASSIPLAY
	SIF (ABL:ASSI:従順 <= 4 || ABL:ASSI:レズっ気 <= 4) && TALENT:ASSI:サド == 0
		RETURN 0
ENDIF

IF TFLAG:192 == -1
	RETURN 0
ENDIF
;時間停止中はスキップ
IF FLAG:70 != 1
	;処女かつ潤滑不足はだめ
	SIF PALAM:ARG:潤滑 < PALAMLV:2 && TALENT:ARG:処女 && TALENT:ARG:処女 != -1
		RETURN 0
ENDIF

;下半身着衣
SIF TEQUIP:ARG:0 & 4
	RETURN 0
;お風呂場プレイ中はプレイマットが無いとダメ
IF TEQUIP:ARG:31
	SIF ITEM:20 == 0 && NOITEM == 0
		RETURN 0
ENDIF
;背面座位からはダメ
; SIF TEQUIP:ARG:体位 == 5
; 	RETURN 0
RETURN 1

@ADD_SEX_CAN_1_7_0()
ADD_SEX_MODULE_DIFFICULTY_TEMP += 30
ADD_SEX_MODULE_COM_ORDER_TEMP |= QOL_COM_ORDER_ANAL_FULL
