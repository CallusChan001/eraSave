;all functions are named ADD_SEX_ACTION_{PLAYER_PART}_{CHARA_PART}(_{POSE} (optional))
;see ADD_SEX_MODULE_PARTNAME and ADD_SEX_MODULE_POSE for an overview of which id maps to which part/pose

;missionary
@ADD_SEX_ACTION_1_6_1(ARG, MULTIPLIER)
#DIM MULTIPLIER
CSTR:PLAYER:10 += @"{ARG}/"
TCVAR:ARG:116 = PLAYER
TCVAR:MASTER:2 = 1
TCVAR:MASTER:4 = MULTIPLY(2500, MULTIPLIER)

DOWNBASE:PLAYER:体力 += 40
DOWNBASE:PLAYER:気力 += 40
IF FLAG:70 == 1
	BASE:MASTER:TSP -= 100
	SOURCE:PLAYER:快Ｃ += MULTIPLY(400, MULTIPLIER)
	SOURCE:ARG:快Ｖ = MULTIPLY(400, MULTIPLIER)
	
	SOURCE:ARG:苦痛 = 250

	;処女だった場合は、苦痛のソースと反発のソースを追加する
	IF TALENT:ARG:処女 == 1
		SOURCE:ARG:苦痛 += 1000
	ENDIF

	;再生処女だった場合は、苦痛のソースと反発のソースを追加する
	IF TALENT:ARG:処女 == 2
		SOURCE:ARG:苦痛 += 250
	ENDIF
ELSE
	DOWNBASE:ARG:体力 += 50
	DOWNBASE:ARG:気力 += 60

	SOURCE:PLAYER:快Ｃ = MULTIPLY(400, MULTIPLIER)
	SOURCE:ARG:快Ｖ = MULTIPLY(300, MULTIPLIER)
	SOURCE:ARG:情愛 = MULTIPLY(150, MULTIPLIER)
	SOURCE:ARG:苦痛 = 500
	SOURCE:ARG:露出 = MULTIPLY(50, MULTIPLIER)
	SOURCE:ARG:反感 = 300
	
	;正常位補正
	;PLAYERがTARGETの膣に挿入している
	IF BEDROOM(CFLAG:PLAYER:現在位置) && TEQUIP:ARG:50 == PLAYER
		TCVAR:PLAYER:4 += EXP:ARG:愛情経験 * 5
		SOURCE:ARG:快Ｖ += EXP:ARG:愛情経験
		SIF SOURCE:ARG:快Ｖ >= MULTIPLY(700, MULTIPLIER)
			SOURCE:ARG:快Ｖ = MULTIPLY(700, MULTIPLIER)
		SIF TCVAR:PLAYER:4 >= 3000
			TCVAR:PLAYER:4 = 3000
	ENDIF

	;上のほうで計算した汚れデータ
	SOURCE:ARG:不潔 = Y*10 + 60
    CALL ADD_TARGET_WRAPPED(ARG, "特殊ソース処理")
	;CALL 特殊ソース処理
ENDIF
;Vへの苦痛は先に処理しておく
CALL PAIN_CHECK_V(ARG)
;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
;射精に関係なく行われる処理はここから
CALL ADD_TARGET_WRAPPED(ARG, "STAIN_膣内Ｐ挿入")

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
EXP:PLAYER:挿入経験 ++
EXP:ARG:Ｖ経験 ++
;同性愛　レズなら戻り値1ゲイなら戻り値2
IF FLAG:70 == 1 || CFLAG:ARG:イタズラ
	IF CFLAG:PLAYER:イタズラ == 2
		EXP:PLAYER:睡眠姦経験 += 1
		EXP:ARG:睡眠姦経験 += 1
	ENDIF
	SIF !CFLAG:ARG:イタズラ
	    EXP:PLAYER:時姦経験 += 1
ELSE
	EXP:ARG:Ｖ性交経験 ++
	IF !ASSIPLAY
		EXP:ARG:愛情経験 += TALENT:恋慕 * 2
		;処女
		SIF TALENT:ARG:処女 == 1 && TALENT:ARG:恋慕
			EXP:ARG:愛情経験 += 10
	ENDIF
ENDIF
Qol_Info_Count_Array:ARG:Qol_Count_VSex ++ ;qol custom code
SIF FLAG:70 == 1 || CFLAG:ARG:イタズラ
	Qol_Info_Count_Array:ARG:Qol_Count_UncVSex ++ ;qol custom code

@ADD_SEX_ABLE_1_6_1(ARG)
;正常位実行判定
SIF !TFLAG:100
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(60)
	RETURN RESULT
SIF IS_STANDING_SEX(60)
	RETURN 0
;調教者がオトコ、ふたなりの場合はある程度勃起してないとダメ
SIF (TALENT:PLAYER:2 & 2) && BASE:PLAYER:勃起 < 1000
	RETURN 0
;バイブ挿入中はダメ
SIF TEQUIP:ARG:13
	RETURN 0
;オトコだとダメ
SIF IS_MALE(ARG)
	RETURN 0
;毒人形だと性器がついてないのでダメ
SIF TALENT:ARG:196 == 99
	RETURN 0
;体格差がある場合は経験がないとダメ
SIF Com_Size_Check("strict", ARG) ;com custom code
	RETURN 0
;さらに体格差があると禁断の知識が必要
SIF Com_Size_Check("reg", ARG) ;com custom code
	RETURN 0
SIF 挿入不可(ARG) == 3
	RETURN 0
;調教者がオトコ、半人半妖、ふたなり、もしくはペニスバンドがないとダメ
SIF !(TALENT:PLAYER:2 & 2) && ITEM:PBAND == 0
	RETURN 0
;処女で、従順4以下・レズっ気4以下のASSIの場合はできない（サドならＯＫ）
IF TALENT:ARG:処女 && ASSIPLAY
	SIF (ABL:ASSI:従順 <= 4 || ABL:ASSI:レズっ気 <= 4) && TALENT:ASSI:サド == 0
		RETURN 0
ENDIF

IF TFLAG:192 == -1
	RETURN 0
ENDIF
;時間停止中はスキップ
IF FLAG:70 != 1
	;処女かつ潤滑不足はだめ
	SIF PALAM:ARG:潤滑 < PALAMLV:2 && TALENT:ARG:処女 && TALENT:ARG:処女 != -1
		RETURN 0
ENDIF

;下半身着衣
SIF TEQUIP:ARG:0 & 4
	RETURN 0
;お風呂場プレイ中はプレイマットが無いとダメ
IF TEQUIP:ARG:31
	SIF ITEM:20 == 0 && NOITEM == 0
		RETURN 0
ENDIF
;背面座位からはダメ
SIF GROUPMATCH(TEQUIP:ARG:体位, POSE_REVLOTUS, POSE_REV_SUSPENDED_CONGRESS)
	RETURN 0
RETURN 1

@ADD_SEX_CAN_1_6_1()
ADD_SEX_MODULE_DIFFICULTY_TEMP += 18
ADD_SEX_MODULE_COM_ORDER_TEMP |= QOL_COM_ORDER_VAGINAL_FULL
