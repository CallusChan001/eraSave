@ADD_SEX_MODULE_START(SCOM_INDEX, EFFICIENCY = 20)
#DIM SCOM_INDEX
#DIM EFFICIENCY
VARSET ADD_SEX_MODULE_ACTOR, -1
VARSET ADD_SEX_MODULE_CHARA, -1
VARSET ADD_SEX_MODULE_PLAYER_PART, -1
VARSET ADD_SEX_MODULE_TARGET_PART, -1
VARSET ADD_SEX_MODULE_POSE, -1
VARSET ADD_SEX_MODULE_PERCENTAGE, 100
VARSET ADD_SEX_MODULE_PRIORITY, -1
VARSET ADD_SEX_MODULE_ACTOR_NUMBER, -1
VARSET ADD_SEX_MODULE_CHARA_NUMBER, -1
VARSET ADD_SEX_MODULE_ACTION
ADD_SEX_MODULE_EFFICIENCY = EFFICIENCY
ADD_SEX_MODULE_VALID = 0
ADD_SEX_MODULE_ACTION_COUNT = 0
ADD_SEX_MODULE_SCOM = SCOM_INDEX

@ADD_SEX_MODULE_ADD_ACTION(SOURCE_PART, DESTINATION_PART, ACTOR_NUMBER = 0, CHARA_NUMBER = 0)
#DIM SOURCE_PART
#DIM DESTINATION_PART
#DIM ACTOR_NUMBER
#DIM CHARA_NUMBER
IF STRLENS(ADD_SEX_MODULE_ACTION:(ADD_SEX_MODULE_COUNT-1))
    DEBUGPRINTFORML Cannot add more sex actions. Maximum is {ADD_SEX_MODULE_COUNT}.
    RETURN
ENDIF
ADD_SEX_MODULE_ACTION:ADD_SEX_MODULE_ACTION_COUNT '= ADD_SEX_MODULE_ACTIONSTRING(SOURCE_PART, DESTINATION_PART, ACTOR_NUMBER, CHARA_NUMBER)

ADD_SEX_MODULE_PLAYER_PART:ADD_SEX_MODULE_ACTION_COUNT = SOURCE_PART
ADD_SEX_MODULE_TARGET_PART:ADD_SEX_MODULE_ACTION_COUNT = DESTINATION_PART
ADD_SEX_MODULE_ACTOR_NUMBER:ADD_SEX_MODULE_ACTION_COUNT = ACTOR_NUMBER
ADD_SEX_MODULE_CHARA_NUMBER:ADD_SEX_MODULE_ACTION_COUNT = CHARA_NUMBER

ADD_SEX_MODULE_ACTION_COUNT++

@ADD_SEX_MODULE_FINISH
#DIM ACTOR_COUNT
#DIM CHARA_COUNT
#DIM ACTION_INDEX

SIF ADD_SEX_MODULE_ACTION_COUNT == 0
    RETURN

ACTOR_COUNT = MAXARRAY(ADD_SEX_MODULE_ACTOR_NUMBER) + 1
CHARA_COUNT = MAXARRAY(ADD_SEX_MODULE_CHARA_NUMBER) + 1

SIF ACTOR_COUNT > CMATCH(NO, MASTER)
    RETURN
SIF CHARA_COUNT > GET_VALID_TARGETNUM()
    RETURN

IF !EXISTFUNCTION(@"ADD_CUSTOM_SCOM_SET{ADD_SEX_MODULE_SCOM}")
    DEBUGPRINTFORML Missing function ADD_CUSTOM_SCOM_SET{ADD_SEX_MODULE_SCOM} in module ({ADD_SEX_MODULE_SCOM}).
    RETURN
ENDIF

CALLFORM ADD_CUSTOM_SCOM_SET{ADD_SEX_MODULE_SCOM}
SIF !RESULT
    RETURN
IF !ADD_SEX_MODULE_CHARA_VALID()
    RETURN
ENDIF
IF !ADD_SEX_MODULE_ACTOR_VALID()
    RETURN
ENDIF

FOR LOCAL, 0, ACTOR_COUNT
    SIF ADD_SEX_MODULE_ACTOR:LOCAL < 0
        RETURN
NEXT

FOR LOCAL, 0, CHARA_COUNT
    SIF ADD_SEX_MODULE_CHARA:LOCAL <= 0
        RETURN
NEXT

FOR ACTION_INDEX, 0, ADD_SEX_MODULE_ACTION_COUNT
    ADD_SEX_MODULE_PERCENTAGE:ACTION_INDEX = 100
    ADD_SEX_MODULE_PRIORITY:ACTION_INDEX = 0

    SIF !ADD_SEX_MODULE_POSE:ACTION_INDEX
        ADD_SEX_MODULE_POSE:ACTION_INDEX = MAX(ADD_CURRENT_POSE(ADD_SEX_MODULE_CHARA:(ADD_SEX_MODULE_CHARA_NUMBER:ACTION_INDEX)), 0)
NEXT

ADD_SEX_MODULE_VALID = 1

@ADD_SEX_MODULE_CURRENT_ACTION_PARSE(SOURCE_PART, DESTINATION_PART, POSE)
#DIM REF SOURCE_PART
#DIM REF DESTINATION_PART
#DIM REF POSE

;only basic actions for now
SELECTCASE SELECTCOM
    CASE 1 ;cunnilingus
        SOURCE_PART = SET_M
        DESTINATION_PART = SET_C
        POSE = 0
    CASE 3 ;fingering
        SOURCE_PART = SET_YUBI
        DESTINATION_PART = SET_V
        POSE = 0
    CASE 60 ;missionary
        SOURCE_PART = SET_P
        DESTINATION_PART = SET_V
        POSE = POSE_MISSIONARY
    CASE 61 ;doggy style
        SOURCE_PART = SET_P
        DESTINATION_PART = SET_V
        POSE = POSE_DOGGYSTYLE
    CASE 65 ;cowgirl
        SOURCE_PART = SET_P
        DESTINATION_PART = SET_V
        POSE = POSE_COWGIRL
    CASE 67 ;lotus
        SOURCE_PART = SET_P
        DESTINATION_PART = SET_V
        POSE = POSE_LOTUS
    CASE 68 ;reverse lotus
        SOURCE_PART = SET_P
        DESTINATION_PART = SET_V
        POSE = POSE_REVLOTUS
    CASE 80 ;handjob
        SOURCE_PART = SET_P
        DESTINATION_PART = SET_YUBI
        POSE = 0
    CASE 81 ;blowjob
        SOURCE_PART = SET_P
        DESTINATION_PART = SET_M
        POSE = 0
    CASE 82 ;titjob
        SOURCE_PART = SET_P
        DESTINATION_PART = SET_B
        POSE = 0
    CASE 83 ;thighjob
        SOURCE_PART = SET_P
        DESTINATION_PART = SET_C
        POSE = 0
    CASEELSE ;invalid
        RETURN
ENDSELECT

@ADD_SEX_MODULE_COM_ABLE
#DIMS ABLE_FUNCTION
#DIM ACTION_INDEX
#DIM CHARA
#DIM SUCCESS
SIF !ADD_SEX_MODULE_VALID
    RETURN 0

SUCCESS = 1
FOR ACTION_INDEX, 0, ADD_SEX_MODULE_ACTION_COUNT
    CHARA = ADD_SEX_MODULE_CHARA:(ADD_SEX_MODULE_CHARA_NUMBER:ACTION_INDEX)
    ABLE_FUNCTION '= @"ADD_SEX_ABLE_{ADD_SEX_MODULE_PLAYER_PART:ACTION_INDEX}_{ADD_SEX_MODULE_TARGET_PART:ACTION_INDEX}"
    IF ADD_SEX_MODULE_PLAYER_PART:ACTION_INDEX == SET_P && GROUPMATCH(ADD_SEX_MODULE_TARGET_PART:ACTION_INDEX, SET_V, SET_A)
        ;with pose
        IF EXISTFUNCTION(@"%ABLE_FUNCTION%_{ADD_SEX_MODULE_POSE:ACTION_INDEX}")
            ABLE_FUNCTION += @"_{ADD_SEX_MODULE_POSE:ACTION_INDEX}"
        ELSE
            ABLE_FUNCTION += "_0"
        ENDIF
    ENDIF
    DEBUGPRINTFORML Calling %ABLE_FUNCTION%({CHARA}) for %CALLNAME:CHARA%.
    TRYCCALLFORM %ABLE_FUNCTION%(CHARA)
        SUCCESS &= RESULT 
    CATCH
        PRINTFORMDL Error: Function %ABLE_FUNCTION% does not exist.
        RETURN 0
    ENDCATCH
NEXT

RETURN SUCCESS

@ADD_SEX_MODULE_CAN_COM(ARG)
#DIMS CAN_FUNCTION
#DIM ACTION_INDEX
#DIM CHARA_INDEX
#DIM CHARA
#DIM DYNAMIC SUCCESS = 1
#DIM DYNAMIC PARTNERS, ADD_SEX_MODULE_COUNT
#DIM DYNAMIC PARTNER_COUNT
SIF !ADD_SEX_MODULE_VALID
    RETURN 0

FOR ACTION_INDEX, 0, ADD_SEX_MODULE_ACTION_COUNT
    CHARA = ADD_SEX_MODULE_CHARA:(ADD_SEX_MODULE_CHARA_NUMBER:ACTION_INDEX)
    SIF FINDELEMENT(PARTNERS, CHARA) == -1
        PARTNERS:(PARTNER_COUNT++) = CHARA
NEXT


FOR CHARA_INDEX, 0, ADD_SEX_MAX_PARTICIPANTS
    SIF ADD_SEX_MODULE_CHARA_NUMBER:CHARA_INDEX <= 0
        CONTINUE
    CHARA = ADD_SEX_MODULE_CHARA_NUMBER:CHARA_INDEX
    VARSET ADD_SEX_MODULE_DIFFICULTY_TEMP
    VARSET ADD_SEX_MODULE_COM_ORDER_TEMP
    FOR ACTION_INDEX, 0, ADD_SEX_MODULE_ACTION_COUNT
        SIF ADD_SEX_MODULE_CHARA:(ADD_SEX_MODULE_CHARA_NUMBER:ACTION_INDEX) != CHARA
            CONTINUE
        
        CAN_FUNCTION '= @"ADD_SEX_CAN_{ADD_SEX_MODULE_PLAYER_PART:ACTION_INDEX}_{ADD_SEX_MODULE_TARGET_PART:ACTION_INDEX}"
        IF ADD_SEX_MODULE_PLAYER_PART:ACTION_INDEX == SET_P && GROUPMATCH(ADD_SEX_MODULE_TARGET_PART:ACTION_INDEX, SET_V, SET_A)
            ;with pose
            IF EXISTFUNCTION(@"%CAN_FUNCTION%_{ADD_SEX_MODULE_POSE:ACTION_INDEX}")
                CAN_FUNCTION += @"_{ADD_SEX_MODULE_POSE:ACTION_INDEX}"
            ELSE
                CAN_FUNCTION += "_0"
            ENDIF
        ENDIF
        DEBUGPRINTFORML Calling %CAN_FUNCTION% for %CALLNAME:CHARA%.
        TRYCCALLFORM %CAN_FUNCTION%
        CATCH
            PRINTFORMDL Error: Function %CAN_FUNCTION% does not exist.
        ENDCATCH
    NEXT
    IF ADD_SEX_MODULE_COM_ORDER_TEMP
        ;todo, more advanced function to determine the added difficulty per member
        IF PARTNER_COUNT > 1
            ADD_SEX_MODULE_DIFFICULTY_TEMP += (PARTNER_COUNT-1) * 8
            ADD_SEX_MODULE_COM_ORDER_TEMP |= QOL_COM_ORDER_DUO
        ENDIF
        SKIPDISP ARG
        CALL QOL_COM_ORDER_STANDARD_CHECKS_PARTNERS(ADD_SEX_MODULE_DIFFICULTY_TEMP, ADD_SEX_MODULE_COM_ORDER_TEMP, CHARA, PARTNERS)
        SUCCESS &= RESULT
        SKIPDISP 0
    ENDIF
NEXT

RETURN SUCCESS

@ADD_SEX_MODULE_EXECUTE
#DIM SOURCE_PART
#DIM DESTINATION_PART
#DIM ACTION_INDEX
#DIM CHARA
#DIM PERCENTAGE
#DIMS DYNAMIC SEX_FUNCTION

SIF !ADD_SEX_MODULE_VALID
    RETURN 0

DEBUGPRINTFORML
FOR ACTION_INDEX, 0, ADD_SEX_MODULE_ACTION_COUNT
    CHARA = ADD_SEX_MODULE_CHARA:(ADD_SEX_MODULE_CHARA_NUMBER:ACTION_INDEX)
    PERCENTAGE = ADD_SEX_MODULE_PERCENTAGE:ACTION_INDEX
    SEX_FUNCTION '= @"ADD_SEX_ACTION_{ADD_SEX_MODULE_PLAYER_PART:ACTION_INDEX}_{ADD_SEX_MODULE_TARGET_PART:ACTION_INDEX}"
    IF ADD_SEX_MODULE_PLAYER_PART:ACTION_INDEX == SET_P && GROUPMATCH(ADD_SEX_MODULE_TARGET_PART:ACTION_INDEX, SET_V, SET_A)
        ;with pose
        IF EXISTFUNCTION(@"%SEX_FUNCTION%_{ADD_SEX_MODULE_POSE:ACTION_INDEX}")
            SEX_FUNCTION += @"_{ADD_SEX_MODULE_POSE:ACTION_INDEX}"
        ELSE
            SEX_FUNCTION += "_0"
        ENDIF
    ENDIF
    DEBUGPRINTFORML Calling %SEX_FUNCTION%({CHARA}, {PERCENTAGE}) for %CALLNAME:CHARA%.
    TRYCCALLFORM %SEX_FUNCTION%(CHARA, PERCENTAGE)
    CATCH
        PRINTFORMDL Error: Function %SEX_FUNCTION% does not exist.
    ENDCATCH
NEXT

CALL ADD_STANDARD_HOMOSEX(ADD_SEX_MODULE_CHARA:0, ADD_SEX_MODULE_CHARA:1, ADD_SEX_MODULE_CHARA:2)

@ADD_SEX_MODULE_IS_ACTION(INDEX, PLAYER_PART, TARGET_PART)
#FUNCTION
#DIM INDEX
#DIM PLAYER_PART
#DIM TARGET_PART

RETURNF ADD_SEX_MODULE_PLAYER_PART:INDEX == PLAYER_PART && ADD_SEX_MODULE_TARGET_PART:INDEX == TARGET_PART

@ADD_DEBUG_SEX_MODULE()
#FUNCTION
DEBUGPRINTFORML Active sex module:
FOR LOCAL, 0, ADD_SEX_MODULE_COUNT
    SIF !STRLENS(ADD_SEX_MODULE_ACTION:LOCAL) || ADD_SEX_MODULE_CHARA:LOCAL < 0
        CONTINUE
    DEBUGPRINTFORML   Module {LOCAL}:
    DEBUGPRINTFORML     Action:     %ADD_SEX_MODULE_ACTION:LOCAL%
    DEBUGPRINTFORML     Character:  {ADD_SEX_MODULE_CHARA:LOCAL} (%CALLNAME:(ADD_SEX_MODULE_CHARA:LOCAL)%)
    DEBUGPRINTFORML     Source:     {ADD_SEX_MODULE_PLAYER_PART:LOCAL} %ADD_SEX_MODULE_PARTNAME(ADD_SEX_MODULE_PLAYER_PART:LOCAL)%
    DEBUGPRINTFORML     Target:     {ADD_SEX_MODULE_TARGET_PART:LOCAL} %ADD_SEX_MODULE_PARTNAME(ADD_SEX_MODULE_TARGET_PART:LOCAL)%
    DEBUGPRINTFORML     Pose:       {ADD_SEX_MODULE_POSE:LOCAL} %COND_STR(ADD_SEX_MODULE_POSE(ADD_SEX_MODULE_POSE:LOCAL), ADD_SEX_MODULE_POSE:LOCAL)%
    DEBUGPRINTFORML     Priority:   {ADD_SEX_MODULE_PRIORITY:LOCAL}
    DEBUGPRINTFORML     Efficiency: {ADD_SEX_MODULE_PERCENTAGE:LOCAL}\%
NEXT
DEBUGPRINTFORML   Efficiency: {ADD_SEX_MODULE_EFFICIENCY}
DEBUGPRINTFORML   Actions:    {ADD_SEX_MODULE_ACTION_COUNT}
DEBUGPRINTFORML   Is Valid:   {ADD_SEX_MODULE_VALID}

@ADD_SEX_MODULE_PARTNAME(ARG)
#FUNCTIONS

SELECTCASE ARG
    CASE 1
        RETURNF "阴茎"
    CASE 2
        RETURNF "阴蒂"
    CASE 3
        RETURNF "指"
    CASE 4
        RETURNF "嘴"
    CASE 5
        RETURNF "乳"
    CASE 6
        RETURNF "阴道"
    CASE 7
        RETURNF "肛门"
    CASE 8
        RETURNF "足"
    CASE 9
        RETURNF "腋下"
    CASE 10
        RETURNF "小腹"
    CASE 11
        RETURNF "阴囊"
ENDSELECT
RETURNF "未知"

@ADD_SEX_MODULE_POSE(ARG)
#FUNCTIONS

SELECTCASE ARG
    CASE POSE_MISSIONARY
        RETURNF "正常式"
    CASE POSE_DOGGYSTYLE
        RETURNF "后入式"
    CASE POSE_COWGIRL
        RETURNF "骑乘式"
    CASE POSE_LOTUS
        RETURNF "坐莲式"
    CASE POSE_REVLOTUS
        RETURNF "背面坐莲式"
    CASE POSE_ONAHOLE
        RETURNF "小人飞机杯"
    CASE POSE_REV_COWGIRL
        RETURNF "背面骑乘式"
    CASE POSE_STANDING_MISSIONARY
        RETURNF "站立正常式"
    CASE POSE_STANDING_FROM_BEHIND
        RETURNF "站立后入式"
    CASE POSE_SUSPENDED_CONGRESS
        RETURNF "火车便当式"
    CASE POSE_REV_SUSPENDED_CONGRESS
        RETURNF "抱起后入式"
ENDSELECT
RETURNF "未知"

;checks all appropriate layers in TEQUIP:100 to find this character's current pose
@ADD_CURRENT_POSE(ARG)
#FUNCTION
FOR LOCAL, 0, 4
    SIF TEQUIP:ARG:110 + 10 * LOCAL
        RETURNF TEQUIP:ARG:110 + 10 * LOCAL
NEXT
RETURNF 0

@ADD_LAST_V_POSE()
#FUNCTION
FOR LOCAL, 0, 4
	LOCAL:1 = MASTER_POSE(SET_V,SET_P,LOCAL)
	SIF LOCAL:1
		RETURNF TEQUIP:(LOCAL:1):110 + 10 * LOCAL
NEXT
RETURNF 0

@ADD_STANDARD_HOMOSEX(ARG:0, ARG:1, ARG:2, ARG:3, ARG:4, ARG:5, ARG:6, ARG:7, ARG:8, ARG:9)
FOR LOCAL, 0, 10
    SIF ARG:LOCAL <= 0
        BREAK

    ;add xp if same sex as player
    IF HETEROSEX(ARG:LOCAL,PLAYER)
        EXP:(ARG:LOCAL):(50 + HETEROSEX(ARG:LOCAL,PLAYER)) += 3
        EXP:PLAYER:(50 + HETEROSEX(ARG:LOCAL,PLAYER)) += 3
    ENDIF

    ;add xp if same sex as any of the others, but only once
    FOR LOCAL:1, 0, 10
        SIF ARG:(LOCAL:1) <= 0
            BREAK
        SIF LOCAL == LOCAL:1
            CONTINUE
        IF HETEROSEX(ARG:LOCAL,ARG:(LOCAL:1))
            EXP:(ARG:LOCAL):(50 + HETEROSEX(ARG:LOCAL,ARG:(LOCAL:1))) += 3
            BREAK
        ENDIF
    NEXT
NEXT

;special function that can call functions that expect TARGET to be set
@ADD_TARGET_WRAPPED(ARG, FUNCTIONNAME)
#DIMS FUNCTIONNAME
LOCAL = TARGET
TARGET = ARG
TRYCALLFORM %FUNCTIONNAME%

TARGET = LOCAL

@ADD_SEX_MODULE_ACTIONSTRING(SOURCE_PART, DESTINATION_PART, ACTOR_NUMBER, CHARA_NUMBER)
#FUNCTIONS
#DIM SOURCE_PART
#DIM DESTINATION_PART
#DIM ACTOR_NUMBER
#DIM CHARA_NUMBER

RETURNF @"{SOURCE_PART},{DESTINATION_PART},{ACTOR_NUMBER},{CHARA_NUMBER}"

@GET_VALID_TARGETNUM()
#FUNCTION
RETURNF GET_TARGETNUM() - CMATCH(NO, MASTER) + 1

@ADD_SEX_MODULE_CHARA_VALID()
#FUNCTION
#DIM EXPECTED_COUNT
#DIM CHARAS, ADD_SEX_MAX_PARTICIPANTS
#DIM DYNAMIC LAST_CHECKED = -1
#DIM DYNAMIC TARGET_FOUND
EXPECTED_COUNT = MAXARRAY(ADD_SEX_MODULE_CHARA_NUMBER) + 1

ARRAYCOPY "ADD_SEX_MODULE_CHARA", "CHARAS"
ARRAYSORT CHARAS, FORWARD, 0, EXPECTED_COUNT

FOR LOCAL, 0, EXPECTED_COUNT
    SIF CHARAS:LOCAL <= 0
        RETURNF 0
    SIF CHARAS:LOCAL == LAST_CHECKED
        RETURNF 0
    SIF CHARAS:LOCAL == TARGET
        TARGET_FOUND = 1

    LAST_CHECKED = CHARAS:LOCAL
NEXT
RETURNF TARGET_FOUND

@ADD_SEX_MODULE_ACTOR_VALID()
#FUNCTION
#DIM EXPECTED_COUNT
#DIM ACTORS, ADD_SEX_MAX_PARTICIPANTS
#DIM DYNAMIC LAST_CHECKED = -1
#DIM DYNAMIC PLAYER_FOUND
EXPECTED_COUNT = MAXARRAY(ADD_SEX_MODULE_ACTOR_NUMBER) + 1

ARRAYCOPY "ADD_SEX_MODULE_ACTOR", "ACTORS"
ARRAYSORT ACTORS, FORWARD, 0, EXPECTED_COUNT

FOR LOCAL, 0, EXPECTED_COUNT
    SIF ACTORS:LOCAL < 0
        RETURNF 0
    SIF ACTORS:LOCAL == LAST_CHECKED
        RETURNF 0
    SIF ACTORS:LOCAL == PLAYER
        PLAYER_FOUND = 1

    LAST_CHECKED = ACTORS:LOCAL
NEXT
RETURNF PLAYER_FOUND
