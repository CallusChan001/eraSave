;本文件用于存放集合相关的方法
;为了避免混淆，集合表示为STRINGSET（所有的基本运算都是与字符串有关的）
;集合中的元素不允许出现斜杠（/），因为它被用于分隔元素
;这里，集合的本质就是一个满足特定格式的字符串（例如“1/2/3/4/”，或者“一/二/三/四/”）
;特别提醒：空字符串（""）就是空集，清空集合直接赋值即可

;判断一个字符串是否符合集合的格式
@STRINGSET_ISSET(ARGS)
#FUNCTION
VARSET LOCALS
VARSET LOCAL
SIF ARGS == ""
    RETURNF 1
IF STRFIND("/" + ARGS, "//") > -1 || STRFIND(ARGS, "//") > -1 || STRFIND(ARGS + "/", "//") == -1
    RETURNF 0
ELSE
    LOCAL = STRCOUNT(ARGS, "/")
    SPLIT ARGS, "/", LOCALS
    FOR LOCAL:1, 0, LOCAL
        SIF STRCOUNT(ARGS, "^" + LOCALS:(LOCAL:1) + "/") + STRCOUNT(ARGS, "/" + LOCALS:(LOCAL:1) + "/") > 1
            RETURNF 0
    NEXT
ENDIF
RETURNF 1

;请确保使用以下函数时你输入的ARGS或者setA、setB都是集合！

;查找集合元素
;ARGS-要操作的集合，ARGS:1-要查找的元素，ARG-默认提醒开关，需要开启时请设置为1
;查找成功会返回1，否则返回0
@STRINGSET_FIND(ARGS, ARGS:1, ARG = 0)
#FUNCTION
VARSET LOCALS
VARSET LOCAL
IF ARGS == ""
    SIF ARG
        PRINTFORMDL 集合为空！
    RETURNF 0
ENDIF
LOCAL = STRCOUNT(ARGS, "/")
SPLIT ARGS, "/", LOCALS
FOR LOCAL:1, 0, LOCAL
    SIF LOCALS:(LOCAL:1) == ARGS:1
        RETURNF 1
NEXT
RETURNF 0

;添加元素
;ARGS-要操作的集合，ARGS:1-要添加的元素，ARG-默认提醒开关，需要开启时请设置为1
;空字符串不能作为元素添加进去！
@STRINGSET_ADD(ARGS, ARGS:1, ARG = 0)
#FUNCTIONS
IF ARGS:1 == ""
    SIF ARG
        PRINTFORMDL 无法添加空字符串
ELSEIF STRFIND(ARGS:1, "/") + STRFIND(ARGS:1, "\^") > -1
    SIF ARG
        PRINTFORMDL 添加的元素包含非法字符
ELSEIF STRINGSET_FIND(ARGS, ARGS:1, 0)
    SIF ARG
        PRINTFORMDL 集合中已存在该元素
ELSE
    ARGS += ARGS:1 + "/"
ENDIF
RETURNF ARGS

;删除元素
;ARGS-要操作的集合，ARGS:1-要删除的元素，ARG-默认提醒开关，需要开启时请设置为1
@STRINGSET_DISCARD(ARGS, ARGS:1, ARG = 0)
#FUNCTIONS
IF STRFIND(ARGS:1, "/") > -1
    SIF ARG
        PRINTFORMDL 删除的元素包含非法字符
ELSEIF !STRINGSET_FIND(ARGS, ARGS:1, 0)
    SIF ARG
        PRINTFORMDL 集合中不存在该元素
ELSE
    ARGS = %REPLACE(ARGS, ARGS:1 + "/", "")%
ENDIF
RETURNF ARGS

;获取集合元素个数
;ARGS-要操作的集合
@STRINGSET_LEN(ARGS)
#FUNCTION
RETURNF STRCOUNT(ARGS, "/")

;随机取出集合内的一个元素
;ARGS-要操作的集合
@STRINGSET_RANDGET(ARGS)
#FUNCTIONS
VARSET LOCALS
VARSET LOCAL
SIF ARGS == ""
    RETURNF ""
LOCAL = STRCOUNT(ARGS, "/")
SPLIT ARGS, "/", LOCALS
RETURNF LOCALS:(RAND:LOCAL)

;随机删除集合内的一个元素，这个元素会被返回
;ARGS-要操作的集合，ARG-默认提醒开关，需要开启时请设置为1
;调用这个函数需要注意结果：RESULTS-操作后的集合，RESULTS:1-被删除的元素
@STRINGSET_RANDPOP(ARGS, ARG = 0)
VARSET LOCALS
VARSET LOCAL
IF ARGS == ""
    SIF ARG
        PRINTFORMDL 集合为空！
    RETURN -1
ENDIF
RESULTS:1 = %STRINGSET_RANDGET(ARGS)%
RESULTS = %REPLACE(ARGS, RESULTS:1 + "/", "")%

;获取两个集合的并集
@STRINGSET_UNION(setA, setB)
#FUNCTIONS
#DIMS setA
#DIMS setB
VARSET LOCALS
VARSET LOCAL
LOCAL:1 = STRINGSET_LEN(setB)
SPLIT setB, "/", LOCALS
FOR LOCAL, 0, LOCAL:1
    SIF !STRINGSET_FIND(setA, LOCALS:LOCAL)
        setA = %STRINGSET_ADD(setA, LOCALS:LOCAL)%
NEXT
RETURNF setA

;获取两个集合的交集
@STRINGSET_INTERSECTION(setA, setB)
#FUNCTIONS
#DIMS setA
#DIMS setB
#DIMS DYNAMIC setResult
VARSET LOCALS
VARSET LOCAL
LOCAL:1 = STRINGSET_LEN(setB)
SPLIT setB, "/", LOCALS
FOR LOCAL, 0, LOCAL:1
    SIF STRINGSET_FIND(setA, LOCALS:LOCAL)
        setResult = %STRINGSET_ADD(setResult, LOCALS:LOCAL)%
NEXT
RETURNF setResult

;获取一个集合相对于另一个集合的差集
;例如以下函数返回的是setA - setB的结果（这里“-”表示集合中的差运算）
@STRINGSET_DIFF(setA, setB)
#FUNCTIONS
#DIMS setA
#DIMS setB
VARSET LOCALS
VARSET LOCAL
LOCAL:1 = STRINGSET_LEN(setB)
SPLIT setB, "/", LOCALS
FOR LOCAL, 0, LOCAL:1
    SIF STRINGSET_FIND(setA, LOCALS:LOCAL)
        setA = %STRINGSET_DISCARD(setA, LOCALS:LOCAL)%
NEXT
RETURNF setA

;获取两个集合的对称差集
@STRINGSET_SYMDIFF(setA, setB)
#FUNCTIONS
#DIMS setA
#DIMS setB
RETURNF STRINGSET_DIFF(STRINGSET_UNION(setA, setB), STRINGSET_INTERSECTION(setA, setB))