;摄影相关的函数，全部都放在这些文件里了
;这是静叶口上的一个大功能，也许以后被改造成通用补丁也说不定？
;现决定将它作为自定义添加指令而非口上独占指令。——25.10.7

;摄影简介：
;添加了自定义指令「摄影」和「查看相册」以及「购买胶卷」

;「摄影」允许玩家在一定条件下使用相机来拍摄照片，照片会包括时间、地点、在场角色及其行为等基本信息
;虽然说使用的相机是文文那样的传统胶片相机，但是为了方便这个系统省去了冲洗胶片的步骤
;可以在拍摄完后决定是否丢弃照片，但请注意每一次拍摄都是要消耗胶片的！
;保留的照片会被添加至默认相册中，可以自行把照片移动到其他相册
;每天拍摄次数次数有限（最多为一盒胶卷所装载的胶片数量）
;实行指令的前置条件为在静叶口上事件中获取到相机（ITEM:408），没有相机就无法摄影
;照片本身有质量等级（1～6星，沿用了礼物评价的格式），由自机的摄影经验决定

@ADD_CUSTOM_COM100
IF CURRENT_ROLLFILM_NUM == 0
	PRINTFORMDL 现在没有胶卷
	TFLAG:193 = -2
	RETURN 0
ELSEIF CURRENT_FILM_NUM == 0
	PRINTFORMDL 相机里已经没有胶片了，暂时先收手吧
	TFLAG:193 = -2
	RETURN 0
ENDIF
LOCAL:1 = GET_TARGETNUM()
IF LOCAL:1 > 0
	PRINTFORMD 目前和%CALLNAME:TARGET%
	FOR LOCAL, 1, LOCAL:1 + 1
		IF TARGET:0 != TARGET:LOCAL
			PRINTFORMD 、%CALLNAME:(TARGET:LOCAL)%
		ENDIF
	NEXT
	PRINTFORMDL 在一起
ELSE
	PRINTFORMD 目前这里只有你一人……
ENDIF
PRINTFORMDL 要拍照吗？        当前胶卷数：{CURRENT_ROLLFILM_NUM}（相机内的也算）
$LOOP_TAKEPHOTO
SIF TARGET != 0
	PRINTFORMDL [0] -	只拍%CALLNAME:TARGET%
SIF GET_TARGETNUM() > 1
	PRINTFORMDL [1] -	拍场上所有人
PRINTFORMDL [2] -	拍你自己
PRINTFORMDL [3] -	不拍人，只拍风景
PRINTFORMDL [4] -	放弃
INPUT
IF INRANGE(RESULT, 0, 3) && !(TARGET == 0 && RESULT == 0) && !(GET_TARGETNUM() <= 1 && RESULT == 1)
	PRINTFORMDL 咔嚓！
	CURRENT_FILM_NUM -= 1
	TFLAG:194 = RESULT
	SIF CURRENT_FILM_NUM == 0
		CURRENT_ROLLFILM_NUM -= 1
	CALL TAKEPHOTO(RESULT)
	TIME += 1

	;TFLAG
	IF TFLAG:192 == -1
		TFLAG:193 = -1
	ELSEIF TFLAG:192 == 0
		SELECTCASE EXP:MASTER:撮影経験
			CASE IS >= 320
				IF RAND:100 > 75
					TFLAG:193 = 1
				ELSEIF RAND:75 > 10
					TFLAG:193 = 0
				ELSE
					TFLAG:193 = -1
				ENDIF
			CASE IS >= 80
				IF RAND:100 > 80
					TFLAG:193 = 1
				ELSEIF RAND:80 > 15
					TFLAG:193 = 0
				ELSE
					TFLAG:193 = -1
				ENDIF
			CASE IS >= 20
				IF RAND:100 > 85
					TFLAG:193 = 1
				ELSEIF RAND:85 > 15
					TFLAG:193 = 0
				ELSE
					TFLAG:193 = -1
				ENDIF
			CASEELSE
				IF RAND:100 > 90
					TFLAG:193 = 1
				ELSEIF RAND:90 > 20
					TFLAG:193 = 0
				ELSE
					TFLAG:193 = -1
				ENDIF
		ENDSELECT
	ELSEIF TFLAG:192 == 1
		SELECTCASE EXP:MASTER:撮影経験
			CASE IS >= 320
				TFLAG:193 = RAND:100 > 69 ? 1 # 0
			CASE IS >= 80
				TFLAG:193 = RAND:100 > 78 ? 1 # 0
			CASE IS >= 20
				TFLAG:193 = RAND:100 > 86 ? 1 # 0
			CASEELSE
				TFLAG:193 = RAND:100 > 91 ? 1 # 0
		ENDSELECT
	ENDIF

	;SOURCE
	;固定で獲得するソース
	SOURCE:歓楽 = 200
	IF TARGET != 0
		;ABL:親密をみる
		IF ABL:親密 <= 1
			SOURCE:歓楽 += (ABL:親密 * 50)
			SOURCE:反感 = 300
		ELSEIF ABL:親密 <= 5
			SOURCE:歓楽 += 400 + (ABL:親密 * 30)
		ELSEIF ABL:親密 <= 10
			SOURCE:歓楽 += 600 + (ABL:親密 * 30)
		ELSE
			SOURCE:歓楽 += 1000 + (ABL:親密 * 10)
		ENDIF
		SOURCE:歓楽 = LIMIT(SOURCE:歓楽, 0 , 3000)

		SOURCE:受動 = 100 + 100 * ABL:従順

		IF TFLAG:193 == -1 || TFLAG:193 == -2
			TIMES SOURCE:歓楽 , 0.10
			TIMES SOURCE:受動 , 0.50
		ELSEIF TFLAG:193 == 0
			TIMES SOURCE:歓楽 , 1.00
			TIMES SOURCE:受動 , 1.00
		ELSE
			TIMES SOURCE:歓楽 , 2.00
			TIMES SOURCE:受動 , 2.00
		ENDIF

		IF TCVAR:烂醉
			TIMES SOURCE:歓楽 , 0.10
			TIMES SOURCE:征服 , 0.10
			TIMES SOURCE:受動 , 0.10
		ENDIF
	ENDIF

	;体力、気力
	DOWNBASE:MASTER:体力 = 20
	DOWNBASE:MASTER:気力 = 50
	IF GROUPMATCH(GET_WEATHER(), "台风", "暴风雪", "钻石尘", "黄沙", "烈日", "大雨", "大雪", "雨雪交加", "豪雨", "冰雹")
		IF !INROOM(CFLAG:MASTER:300)
			DOWNBASE:MASTER:体力 += 80
			DOWNBASE:MASTER:気力 += 150
		ELSE
			DOWNBASE:MASTER:体力 += 20
			DOWNBASE:MASTER:気力 += 50
		ENDIF
	ENDIF

	;经验
	IF TFLAG:193 == -1
		IF RAND:10 >= 8
			EXP:MASTER:撮影経験 += 2
		ELSEIF RAND:8 >= 4
			EXP:MASTER:撮影経験 += 1
		ENDIF
	ELSEIF TFLAG:193 == 0
		IF RAND:10 >= 7
			EXP:MASTER:撮影経験 += 2
		ELSEIF RAND:7 >= 2
			EXP:MASTER:撮影経験 += 1
		ENDIF
	ELSE
		IF RAND:10 >= 5
			EXP:MASTER:撮影経験 += 2
		ELSE
			EXP:MASTER:撮影経験 += 1
		ENDIF
	ENDIF
	IF TARGET != 0
		IF TFLAG:194 <= 1
			LOCAL:1 = TFLAG:194 == 0 ? 0 # 1
			LOCAL:2 = TFLAG:194 == 0 ? 1 # GET_TARGETNUM() + 1
			FOR LOCAL, LOCAL:1, LOCAL:2
				EXP:(TARGET:LOCAL):被写経験 ++
				IF CHK_DATENOW(CFLAG:(TARGET:LOCAL):12)
					EXP:(TARGET:LOCAL):約會経験 ++
					EXP:(TARGET:LOCAL):愛情経験 += TALENT:恋慕 + MIN(0, ABL:(TARGET:LOCAL):親密 / 5)
				ENDIF
				IF CFLAG:(TARGET:LOCAL):317
					EXP:(TARGET:LOCAL):調教経験 ++
					EXP:(TARGET:LOCAL):奉仕快楽経験 += TALENT:恋慕 + MIN(0, ABL:(TARGET:LOCAL):奉仕精神 / 5)
				ENDIF
				EXP:(TARGET:LOCAL):露出快楽経験 += MIN(0, ABL:(TARGET:LOCAL):露出癖 / 5)
			NEXT
		ENDIF
	ENDIF
ELSEIF RESULT == 4
	PRINTFORMDL 你放下了相机
	TFLAG:193 = -2
	RETURN 0
ELSE
	GOTO LOOP_TAKEPHOTO
ENDIF
RETURN 1

@ADD_CUSTOM_COM_ABLE100
;実行判定
SIF !TFLAG:100
	RETURN 0
;处于浴室时
SIF BATHCHECK(CFLAG:MASTER:300)
	RETURN 0
;体力或気力为0
SIF BASE:MASTER:体力 <= 0 || BASE:MASTER:気力 <= 0
	RETURN 0
;没有相机时
SIF !ITEM:408
	RETURN 0
RETURN 1

@ADD_CUSTOM_MESSAGE_COM100
IF TFLAG:193 == -2
	IF TARGET != 0
		IF TALENT:TARGET:恋慕
			PRINTFORMDW %CALLNAME:TARGET%询问你是不是打算拍照……
		ELSEIF ABL:TARGET:親密 > 5
			PRINTFORMDW %CALLNAME:TARGET%好像看到你拿出了什么又收了回去……
		ELSE
			PRINTFORMDW %CALLNAME:TARGET%甚至没有注意到你的动作……
		ENDIF
	ENDIF
ELSE
	IF GROUPMATCH(GET_WEATHER(), "台风", "暴风雪", "钻石尘", "黄沙", "烈日", "大雨", "大雪", "雨雪交加", "豪雨", "冰雹")
		PRINTFORMD 你不顾肆虐的%GET_WEATHER()%
	ELSE
		PRINTFORMD 你
	ENDIF
	IF TFLAG:193 == 1
		PRINTFORMD %TEXTR("娴熟地/漂亮地/熟练地/轻松地/抓准时机/抓住机会")%
	ELSEIF TFLAG:193 == -1
		PRINTFORMD %TEXTR("费了一番劲才/错过了最好时机后/有些笨拙地/有些不熟练地/犹豫了一会才/没能找到最好的机会，但还是")%
	ENDIF
	PRINTFORMDL \@ TFLAG:194 == 2 ? 为自己 # \@拍下了一张相片
	IF TARGET != 0
		IF TFLAG:194 <= 1
			LOCAL:1 = TFLAG:194 == 0 ? 0 # 1
			LOCAL:2 = TFLAG:194 == 0 ? 1 # GET_TARGETNUM() + 1
			FOR LOCAL, LOCAL:1, LOCAL:2
				IF TALENT:(TARGET:LOCAL):恋慕
					PRINTFORMDL %CALLNAME:(TARGET:LOCAL)%看起来很乐意作为你的摄影目标，并说着一定要看看你拍的相片……
				ELSEIF ABL:(TARGET:LOCAL):親密 > 5
					PRINTFORMDL %CALLNAME:(TARGET:LOCAL)%好像对你的拍摄没什么感觉，只是要求你给她看你拍下的相片……
				ELSEIF ABL:(TARGET:LOCAL):親密 > 2
					PRINTFORMDL %CALLNAME:(TARGET:LOCAL)%有些害羞地想回避你……
				ELSE
					PRINTFORMDL %CALLNAME:(TARGET:LOCAL)%似乎不是很喜欢你无故的拍摄行为……
				ENDIF
			NEXT
		ELSE
			IF TALENT:TARGET:恋慕
				PRINTFORMDL %CALLNAME:TARGET%、好像希望你能拍摄她的样子……
			ELSEIF ABL:TARGET:親密 > 5
				PRINTFORMDL %CALLNAME:TARGET%悄悄凑了过来……
			ELSEIF ABL:TARGET:親密 > 2
				PRINTFORMDL %CALLNAME:TARGET%有些好奇你在拍什么……
			ELSE
				PRINTFORMDL %CALLNAME:TARGET%默默注视着你的动作……
			ENDIF
		ENDIF
	ENDIF
ENDIF
RETURN 1

;保存一张拍好的相片
;ARG是开关
@TAKEPHOTO(ARG)
#DIM TP_ID = -1
#DIMS TP_DATE
#DIMS TP_TIME
#DIMS TP_SPOT
#DIMS DYNAMIC TP_CHARA
#DIMS TP_DESC
#DIM TP_QUALITY
#DIMS CHARA_INFO
TP_ID += 1
PRINTFORMDL 当前相片ID：{TP_ID}
$TP_LOOP
PRINTFORMDL 要留下这张相片吗？
CALL ASK_YN("是", "否")
IF !RESULT && PHOTO_NUM >= PHOTO_MAXNUM
	PRINTFORMDW 相片数已达到上限
	GOTO TP_LOOP
ELSEIF !RESULT
	TP_DATE = %TOSTR(DAY)%
	TP_TIME = %TOSTR(TIME)%
	TP_SPOT = %NAME_FROM_PLACE(CFLAG:MASTER:現在位置)%
	LOCAL:1 = GET_TARGETNUM()
	SELECTCASE ARG
		CASE 0
			CALL GET_CHARA_FACEINFO(TARGET)
			TP_CHARA = %STRINGSET_ADD(TP_CHARA, RESULTS, 0)%
		CASE 1
			FOR LOCAL, 1, GET_TARGETNUM() + 1
				CALL GET_CHARA_FACEINFO(TARGET:LOCAL)
				TP_CHARA = %STRINGSET_ADD(TP_CHARA, RESULTS, 0)%
			NEXT
		CASE 2
			TP_CHARA = %STRINGSET_ADD(TP_CHARA, "0", 0)%
		CASE 3
			TP_CHARA =
	ENDSELECT
	TP_DESC =
	;TP_DESC = %GENERATE_PHOTO_DESC(ARG)%
	TP_QUALITY = CAL_PHOTO_QUALITY()
	CHECK_PHOTO:0 '= TP_SPOT
	CHECK_PHOTO:1 '= PRINT_DATE_F(DAY)
	CHECK_PHOTO:2 '= 时刻表示(TIME)
	CHECK_PHOTO:3 '= TOSTR(TP_QUALITY)
	CHECK_PHOTO:4 '= TP_CHARA
	CHECK_PHOTO:5 '= TP_DESC
	PHOTOINFO:PHOTO_NUM = %DIC_SET(PHOTOINFO:PHOTO_NUM, "P_ID", TOSTR(TP_ID))%
	PHOTOINFO:PHOTO_NUM = %DIC_SET(PHOTOINFO:PHOTO_NUM, "A_ID", "0")%
	PHOTOINFO:PHOTO_NUM = %DIC_SET(PHOTOINFO:PHOTO_NUM, "DATE", TP_DATE)%
	PHOTOINFO:PHOTO_NUM = %DIC_SET(PHOTOINFO:PHOTO_NUM, "TIME", TP_TIME)%
	PHOTOINFO:PHOTO_NUM = %DIC_SET(PHOTOINFO:PHOTO_NUM, "SPOT", TP_SPOT)%
	PHOTOINFO:PHOTO_NUM = %DIC_SET(PHOTOINFO:PHOTO_NUM, "CHARA", TP_CHARA)%
	PHOTOINFO:PHOTO_NUM = %DIC_SET(PHOTOINFO:PHOTO_NUM, "DESC", TP_DESC)%
	PHOTOINFO:PHOTO_NUM = %DIC_SET(PHOTOINFO:PHOTO_NUM, "QUALITY", TOSTR(TP_QUALITY))%
	PHOTOINFO:PHOTO_NUM = %DIC_SET(PHOTOINFO:PHOTO_NUM, "NOTE", "")%
	IF ALBUM_NUM != 0
		LOCAL = GET_ALBUM_PHOTONUM(0)
		ALBUMINFO:0 = %DIC_SET(ALBUMINFO:0, "PHOTONUM", TOSTR(LOCAL + 1))%
	ENDIF
	PRINTFORMDL 相片{TP_ID}被添加至<\@ ALBUM_NUM == 0 ? 默认相册 # %GET_ALBUM_NAME(0)%\@>中
	PHOTO_NUM += 1
	VARSET LOCAL

ELSE
	PRINTFORMDL 你丢掉了相片……
ENDIF

;获取角色信息以方便图像打印
;只有我一个人看图像打印的话，估计就是纯大海捞针了，在此感谢大佬指点
;RESULTS为图像名称（结构为“BASE图像名称◇(EFFECT图像0名称◇(EFFECT图像1名称◇(...)))”）
@GET_CHARA_FACEINFO(ARG)
#DIMS TEMP_INFO
SIF ARG == 0
	RETURN
IF CFLAG:ARG:変化先 > 0
	CALL GET_BASE_RESOURCE(ARG, CFLAG:ARG:変化先)
ELSE
	CALL GET_BASE_RESOURCE(ARG)
ENDIF
IF !RESULT
	IF CFLAG:ARG:変化先 > 0
		RESULTS = %GET_BASESTYLE_TRANS(ARG, CFLAG:ARG:変化先)%_服_通常_{CFLAG:ARG:変化先}
	ELSE
		RESULTS = %GET_BASESTYLE(ARG, "顔絵")%_服_通常_{ARG}
	ENDIF
ENDIF
TEMP_INFO '= RESULTS + "◇"
IF CFLAG:ARG:変化先 > 0
	CALL GET_EFFECT_RESOURCE_TRANS(ARG, CFLAG:ARG:変化先)
ELSE
	CALL GET_EFFECT_RESOURCE(ARG)
ENDIF
IF RESULT > 0
	FOR LOCAL, 0, RESULT
		TEMP_INFO '= TEMP_INFO + RESULTS:LOCAL + "◇"
	NEXT
ENDIF
RESULTS '= TEMP_INFO

;计算相片品质
;相片品质受自机的摄影经验与TFLAG:193的影响
@CAL_PHOTO_QUALITY
#FUNCTION
RETURNF LIMIT(logarithm(EXP:MASTER:撮影経験 / 10, 2), 0, 5) + (RAND:(POWER(10, 2 - TFLAG:193)) == 0 ? 1 # 0)

;以base_num为底的对数函数
@logarithm(num, base_num)
#FUNCTION
#DIM num
#DIM base_num
#DIM DYNAMIC count_num
DO
	num = num / base_num
	SIF num != 0
		count_num ++
LOOP num != 0
RETURNF count_num

;生成相片基本描述
;有关相片的描述问题，之前尝试做摄影补丁的零度也遇到过，最后他没能完全解决
;所以......我会赢吗？（这个暂时先搁置了）
;ARG沿用之前拍摄角色的设置
@GENERATE_PHOTO_DESC(ARG)
#FUNCTIONS
#DIMS DYNAMIC FES_DESC
#DIMS DYNAMIC TIME_DESC
#DIMS DYNAMIC WEATHER_DESC
#DIMS DYNAMIC SPOTNAME_DESC
#DIMS DYNAMIC SPOT_DESC
#DIMS DYNAMIC CHARA_DESC

;节日
SIF FESTIVAL() != ""
	FES_DESC '= @"在%FESTIVAL()%"

;时间
SELECTCASE TIME:2
	CASE 1
		TIME_DESC = %TEXTR("破晓/拂晓/凌晨/黎明/薄晓")%
	CASE 2
		TIME_DESC = %TEXTR("早朝/早晨/上午/日间")%
	CASE 3
		TIME_DESC = %TEXTR("中午/午间/白昼/昼")%
	CASE 4
		TIME_DESC = %TEXTR("午后/下午/下昼/下晌")%
	CASE 5
		TIME_DESC = %TEXTR("落日/暮色/暮间/傍晚/薄暮")%
	CASE 6
		TIME_DESC = %TEXTR("夜晚/夜幕/晚间/夜色")%
	CASE 7
		TIME_DESC = %TEXTR("午夜/深夜/半夜/夜阑")%
ENDSELECT

;天气，无天气感知区不显示天气描述
IF !IS_NO_WEATHER_SEE(AT_AREA(ARG)) && OUTROOF(CFLAG:ARG:現在位置)
	TIME_DESC += "正是"
	IF INRANGE(FLAG:異常気象, 0, 8) || FLAG:異常気象 > 10
		SELECTCASE TIME:5
			CASE 0, 1
				WEATHER_DESC = %TEXTR("晴日/晴空/晴天")%
			CASE 2, 3
				WEATHER_DESC = %TEXTR("阴天/多云/云海/稠云")%
			CASE 4
				WEATHER_DESC = %TEXTR("雨天/细雨/轻雨/小雨")%
			CASE 5
				WEATHER_DESC = %TEXTR("大雨/落雨")%
			CASE 6
				WEATHER_DESC = %TEXTR("雾雨/薄雨")%
			CASE 7
				WEATHER_DESC = %TEXTR("青雾/雾/薄雾/雾色朦胧")%
			CASE 8
				WEATHER_DESC = %TEXTR("雪/落雪/飘雪/飞雪")%
			CASE 9
				WEATHER_DESC = %TEXTR("大雪/纷飞大学/鹅毛大雪/漫天飞雪")%
			CASE 10
				WEATHER_DESC = %TEXTR("冰雾/细雪")%
			CASE 11
				WEATHER_DESC = 雾雪
			CASE 12
				WEATHER_DESC = %TEXTR("雨夹雪/交加雪雨")%
			CASE 13
				WEATHER_DESC = %TEXTR("下落冰雹/下落冰粒")%
			CASE 14
				WEATHER_DESC = %TEXTR("暴雨/倾盆大雨/滂沱大雨")%
			CASE 15
				WEATHER_DESC = 落下大块冰雹
			; CASE 16
			; 	WEATHER_DESC = 交加风雨
			; CASE 17
			; 	WEATHER_DESC = 晴岚
		ENDSELECT
	ELSE
		SELECTCASE FLAG:異常気象
			CASE 9
				WEATHER_DESC = %TEXTR("台风/狂风暴雨/疾风骤雨/狂风骤雨/暴风骤雨")%
			CASE 10
				WEATHER_DESC = %TEXTR("猛吹雪/暴雪/雪虐风饕")%
			; CASE 11
			; 	WEATHER_DESC = 钻石尘
			; CASE 12
			; 	WEATHER_DESC = %TEXTR("沙尘暴/漫天黄沙/肆虐黄风")%
			; CASE 13
			; 	WEATHER_DESC = %TEXTR("烈日/毒日")%
			; CASE 14
			; 	WEATHER_DESC = 极光
		ENDSELECT
	ENDIF
	WEATHER_DESC += "时，"
ENDIF

;无时间感知区，以前包括地底之类，现在可以直接用精细一点
;SIF GET_MAPID(CFLAG:ARG:現在位置) == 9 || GET_MAPID(CFLAG:ARG:現在位置) == 11
SIF IS_NO_TIME_SEE(AT_AREA(ARG))
	VARSET TIME_DESC

SIF FES_DESC != "" && TIME_DESC == ""
	FES_DESC += "，"
SIF FES_DESC == "" && TIME_DESC != "" && WEATHER_DESC == ""
	TIME_DESC '= @"在%TIME_DESC%，"

;地点
SPOTNAME_DESC '= @"在%NAME_FROM_PLACE(CFLAG:MASTER:現在位置)%，"
;IF GET_MAPID(CFLAG:MASTER:現在位置) == MAIN_MAP
;	SPOT_DESC += STR_TR(8000 + CFLAG:MASTER:現在位置)
;ELSE
;	SPOT_DESC += STR_TR(6000 + CFLAG:MASTER:現在位置 / 10)
;ENDIF

;角色
SELECTCASE ARG
	;只有TARGET
	CASE 0
		CHARA_DESC = %GENERATE_CHARA_DESC(TARGET)%
	;连同TARGET在内的全员
	CASE 1
		FOR LOCAL, 1, GET_TARGETNUM() + 1
			CHARA_DESC = %GENERATE_CHARA_DESC(TARGET)%
			IF TARGET:0 != TARGET:LOCAL
				CHARA_DESC += @"，%GENERATE_CHARA_DESC(TARGET:(LOCAL))%"
			ENDIF
		NEXT
	;只有你
	CASE 2
		CHARA_DESC = %GENERATE_CHARA_DESC(0)%
	;没有角色
	CASE 3
		CHARA_DESC '= ""
ENDSELECT

LOCALS '= @"%FES_DESC%%TIME_DESC%%WEATHER_DESC%%SPOTNAME_DESC%%CHARA_DESC%"

RETURNF LOCALS

;生成角色基本描述
@GENERATE_CHARA_DESC(ARG)
#FUNCTIONS
#DIMS DYNAMIC CHARANAME_DESC
#DIMS DYNAMIC CLOTHING_DESC
#DIMS DYNAMIC STATUS_DESC
#DIMS DYNAMIC ACT_DESC

;先处理特殊值
IF ARG == 0
	CHARANAME_DESC = %CALLNAME:MASTER%
ENDIF

;角色名称
CHARANAME_DESC = %CALLNAME:ARG%

;角色着装

;角色状态

;角色行动

RETURNF LOCALS
