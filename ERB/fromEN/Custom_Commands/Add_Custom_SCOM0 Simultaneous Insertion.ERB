;轮流插入, very similar to alternate insertion, but with higher values and different descriptions
@ADD_CUSTOM_SCOM0
;-------------------------------------------------
;COM別FLAGの設定
;-------------------------------------------------
;行為者
TCVAR:116 = PLAYER
;TCVAR:0 射精対象フラグ（ビット0=TARGET 1=PLAYER 2=MASTER 3=ASSI）
;TCVAR:1 予備射精対象フラグ（ビットを使いたくない人用、CFLAG:100に翻訳されます）	（1=TARGET 2=PLAYER 3=MASTER 4=ASSI 5=TARGET+ASSI 6=MASTER+ASSI）
;TCVAR:2 射精箇所フラグ（-1=コンドーム 1=膣内 2=アナル 3=手淫 4=口淫 5=パイズリ 6=素股 7=足コキ 8=体表 9=アナル奉仕 10=道具 11=触手）
;二人目
LOCAL = MASTER_POSE(SET_V,SET_P,1)

LOCALS = {TARGET}/{LOCAL}/
CSTR:PLAYER:10 += LOCALS
TCVAR:MASTER:2 = 1
;強度
TCVAR:MASTER:4 = 2500

;奉仕快楽フラグ
TCVAR:PLAYER:108 = 1

DOWNBASE:PLAYER:体力 += 60
DOWNBASE:PLAYER:気力 += 60

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
IF FLAG:70 == 1
	BASE:MASTER:TSP -= 55
	SOURCE:MASTER:快Ｃ = 1035
	FOR LOCAL:1,0,2
		IF LOCAL:1 == 0
			LOCAL:2 = TARGET
		ELSE
			LOCAL:2 = LOCAL
		ENDIF
		SOURCE:(LOCAL:2):快Ｖ = 460
		SOURCE:(LOCAL:2):苦痛 = 260

		;処女だった場合は、苦痛のソースと反発のソースを追加する
		IF TALENT:(LOCAL:2):処女 == 1
			SOURCE:(LOCAL:2):苦痛 += 1020
		ENDIF

		;再生処女だった場合は、苦痛のソースと反発のソースを追加する
		IF TALENT:(LOCAL:2):処女 == 2
			SOURCE:(LOCAL:2):苦痛 += 260

		ENDIF

		;Vへの苦痛は先に処理しておく
		CALL PAIN_CHECK_V(LOCAL:2)
	NEXT

ELSE

	DOWNBASE:体力 += 50
	DOWNBASE:気力 += 110
	SOURCE:MASTER:快Ｃ = 1035

	;-------------------------------------------------
	;特殊ソース処理
	;-------------------------------------------------
	FOR LOCAL:1,0,2
		IF LOCAL:1 == 0
			LOCAL:2 = TARGET
		ELSE
			LOCAL:2 = LOCAL
		ENDIF

		SOURCE:(LOCAL:2):快Ｖ = 460
		SOURCE:(LOCAL:2):情愛 = 210
		SOURCE:(LOCAL:2):苦痛 = 510
		SOURCE:(LOCAL:2):露出 = 80
		SOURCE:(LOCAL:2):反感 = 350
		;上のほうで計算した汚れデータ
		SOURCE:(LOCAL:2):不潔 = Y*10 + 60

		;抑圧、抵抗持ちには、抑鬱ソース追加
		SIF TALENT:(LOCAL:2):自己愛 < 0 || TALENT:(LOCAL:2):抵抗
			SOURCE:(LOCAL:2):鬱屈 += 310

		;貞操観念持ちで恋慕、親愛持ち以外には常に反発のソースを追加する（セックス系のみ）
		SIF TALENT:(LOCAL:2):貞操 > 0 && !TALENT:(LOCAL:2):恋慕
			SOURCE:(LOCAL:2):反感 += 1300

		;処女だった場合は、苦痛のソースと反発のソースを追加する
		IF TALENT:(LOCAL:2):処女 == 1
			SOURCE:(LOCAL:2):苦痛 += 2040
			SOURCE:(LOCAL:2):反感 += 3500
		ENDIF

		;再生処女だった場合は、苦痛のソースと反発のソースを追加する
		IF TALENT:(LOCAL:2):処女 == 2
			SOURCE:(LOCAL:2):苦痛 += 510
			SOURCE:(LOCAL:2):反感 += 1100
		ENDIF

		;Vへの苦痛は先に処理しておく
		CALL PAIN_CHECK_V(LOCAL:2)
	NEXT
ENDIF
;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
;射精に関係なく行われる処理はここから
;膣内に破瓜の血の汚れを付加
SIF TALENT:処女 && TALENT:処女 != -1
	STAIN:6 |= 64
;奴隷のＶ⇔調教者のＰの汚れが移動
STAIN:PLAYER:2 |= STAIN:3
STAIN:PLAYER:2 |= STAIN:6
STAIN:3 |= STAIN:PLAYER:2
STAIN:6 |= STAIN:PLAYER:2
;助手のＶ⇔調教者のＰの汚れが移動
STAIN:PLAYER:2 |= STAIN:LOCAL:3
STAIN:PLAYER:2 |= STAIN:LOCAL:6
STAIN:LOCAL:3 |= STAIN:PLAYER:2
STAIN:LOCAL:6 |= STAIN:PLAYER:2
;奴隷のＶ⇔調教者のＰの汚れが移動
STAIN:PLAYER:2 |= STAIN:3
STAIN:PLAYER:2 |= STAIN:6
STAIN:3 |= STAIN:PLAYER:2
STAIN:6 |= STAIN:PLAYER:2

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
EXP:PLAYER:挿入経験 ++
;同性愛　レズなら戻り値1ゲイなら戻り値2
IF HETEROSEX(TARGET,PLAYER)
	EXP:(50 + HETEROSEX(TARGET,PLAYER)) += 3
	EXP:PLAYER:(50 + HETEROSEX(TARGET,PLAYER)) += 3
ENDIF
;同性愛　レズなら戻り値1ゲイなら戻り値2
IF HETEROSEX(TARGET,LOCAL)
	EXP:LOCAL:(50 + HETEROSEX(TARGET,LOCAL)) += 3
	EXP:TARGET:(50 + HETEROSEX(TARGET,LOCAL)) += 3
ENDIF
;同性愛　レズなら戻り値1ゲイなら戻り値2
IF HETEROSEX(LOCAL,PLAYER)
	EXP:LOCAL:(50 + HETEROSEX(LOCAL,PLAYER)) += 3
	EXP:PLAYER:(50 + HETEROSEX(LOCAL,PLAYER)) += 3
ENDIF

IF FLAG:70 == 1
	EXP:MASTER:時姦経験 += 1
ELSE
	EXP:Ｖ性交経験 ++
	EXP:LOCAL:Ｖ性交経験 ++
	IF !ASSIPLAY
		EXP:愛情経験 += TALENT:恋慕 * 2
		;処女
		SIF TALENT:処女 && TALENT:恋慕 && TALENT:処女 != -1
			EXP:愛情経験 += 4
	ENDIF
ENDIF
Qol_Info_Count_Array:TARGET:Qol_Count_VSex ++ ;qol custom code
Qol_Info_Count_Array:LOCAL:Qol_Count_VSex ++ ;qol custom code
IF FLAG:70 == 1 || CFLAG:イタズラ
	Qol_Info_Count_Array:TARGET:Qol_Count_UncVSex ++ ;qol custom code
	Qol_Info_Count_Array:LOCAL:Qol_Count_UncVSex ++ ;qol custom code
ENDIF
RETURN 1

@ADD_CUSTOM_CAN_SCOM0(ARG)
;-------------------------------------------------
;実行判定処理
;-------------------------------------------------
SKIPDISP ARG
IF FLAG:70 == 1
	PRINTL 轮流时奸插入
ELSE
	;縄で縛られている場合は無視（縄が無い時のみ実行判定を行う）
	IF TEQUIP:縄 == 0
		PRINTL 轮流插入

		LOCAL = MASTER_POSE(SET_V,SET_P,1)
		CALL QOL_COM_ORDER_STANDARD_CHECKS_DOUBLE(30, QOL_COM_ORDER_VAGINAL_FULL | QOL_COM_ORDER_DUO, TARGET, LOCAL)
		LOCAL = RESULT
		SKIPDISP 0
		RETURN LOCAL
	ELSE
		PRINTL 轮流插入
	ENDIF
ENDIF
SKIPDISP 0
RETURN 1

;-------------------------------------------------
;選択可能判定
;-------------------------------------------------
@ADD_CUSTOM_SCOM_ABLE0
;交互挿入
SIF !TFLAG:100
	RETURN 0
;一括管理
SIF TFLAG:102 != 2
	RETURN 0
SIF !TALENT:PLAYER:双槍
	RETURN 0
LOCAL = MASTER_POSE(SET_V,SET_P,1)
;バイブ挿入中はダメ
SIF TEQUIP:13
	RETURN 0
SIF TEQUIP:LOCAL:13
	RETURN 0
;オトコだとダメ
SIF !(TALENT:2 & 1)
	RETURN 0
SIF !(TALENT:LOCAL:2 & 1)
	RETURN 0
;体格差がある場合は経験がないとダメ
SIF Com_Size_Check("strict", TARGET)
	RETURN 0
;さらに体格差があると禁断の知識が必要
SIF Com_Size_Check("reg", TARGET)
	RETURN 0
;体格差がある場合は経験がないとダメ
SIF Com_Size_Check("strict", LOCAL)
	RETURN 0
;さらに体格差があると禁断の知識が必要
SIF Com_Size_Check("reg", LOCAL)
	RETURN 0
;調教者がオトコ、半人半妖、ふたなり、もしくはペニスバンドがないとダメ
SIF !(HAS_PENIS(MASTER)) && ITEM:PBAND == 0
	RETURN 0
;処女で、従順4以下・レズっ気4以下のASSIの場合はできない（サドならＯＫ）
IF TALENT:処女 && ASSIPLAY
	SIF (ABL:ASSI:従順 <= 4 || ABL:ASSI:レズっ気 <= 4) && TALENT:ASSI:サド == 0
		RETURN 0
ENDIF
;時間停止中は許可
IF FLAG: 70 != 1
	;処女かつ潤滑不足はだめ
	SIF PALAM:潤滑 < PALAMLV:2 && TALENT:処女 && TALENT:処女 != -1
		RETURN 0
	SIF PALAM:LOCAL:潤滑 < PALAMLV:2 && TALENT:LOCAL:処女 && TALENT:LOCAL:処女 != -1
		RETURN 0
ENDIF

;下半身着衣
SIF TEQUIP:0 & 4
	RETURN 0
SIF TEQUIP:LOCAL:0 & 4
	RETURN 0
;お風呂場プレイ中はプレイマットが無いとダメ
IF TEQUIP:31
	SIF ITEM:20 == 0 && NOITEM == 0
		RETURN 0
ENDIF
RETURN 1

@ADD_CUSTOM_MESSAGE_SCOM0
LOCAL = MASTER_POSE(SET_V,SET_P,1)

IF FLAG:70 == 1
	PRINTFORM 
	IF TEQUIP:体位 != TEQUIP:LOCAL:体位２
		PRINTFORML 让%CALLNAME:TARGET%和%CALLNAME:LOCAL%面对面抱着，
	ELSE
		PRINTFORML 让%CALLNAME:TARGET%和%CALLNAME:LOCAL%肩并着肩贴在一起，
	ENDIF
	PRINTFORM 将%TINKO(MASTER)%轮流插入她们体内，感受着几乎要满溢出来的满足感，并仔细品味着%CALLNAME:TARGET%%SLIT(TARGET,RAND:4)%和%CALLNAME:LOCAL%%SLIT(LOCAL,RAND:4)%……
	CALL PRINT_BREAK

	;保存上次的共同参与者
	TFLAG:152 = LOCAL
ELSE
	PRINTFORM 小心翼翼地
	IF TEQUIP:体位 != TEQUIP:LOCAL:体位２
		PRINTFORML 让%CALLNAME:TARGET%和%CALLNAME:LOCAL%面对面抱着，
	ELSE
		PRINTFORML 让%CALLNAME:TARGET%和%CALLNAME:LOCAL%肩并肩贴在一起，
	ENDIF
	PRINTFORM 将%TINKO(MASTER)%轮流插入她们体内，感受着几乎要满溢出来的满足感，并仔细品味着%CALLNAME:TARGET%%SLIT(TARGET,RAND:4)%和%CALLNAME:LOCAL%%SLIT(LOCAL,RAND:4)%……
	CALL PRINT_BREAK

	IF TALENT:恋慕 && TALENT:LOCAL:恋慕 && RAND:4
		PRINTFORML +++卖力地轮流插入时，两人娇声央求着更多……
	ELSEIF ABL:レズっ気 >= 5 && ABL:LOCAL:レズっ気 >= 5 && TEQUIP:体位 != TEQUIP:LOCAL:体位２
		PRINTFORML 她们的舌头缠绕在一起，承受着肉棒的轮流插入……
	ENDIF

	;前回の共同参加者の保存
	TFLAG:152 = LOCAL
ENDIF

@ASSISTANT_KOJO_CUSTOM_SCOM0
LOCAL = MASTER_POSE(SET_V,SET_P,1)
CALL KOJO_MESSAGE_SEND("COMMAND_ASSISTANT",-1,LOCAL,-1,-1)
