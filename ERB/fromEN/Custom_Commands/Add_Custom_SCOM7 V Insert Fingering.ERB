;V插入同时指交
@ADD_CUSTOM_SCOM7
CALL ADD_CUSTOM_SCOM_MODULE7
CALL ADD_SEX_MODULE_EXECUTE

RETURN 1

@ADD_CUSTOM_SCOM_ABLE7
CALL ADD_CUSTOM_SCOM_MODULE7
CALL ADD_SEX_MODULE_COM_ABLE
SIF !RESULT
	RETURN 0
	
;need to be somewhat skilled in both aspects
SIF ABL:MASTER:指 < 3
	RETURN 0
SIF ABL:MASTER:腰 < 3
	RETURN 0

RETURN 1

@ADD_CUSTOM_CAN_SCOM7(ARG)
SKIPDISP ARG
LOCAL = ADD_LAST_V_POSE()
IF LOCAL
	PRINTFORML %ADD_SEX_MODULE_POSE(LOCAL)%同时指交
ELSE
	PRINTFORML V插入同时指交
ENDIF
SKIPDISP 0

CALL ADD_CUSTOM_SCOM_MODULE7
CALL ADD_SEX_MODULE_CAN_COM(ARG)
RETURN RESULT

@ADD_CUSTOM_MESSAGE_SCOM7
#DIM DYNAMIC V_INDEX = -1
#DIM DYNAMIC FINGERING_INDEX = -1

FOR LOCAL, 0, ADD_SEX_MODULE_ACTION_COUNT
	IF ADD_SEX_MODULE_IS_ACTION(LOCAL, SET_YUBI, SET_V)
		FINGERING_INDEX = LOCAL
	ELSEIF ADD_SEX_MODULE_IS_ACTION(LOCAL, SET_P, SET_V)
		V_INDEX = LOCAL
	ENDIF
NEXT

IF FINGERING_INDEX < 0 || V_INDEX < 0
	PRINTFORMDW ERROR: Targets not valid for @ADD_CUSTOM_MESSAGE_SCOM7
	RETURN
ENDIF

PRINTFORMD %CALLNAME:MASTER%一边爱抚%CALLNAME:(ADD_SEX_MODULE_CHARA:FINGERING_INDEX)%的小穴，同时
SELECTCASE ADD_SEX_MODULE_POSE:V_INDEX
	CASE 1, 2
		PRINTFORMD 抽插着%CALLNAME:(ADD_SEX_MODULE_CHARA:V_INDEX)%。
	CASE 3, 4
		PRINTFORMD 挺腰顶弄着%CALLNAME:(ADD_SEX_MODULE_CHARA:V_INDEX)%。
	CASE 5
		PRINTFORMD 挺腰顶弄着%CALLNAME:(ADD_SEX_MODULE_CHARA:V_INDEX)%，另一只手肆意地搂在她身上爱抚。
ENDSELECT
PRINTFORMDL

@ASSISTANT_KOJO_CUSTOM_SCOM7
FOR LOCAL, 0, ADD_SEX_MODULE_ACTION_COUNT
	SIF ADD_SEX_MODULE_CHARA:LOCAL == TARGET
		CONTINUE
	CALL KOJO_MESSAGE_SEND("COMMAND_ASSISTANT",-1,ADD_SEX_MODULE_CHARA:LOCAL,-1,-1)
NEXT

@ADD_CUSTOM_SCOM_SET7
#DIM DYNAMIC P0
#DIM DYNAMIC F0

SIF !IS_COM_SEX("v") && SELECTCOM != 3
	RETURN 0

P0 = ADD_MASTER_POSE_TR(PLAYER, SET_V,SET_P)
F0 = ADD_MASTER_POSE_TR(PLAYER, SET_V,SET_YUBI)

SIF P0 <= 0 && F0 <= 0 ;nothing valid in the current turn
	RETURN 0

SIF !P0
	P0 = ADD_MASTER_POSE_TR(PLAYER, SET_V,SET_P,1)
SIF !F0
	F0 = ADD_MASTER_POSE_TR(PLAYER, SET_V,SET_YUBI,1)
SIF P0 <= 0 || F0 <= 0
	RETURN 0
SIF P0 == F0
	RETURN 0

IF F0 == TARGET && SELECTCOM != 3
	RETURN 0
ELSEIF P0 == TARGET && !IS_COM_SEX("v")
	RETURN 0
ENDIF

ADD_SEX_MODULE_ACTOR:0 = PLAYER
ADD_SEX_MODULE_CHARA:0 = F0
ADD_SEX_MODULE_CHARA:1 = P0
ADD_SEX_MODULE_POSE:1 = ADD_TOUCH_CURRENT_POSE(PLAYER, P0)

RETURN 1

@ADD_CUSTOM_SCOM_MODULE7
CALL ADD_SEX_MODULE_START(7)
CALL ADD_SEX_MODULE_ADD_ACTION(SET_YUBI, SET_V,, 0)
CALL ADD_SEX_MODULE_ADD_ACTION(SET_P, SET_V,, 1)
CALL ADD_SEX_MODULE_FINISH
