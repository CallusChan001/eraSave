;COM
@Add_Custom_USERCOM(ARG)
#DIM ADD_CUSTOMCOMVALUE
SIF !ADD_CUSTOM_COM_SWITCH
	RETURN ARG
ADD_CUSTOMCOMVALUE = Add_Number_To_Custom_COM(ARG)
ADD_SELECTCUSTOMCOM = -1
IF ADD_CUSTOMCOMVALUE >= 0
	RESULT = 0
	TRYCALLFORM ADD_CUSTOM_COM_ABLE{ADD_CUSTOMCOMVALUE}
	IF RESULT
		ADD_SELECTCUSTOMCOM = ADD_CUSTOMCOMVALUE
		RETURN ADD_CUSTOM_COM
	ENDIF
	RETURN -1
ENDIF
RETURN ARG

@Add_Custom_Set_PREV
ADD_PREVPREVCUSTOMCOM = ADD_PREVCUSTOMCOM
ADD_PREVCUSTOMCOM = ADD_SELECTCUSTOMCOM
ADD_PREV_SELECTED_SCOM = ADD_SELECTED_SCOM

@Add_Custom_List_COMS
#DIM HIGHLIGHT_SCOM
#DIM nFilterPass
SIF STRCOUNT(SAVESTR:0,@"{ADD_CUSTOM_COM}") ;custom commands disabled
	RETURN

FOR LOCAL, 0, ADD_CUSTOM_MAX_COM + 1
	;NAS: Lucid Dream
	; SIF inTrain == 3 && !LucidDreamNormalCommand(DAILY_Lucid_Stats:3) && !GROUPMATCH(LOCAL,357) && !INRANGE(LOCAL,930,939)
	; 	CONTINUE
	;PRINTFORML Able to execute {ID_COM} %Add_Custom_COM_Name(ID_COM)%
	;过滤器，暂时用用
	SIF !Quickfilter_Custom(LOCAL+9000)
		CONTINUE
	RESULT = 0
	TRYCALLFORM ADD_CUSTOM_COM_ABLE{LOCAL}
	IF RESULT
		
		RESULT = 0
		CALL QOL_CUSTOM_SCOM_CHECK(LOCAL)
		HIGHLIGHT_SCOM = RESULT

		IF HIGHLIGHT_SCOM
			SETCOLOR C_HEARTPINK
			PRINTFORMC %RESULTS%[{Add_Custom_COM_To_Number(LOCAL)}]
			RESETCOLOR
		ELSE
			TRYCCALLFORM ADD_CUSTOM_COM_PRINT{LOCAL}
			CATCH
				PRINTFORMC %Add_Custom_COM_Name(LOCAL)%[{Add_Custom_COM_To_Number(LOCAL)}]
			ENDCATCH
		ENDIF
	ENDIF
NEXT

@Add_Custom_COM_To_Number(ARG)
#LOCALSIZE 1
#LOCALSSIZE 1
#FUNCTION
RETURNF ARG + 9000

@Add_Number_To_Custom_COM(ARG)
#LOCALSIZE 1
#LOCALSSIZE 1
#FUNCTION
;custom code: Increased Cap
SIF ARG < 9000 || ARG > 99998
	RETURNF -1

RETURNF ARG - 9000

@COM381
#LOCALSIZE 1
#LOCALSSIZE 1
SIF ADD_SELECTCUSTOMCOM < 0
	RETURN -1

RESULT = -1
TRYCALLFORM ADD_CUSTOM_COM{ADD_SELECTCUSTOMCOM}
RETURN RESULT

@CAN_COM381(ARG)
#LOCALSIZE 1
#LOCALSSIZE 1
SKIPDISP ARG
SIF ADD_SELECTCUSTOMCOM < 0
	RETURN 0

TRYCCALLFORM ADD_CUSTOM_CAN_COM{ADD_SELECTCUSTOMCOM}(ARG)
	RETURN RESULT
CATCH
	PRINTFORML %Add_Custom_COM_Name(ADD_SELECTCUSTOMCOM)%
ENDCATCH
SKIPDISP 0
RETURN 1

@COM_ABLE381
#LOCALSIZE 1
#LOCALSSIZE 1
;general block
SIF !TFLAG:100
	RETURN 0
SIF ADD_SELECTCUSTOMCOM < 0
	RETURN 0

TRYCCALLFORM ADD_CUSTOM_COM_ABLE{ADD_SELECTCUSTOMCOM}
	RETURN RESULT
CATCH
ENDCATCH
RETURN 0

@MESSAGE_COM381
#LOCALSIZE 1
#LOCALSSIZE 1
SIF ADD_SELECTCUSTOMCOM < 0
	RETURN

TRYCALLFORM ADD_CUSTOM_MESSAGE_COM{ADD_SELECTCUSTOMCOM}

;- Mr Pops Alot
@EQUIP_COM381(ARG)
SKIPDISP ARG
SIF ADD_SELECTCUSTOMCOM < 0
	RETURN 0

TRYCCALLFORM ADD_CUSTOM_EQUIP_COM{ADD_SELECTCUSTOMCOM}(ARG)
	RETURN RESULT
CATCH
ENDCATCH
RETURN 0

;SCOM
@Add_Custom_CALL_COM(ARG)
#DIM CONST MODULAR_SCOMS = 8, 11, 7, 9, 10, 12, 16, 17, 19, 18, 20 ;the order in which they are checked
ADD_SELECTED_SCOM = -1

IF CFLAG:MASTER:うふふ && (SELECTCOM < 300 || SELECTCOM == ADD_CUSTOM_COM)
	FOR LOCAL, 0, VARSIZE("MODULAR_SCOMS")
		RESULT = 0
		TRYCCALLFORM Add_Custom_SCOM{MODULAR_SCOMS:LOCAL}_Check
		CATCH
			TRYCALLFORM ADD_CUSTOM_SCOM_MODULE{MODULAR_SCOMS:LOCAL}
			RESULT = ADD_SEX_MODULE_VALID
			SIF RESULT
				TRYCALLFORM ADD_CUSTOM_SCOM_ABLE{MODULAR_SCOMS:LOCAL}
		ENDCATCH
		IF RESULT
			CALL Add_Custom_Set_SCOM(MODULAR_SCOMS:LOCAL)
			RETURN
		ENDIF
	NEXT
ENDIF

IF Add_Custom_SCOM0_Check(ARG)
	CALL Add_Custom_Set_SCOM(0)
ELSEIF Add_Custom_SCOM1_Check(ARG)
	CALL Add_Custom_Set_SCOM(1)
ELSEIF TEQUIP:ARG:羞恥プレイ && SELECTCOM == 70
	CALL Add_Custom_Set_SCOM(2)
ELSEIF TEQUIP:ARG:羞恥プレイ && SELECTCOM == 8
	CALL Add_Custom_Set_SCOM(3)
ELSEIF Add_Custom_SCOM5_Check(ARG)
	CALL Add_Custom_Set_SCOM(5)
ELSEIF Add_Custom_SCOM6_Check(ARG)
	CALL Add_Custom_Set_SCOM(6)
ELSEIF Add_Custom_SCOM13_Check(ARG)
	CALL Add_Custom_Set_SCOM(13)
ELSEIF Add_Custom_SCOM14_Check(ARG)
	CALL Add_Custom_Set_SCOM(14)
ELSEIF Add_Custom_SCOM15_Check(ARG)
	CALL Add_Custom_Set_SCOM(15)
ENDIF

@Add_Custom_SCOM0_Check(ARG)
#FUNCTION
SIF !TALENT:MASTER:双槍
	RETURNF 0
;inserted in other character, only in missionary and doggy positions
SIF GROUPMATCH(SELECTCOM,60,61) && ADD_CHARA_POSE(PLAYER, ARG, SET_P, SET_V) && (ADD_MASTER_POSE_TR(PLAYER, SET_V,SET_P,1) && ADD_MASTER_POSE_TR(PLAYER, SET_V,SET_P,1) != ARG) && GROUPMATCH(TEQUIP:(ADD_MASTER_POSE_TR(PLAYER, SET_V,SET_P,1)):体位２,1,2)
	RETURNF 1
RETURNF 0

@Add_Custom_SCOM1_Check(ARG)
#FUNCTION
SIF !TALENT:MASTER:双槍
	RETURNF 0
;inserted in other character, only in anal missionary and doggy positions
SIF GROUPMATCH(SELECTCOM,62,63) && ADD_CHARA_POSE(PLAYER, ARG, SET_P, SET_A) && (ADD_MASTER_POSE_TR(PLAYER, SET_A,SET_P,1) && ADD_MASTER_POSE_TR(PLAYER, SET_A,SET_P,1) != ARG) && GROUPMATCH(TEQUIP:(ADD_MASTER_POSE_TR(PLAYER, SET_A,SET_P,1)):体位２,1,2)
	RETURNF 1
RETURNF 0

@Add_Custom_SCOM5_Check(ARG)
#FUNCTION
#DIM P_IN_V_RECEIVER
#DIM M_ON_C_RECEIVER
P_IN_V_RECEIVER = ADD_MASTER_POSE_TR(PLAYER, SET_V,SET_P,1)
M_ON_C_RECEIVER = ADD_MASTER_POSE_TR(PLAYER, SET_C,SET_M,1)
;currently in cowgirl position with other girl
SIF SELECTCOM == 1 && P_IN_V_RECEIVER && P_IN_V_RECEIVER != TARGET && TEQUIP:P_IN_V_RECEIVER:体位 == 3
	RETURNF 1
;currently performing cunnilingus on the other girl
SIF SELECTCOM == 65 && M_ON_C_RECEIVER && M_ON_C_RECEIVER != TARGET
	RETURNF 1
RETURNF 0

@Add_Custom_SCOM6_Check(ARG)
#FUNCTION
SIF SELECTCOM == 3 && (ADD_MASTER_POSE_TR(PLAYER, SET_V,SET_YUBI,1) && ADD_MASTER_POSE_TR(PLAYER, SET_V,SET_YUBI,1) != ARG)
	RETURNF 1
RETURNF 0

@Add_Custom_SCOM11_Check(ARG)
#DIM INDEX
CALL ADD_CUSTOM_SCOM_MODULE11
INDEX = FINDELEMENT(ADD_SEX_MODULE_ACTION, ADD_SEX_MODULE_ACTIONSTRING(SET_P, SET_V, 0))
IF INDEX < 0
	RETURN 0
ELSEIF ADD_SEX_MODULE_POSE:INDEX != POSE_COWGIRL
	RETURN 0
ENDIF
RETURN ADD_SEX_MODULE_VALID

@Add_Custom_SCOM13_Check(ARG)
#FUNCTION
SIF CMATCH(NO, MASTER) == 1
	RETURNF 0
;other body is already receiving handjob
SIF ADD_CHARA_WHO_TOUCHING(ARG, SET_YUBI, SET_P, 1) != PLAYER && SELECTCOM == 80
	RETURNF 1
RETURNF 0

@Add_Custom_SCOM14_Check(ARG)
#FUNCTION
SIF CMATCH(NO, MASTER) == 1
	RETURNF 0
;other body is already receiving fellatio
SIF ADD_CHARA_WHO_TOUCHING(ARG, SET_M, SET_P, 1) != PLAYER && GROUPMATCH(SELECTCOM, 60, 61, 65, 67, 68)
	RETURNF 1
;other body is already inserted
SIF ADD_CHARA_WHO_TOUCHING(ARG, SET_V, SET_P, 1) != PLAYER && SELECTCOM == 81
	RETURNF 1
RETURNF 0

@Add_Custom_SCOM15_Check(ARG)
#FUNCTION
SIF !(SELECTCOM == ADD_CUSTOM_COM && ADD_SELECTCUSTOMCOM == 15)
	RETURNF 0
SIF ADD_CHARA_WHO_TOUCHING(TARGET, SET_V, SET_P, 1) != PLAYER && ADD_CHARA_WHO_TOUCHING(TARGET, SET_A, SET_P, 1) != PLAYER
	RETURNF 0
SIF !TCVAR:PLAYER:イきそう
	RETURNF 0
RETURNF 1

@Add_Custom_Set_SCOM(ARG)
TFLAG:50 = ADD_CUSTOM_SCOM
ADD_SELECTED_SCOM = ARG

@SCOM99
SIF ADD_SELECTED_SCOM < 0
	RETURN -1

RESULT = -1
TRYCALLFORM ADD_CUSTOM_SCOM{ADD_SELECTED_SCOM}
RETURN RESULT

@CAN_SCOM99(ARG)
SIF ADD_SELECTED_SCOM < 0
	RETURN 0

TRYCCALLFORM ADD_CUSTOM_CAN_SCOM{ADD_SELECTED_SCOM}(ARG)
	RETURN RESULT
CATCH
	SIF !ARG
		PRINTFORML %Add_Custom_SCOM_Name(ADD_SELECTED_SCOM)%
ENDCATCH
RETURN 1

@COM_ABLE599
SIF ADD_SELECTED_SCOM < 0
	RETURN 0

RESULT = 0
TRYCALLFORM ADD_CUSTOM_SCOM_ABLE{ADD_SELECTED_SCOM}
RETURN RESULT

@MESSAGE_SCOM99
SIF ADD_SELECTED_SCOM < 0
	RETURN

TRYCALLFORM ADD_CUSTOM_MESSAGE_SCOM{ADD_SELECTED_SCOM}

@ADD_LESBIANISM_SCORE(CHARA1, CHARA2)
#FUNCTION
#DIM CHARA1
#DIM CHARA2
#DIM DYNAMIC SCORE1
#DIM DYNAMIC SCORE2

SIF !IS_FEMALE(CHARA1) || !IS_FEMALE(CHARA2)
	RETURNF 0

SCORE1 = ADD_LESBIANISM_SCORE_PART(CHARA1, CHARA2)
SCORE2 = ADD_LESBIANISM_SCORE_PART(CHARA2, CHARA1)

SIF !SCORE1 || !SCORE2
	RETURNF 0

;highest + half of other
RETURNF MAX(0, MAX(SCORE1, SCORE2) + MIN(SCORE1, SCORE2) / 2)

@ADD_LESBIANISM_SCORE_PART(CHARA, OTHER)
#FUNCTION
#DIM CHARA
#DIM OTHER
#DIM SCORE

SCORE = ABL:CHARA:レズっ気
IF TALENT:CHARA:性別嗜好 == -1
	TIMES SCORE, 1.50
	SCORE += 2
ENDIF
SIF !SCORE
	RETURNF 0
;minimum lesbianism threshold for enemies
IF RELATION:CHARA:OTHER
	SIF RELATION:CHARA:OTHER < 100 && (100-RELATION:CHARA:OTHER) < SCORE
		RETURNF 0
	
	SCORE = SCORE * RELATION:CHARA:OTHER / 100
ENDIF
RETURNF SCORE

@Counter不发_ADD_CUSTOM(ARG)
#LOCALSIZE 1
#LOCALSSIZE 1
#FUNCTION
SELECTCASE ARG
	;游泳
	CASE 1 TO 3
		RETURNF 1
	;换装补丁
	CASE 4
		RETURNF 1
	;摄影系统
	CASE 100, 101, 102
		RETURNF 1
	CASEELSE
		RETURNF 0
ENDSELECT

@AUTO_AEGI_BLOCKED_ADD_CUSTOM()
#LOCALSIZE 1
#LOCALSSIZE 1
#FUNCTION
SIF ADD_SELECTCUSTOMCOM < 0 || SELECTCOM != ADD_CUSTOM_COM
	RETURNF 0
;禁用自动娇喘
SIF GROUPMATCH(ADD_SELECTCUSTOMCOM,12,228)
	RETURNF 1

	

