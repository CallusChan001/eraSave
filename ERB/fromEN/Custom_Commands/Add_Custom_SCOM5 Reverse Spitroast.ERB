;Reverse Spitroast
@ADD_CUSTOM_SCOM5
#DIM C_TARGET
#DIM V_TARGET

V_TARGET = MASTER_POSE(SET_V,SET_P,1)
C_TARGET = MASTER_POSE(SET_C,SET_M,1)

;check intended position for target (SELECTCOM should be set at this point)
IF SELECTCOM == 65 && C_TARGET != TARGET ;not if TARGET is already target for cunnilingus
	V_TARGET = TARGET
ELSEIF SELECTCOM == 1 && V_TARGET != TARGET ;not if TARGET is already inserted in
	C_TARGET = TARGET
ENDIF

;cunnilingus part
;set this value manually to ensure cunnilingus continues
TEQUIP:C_TARGET:Ｃ接触部位 = 4

;行為者
TCVAR:C_TARGET:116 = PLAYER
;奉仕快楽
TCVAR:PLAYER:108 = 1

CSTR:C_TARGET:10 += @"{PLAYER}/"
;TCVAR:2 射精箇所フラグ（-1=コンドーム 1=膣内 2=アナル 3=手淫 4=口淫 5=パイズリ 6=素股 7=足コキ 8=体表 9=アナル奉仕 10=道具 11=触手 12=Ａ責め 13=Ｖ責め）
TCVAR:C_TARGET:2 = 4
;強度
TCVAR:C_TARGET:4 = 500

DOWNBASE:C_TARGET:体力 += 5
DOWNBASE:C_TARGET:気力 += 50
DOWNBASE:PLAYER:体力 += 10
DOWNBASE:PLAYER:気力 += 20
SOURCE:PLAYER:快Ｍ += 50
SOURCE:C_TARGET:快Ｃ = 80
SOURCE:C_TARGET:液体 = 1000
SOURCE:C_TARGET:露出 = 10
SOURCE:C_TARGET:逸脱 = 15
SOURCE:C_TARGET:反感 = 15

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:C_TARGET:自己愛 < 0 || TALENT:C_TARGET:抵抗
	SOURCE:C_TARGET:鬱屈 += 20

;性技補正
IF ABL:PLAYER:舌 >= 4
	SOURCE:C_TARGET:快Ｃ += 100
	SOURCE:C_TARGET:液体 += 400
ELSE
	SOURCE:C_TARGET:快Ｃ += ABL:PLAYER:舌 * 20
	SOURCE:C_TARGET:液体 += ABL:PLAYER:舌 * 80
ENDIF

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;同性愛　レズなら戻り値1ゲイなら戻り値2
IF HETEROSEX(C_TARGET,PLAYER)
	EXP:C_TARGET:(50 + HETEROSEX(C_TARGET,PLAYER)) += 3
	EXP:PLAYER:(50 + HETEROSEX(C_TARGET,PLAYER)) += 3
ENDIF

;口淫経験
EXP:PLAYER:口淫経験 ++
Qol_Info_Count_Array:C_TARGET:Qol_Count_MSexCunni += EXP_UP(GETNUM(EXP, "口淫経験"), PLAYER) ;qol custom code
;-------------------------------------------------
;時間停止分岐終わり、共通する素質補正や汚れの処理
;-------------------------------------------------
;舌を使う調教では、調教者の舌使いによって、快感のソース増加
IF TALENT:PLAYER:舌使い
	TIMES SOURCE:C_TARGET:快Ｃ , 1.50
	TIMES SOURCE:C_TARGET:液体 , 1.50
ENDIF
;猫舌では減少、調教者に嗜虐快楽経験を追加
IF TALENT:PLAYER:猫舌
	TIMES SOURCE:C_TARGET:快Ｃ , 0.75
	EXP:PLAYER:嗜虐快楽経験 += 1
ENDIF

;奴隷のＶ⇔調教者の口の汚れが移動
STAIN:C_TARGET:Ｖ |= STAIN:PLAYER:口
STAIN:PLAYER:口 |= STAIN:C_TARGET:Ｖ
IF HETEROSEX(C_TARGET,PLAYER)
	EXP:PLAYER:(50 + HETEROSEX(C_TARGET,PLAYER)) += 3
ENDIF

;vaginal part
;行為者
TCVAR:V_TARGET:116 = V_TARGET
;奉仕快楽フラグ
TCVAR:V_TARGET:108 = 1

CSTR:PLAYER:10 += @"{V_TARGET}/"
;TCVAR:2 射精箇所フラグ（-1=コンドーム 1=膣内 2=アナル 3=手淫 4=口淫 5=パイズリ 6=素股 7=足コキ 8=体表 9=アナル奉仕 10=道具 11=触手 12=Ａ責め 13=Ｖ責め）
TCVAR:PLAYER:2 = 1
TCVAR:PLAYER:童貞喪失 = 1
;強度
TCVAR:PLAYER:4 = 2000

CSTR:V_TARGET:10 += @"{PLAYER}/"
;TCVAR:2 射精箇所フラグ（-1=コンドーム 1=膣内 2=アナル 3=手淫 4=口淫 5=パイズリ 6=素股 7=足コキ 8=体表 9=アナル奉仕 10=道具 11=触手 12=Ａ責め 13=Ｖ責め）
TCVAR:V_TARGET:2 = 13
;強度
TCVAR:V_TARGET:4 = 1300

DOWNBASE:PLAYER:体力 += 20
DOWNBASE:PLAYER:気力 += 40

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
DOWNBASE:V_TARGET:体力 += 80
DOWNBASE:V_TARGET:気力 += 50
SOURCE:PLAYER:快Ｃ = 400

SOURCE:V_TARGET:快Ｖ = 500
SOURCE:V_TARGET:情愛 = 250
SOURCE:V_TARGET:性行動 = 250
SOURCE:V_TARGET:達成 = 250
SOURCE:V_TARGET:苦痛 = 500
SOURCE:V_TARGET:露出 = 120
SOURCE:V_TARGET:逸脱 = 600
SOURCE:V_TARGET:反感 = 800
;com stuff custom code, if they're horny af then we reduce hatred, it's bs that they get hate mark even though they'd ride the shit out of you in reverse mode
SIF CFLAG:V_TARGET:溜まってる度 >= 500 || ABL:V_TARGET:欲望 >= 5
	TIMES SOURCE:V_TARGET:反感, 0.50

;上のほうで計算した汚れデータ
SOURCE:V_TARGET:不潔 = Y*10 + 60
;Vへの苦痛は先に処理しておく
CALL PAIN_CHECK_V(V_TARGET)

;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
;射精に関係なく行われる処理はここから
LOCAL = TARGET
TARGET = V_TARGET
CALL 特殊ソース処理
CALL STAIN_膣内Ｐ挿入
TARGET = LOCAL

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;性交経験
EXP:PLAYER:挿入経験 ++
EXP:V_TARGET:Ｖ経験 ++

;異常経験
IF TALENT:V_TARGET:処女 && TALENT:V_TARGET:処女 != -1 && !GETBIT(CFLAG:3,1)
	IF ASSIPLAY && (TALENT:PLAYER:2 & 1)
		EXP:V_TARGET:異常経験 += 3
	ELSE
		EXP:V_TARGET:異常経験 += 2
	ENDIF
	SETBIT CFLAG:3,1
ENDIF
;同性愛　レズなら戻り値1ゲイなら戻り値2
IF HETEROSEX(V_TARGET,PLAYER)
	EXP:(50 + HETEROSEX(V_TARGET,PLAYER)) += 7
	EXP:PLAYER:(50 + HETEROSEX(V_TARGET,PLAYER)) += 7
ENDIF
EXP:V_TARGET:Ｖ性交経験 ++
IF !ASSIPLAY
	EXP:V_TARGET:愛情経験 += TALENT:V_TARGET:恋慕 * 2
	;処女
	SIF TALENT:V_TARGET:処女 && TALENT:V_TARGET:処女 != -1 && TALENT:V_TARGET:恋慕
		EXP:V_TARGET:愛情経験 += 5
ENDIF

Qol_Info_Count_Array:V_TARGET:Qol_Count_VSex ++

;同性愛　レズなら戻り値1ゲイなら戻り値2
IF HETEROSEX(V_TARGET,C_TARGET)
	EXP:V_TARGET:(50 + HETEROSEX(V_TARGET,C_TARGET)) += 3
	EXP:C_TARGET:(50 + HETEROSEX(V_TARGET,C_TARGET)) += 3
ENDIF
RETURN 1

@ADD_CUSTOM_CAN_SCOM5(ARG)
#DIM C_TARGET
#DIM V_TARGET

V_TARGET = MASTER_POSE(SET_V,SET_P,1)
C_TARGET = MASTER_POSE(SET_C,SET_M,1)

;check intended position for target (SELECTCOM should be set at this point)
IF SELECTCOM == 65 && C_TARGET != TARGET ;not if TARGET is already target for cunnilingus
	V_TARGET = TARGET
ELSEIF SELECTCOM == 1 && V_TARGET != TARGET ;not if TARGET is already inserted in
	C_TARGET = TARGET
ENDIF

SKIPDISP ARG
PRINTL 双女上位

LOCAL = 1
CALL QOL_COM_ORDER_STANDARD_CHECKS(36, QOL_COM_ORDER_VAGINAL_FULL | QOL_COM_ORDER_DUO, V_TARGET, C_TARGET)
LOCAL &= RESULT
CALL QOL_COM_ORDER_STANDARD_CHECKS(36, QOL_COM_ORDER_EXPOSURE_FULL | QOL_COM_ORDER_DUO, C_TARGET, V_TARGET)
LOCAL &= RESULT
SKIPDISP 0
RETURN LOCAL

@ADD_CUSTOM_SCOM_ABLE5
#DIM C_TARGET
#DIM V_TARGET
;交互挿入
SIF !TFLAG:100
	RETURN 0
;not during time stop
SIF FLAG:70
	RETURN 0
;一括管理
;NAS: Non 0 Master
SIF TARGET == MASTER
	RETURN 0
SIF TFLAG:102 != 2
	RETURN 0

V_TARGET = MASTER_POSE(SET_V,SET_P,1)
C_TARGET = MASTER_POSE(SET_C,SET_M,1)

;check intended position for target (SELECTCOM should be set at this point)
IF SELECTCOM == 65 && C_TARGET != TARGET ;not if TARGET is already target for cunnilingus
	V_TARGET = TARGET
ELSEIF SELECTCOM == 1 && V_TARGET != TARGET ;not if TARGET is already inserted in
	C_TARGET = TARGET
ENDIF

SIF !V_TARGET || !C_TARGET
	RETURN 0

;バイブ挿入中はダメ
SIF TEQUIP:V_TARGET:13
	RETURN 0

;オトコだとダメ
SIF !(TALENT:V_TARGET:2 & 1)
	RETURN 0
SIF !(TALENT:C_TARGET:2 & 1)
	RETURN 0

;体格差がある場合は経験がないとダメ
SIF Com_Size_Check("strict", V_TARGET)
	RETURN 0
;さらに体格差があると禁断の知識が必要
SIF Com_Size_Check("reg", V_TARGET)
	RETURN 0

;調教者がオトコ、半人半妖、ふたなり、もしくはペニスバンドがないとダメ
SIF !(HAS_PENIS(MASTER)) && ITEM:PBAND == 0
	RETURN 0
;処女で、従順4以下・レズっ気4以下のASSIの場合はできない（サドならＯＫ）
IF TALENT:処女 && ASSIPLAY
	SIF (ABL:ASSI:従順 <= 4 || ABL:ASSI:レズっ気 <= 4) && TALENT:ASSI:サド == 0
		RETURN 0
ENDIF

;処女かつ潤滑不足はだめ
SIF PALAM:V_TARGET:潤滑 < PALAMLV:2 && TALENT:V_TARGET:処女 && TALENT:V_TARGET:処女 != -1
	RETURN 0

;性器が汚れているとPLAYERの場合クンニはできない（調教者が汚れ無視か愛液、母乳、チョコのよごれならＯＫ）
SIF STAIN:C_TARGET:3 >= 2 && (STAIN:C_TARGET:3 != 16 && STAIN:C_TARGET:3 != 17 && STAIN:C_TARGET:3 != 128 && STAIN:C_TARGET:3 != 129 && STAIN:C_TARGET:3 != 144 && STAIN:C_TARGET:3 != 145) && ASSIPLAY == 0 && TALENT:PLAYER:汚臭耐性 < 2
	RETURN 0
;性器が汚れており、汚臭敏感で従順3以下のASSIの場合クンニはできない（調教者が汚れ無視か愛液、母乳、チョコのよごれならＯＫ）
IF STAIN:C_TARGET:3 >= 2 && (STAIN:C_TARGET:3 != 16 && STAIN:C_TARGET:3 != 17 && STAIN:C_TARGET:3 != 128 && STAIN:C_TARGET:3 != 129 && STAIN:C_TARGET:3 != 144 && STAIN:C_TARGET:3 != 145) && ASSIPLAY
	SIF ABL:ASSI:従順 <= 3 && TALENT:ASSI:汚臭耐性 < 0
		RETURN 0
ENDIF
		
;下半身着衣
SIF TEQUIP:V_TARGET:0 & 4
	RETURN 0
SIF TEQUIP:C_TARGET:0 & 4
	RETURN 0
;お風呂場プレイ中はプレイマットが無いとダメ
IF TEQUIP:31
	SIF ITEM:20 == 0 && NOITEM == 0
		RETURN 0
ENDIF

RETURN 1

@ADD_CUSTOM_MESSAGE_SCOM5
#DIM C_TARGET
#DIM V_TARGET
#DIM STATE_CHANGE ;0=same, 1=added vaginal, 2=added cunnilingus

V_TARGET = MASTER_POSE(SET_V,SET_P,1)
C_TARGET = MASTER_POSE(SET_C,SET_M,1)

;check intended position for target and state change
STATE_CHANGE = 0
IF SELECTCOM == 65 && C_TARGET != TARGET && V_TARGET != TARGET
	V_TARGET = TARGET
	STATE_CHANGE = 1
ELSEIF SELECTCOM == 1 && V_TARGET != TARGET && C_TARGET != TARGET
	C_TARGET = TARGET
	STATE_CHANGE = 2
ENDIF

IF ONGOING()
	PRINTL
	CLEARLINE 1
ENDIF

IF STATE_CHANGE == 0
	IF SELECTCOM == 65
		PRINTFORML 在继续侍奉%CALLNAME:C_TARGET%小穴的同时，%CALLNAME:MASTER%从下方猛烈挺腰插入了%CALLNAME:V_TARGET%的体内。
	ELSEIF SELECTCOM == 1
		PRINTFORML 在%CALLNAME:V_TARGET%继续骑在%TINKO(PLAYER)%上套弄时，%CALLNAME:MASTER%热情地舔遍了%CALLNAME:C_TARGET%的%SLIT(C_TARGET)%。
	ENDIF
	IF ABL:V_TARGET:レズっ気 >= 4 && ABL:C_TARGET:レズっ気 >= 4 && !RAND:4
		PRINTFORML %CALLNAME:C_TARGET%和%CALLNAME:V_TARGET%紧紧拥抱在一起，一边粗重地喘息着，一边让彼此火热的身体不断磨蹭着。
	ELSEIF ABL:V_TARGET:レズっ気 >= 2 && ABL:C_TARGET:レズっ気 >= 2 && !RAND:3
		PRINTFORML %CALLNAME:C_TARGET%和%CALLNAME:V_TARGET%手扣手支撑着彼此，小心翼翼地在%CALLNAME:MASTER%身上保持平衡。
	ENDIF
ELSEIF STATE_CHANGE == 1
	PRINTFORML 在用舌头侍奉%CALLNAME:C_TARGET%的同时，%CALLNAME:MASTER%示意%CALLNAME:V_TARGET%骑上来。
	PRINTFORM %CALLNAME:V_TARGET%
	SIF TEQUIP:V_TARGET:0 & 2
		PRINTFORM 将%SHOW_BOTTOM(V_TARGET,2)%拨到一边，然后

	IF TALENT:V_TARGET:処女 == 1
		PRINTFORM 跨坐上来，扶着肉棒缓缓地将腰肢降下，
		;処女＋小柄体型
		IF TALENT:V_TARGET:体型 < 0
			PRINTFORML 用稚嫩的处女蜜穴吞下%CALLNAME:MASTER%%TINKO(PLAYER)%……
		;処女
		ELSE
			PRINTFORML 用稚嫩的处女蜜穴吞下%CALLNAME:MASTER%%TINKO(PLAYER)%……
		ENDIF
	ELSE
		PRINTFORML 跨坐到%CALLNAME:MASTER%身上，缓缓地放低腰肢含住%TINKO(PLAYER)%……
	ENDIF
ELSEIF STATE_CHANGE == 2
	PRINTFORML 在与%CALLNAME:V_TARGET%女上位交合时，%CALLNAME:MASTER%招呼%CALLNAME:C_TARGET%坐到自己的脸上。
	PRINTFORM %CALLNAME:C_TARGET%跨上来、小心翼翼地把屁股压在%CALLNAME:MASTER%脸上。\n%CALLNAME:MASTER%
	SIF TEQUIP:C_TARGET:0 & 2
		PRINTFORM 勾住%SHOW_BOTTOM(C_TARGET,2)%拉到一边，
	PRINTFORML 抓住那%HIP(C_TARGET,1)%，开始热情地舔舐%SLIT(C_TARGET)%。
ENDIF

@ASSISTANT_KOJO_CUSTOM_SCOM5
#DIM C_TARGET
#DIM V_TARGET
V_TARGET = MASTER_POSE(SET_V,SET_P,1)
C_TARGET = MASTER_POSE(SET_C,SET_M,1)

IF TARGET == C_TARGET || C_TARGET == 0
    CALL KOJO_MESSAGE_SEND("COMMAND_ASSISTANT",-1,V_TARGET,-1,-1)
ELSE
    CALL KOJO_MESSAGE_SEND("COMMAND_ASSISTANT",-1,C_TARGET,-1,-1)
ENDIF