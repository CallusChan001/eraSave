@ADD_CUSTOM_COM1
;游泳
IF !TEQUIP:MASTER:SWIMMING
	SIF FLAG:デート相手
		CALL ADD_START_SWIMMING(FLAG:デート相手)
	CALL ADD_START_SWIMMING(MASTER)
	CALL ADD_BYSTANDERS_JOIN_SWIMMING
	TIME += 5
ELSE
	FOR LOCAL, 0, CHARANUM
		SIF TEQUIP:LOCAL:SWIMMING && Activity_Type:LOCAL != 100
			CALL ADD_END_SWIMMING(LOCAL)
	NEXT
ENDIF

RETURN 1

@ADD_CUSTOM_COM_ABLE1
SIF !TFLAG:100
	RETURN 0
SIF FLAG:70
	RETURN 0

SIF CFLAG:うふふ
	RETURN 0
;一括管理
SIF !ADD_SWIMMING_AREA(CFLAG:MASTER:現在位置)
	RETURN 0
;NAS: Non 0 MASTER
IF FLAG:デート相手 != MASTER ;if with a date, must target date
	;SIF (!MOREDATE && TARGET != FLAG:デート相手) || (MOREDATE && !IS_IN_GROUP_DATE(TARGET))
	;	RETURN 0
	SIF BASE:気力 <= 0 ;date is tired
		RETURN 0
	SIF CFLAG:行動
		RETURN 0
	SIF !ADD_SWIMMING_CONSTITUTION(FLAG:デート相手)
		RETURN 0
ENDIF

SIF BASE:MASTER:気力 <= 0
	RETURN 0
SIF TFLAG:デート道中
	RETURN 0
RETURN 1

@ADD_CUSTOM_MESSAGE_COM1
IF TEQUIP:MASTER:SWIMMING
	PRINTFORML +++打算%COND_STR(@"和%CALLNAME:TARGET%", (FLAG:デート相手 != MASTER || CFLAG:同行))%游一会儿泳
ELSE
	PRINTFORML 游泳结束了，+++换上了原来的衣服
ENDIF

@ADD_SWIMMING_DESCRIPTION(LOCATION)
#FUNCTIONS
#DIM LOCATION
SELECTCASE LOCATION
	CASE GET_MAP_REPLACEMENT(1015)
		RETURNF "静海"
	CASE GET_MAP_REPLACEMENT(P343湖畔), GET_MAP_REPLACEMENT(347)
		RETURNF "湖"
	CASE GET_MAP_REPLACEMENT(703), GET_MAP_REPLACEMENT(602), GET_MAP_REPLACEMENT(945)
		RETURNF "河"
	CASEELSE
		RETURNF "水"
ENDSELECT

@ADD_SWIMMING_AREA(LOCATION)
#FUNCTION
#DIM LOCATION
;RETURNF GROUPMATCH(LOCATION, GET_MAP_REPLACEMENT(P343湖畔), GET_MAP_REPLACEMENT(P347霧の湖), GET_MAP_REPLACEMENT(P703玄武の沢), GET_MAP_REPLACEMENT(P945河岸), GET_MAP_REPLACEMENT(P602赛河原), GET_MAP_REPLACEMENT(P806瀑布背面), GET_MAP_REPLACEMENT(P1015沙滩))
RETURNF GROUPMATCH(LOCATION, GET_MAP_REPLACEMENT(343), GET_MAP_REPLACEMENT(347), GET_MAP_REPLACEMENT(703), GET_MAP_REPLACEMENT(945), GET_MAP_REPLACEMENT(602), GET_MAP_REPLACEMENT(806), GET_MAP_REPLACEMENT(1015))

@ADD_RUNNING_WATER(LOCATION)
#FUNCTION
#DIM LOCATION
;RETURNF GROUPMATCH(LOCATION,GET_MAP_REPLACEMENT(P703玄武の沢),GET_MAP_REPLACEMENT(P945河岸), GET_MAP_REPLACEMENT(P602赛河原), GET_MAP_REPLACEMENT(P1015沙滩))
RETURNF GROUPMATCH(LOCATION,GET_MAP_REPLACEMENT(703),GET_MAP_REPLACEMENT(945), GET_MAP_REPLACEMENT(602), GET_MAP_REPLACEMENT(1015))

@ADD_SWIMMING_DAMAGE(ARG)
#FUNCTION
SIF TALENT:ARG:143 == 4
	RETURNF 1
SIF (TALENT:ARG:妖怪 == 3 || TALENT:ARG:吸血鬼) && ADD_RUNNING_WATER(CFLAG:ARG:現在位置)
	RETURNF 2
SIF (!ADD_SWIMMING_SEASON() && CFLAG:ARG:現在位置/100 != 9) && !TALENT:ARG:水棲 && !TALENT:ARG:氷精
	RETURNF 1
RETURNF 0

@ADD_SWIMMING_SEASON
#FUNCTION
SELECTCASE DAY:2
	CASE 1
		RETURNF DAY:3 > 15 ;暮春
	CASE 2
		RETURNF 1	;夏天
	CASE 3
		RETURNF DAY:3 < 15 ;早秋
	CASE 4
		RETURNF 0 ;早春、冬季、暮秋——冬泳
ENDSELECT

@ADD_START_SWIMMING(ARG)
IF !ADD_IN_SWIMWEAR(ARG)
	CALL ADD_SWIMMING_OUTFIT(ARG)
ENDIF
TEQUIP:ARG:SWIMMING = 1

@ADD_END_SWIMMING(ARG)
IF TCVAR:ARG:EVENT_CLOTHING_SET
	SIF CFLAG:ARG:現在位置 == CFLAG:MASTER:現在位置
		PRINTFORML %CALLNAME:ARG%换回了衣服
	CALL RESET_EVENT_CLOTHES(ARG)
ENDIF
TEQUIP:ARG:SWIMMING = 0

@ADD_SWIMMING_OUTFIT(ARG)
#DIM DYNAMIC WEIGHTS, 100
;skinny dipping? (do later)
#DIM BIKINI
#DIM MICRO_BIKINI
#DIM SCHOOL_SWIMSUIT
#DIM COMPETITION_SWIMSUIT
#DIM ONE_PIECE_SWIMSUIT
#DIM SLING_SWIMSUIT
#DIM SWIMMING_TRUNKS
#DIM SWIM_DIAP
#DIM SELECTED_SWIMWEAR
#DIM DYNAMIC EROTICISM

IF !BIKINI ;only fetch ids once, since they're not character-dependent
	SWIMMING_TRUNKS = OBJNAME_TO_ID(ARG, "GET", "衣装セット", "泳裤装")
	BIKINI = OBJNAME_TO_ID(ARG, "GET", "衣装セット", "比基尼套装")
	MICRO_BIKINI = OBJNAME_TO_ID(ARG, "GET", "衣装セット", "迷你比基尼套装")
	SCHOOL_SWIMSUIT = OBJNAME_TO_ID(ARG, "GET", "衣装セット", "死库水泳衣")
	COMPETITION_SWIMSUIT = OBJNAME_TO_ID(ARG, "GET", "衣装セット", "竞技泳衣")
	ONE_PIECE_SWIMSUIT = OBJNAME_TO_ID(ARG, "GET", "衣装セット", "连体泳衣")
	SLING_SWIMSUIT = OBJNAME_TO_ID(ARG, "GET", "衣装セット", "Y字泳衣")
	;SWIM_DIAP = OBJNAME_TO_ID(ARG, "GET", "衣装セット", "Swim Diaper Set")
ENDIF

IF 0;CANT_UNDIES(ARG)
	;skip those who can't wear undies
	SELECTED_SWIMWEAR = 100
ELSEIF 0;(CanWearUnderwear(ARG, "Diaper")) && nOmutsu
	;NAS: incon 2hus wear swim diapers
	SELECTED_SWIMWEAR = SWIM_DIAP
ELSEIF IS_FEMALE(ARG)
	IF !TALENT:ARG:無知 || TALENT:ARG:小悪魔 ;not ignorant, or a temptress
		EROTICISM = MIN(ABL:ARG:露出癖, 30) ;exposure
		EROTICISM -= OTOKOGIRAI(ARG) ? 3 # 0 ;misandrist
		EROTICISM += TALENT:ARG:性的興味 * 2 ;conservative/curious
		EROTICISM += TALENT:ARG:目立ちたがり * 3 ;show-off
		EROTICISM += TALENT:ARG:セフレ ? 3 # 0 ;sex friend
		EROTICISM += TALENT:ARG:愛欲 * 5 ;lust
		EROTICISM -= TALENT:ARG:貞操 * 5 ;virtuous/unchaste
		EROTICISM += TALENT:ARG:小悪魔 * 10 ;seductress
		EROTICISM += TALENT:ARG:胸圍 * 2 ;breast size
		EROTICISM += TALENT:ARG:ヒップサイズ * 2 ;butt size
		EROTICISM -= TALENT:ARG:羞恥心 * 6 ;shameless/shy

		SIF GET_TARGETNUM() > 1 && !TALENT:ARG:淫乱
			EROTICISM /= 2
		EROTICISM = MAX(EROTICISM, 0)
		SIF TALENT:ARG:無知 && !TALENT:ARG:小悪魔 ;ignorant, not a temptress
			EROTICISM = 0
	ENDIF

	WEIGHTS:BIKINI = 20
	WEIGHTS:MICRO_BIKINI = EROTICISM
	WEIGHTS:ONE_PIECE_SWIMSUIT = 10
	WEIGHTS:COMPETITION_SWIMSUIT = ABL:ARG:戦闘能力 * 2 * (CFLAG:ARG:性格傾向 == 4 ? 2 # 1) ;combat skill + serious personality

	IF (IS_CHILD(ARG, "mind") || IS_CHILD(ARG, "age")) && !TALENT:ARG:小悪魔 ;child's mindset
		WEIGHTS:MICRO_BIKINI /= 2
		WEIGHTS:SCHOOL_SWIMSUIT = 40
		IF TALENT:ARG:胸圍 < 0
			WEIGHTS:SWIMMING_TRUNKS = 5
		ELSE
			WEIGHTS:SLING_SWIMSUIT = EROTICISM / 4
		ENDIF
	ELSEIF IS_CHILD(ARG, "body") && !TALENT:ARG:小悪魔 ;child's body
		WEIGHTS:SCHOOL_SWIMSUIT = 10
		WEIGHTS:ONE_PIECE_SWIMSUIT = 20
		IF TALENT:ARG:胸圍 < 0
			WEIGHTS:SWIMMING_TRUNKS = 5
		ELSE
			WEIGHTS:SLING_SWIMSUIT = EROTICISM / 2
		ENDIF
	ELSE ;adults
		WEIGHTS:SCHOOL_SWIMSUIT = EROTICISM / 2 ;it's basically fetish wear to adults
		WEIGHTS:SLING_SWIMSUIT = EROTICISM
		SIF TALENT:ARG:胸圍 < 0
			WEIGHTS:SWIMMING_TRUNKS = EROTICISM / 4
	ENDIF
	SELECTED_SWIMWEAR = ARRAY_HIT(WEIGHTS, 100)
ELSE
	SELECTED_SWIMWEAR = SWIMMING_TRUNKS
ENDIF
CALL KOJO_MESSAGE_SEND("CUSTOM_EVENT",1,ARG,SELECTED_SWIMWEAR)
CALL SET_EVENT_CLOTHES(ARG, SELECTED_SWIMWEAR)

@ADD_IN_SWIMWEAR(ARG)
#FUNCTION
RETURNF GET_INT(ARG, "衣装セット", CFLAG:ARG:基本服装セット, "SWIMWEAR")

@ADD_BYSTANDERS_JOIN_SWIMMING
FOR LOCAL, 1, GET_TARGETNUM() + 1
	SIF TEQUIP:(TARGET:LOCAL):SWIMMING 
		CONTINUE
	SIF !ADD_SWIMMING_CONSTITUTION(TARGET:LOCAL)
		CONTINUE
	SIF MARK:(TARGET:LOCAL):反発刻印 || CFLAG:(TARGET:LOCAL):ブチギレ
		CONTINUE
	;吸血鬼や動物耳には従順5がないと使用不可
	SIF ADD_SWIMMING_DAMAGE(TARGET:LOCAL) && ABL:(TARGET:LOCAL):従順 < 5
		CONTINUE
	SIF CFLAG:(TARGET:LOCAL):同行 || CFLAG:(TARGET:LOCAL):態度 > 0
		CALL ADD_START_SWIMMING(TARGET:LOCAL)
NEXT

;检测天气情况（参与下面的角色加入游泳判定）
@ADD_WEATHER_HARSHNESS(LOCATION)
#FUNCTION
#DIM LOCATION
;基于COM411(战斗训练)
SIF !OUTROOF(LOCATION) ;室内强制掷0，一般用不着（除非红魔馆地下（好像）的那块水池也能算为游泳地点）
	RETURNF 0
SELECTCASE TIME:5
	CASE 4,6,8,10,12,13	;雨、雾雨、雪、细雪、雨夹雪、冰雹
		IF WIND_VELOCITY >= 1
		;(天气补丁特供)强/暴风
			RETURNF 2
		ELSE
			;恶劣天气
			RETURNF 1
		ENDIF
	CASE 5,9,14,15	;(大/暴)(雨/雪)
		IF WIND_VELOCITY >= 1
		;(天气补丁特供)强/暴风
			RETURNF 3
		ELSE
			RETURNF 2
		ENDIF
	;それ以外
	CASEELSE	;晴天、多云、阴天、雾、雾雪
		SIF WIND_VELOCITY == 1
		;(天气补丁特供)强风
			RETURNF 1
		SIF WIND_VELOCITY >= 2
		;(天气补丁特供)暴风
			RETURNF 2
ENDSELECT
RETURNF 0

;检测在场角色是否会加入游泳（包括恶劣环境）
;总结：对应角色还有气力+身体素质过硬（流水与吸血鬼、一般环境判断）=掷1
@ADD_SWIMMING_CONSTITUTION(CHARA)
#FUNCTION
#DIM CHARA
SIF !ADD_SWIMMING_AREA(CFLAG:CHARA:現在位置)
	RETURNF 0
IF !BASE:CHARA:気力 ;角色气力为0
	SIF CHARA == TARGET
		TFLAG:194 = 1
	RETURNF 0
ENDIF
SIF !CHARA
	RETURNF 1 ;master can always join

LOCAL = ADD_SWIMMING_DAMAGE(CHARA)
;吸血鬼
IF LOCAL == 2
	SIF CHARA == TARGET
		TFLAG:194 = 3
	SIF MAXBASE:CHARA:体力 < 4000
		RETURNF 0
;猫耳
ELSEIF LOCAL == 1 && TALENT:CHARA:143 == 4
	SIF CHARA == TARGET
		TFLAG:194 = 5
	SIF MAXBASE:CHARA:体力 < 3000
		RETURNF 0
;冷水
ELSEIF LOCAL == 1
	SIF CHARA == TARGET
		TFLAG:194 = 4
	SIF MAXBASE:CHARA:体力 < 2500
		RETURNF 0
ENDIF
SELECTCASE ADD_WEATHER_HARSHNESS(CFLAG:MASTER:現在位置) ;不同天气条件对应的角色身体素质（体力上限）要求不同
	CASE 0
		RETURNF 1
	CASE 1
		SIF MAXBASE:CHARA:体力 >= 2000
			RETURNF 1
	CASE 2
		SIF MAXBASE:CHARA:体力 >= 3500
			RETURNF 1
	CASE 3
		SIF MAXBASE:CHARA:体力 >= 5000
			RETURNF 1
ENDSELECT
RETURNF 1