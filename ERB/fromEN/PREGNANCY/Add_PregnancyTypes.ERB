@PREGNANCY_TYPE_OPTIONS
#DIM DYNAMIC STARTLINECOUNT,2
#DIM DYNAMIC MANUAL_CHARA_RESULT
#DIM CONST COLOR_NORMAL = 0x6A7783 ; Gray
#DIM CONST COLOR_LITTERS = 0x03B484 ; Green
#DIM CONST COLOR_EGG_LAYERS = 0xD55E00 ; Orange
#DIM CONST COLOR_CLUTCHERS = 0xF0E442 ; Yellow
	;Pregnancy type settings menu, accessible through new game or modded options.
STARTLINECOUNT:0 = LINECOUNT
$TOP
CLEARLINE LINECOUNT - STARTLINECOUNT:0
PRINTFORMDL
CALL D_LINE
PRINTFORMDL
PRINTFORMDL [0] 全员手动设定
PRINTFORMDL
PRINTFORMDL [1] 多产角色育儿补贴: \@ FLAG:CHILD_SUPPORT_RELIEF ? ON # OFF \@
PRINTFORMDL [2] 路人角色特殊妊娠: \@ FLAG:MOB_PREGNANCY_TYPES ? ON # OFF \@
PRINTFORMDL
PRINTFORMDL [3] 胎内孵化选项: \@ FLAG:EGGNANCY ? ON # OFF \@
PRINTFORMDL [4] 双胞胎发生率: {FLAG:TWINS_CHANCE}\%
PRINTFORMDL [5] 三胞胎发生率: {FLAG:TRIPLETS_CHANCE}\%
PRINTFORMDL
PRINTFORMDL [6] 说明
PRINTFORMDL
PRINTFORMDL [7] 全员采用新系统预设
PRINTFORMDL [8] 关闭特殊妊娠（默认）
PRINTFORMDL
PRINTFORMDL [9] 返回
INPUT
SELECTCASE RESULT
	CASE 0
		STARTLINECOUNT:1 = LINECOUNT
		$MANUALSETTING
		CLEARLINE LINECOUNT - STARTLINECOUNT:1
		PRINTFORMDL
		SIF FLAG:並び替え
			CALL CHARASORT_EX
		FOR LOCAL:2, 1, EX_CHARANUM + 1
			IF FLAG:並び替え
				LOCAL:1 = RESULT:(LOCAL:2)
				SIF LOCAL:1 < 0
					CONTINUE
			ELSE
				LOCAL:1 = LOCAL:2
				SIF LOCAL:1 >= CHARANUM
					BREAK
			ENDIF
			SIF CFLAG:(LOCAL:1):出禁
				CONTINUE
			SIF !IS_FEMALE(LOCAL:1)
				CONTINUE
			SIF TALENT:(LOCAL:2):行きずり
				CONTINUE
			SELECTCASE TALENT:(LOCAL:1):PREGNANCY_TYPE
				CASE PREGNANCY_LITTERS
					SETCOLOR COLOR_LITTERS
				CASE PREGNANCY_EGGS
					SETCOLOR COLOR_EGG_LAYERS
				CASE PREGNANCY_CLUTCH
					SETCOLOR COLOR_CLUTCHERS
				CASEELSE
					SETCOLOR COLOR_NORMAL
			ENDSELECT
			PRINTFORMLC [{LOCAL:1, 3}] %CALLNAME:(LOCAL:1)%
			RESETCOLOR
		NEXT
		PRINTFORML 
		DRAWLINE
		PRINTFORMD 颜色：
		CALL COLORMESSAGE("正常妊娠", COLOR_NORMAL, 0)
		CALL COLORMESSAGE("多胎    ", COLOR_LITTERS, 0)
		CALL COLORMESSAGE("产卵（单） ", COLOR_EGG_LAYERS, 0)
		CALL COLORMESSAGE("产卵（多）", COLOR_CLUTCHERS, 0)
		PRINTL
		PRINTL
		PRINTBUTTON "[全员正常妊娠]※默认", 980
		PRINT   
		PRINTBUTTON "[全员多胎]", 981
		PRINT   
		PRINTBUTTON "[全员产卵（单）]", 982
		PRINT   
		PRINTBUTTON "[全员产卵（多）]", 983
		PRINTL
		PRINTBUTTON "[没有多胎]", 984
		PRINT   
		PRINTBUTTON "[没有产卵]", 985
		PRINTL
		PRINTBUTTON "[生产时，必定生多个]", 986
		PRINT   
		PRINTBUTTON "[产卵时，必定产单个]", 987
		PRINT   
		PRINTBUTTON "[产卵时，必定产多个]", 988
		PRINTL
		PRINTBUTTON "[全员采用新系统预设]", 989
		PRINTL
		PRINTL
		PRINTFORML [999] 返回
		INPUT
		LOCAL:1 = RESULT
		SIF LOCAL:1 == 999
			GOTO TOP
		IF INRANGE(LOCAL:1,1,CHARANUM)
			MANUAL_CHARA_RESULT = LOCAL:1
			LOCAL:1 = 1
		ENDIF
		SELECTCASE LOCAL:1
			CASE 1
				IF !IS_FEMALE(MANUAL_CHARA_RESULT)
					PRINTFORMDW %CALLNAME:MANUAL_CHARA_RESULT%不能怀孕
					GOTO MANUALSETTING
				ENDIF
				CLEARLINE 1
				PRINTL 
				SELECTCASE TALENT:MANUAL_CHARA_RESULT:PREGNANCY_TYPE
					CASE PREGNANCY_NORMAL
						PRINTFORMDL %CALLNAME:MANUAL_CHARA_RESULT%目前是正常妊娠
					CASE PREGNANCY_LITTERS
						PRINTFORMDL %CALLNAME:MANUAL_CHARA_RESULT%目前是多胎妊娠
					CASE PREGNANCY_EGGS
						PRINTFORMDL %CALLNAME:MANUAL_CHARA_RESULT%目前会产卵
					CASE PREGNANCY_CLUTCH
						PRINTFORMDL %CALLNAME:MANUAL_CHARA_RESULT%目前会产下多枚卵
					ENDSELECT
				FOR LOCAL, 0, 4
					SIF TALENT:MANUAL_CHARA_RESULT:PREGNANCY_TYPE == LOCAL
					SETCOLOR 50,50,50
					PRINTFORM [\@ TALENT:MANUAL_CHARA_RESULT:PREGNANCY_TYPE == LOCAL ? X # {LOCAL} \@] 
					SELECTCASE LOCAL
						CASE PREGNANCY_NORMAL
							PRINTFORML 正常妊娠
						CASE PREGNANCY_LITTERS
							PRINTFORML 多胎
						CASE PREGNANCY_EGGS
							PRINTFORML 产卵（单）
						CASE PREGNANCY_CLUTCH
							PRINTFORML 产卵（多）
					ENDSELECT
					RESETCOLOR
				NEXT
				PRINTFORMDL
				PRINTFORMDL [4] 决定
				$LOOP2
				INPUT
				SELECTCASE RESULT
					CASE 0
						TALENT:MANUAL_CHARA_RESULT:PREGNANCY_TYPE = PREGNANCY_NORMAL
						PRINTFORMDW %CALLNAME:MANUAL_CHARA_RESULT%会进行正常的生产。可能会生双胞胎或三胞胎。
					CASE 1
						TALENT:MANUAL_CHARA_RESULT:PREGNANCY_TYPE = PREGNANCY_LITTERS
						PRINTFORMDW %CALLNAME:MANUAL_CHARA_RESULT%现在是多胎妊娠。
					CASE 2
						TALENT:MANUAL_CHARA_RESULT:PREGNANCY_TYPE = PREGNANCY_EGGS
						PRINTFORMDW %CALLNAME:MANUAL_CHARA_RESULT%现在是产卵生育。
					CASE 3
						TALENT:MANUAL_CHARA_RESULT:PREGNANCY_TYPE = PREGNANCY_CLUTCH
						PRINTFORMDW %CALLNAME:MANUAL_CHARA_RESULT%现在是产多个卵生育。
					CASE 4
					CASEELSE
						GOTO LOOP2
				ENDSELECT
			CASE 980
				CVARSET TALENT, GETNUM(TALENT, "PREGNANCY_TYPE"), PREGNANCY_NORMAL
				PRINTFORMDW 所有特殊妊娠都已禁用
			CASE 981
				CVARSET TALENT, GETNUM(TALENT, "PREGNANCY_TYPE"), PREGNANCY_LITTERS
				PRINTFORMDW 所有人都将多胎生育
			CASE 982
				CVARSET TALENT, GETNUM(TALENT, "PREGNANCY_TYPE"), PREGNANCY_EGGS
				PRINTFORMDW 所有人都将产卵
			CASE 983
				CVARSET TALENT, GETNUM(TALENT, "PREGNANCY_TYPE"), PREGNANCY_CLUTCH
				PRINTFORMDW 所有人都将产卵(多个)
			CASE 984
				FOR LOCAL,1,CHARANUM
					SIF TALENT:LOCAL:PREGNANCY_TYPE == PREGNANCY_LITTERS
						TALENT:LOCAL:PREGNANCY_TYPE = PREGNANCY_NORMAL
				NEXT
				PRINTFORMDW 多胞胎现在禁用了
			CASE 985
				FOR LOCAL,1,CHARANUM
					SIF GROUPMATCH(TALENT:LOCAL:PREGNANCY_TYPE, PREGNANCY_EGGS, PREGNANCY_CLUTCH)
						TALENT:LOCAL:PREGNANCY_TYPE = PREGNANCY_NORMAL
				NEXT
				PRINTFORMDW 产卵现在禁用了
			CASE 986
				FOR LOCAL,1,CHARANUM
					SIF TALENT:LOCAL:PREGNANCY_TYPE == PREGNANCY_NORMAL
						TALENT:LOCAL:PREGNANCY_TYPE = PREGNANCY_LITTERS
				NEXT
				PRINTFORMDW 胎生的角色都变成了多胎妊娠
			CASE 987
				FOR LOCAL,1,CHARANUM
					SIF TALENT:LOCAL:PREGNANCY_TYPE == PREGNANCY_CLUTCH
						TALENT:LOCAL:PREGNANCY_TYPE = PREGNANCY_EGGS
				NEXT
				PRINTFORMDW 产卵的角色都变成了一次只产一枚
			CASE 988
				FOR LOCAL,1,CHARANUM
					SIF TALENT:LOCAL:PREGNANCY_TYPE == PREGNANCY_EGGS
						TALENT:LOCAL:PREGNANCY_TYPE = PREGNANCY_CLUTCH
				NEXT
				PRINTFORMDW 产卵的角色都变成了一次产多枚
			CASE 989
				FOR LOCAL,1,CHARANUM
					TALENT:LOCAL:PREGNANCY_TYPE = PREGNANCY_CHARA_DEFAULT(LOCAL)
				NEXT
				PRINTFORMDW 全员采用新系统预设
			CASEELSE
		ENDSELECT
		GOTO MANUALSETTING
	CASE 1
		IF FLAG:CHILD_SUPPORT_RELIEF
			PRINTFORMDW 多胞胎子女将按照人数产生以往默认的抚养费。
		ELSE
			PRINTFORMDW 多胞胎子女的抚养费降低，期望上将产生与旧系统中一次抚养周期相近的抚养费
		ENDIF
		FLAG:CHILD_SUPPORT_RELIEF = !FLAG:CHILD_SUPPORT_RELIEF
	CASE 2
		IF FLAG:MOB_PREGNANCY_TYPES
			PRINTFORMDW 新创建的路人角色全部为正常妊娠
		ELSE
			PRINTFORMDW 新创建的路人角色会根据种族决定妊娠类型
		ENDIF
		FLAG:MOB_PREGNANCY_TYPES = !FLAG:MOB_PREGNANCY_TYPES
	CASE 3
		IF FLAG:EGGNANCY
			PRINTFORMDW 产卵角色会产下受精卵。
		ELSE
			PRINTFORMDW 产卵角色会在腹中孵育。
		ENDIF
		FLAG:EGGNANCY = !FLAG:EGGNANCY
	CASE 4
		PRINTFORML 请输入双胞胎出生的概率(0-100)。 当前 {FLAG:TWINS_CHANCE}\%
		CALL INPUT_RANGE(0, 100)
		FLAG:TWINS_CHANCE = RESULT
	CASE 5
		PRINTFORML 请输入三胞胎出生的概率(0-100)。 当前 {FLAG:TRIPLETS_CHANCE}\%
		CALL INPUT_RANGE(0, 100)
		FLAG:TRIPLETS_CHANCE = RESULT
	CASE 6
		PRINTFORMDL
		CALL D_LINE
		PRINTFORMDL
		PRINTFORMDL 可以设置妊娠类型。
		PRINTFORML ※蛇版不默认开启新生育系统，全员应用原版正常妊娠
		PRINTFORMDL
		PRINTFORMDL 通常 - 基本上生一个孩子。也可能生双胞胎或三胞胎。
		PRINTFORMDL 多胎 - 一次生2～6个孩子。
		PRINTFORMDL 产卵（单） - 受精后立即产卵，并照顾到孵化。在此期间也可能再次受精。
		PRINTFORMDL 产卵（复） - 一次产2～6个卵。
		PRINTFORMDL
		PRINTFORMDL 陷落后的产卵角色可能会在约会结束后给你少女之卵。
		PRINTFORMDL
		PRINTFORMDL 启用育儿补贴后，多产角色的抚养费将从通常的¥1000变为每人¥330。
		PRINTFORMDL 平均来说，总额大致相同。
		PRINTFORMDL
		PRINTFORMDL 开启胎内孵化选项后，卵生角色不会立即产卵，而是在体内孵化，外观和行为上与普通妊娠相同。
		PRINTFORMDL 产卵会在孵化前进行。给你少女之卵的事件也会正常发生。
		WAIT
	CASE 7
		PRINTL
		PRINTFORMDL 是否重置特殊妊娠设置为新生育系统的预设？尚未出生的孩子不会消失。
		PRINTL
		CALL ASK_YN
		IF !RESULT
			CALL PREGNANCY_TYPES_NEWGAME_SET
			FLAG:CHILD_SUPPORT_RELIEF = 1
			FLAG:MOB_PREGNANCY_TYPES = 1
			FLAG:EGGNANCY = 0
			FLAG:TWINS_CHANCE = 10
			FLAG:TRIPLETS_CHANCE = 2
		ENDIF
	CASE 8
		PRINTL
		PRINTFORMDL 是否禁用所有特殊妊娠，采用旧的生育系统？尚未出生的孩子不会消失。（这是默认选项）
		PRINTL
		CALL ASK_YN
		IF !RESULT
			FOR LOCAL,1,CHARANUM
				TALENT:LOCAL:PREGNANCY_TYPE = PREGNANCY_NORMAL
			NEXT
			FLAG:MOB_PREGNANCY_TYPES = 0
			FLAG:EGGNANCY = 0
			FLAG:TWINS_CHANCE = 0
			FLAG:TRIPLETS_CHANCE = 0
			FLAG:USE_EXPANDED_PREGNANCY_OPTIONS = 0
		ELSE
			FLAG:USE_EXPANDED_PREGNANCY_OPTIONS = 1
		ENDIF
	CASE 9
		CALL PREGNANCY_CONVERT_TYPES
		RETURN
	CASEELSE
		GOTO TOP
ENDSELECT
GOTO TOP

@CONCEPTION_COUNT(Character,ProgressDay=1)
	;Determine how many children should be conceived from a single impregnation.
#FUNCTION
#DIM DYNAMIC Character
#DIM DYNAMIC ProgressDay
#DIM DYNAMIC ConceptionCount
IF !FLAG:USE_EXPANDED_PREGNANCY_OPTIONS
	CFLAG:Character:妊娠経過日数 = 1
	CFLAG:Character:WithChildCount ++
	RETURNF 1
ENDIF
SELECTCASE TALENT:Character:PREGNANCY_TYPE
	CASE PREGNANCY_LITTERS
			;Has Litters.
		CFLAG:Character:妊娠経過日数 = MAX(CFLAG:Character:妊娠経過日数,ProgressDay)
			;Usually has 3-4, with a small chance to have up to 6 or down to 2.
		ConceptionCount += RAND:2+3
		IF !RAND:10
			ConceptionCount += RAND:2+1
		ELSEIF !RAND:10
			ConceptionCount -= 1
		ENDIF
	CASE PREGNANCY_EGGS
			;Egg Layer.
		IF FLAG:EGGNANCY
				;Eggnancies are treated as regular pregnancies to work with existing functions.
			CFLAG:Character:妊娠経過日数 = MAX(CFLAG:Character:妊娠経過日数,ProgressDay)
		ELSE
			TALENT:Character:NESTING = MAX(TALENT:Character:NESTING,ProgressDay)
		ENDIF
		ConceptionCount++
	CASE PREGNANCY_CLUTCH
			;Clutch Layer.
		IF FLAG:EGGNANCY
			CFLAG:Character:妊娠経過日数 = MAX(CFLAG:Character:妊娠経過日数,ProgressDay)
		ELSE
			TALENT:Character:NESTING = MAX(TALENT:Character:NESTING,ProgressDay)
		ENDIF
		ConceptionCount += RAND:2+3
		IF !RAND:10
			ConceptionCount += RAND:2+1
		ELSEIF !RAND:10
			ConceptionCount -= 1
		ENDIF
	CASEELSE
			;Normal.
		CFLAG:Character:妊娠経過日数 = MAX(CFLAG:Character:妊娠経過日数,ProgressDay)
		ConceptionCount++
		IF RAND:100 < FLAG:TRIPLETS_CHANCE
			ConceptionCount += 2
		ELSEIF RAND:100 < FLAG:TWINS_CHANCE
			ConceptionCount++
		ENDIF
ENDSELECT
CFLAG:Character:WithChildCount = MIN(CFLAG:Character:WithChildCount+ConceptionCount,MAXCHILD)
RETURNF ConceptionCount

@NESTING_MESSAGE
	;Display descriptions of fertilized eggs in the rooms of nesting girls.
#DIM DYNAMIC Children
#DIMS DYNAMIC eggs
SIF !FLAG:USE_EXPANDED_PREGNANCY_OPTIONS
	RETURN

FOR LOCAL,1,CHARANUM
		;Loop through all characters.
	IF CFLAG:LOCAL:初期位置 == CFLAG:MASTER:現在位置 && TALENT:LOCAL:NESTING
			;Find if the player is in their room and they're nesting.
		IF IS_HERE(LOCAL) && !FLAG:70 && !CFLAG:LOCAL:睡眠 && !CFLAG:LOCAL:うふふ && !TCVAR:LOCAL:泥酔 && !CFLAG:LOCAL:イタズラ && !CFLAG:LOCAL:添い寝中
				;If time isn't stopped and they're not asleep, having sex, drunk, being molested, or in bed with the player.
			Children = CFLAG:LOCAL:WithChildCount
			PRINTFORM %CALLNAME:LOCAL%正在
			SELECTCASE RAND:2
				CASE 0
					PRINTFORML 观察卵的情况
				CASE 1
					PRINTDATAL
						DATAFORM 蜷缩在窝上温暖卵
						DATAFORM 用体温孵育卵
						DATAFORM 珍重地抱着卵
					ENDDATA
			ENDSELECT
		ELSEIF Children > 1
			PRINTFORMDL %CALLNAME:LOCAL%的{Children}枚卵正在成长
		ELSE
			PRINTFORMDL %CALLNAME:LOCAL%的卵正在成长
		ENDIF
	ENDIF
NEXT

@CHILD_SUPPORT_COST()
	;Loop through characters and calculate child support cost based on their pregnancy type.
#FUNCTION
#DIM DYNAMIC Cost
FOR LOCAL,1,CHARANUM
	IF GROUPMATCH(TALENT:LOCAL:PREGNANCY_TYPE,PREGNANCY_LITTERS,PREGNANCY_CLUTCH) && FLAG:CHILD_SUPPORT_RELIEF
		Cost += CFLAG:LOCAL:育児人数 * 330 / (1 + (FLAG:甲斐性無 > 0))
	ELSE
		Cost += CFLAG:LOCAL:育児人数 * 1000 / (1 + (FLAG:甲斐性無 > 0))
	ENDIF
NEXT
RETURNF Cost

@EGG_GIFT_DATE_CHECK(CHARA)
	;Like GIFT_DATE_CHECK, but for eggs. Only called if no regular gift is given.
#FUNCTION
#DIM CHARA
#DIM 上限
#DIM 発生率
IF FESTIVAL() == ""
	上限 = 80
ELSE
	上限 = 100
ENDIF

SIF TALENT:CHARA:行きずり
	RETURNF 0
SIF DAY == CFLAG:CHARA:LatestEggGiftDay
		;No giving twice in one day.
	RETURNF 0
SIF MARK:CHARA:反発刻印 || TALENT:CHARA:機嫌 < 0 || CFLAG:CHARA:ブチギレ
	RETURNF 0
SIF BASE:CHARA:ムード < 1200
	RETURNF 0

	;Higher chance than regular gift.
発生率 = (ABL:CHARA:親密 - 4) * 6
発生率 += MIN(CFLAG:CHARA:好感度 / 10000, 10)
;----------------------------------------------------------------
SIF FESTIVAL() != ""
	TIMES 発生率, 1.5
SIF TALENT:CHARA:恋人
	TIMES 発生率, 1.5
SIF MIN(発生率, 上限) > RAND:100
	RETURNF 1
RETURNF 0

@EGG_GIFT_DATE_EVENT(CHARA)
	;Like GIFT_DATE_EVENT, but for eggs.
#DIM CHARA
#DIM EGG
#DIM RELATIONSHIP
#DIM CONST MAX_EGG = 6
SIF !FLAG:USE_EXPANDED_PREGNANCY_OPTIONS
	RETURN

PRINTL 
SELECTCASE CFLAG:CHARA:性格傾向
CASE 1
	LOCALS = 略显不安地从口袋里掏出什么，小心翼翼地递了过来…
CASE 2
	LOCALS = 看了看你的脸，像是想起了什么似的掏出东西，毫不犹豫地递了过来…
CASE 3
	LOCALS = 突然掏出什么东西说是礼物，然后递了过来…
CASE 4
	LOCALS = 取出一个盒子，说有东西想送给你，然后郑重地递了过来…
CASEELSE
	LOCALS = 掏出什么东西，一边说着希望你能收下，一边递了过来…
ENDSELECT
PRINTFORM 在回家的路上，%CALLNAME:CHARA%%LOCALS%

CALL PRINT_BREAK
PRINTW 

	;Give as many eggs as have been built up since the last time, to a maximum of MAX_EGG.
EGG = DAY-CFLAG:CHARA:LatestEggGiftDay
SIF TALENT:CHARA:PREGNANCY_TYPE == PREGNANCY_CLUTCH
		;Clutch layers give three times as many, but still have the same maximum.
	TIMES EGG, 3
EGG = MIN(EGG,MAX_EGG)
CFLAG:CHARA:LatestEggGiftDay = DAY

RELATIONSHIP = 3
IF TALENT:CHARA:恋慕
	RELATIONSHIP = 1
ELSEIF TALENT:CHARA:愛欲
	RELATIONSHIP = 2
ENDIF
CALL KOJO_MESSAGE_SEND("EVENT", 0, CHARA, RELATIONSHIP, EGG, "EGG_GIFT")
ITEM:少女の卵 += EGG
PRINTFORMW 从%CALLNAME:CHARA%那里得到了\@ EGG > 1 ? {EGG}个 # \@少女的蛋！

@CUSTOM_RESET_VAR_1(ARG)
	;Reset custom pregnancy variables when resetting characters.
CFLAG:ARG:WithChildCount = 0
CFLAG:ARG:BirthEventsCount = 0
CFLAG:ARG:LatestChildCount = 0
TALENT:ARG:NESTING = 0
FOR LOCAL,0,MAXCHILD
	PregnancyProgress:ARG:LOCAL = 0
NEXT
CVARSET TALENT, GETNUM(TALENT, "PREGNANCY_TYPE"), PREGNANCY_NORMAL

@UPDATE_SET_PREGNANCY_COUNTERS
	;Set the pregnancy progress array for all characters and the birth event count for all characters who have given birth, for older saves.
FOR LOCAL,0,CHARANUM
	PregnancyProgress:LOCAL:0 = MAX(CFLAG:LOCAL:妊娠経過日数,TALENT:LOCAL:NESTING)
	IF (CFLAG:LOCAL:妊娠経過日数 || TALENT:LOCAL:NESTING) && !CFLAG:LOCAL:WithChildCount
		FOR LOCAL:1,0,MAXCHILD
			SIF PregnancyProgress:LOCAL:(LOCAL:1)
				CFLAG:LOCAL:WithChildCount++
		NEXT
	ENDIF
	SIF !CFLAG:LOCAL:BirthEventsCount && CFLAG:LOCAL:子供人数
		CFLAG:LOCAL:BirthEventsCount = CFLAG:LOCAL:子供人数
NEXT

@ADD_PREGNANCY_PROGRESS(CharacterNumber,ConceptionCount,ProgressDay=1)
	;Set a progress tracker for a new pregnancy or egg, so that they can be born at different times if applicable.
#DIM DYNAMIC CharacterNumber
#DIM DYNAMIC ConceptionCount
#DIM DYNAMIC ProgressDay
#DIM DYNAMIC ProgressList,MAXCHILD
VARSET LOCAL
FOR LOCAL,0,ConceptionCount
	FOR LOCAL:1,0,MAXCHILD
		IF PregnancyProgress:CharacterNumber:(LOCAL:1) == 0
			PregnancyProgress:CharacterNumber:(LOCAL:1) = ProgressDay
			BREAK
		ENDIF
	NEXT
NEXT
	;Sort pregnancy progress in case a later pregnancy is added with a further progress.
FOR LOCAL,0,MAXCHILD
	SIF PregnancyProgress:CharacterNumber:LOCAL == 0
		BREAK
	ProgressList:LOCAL = PregnancyProgress:CharacterNumber:LOCAL
NEXT
ARRAYSORT ProgressList,BACK
FOR LOCAL,0,MAXCHILD
	SIF ProgressList:LOCAL == 0
		BREAK
	PregnancyProgress:CharacterNumber:LOCAL = ProgressList:LOCAL
NEXT

@PROGRESS_PREGNANCIES(CharacterNumber)
	;Progress all pregnancy trackers in sync with the vanilla one.
#DIM DYNAMIC CharacterNumber
#DIM DYNAMIC ProgressNumber
ProgressNumber = MAX(CFLAG:CharacterNumber:妊娠経過日数,TALENT:CharacterNumber:NESTING)
ProgressNumber -= PregnancyProgress:CharacterNumber:0
FOR LOCAL,0,MAXCHILD
	IF PregnancyProgress:CharacterNumber:LOCAL == 0
		BREAK
	ELSE
		PregnancyProgress:CharacterNumber:LOCAL += ProgressNumber
	ENDIF
NEXT

@REMOVE_PREGNANCY_PROGRESS(CharacterNumber)
	;Remove a pregnancy tracker for when the child is born.
#DIM DYNAMIC CharacterNumber
FOR LOCAL,0,MAXCHILD
	IF PregnancyProgress:CharacterNumber:LOCAL == 0
		BREAK
	ELSE
		SIF LOCAL > 0
			PregnancyProgress:CharacterNumber:(LOCAL-1) = PregnancyProgress:CharacterNumber:LOCAL
		PregnancyProgress:CharacterNumber:LOCAL = 0
	ENDIF
NEXT

@COUNT_BIRTH_MATES(CharacterNumber)
	;Count how many children will be born within a single birth event.
#FUNCTION
#DIM DYNAMIC CharacterNumber
#DIM DYNAMIC ChildCount
FOR LOCAL,0,MAXCHILD
	IF PregnancyProgress:CharacterNumber:LOCAL == 0
		BREAK
	ELSE
		IF PregnancyProgress:CharacterNumber:LOCAL == PregnancyProgress:CharacterNumber:0
			ChildCount++
		ELSE
			BREAK
		ENDIF
	ENDIF
NEXT
RETURNF ChildCount

@ADD_WITH_CHILD(CharacterNumber,Quantity=1,ProgressDay=1)
	;Helper function to make a character pregnant or nesting with one or more additional children.
	;If Quantity is -1, decides based on pregnancy type.
#DIM DYNAMIC CharacterNumber
#DIM DYNAMIC Quantity
#DIM DYNAMIC ProgressDay
Quantity = MIN(CFLAG:CharacterNumber:WithChildCount+Quantity,MAXCHILD)
SIF Quantity < 1
	RETURN
IF GROUPMATCH(TALENT:CharacterNumber:PREGNANCY_TYPE, PREGNANCY_EGGS, PREGNANCY_CLUTCH) && !FLAG:EGGNANCY
	TALENT:CharacterNumber:NESTING = MAX(TALENT:CharacterNumber:NESTING,ProgressDay)
ELSE
	CFLAG:CharacterNumber:妊娠経過日数 = MAX(CFLAG:CharacterNumber:妊娠経過日数,ProgressDay)
ENDIF
IF Quantity < 0
	Quantity = CONCEPTION_COUNT(CharacterNumber,ProgressDay)
ELSE
	CFLAG:CharacterNumber:WithChildCount += Quantity
ENDIF
CALL ADD_PREGNANCY_PROGRESS(CharacterNumber,Quantity,ProgressDay)

@PREGNANCY_CONVERT_TYPES
FOR LOCAL, 1, CHARANUM
	IF !PREGNANCY_NESTER(LOCAL) && TALENT:LOCAL:NESTING > 0
		CFLAG:LOCAL:妊娠経過日数 = PregnancyProgress:LOCAL:0
		TALENT:LOCAL:NESTING = 0
	ELSEIF PREGNANCY_NESTER(LOCAL) && CFLAG:LOCAL:妊娠経過日数 > 0
		TALENT:LOCAL:NESTING = PregnancyProgress:LOCAL:0
		CFLAG:LOCAL:妊娠経過日数 = 0
	ENDIF
NEXT

@PREGNANCY_TYPES_NEWGAME_SET()
FOR LOCAL,1,CHARANUM
	TALENT:LOCAL:PREGNANCY_TYPE = PREGNANCY_CHARA_DEFAULT(LOCAL)
NEXT

;维持原有的系统不变，禁用多胞胎，禁用新生育类型
@旧生育系统预设()
CVARSET TALENT, GETNUM(TALENT, "PREGNANCY_TYPE"), PREGNANCY_NORMAL
CVARSET TALENT, GETNUM(TALENT, "HYPERFERTILE"), 0
FLAG:CHILD_SUPPORT_RELIEF = 1
FLAG:MOB_PREGNANCY_TYPES = 0
FLAG:EGGNANCY = 0
FLAG:TWINS_CHANCE = 0
FLAG:TRIPLETS_CHANCE = 0

@LATEST_CHILD(Character,Child)
	;Returns the number of a specific child from the character's latest (or current ongoing) birthing. 1 for the first child in the litter, and so on.
#FUNCTION
#DIM DYNAMIC Character
#DIM DYNAMIC Child
Child = MIN(Child,CFLAG:Character:LatestChildCount)
Child = (CFLAG:Character:子供人数-CFLAG:Character:LatestChildCount+Child) % MAXCHILD
SIF !(Child % MAXCHILD)
	Child = MAXCHILD
RETURNF Child

@BABYMULTIAWARE(Character)
	;Returns true if the character would normally be aware that she's carrying more than one child.
#FUNCTION
#DIM DYNAMIC Character
SIF CFLAG:Character:WithChildCount > 1 && TALENT:Character:PREGNANCY_TYPE && !(TALENT:Character:PREGNANCY_TYPE == PREGNANCY_EGGS && FLAG:EGGNANCY)
	RETURNF 1
RETURNF 0

@BABYPLNOUN(Noun,Character)
	;Calls PLNOUN() if the character would normally be aware that she's carrying more than one child.
#FUNCTIONS
#DIMS DYNAMIC Noun
#DIM DYNAMIC Character
IF BABYMULTIAWARE(Character)
	SIF Noun == " a"
		RETURNF 
	RETURNF PLNOUN(Noun,CFLAG:Character:WithChildCount)
ENDIF
RETURNF Noun

@BABYPLVERB(Verb,Character)
	;Calls PLVERB() if the character would normally be aware that she's carrying more than one child.
#FUNCTIONS
#DIMS DYNAMIC Verb
#DIM DYNAMIC Character
SIF BABYMULTIAWARE(Character)
	RETURNF PLVERB(Verb,CFLAG:Character:WithChildCount)
RETURNF Verb

@BABYCONDSTR(String,Character,Reverse)
	;Returns a string if the character would normally be aware that she's carrying more than one child, or would not be aware if Reverse is true.
#FUNCTIONS
#DIMS DYNAMIC String
#DIM DYNAMIC Character
#DIM DYNAMIC Reverse
SIF Reverse
	RETURNF COND_STR(String,!BABYMULTIAWARE(Character))
RETURNF COND_STR(String,BABYMULTIAWARE(Character))

@TOTAL_CONCEPTION_COUNT(Character,All)
	;Returns the number of times the character has conceived.
	;If the maximum number of children has been exceeded, the result may be incorrect.
	;Set All to 1 if you want to include conceptions whose pregnancy event hasn't happened yet.
#FUNCTION
#DIM DYNAMIC Character
#DIM DYNAMIC LastNumber
#DIM DYNAMIC ConceptionCount
#DIM DYNAMIC All
SIF !CFLAG:Character:子供人数 && !CFLAG:Character:WithChildCount
	RETURNF 0
LastNumber = -1
IF CFLAG:Character:子供人数
	FOR LOCAL,1,MIN(CFLAG:Character:子供人数+1,MAXCHILD)
		IF CHILD_BIRTHDATE:Character:LOCAL > LastNumber
			ConceptionCount++
			LastNumber = CHILD_BIRTHDATE:Character:LOCAL
		ENDIF
	NEXT
ENDIF
LastNumber = 1000000
IF CFLAG:Character:WithChildCount
	FOR LOCAL,0,CFLAG:Character:WithChildCount
		IF VAR_OR_DEFAULT(@"PregnancyProgress:{Character}:{LOCAL}") < LastNumber && (All || GROUPMATCH(TALENT:Character:PREGNANCY_TYPE, PREGNANCY_EGGS, PREGNANCY_CLUTCH) || VAR_OR_DEFAULT(@"PregnancyProgress:{Character}:{LOCAL}") > 19)
			ConceptionCount++
			LastNumber = VAR_OR_DEFAULT(@"PregnancyProgress:{Character}:{LOCAL}")
		ENDIF
	NEXT
ENDIF
RETURNF ConceptionCount

@CHILD_MULTIPLE_COUNT(Character,Child,SameMother=1)
	;Returns the total number of children in the given child's birthing.
	;If SameMother is 0, it will also count children of other characters born on the same day.
#FUNCTION
#DIM DYNAMIC Character
#DIM DYNAMIC Child
#DIM DYNAMIC SameMother
#DIM DYNAMIC ChildCount
SIF Child > CFLAG:Character:子供人数 || Child < 1
	RETURNF 0
FOR LOCAL,1,MIN(CFLAG:Character:子供人数+1,MAXCHILD)
	SIF CHILD_BIRTHDATE:Character:LOCAL == CHILD_BIRTHDATE:Character:Child
		ChildCount++
NEXT
IF !SameMother
	FOR LOCAL:1,1,CHARANUM
		SIF LOCAL:1 == Character
			CONTINUE
		FOR LOCAL,1,MIN(CFLAG:(LOCAL:1):子供人数+1,MAXCHILD)
			SIF CHILD_BIRTHDATE:(LOCAL:1):LOCAL == CHILD_BIRTHDATE:Character:Child
				ChildCount++
		NEXT
	NEXT
ENDIF
RETURNF ChildCount

@CHILD_MULTIPLE_FIRST(Character,Child)
	;Returns the firstborn child in a single birthing.
#FUNCTION
#DIM DYNAMIC Character
#DIM DYNAMIC Child
FOR LOCAL,1,MIN(CFLAG:Character:子供人数+1,MAXCHILD)
	SIF CHILD_BIRTHDATE:Character:LOCAL == CHILD_BIRTHDATE:Character:Child
		RETURNF LOCAL
NEXT
RETURNF -1

@CHILD_MULTIPLE_LAST(Character,Child)
	;Returns the lastborn child in a single birthing.
#FUNCTION
#DIM DYNAMIC Character
#DIM DYNAMIC Child
#DIM DYNAMIC LastChild
LastChild = -1
FOR LOCAL,1,MIN(CFLAG:Character:子供人数+1,MAXCHILD)
	IF CHILD_BIRTHDATE:Character:LOCAL == CHILD_BIRTHDATE:Character:Child
		LastChild = LOCAL
		SIF LOCAL == CFLAG:Character:子供人数
			RETURNF LastChild
	ELSEIF LastChild > -1
		RETURNF LastChild
	ENDIF
NEXT
RETURNF -1

@PREGNANCY_EGGLAYER(ARG)
#FUNCTION
SIF !ARG
	ARG = TARGET
RETURNF FLAG:USE_EXPANDED_PREGNANCY_OPTIONS && GROUPMATCH(TALENT:ARG:PREGNANCY_TYPE, PREGNANCY_EGGS, PREGNANCY_CLUTCH)

@PREGNANCY_NESTER(ARG)
#FUNCTION
SIF !ARG
	ARG = TARGET
RETURNF FLAG:USE_EXPANDED_PREGNANCY_OPTIONS && GROUPMATCH(TALENT:ARG:PREGNANCY_TYPE, PREGNANCY_EGGS, PREGNANCY_CLUTCH) && !FLAG:EGGNANCY

@PREGNANCY_MULTIPLES(ARG)
#FUNCTION
SIF !ARG
	ARG = TARGET
RETURNF FLAG:USE_EXPANDED_PREGNANCY_OPTIONS && TALENT:ARG:PREGNANCY_TYPE > 0

@PREGNANCY_CHARA_DEFAULT(CHARA)
#FUNCTION
#DIM CHARA
SELECTCASE CHARA
	CASE [[橙]]
		RETURNF PREGNANCY_LITTERS
	CASE [[藍]]
		RETURNF PREGNANCY_LITTERS
	CASE [[リグル]]
		RETURNF PREGNANCY_CLUTCH
	CASE [[ミスティア]]
		RETURNF PREGNANCY_EGGS
	CASE [[文]]
		RETURNF PREGNANCY_EGGS
;Suwako isn't a frog, so don't add her here.
	CASE [[衣玖]]
		RETURNF PREGNANCY_CLUTCH
	CASE [[お燐]]
		RETURNF PREGNANCY_LITTERS
	CASE [[お空]]
		RETURNF PREGNANCY_EGGS
	CASE [[ナズーリン]]
		RETURNF PREGNANCY_LITTERS
	CASE [[はたて]]
		RETURNF PREGNANCY_EGGS
	CASE [[鈴仙]]
		RETURNF PREGNANCY_LITTERS
	CASE [[てゐ]]
		RETURNF PREGNANCY_LITTERS
	CASE [[影狼]]
		RETURNF PREGNANCY_LITTERS
	CASE [[椛]]
		RETURNF PREGNANCY_LITTERS
	CASE [[マミゾウ]]
		RETURNF PREGNANCY_LITTERS
	CASE [[ヤマメ]]
		RETURNF PREGNANCY_CLUTCH
	CASE [[星]]
		RETURNF PREGNANCY_LITTERS
	CASE [[響子]]
		RETURNF PREGNANCY_LITTERS
	CASE [[わかさぎ姫]]
		RETURNF PREGNANCY_CLUTCH
	CASE [[レイセン]]
		RETURNF PREGNANCY_LITTERS
	CASE [[朱鷺子]]
		RETURNF PREGNANCY_EGGS
	CASE [[清蘭]]
		RETURNF PREGNANCY_LITTERS
	CASE [[鈴瑚]]
		RETURNF PREGNANCY_LITTERS
;Larva isn't a butterfly, so don't add her here.
	CASE [[あうん]]
		RETURNF PREGNANCY_LITTERS
	CASE [[久侘歌]]
		RETURNF PREGNANCY_EGGS
	CASE [[ミケ]]
		RETURNF PREGNANCY_LITTERS
	CASE [[八千慧]]
		RETURNF PREGNANCY_EGGS
	CASE [[典]]
		RETURNF PREGNANCY_LITTERS
;Megumu isn't a crow tengu as far as we know, so don't add her here.
	CASE [[百々世]]
		RETURNF PREGNANCY_LITTERS
	CASE [[慧ノ子]]
		RETURNF PREGNANCY_LITTERS
	CASE [[ちやり]]
		RETURNF PREGNANCY_LITTERS
ENDSELECT
RETURNF PREGNANCY_NORMAL