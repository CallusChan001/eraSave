@ModImg_Set
#DIM ORIGINAL_SET
PRINTFORMDL 检测立绘包中……
TWAIT 1, 0 ;allow it to print the preceding line before continuing
IF !IMG_CFLAG_JOINED
	FOR LOCAL, 1, CHARANUM
		LOCAL:1 = MAX(CFLAG:LOCAL:別顔有, CFLAG:LOCAL:別立ち有)
		CFLAG:LOCAL:別顔有 = LOCAL:1
		CFLAG:LOCAL:別立ち有 = LOCAL:1
	NEXT
	IMG_CFLAG_JOINED = 1
ENDIF

FOR LOCAL, 1, 人物数量上限
	SIF !EXISTCSV(LOCAL)
		CONTINUE
	SIF LOCAL < CHARANUM && NO:LOCAL != LOCAL ;not for any potential duplicates
		CONTINUE
	SIF CSVTALENT(LOCAL, GETNUM(TALENT, "行きずり")) ;ignore mobs
		CONTINUE

	SIF LOCAL < CHARANUM
		ORIGINAL_SET = CFLAG:LOCAL:差し替え適用
	
	FOR LOCAL:1, 1, 11
		IF LOCAL:1 > 1
			LOCALS '= TOSTR(LOCAL:1)
		ELSE
			LOCALS =
		ENDIF
		;do initial check so we don't call COMPOSE_SPRITE unless strictly necessary
		IF RM_リソースチェック(@"別顔%LOCALS%_服_通常_{LOCAL}") || RM_リソースチェック(@"別立ち%LOCALS%_服_通常_{LOCAL}")
			IMG_ALTERNATE_SETS:LOCAL = LOCAL:1
			CONTINUE
		ENDIF

		IF LOCAL < CHARANUM ;only works correctly for existing characters
			;often depends on a specific image set being selected
			CFLAG:LOCAL:差し替え適用 = LOCAL:1
			TRYCALLFORM IMAGE_EX_I{LOCAL}_PREリソースチェック(LOCAL, "", "服", "通常", "", "", "")
		ENDIF
		
		IF RM_リソースチェック(@"別顔%LOCALS%_服_通常_{LOCAL}") || RM_リソースチェック(@"別立ち%LOCALS%_服_通常_{LOCAL}")
			IMG_ALTERNATE_SETS:LOCAL = LOCAL:1
		ELSE
			BREAK ;image sets are always expected to follow up sequentially
		ENDIF
	NEXT
	SIF LOCAL < CHARANUM
		CFLAG:LOCAL:差し替え適用 = ORIGINAL_SET
NEXT
CLEARLINE 1
