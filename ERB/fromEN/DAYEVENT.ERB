;activate the event for morning/night subevents
;original functions are not implemented in TW
;conditions and descriptions are ported from the original game era命蓮寺OvDr!!Ver1.4 + modifications to apply exp and sex effects from nasikuzusi.erb (onabare event)
;seeing morning event will set cMorningGreeting flag which is different from TCVAR:今日会ったか when deciding whether you met the character today or not. cMorningGreeting will merely hide the line about not greeting them early.
;the reason for that is because many of the greeting events are ties to this flag so they must be seen for proper progression, and we can't let the morning event replace the morning greeting event
;if there are discrepancies in the dialogue, then the flags can be set manually anyway
@DAYEVENT_MAIN(TYPE)
#DIMS TYPE
#DIM DYNAMIC ARRAY_INDEX
#DIM DYNAMIC POSSIBLE_EVENTS_CHARA, 人物数量上限
#DIM DYNAMIC POSSIBLE_EVENTS_SCENARIO, 人物数量上限
#DIM DYNAMIC POSSIBLE_EVENTS_WEIGHT, 人物数量上限
#DIM ORIGINAL_TARGET
DAYEVENT_TARGET = 0
DAYEVENT_SCENARIO = 0
MORNING_SEX_CHARA = 0

SIF !INRANGE(ENABLE_DAYEVENTS, 1, 2)
	RETURN

ORIGINAL_TARGET = TARGET

SIF CFLAG:MASTER:初期位置 != CFLAG:MASTER:現在位置 ;if not sleeping at your original spot
	RETURN

FOR TARGET, 1, CHARANUM - 1
	CALL DAYEVENT_CHECK_CHARA(TYPE, ARRAY_INDEX, POSSIBLE_EVENTS_CHARA, POSSIBLE_EVENTS_SCENARIO, POSSIBLE_EVENTS_WEIGHT)
NEXT
DEBUGPRINTFORML ------------------

IF ARRAY_INDEX
	LOCAL = ARRAY_HIT(POSSIBLE_EVENTS_WEIGHT, ARRAY_INDEX)
	DAYEVENT_TARGET = POSSIBLE_EVENTS_CHARA:LOCAL
	DAYEVENT_SCENARIO = POSSIBLE_EVENTS_SCENARIO:LOCAL

	DEBUGPRINTFORML Final decision: %CALLNAME:DAYEVENT_TARGET%: %DAYEVENT_SCENARIO_STR(DAYEVENT_SCENARIO, 1)%
ELSE
	DEBUGPRINTFORML Final decision: Nothing
ENDIF
DEBUGPRINTFORML ------------------

TARGET = ORIGINAL_TARGET

@DAYEVENT_CHECK_CHARA(TYPE, ARRAY_INDEX, POSSIBLE_EVENTS_CHARA, POSSIBLE_EVENTS_SCENARIO, POSSIBLE_EVENTS_WEIGHT) ;assume target is set
#DIMS TYPE
#DIM REF ARRAY_INDEX
#DIM REF POSSIBLE_EVENTS_CHARA
#DIM REF POSSIBLE_EVENTS_SCENARIO
#DIM REF POSSIBLE_EVENTS_WEIGHT
SIF CFLAG:出禁
	RETURN

CALL KOJO_CHECK(TARGET, "DAYEVENT_COND", TYPE, DAYEVENT_GENERAL)
IF RESULT != 2 && (RESULT < 0 || (!RESULT && ENABLE_DAYEVENTS != 2) || !DAYEVENT_VISIT(TARGET, TYPE)) ;not allowed if hard blocked, not allowed without dialogue, or if they're sleeping in the morning
	IF TYPE == "day" && CFLAG:初期位置 == CFLAG:MASTER:初期位置 ;should still have a chance to wake up next to, even without events
		POSSIBLE_EVENTS_CHARA:ARRAY_INDEX = TARGET
		POSSIBLE_EVENTS_SCENARIO:ARRAY_INDEX = 0
		POSSIBLE_EVENTS_WEIGHT:ARRAY_INDEX = 10
		ARRAY_INDEX++
	ENDIF
	RETURN
ENDIF

SIF LastHomeVisit:TARGET:LAST_HOME_VISIT_NIGHT > DAY ;failsafe
	LastHomeVisit:TARGET:LAST_HOME_VISIT_NIGHT = DAY
SIF LastHomeVisit:TARGET:LAST_HOME_VISIT_DAY > DAY ;failsafe
	LastHomeVisit:TARGET:LAST_HOME_VISIT_DAY = DAY

;check if you recently had an event already with a non-roommate
IF RESULT == 2
ELSEIF CFLAG:初期位置 != CFLAG:MASTER:初期位置 && (DAY - LastHomeVisit:TARGET:LAST_HOME_VISIT_NIGHT < 3 || DAY - LastHomeVisit:TARGET:LAST_HOME_VISIT_DAY < 3)
	DEBUGPRINTFORML %CALLNAME:(TARGET)% Fail: Too early since last time
	RETURN 0
ELSE ;randomize occurrence a little
	LOCAL = 1
	SELECTCASE DAY - LastHomeVisit:TARGET:(TYPE == "day")
		CASE IS < 3
			LOCAL = 0
		CASE 3
			LOCAL = PERCENT(15)
		CASE 4
			LOCAL = PERCENT(30)
		CASE 5
			LOCAL = PERCENT(50)
		CASE 6
			LOCAL = PERCENT(65)
		CASE 7
			LOCAL = PERCENT(85)
	ENDSELECT
	IF !LOCAL
		DEBUGPRINTFORML %CALLNAME:(TARGET)% Fail: Too early since last time
		RETURN
	ENDIF
ENDIF

DEBUGPRINTFORML ------------------
DEBUGPRINTFORML Checking %TYPE% events for %CALLNAME:(TARGET)%:
CALL DAYEVENT_SELECTION(TYPE)
IF RESULT
	POSSIBLE_EVENTS_CHARA:ARRAY_INDEX = TARGET
	POSSIBLE_EVENTS_SCENARIO:ARRAY_INDEX = RESULT
	POSSIBLE_EVENTS_WEIGHT:ARRAY_INDEX = MAX(DAY - LastHomeVisit:TARGET:(TYPE == "day"), 1)

	SIF TYPE == "day" && CFLAG:初期位置 == CFLAG:MASTER:初期位置 ;roommates are prioritized
		POSSIBLE_EVENTS_WEIGHT:ARRAY_INDEX *= 4

	DEBUGPRINTFORML Result: %DAYEVENT_SCENARIO_STR(RESULT, 1)% ({POSSIBLE_EVENTS_WEIGHT:ARRAY_INDEX})
	ARRAY_INDEX++
ENDIF

;we assume that target is already set here
@DAYEVENT_SELECTION(TYPE)
#DIMS TYPE
RESULT = 0
SELECTCASE TYPE
	CASE "day"
		;lewd events
		IF !DAYEVENT_ALLOWED_IN_ROOM(TARGET, 1) ;can't do lewd if there's someone else in the room
			DEBUGPRINTFORML Lewd Fail: Not allowed in room
		ELSEIF !RAND:2 && (CFLAG:溜まってる度 > 300 || ABL:欲望 > 10)
			CALL DAYEVENT_CHECK_RANGE(DAYEVENT_ORAL, DAYEVENT_COWGIRL)
		ENDIF

		;normal events
		IF !RESULT
			IF !DAYEVENT_ALLOWED_IN_ROOM(TARGET)
				DEBUGPRINTFORML Normal Fail: Not allowed in room
			ELSE
				CALL DAYEVENT_CHECK_RANGE(DAYEVENT_KISS, DAYEVENT_PRANK)
			ENDIF
		ENDIF

		SIF RESULT > 0
			RETURN RESULT
	CASE "night"
		;target must not be exhausted
		IF BASE:0 < 1000 || BASE:1 <= 0 || CFLAG:衰弱
			DEBUGPRINTFORML Fail: Too exhausted
			RETURN 0
		ENDIF
		
		;lewd events
		IF !DAYEVENT_ALLOWED_IN_ROOM(TARGET, 1) ;can't do lewd if there's someone else in the room
			DEBUGPRINTFORML Lewd Fail: Not allowed in room
		ELSEIF !RAND:2 && (ABL:欲望 > 6 || PALAM:欲情 > PALAMLV:5)
			CALL DAYEVENT_CHECK_RANGE(NIGHTEVENT_KISS, NIGHTEVENT_YOBAI)
		ENDIF

		;normal events
		IF !RESULT
			IF !DAYEVENT_ALLOWED_IN_ROOM(TARGET)
				DEBUGPRINTFORML Normal Fail: Not allowed in room
			ELSE
				CALL DAYEVENT_CHECK_RANGE(NIGHTEVENT_DRINK, NIGHTEVENT_SLEEPOVER)
			ENDIF
		ENDIF
		SIF RESULT > 0
			RETURN RESULT
CASEELSE
	THROW "WRONG EVENT TYPE DUMBASS - %TYPE%"
ENDSELECT

@DAYEVENT_CHECK_RANGE(START, END)
#DIM START
#DIM END
#DIM DYNAMIC OPTIONS, 10
#DIM DYNAMIC INDEX

FOR LOCAL, START, END+1
	OPTIONS:(INDEX++) = LOCAL
NEXT
CALL FISHER_YATES_SHAFFLE(INDEX)

FOR LOCAL, 0, INDEX
	CALL DAYEVENT_CONDITION_CHECK(OPTIONS:(SHAFFLE_ARRAY:LOCAL))
	SIF RESULT > 0
		RETURN OPTIONS:(SHAFFLE_ARRAY:LOCAL)
NEXT
RETURN 0

@DAYEVENT_CONDITION_CHECK(SCENARIO)
#DIM SCENARIO

DEBUGPRINTFORML Checking DAYEVENT: %DAYEVENT_SCENARIO_STR(SCENARIO, 1)%

;checks that should be performed in all cases
SELECTCASE SCENARIO
	CASE NIGHTEVENT_DRINK
		IF (TALENT:妊娠 || TALENT:育児中)
			DEBUGPRINTFORML Drink Pregnancy Fail
			RETURN 0
		ENDIF
	CASE NIGHTEVENT_SLEEPOVER
		IF CFLAG:現在位置 == CFLAG:MASTER:現在位置
			DEBUGPRINTFORML Sleepover Fail - Already sleeping together
			RETURN 0
		ENDIF
	CASE NIGHTEVENT_MOONVIEWING
		;20:00 - 6:00 (moon viewing up to 4 am) (unless you got knocked out previously)
		IF !(TIME:0 >= 1200 || TIME:0 <= 360)
			IF CFLAG:MASTER:衰弱
			ELSE
				DEBUGPRINTFORML Fail: Time Incorrect (Must Be Between 20:00 - 6:00 (>=1200 or <= 360), Current: {TIME})
				RETURN 0
			ENDIF
		ENDIF
ENDSELECT

CALL KOJO_CHECK(TARGET, "DAYEVENT_COND",,SCENARIO)

IF !RESULT ;if unchanged (0), fall back to the original cond
	SELECTCASE SCENARIO
		CASE DAYEVENT_ORAL
			RESULT = ABL:欲望 >= 3 && ABL:技巧 >= 3 && EXP:奉仕快楽経験 >= 50 && EXP:口淫経験 >= 20
		CASE DAYEVENT_PAIZURI
			RESULT = ABL:欲望 >= 3 && ABL:技巧 >= 5 && EXP:奉仕快楽経験 >= 50 && EXP:パイズリ経験 >= 10 && HAS_PENIS(MASTER)
		CASE DAYEVENT_COWGIRL
			RESULT = ABL:欲望 >= 5 && EXP:奉仕快楽経験 >= 50
			SIF RESULT && HAS_PENIS(MASTER) ;for non-les scenario
				RESULT = !TALENT:処女 && !IS_DOUTEI(MASTER) && EXP:Ｖ性交経験 >= 50
		CASE DAYEVENT_KISS
			RESULT = !TALENT:キス未経験 && !TALENT:MASTER:キス未経験 && ABL:親密 >= 10 && EXP:キス経験 >= 50
		CASE DAYEVENT_WAKEUP
			RESULT = ABL:親密 >= 7 && ABL:奉仕精神 >= 3
		CASE DAYEVENT_COSLEEPING
			RESULT = ABL:親密 >= 10 && ABL:欲望 >= 3 && EXP:愛情経験 >= 50
		CASE DAYEVENT_STARE
			RESULT = ABL:親密 >= 10 && ABL:従順 >= 7 && EXP:愛情経験 >= 50
		CASE DAYEVENT_PRANK
			RESULT = ABL:親密 >= 5 && ABL:欲望 >= 5 && (MARK:反発刻印 || CFLAG:10 || TALENT:機嫌 < 0 || RAND:3)
		CASE NIGHTEVENT_KISS
			RESULT = !TALENT:キス未経験 && !TALENT:MASTER:キス未経験 && ABL:親密 >= 10 && EXP:キス経験 >= 30
		CASE NIGHTEVENT_YOBAI
			RESULT = ABL:欲望 >= 5 && CFLAG:溜まってる度 > 300
			IF RESULT && HAS_VAGINA(TARGET) && !HAS_VAGINA(MASTER) ;hetero, player fucks target
				RESULT = !TALENT:処女 && !IS_DOUTEI(MASTER) && EXP:Ｖ性交経験 >= 50
			ELSEIF RESULT && HAS_PENIS(TARGET) && HAS_VAGINA(MASTER) ;target fucks player (futa)
				RESULT = !TALENT:MASTER:処女 && !IS_DOUTEI(TARGET) && EXP:MASTER:Ｖ性交経験 >= 50 && EXP:挿入経験 >= 10
			ELSEIF RESULT ;lez, pretty lenient huh
			ENDIF
		CASE NIGHTEVENT_DRINK
			RESULT = ABL:奉仕精神 >= 3 && EXP:奉仕快楽経験 >= 50
		CASE NIGHTEVENT_MOONVIEWING
			;time:5 - checks for favorable weather
			RESULT = ABL:親密 >= 10 && (TIME:5 <= 4 || TIME:5 == 8)
		CASE NIGHTEVENT_SLEEPOVER
			RESULT = ABL:親密 >= 10 && ABL:欲望 >= 3 && EXP:愛情経験 >= 10 
	ENDSELECT
ENDIF

IF RESULT < 1 ;if the dialogue condition returns 1, or the above check is passed then it is allowed; not allowed if the dialogue check returns -1
	DEBUGPRINTFORML %DAYEVENT_SCENARIO_STR(SCENARIO)% \@ RESULT == -1 ? Disallowed # Fail \@
	RETURN 0
ENDIF

DEBUGPRINTFORML %DAYEVENT_SCENARIO_STR(SCENARIO)% Allowed
RETURN RESULT

@DAYEVENT_SCENARIO_STR(SCENARIO, FULL)
#FUNCTIONS
#DIM SCENARIO
#DIM FULL
SELECTCASE SCENARIO
	CASE DAYEVENT_ORAL
		RETURNF @"%COND_STR("Day: Lewd: ", FULL)%Oral Caress"
	CASE DAYEVENT_PAIZURI
		RETURNF @"%COND_STR("Day: Lewd: ", FULL)%Paizuri"
	CASE DAYEVENT_COWGIRL
		RETURNF @"%COND_STR("Day: Lewd: ", FULL)%Cowgirl"
	CASE DAYEVENT_KISS
		RETURNF @"%COND_STR("Day: Normal: ", FULL)%Kiss"
	CASE DAYEVENT_WAKEUP
		RETURNF @"%COND_STR("Day: Normal: ", FULL)%Wake Up"
	CASE DAYEVENT_COSLEEPING
		RETURNF @"%COND_STR("Day: Normal: ", FULL)%Sleeping Together"
	CASE DAYEVENT_STARE
		RETURNF @"%COND_STR("Day: Normal: ", FULL)%Stare"
	CASE DAYEVENT_PRANK
		RETURNF @"%COND_STR("Day: Normal: ", FULL)%Prank"
	CASE NIGHTEVENT_KISS
		RETURNF @"%COND_STR("Night: Lewd: ", FULL)%Kiss"
	CASE NIGHTEVENT_YOBAI
		RETURNF @"%COND_STR("Night: Lewd: ", FULL)%Yobai"
	CASE NIGHTEVENT_DRINK
		RETURNF @"%COND_STR("Night: Normal: ", FULL)%Drink"
	CASE NIGHTEVENT_MOONVIEWING
		RETURNF @"%COND_STR("Night: Normal: ", FULL)%Moon Viewing"
	CASE NIGHTEVENT_SLEEPOVER
		RETURNF @"%COND_STR("Night: Normal: ", FULL)%Sleepover"
ENDSELECT

@DAYEVENT_EXECUTE_EVENT()
#DIM DYNAMIC nExp, 58
#DIM DYNAMIC nSexCount
#DIM PREVTARGET
SIF !DAYEVENT_TARGET || !DAYEVENT_SCENARIO
	RETURN
PREVTARGET = TARGET
TARGET = DAYEVENT_TARGET

;move them to the player's location if necessary
SIF INRANGE(DAYEVENT_SCENARIO, DAYEVENT_ORAL, DAYEVENT_PRANK)
	CALL DAYEVENT_COMMUTE(TARGET)

SELECTCASE DAYEVENT_SCENARIO
	CASE DAYEVENT_ORAL
		PALAM:欲情 += 1000
		PALAM:恭順 += 500
		PALAM:優越 += 300
		IF !HAS_PENIS(MASTER)
			STAIN:口 = 1 ;pussy juice
			STAIN:手 = 1
			STAIN:MASTER:口 = 1
		ELSE
			STAIN:口 = 6 ;penis + semen
			STAIN:手 = 2 ;penis
			STAIN:MASTER:Ｐ = 6
		ENDIF
		
		CALL KOJO_MESSAGE_SEND("SP_EVENT",4,TARGET,1)		
		
		nExp:4 += 3 + ABL:Ｍ感覚 * 2 ;m exp
		nExp:25 = nExp:4 ;bj exp
		SIF HAS_PENIS(MASTER)
			nExp:14 = 1 ;swallow
		LOCAL = (ABL:舌 + 1) * 20 + BASE:MASTER:精力 /25

		CALL JUEL_UP_SPECIAL(TARGET,"快Ｍ","Ｍ感覚",20,1)
		CALL JUEL_UP_SPECIAL(MASTER,"快Ｃ","Ｃ感覚",10,1)
	CASE DAYEVENT_PAIZURI
		PALAM:恭順 += 1000
		PALAM:快Ｂ += 500
		PALAM:潤滑 += 500
		PALAM:欲情 += 300
		STAIN:0 = 6
		STAIN:5 = 6
		STAIN:MASTER:2 = 6

		CALL KOJO_MESSAGE_SEND("SP_EVENT",4,TARGET,2)
		
		nExp:3 += 3 + ABL:Ｂ感覚 * 2 ;b exp
		nExp:26 = nExp:4 ;paizuri exp
		nExp:8 += nExp:3 / 2 ;b orgasm
		nExp:10 += nExp:3 / 2 ;orgasm
		SIF EXP:精飲経験
			nExp:14 = 1 ;swallow
		LOCAL = (ABL:胸 + 1) * 20 + BASE:MASTER:精力 /25

		CALL JUEL_UP_SPECIAL(TARGET,"快Ｂ","Ｂ感覚",20,1)
		CALL JUEL_UP_SPECIAL(MASTER,"快Ｃ","Ｃ感覚",10,1)
	CASE DAYEVENT_COWGIRL
		PALAM:潤滑 += 2300
		PALAM:快Ｖ += 800
		PALAM:欲情 += 700
		PALAM:恭順 += 500
		CALL KOJO_MESSAGE_SEND("SP_EVENT",4,TARGET,3,0)
		nSexCount = MAX(RESULT-1, 0) ;get additional sex count from returning result from the event
		;have sex once + sex count above
		nExp:10 += 2 + nSexCount ;orgasm
		nExp:6 += 2 + nSexCount ;v orgasm
		nExp:1 += 6 + nSexCount ;v exp
		
		DOWNBASE:体力 = 100
		DOWNBASE:気力 = 150
		
		IF HAS_PENIS(MASTER)
			LOCAL = MAX((ABL:膣 + 1) * 20 + BASE:MASTER:精力 / 25, 1)
			nExp:20 += 3 + nSexCount ;v sex exp
			nExp:16 += 1 + nSexCount ;v creampie
			EXP:MASTER:挿入経験 += 3 + nSexCount
			EXP:MASTER:射精経験 += 1 + nSexCount
			IF nSexCount > 0
				FOR LOCAL:1,1,nSexCount
					LOCAL:2 += LOCAL + RAND:(LOCAL)
				NEXT
			ELSE
				LOCAL:2 = LOCAL
			ENDIF
			CALL ADD_SAMEN_V(TARGET, LOCAL:2)
			BASE:MASTER:勃起 = 0
		ENDIF
		CALL JUEL_UP_SPECIAL(TARGET,"快Ｖ","Ｖ感覚",15,1)
		CALL JUEL_UP_SPECIAL(MASTER,"快Ｃ","Ｃ感覚",10,1)
		
		PRINTL
		PRINTFORMDL 你下一步的行动是……
		CALL ASK_YN("整理衣服，从床上起身", "把她压倒，索要更多！")
		
		IF RESULT ;activate sex
			TFLAG:193 = 1
			;騎乗位のままウフフモード化
			IF HETEROSEX(MASTER,TARGET) == 0
				STAIN:3 = 6
				STAIN:6 = 6
				STAIN:MASTER:2 = 6
			ENDIF
			CFLAG:うふふ = 1
			CFLAG:MASTER:うふふ = 1
			CFLAG:前ターン位置 = CFLAG:現在位置
			CALL DATUI_BOTTOM(MASTER,0)
			;CALL DATUI_BOTTOM_T(TARGET,1)
			CALL DATUI_BOTTOM_T(TARGET,0)
			TCVAR:23 = 0
			TCVAR:MASTER:22 = 0
			MORNING_SEX_CHARA = TARGET
			UFUFU_START_TIME = TIME
			UFUFU_START_DAY = DAY
		ELSE
			;do nothing
		ENDIF
		CALL KOJO_MESSAGE_SEND("SP_EVENT",4,TARGET,3, 1)
		LOCAL:9 = 1
	CASE DAYEVENT_KISS
		BASE:ムード = 500
		BASE:理性 = 700
		PALAM:好意 += 3000
		PALAM:恥情 += 1000
		PALAM:優越 += 700
		PALAM:恭順 += 500

		CALL KOJO_MESSAGE_SEND("SP_EVENT",5,TARGET,1)
		
		CALL MUTUAL_KISS
		CALL ADD_EXP_LESNIAN(5, TARGET, "PRINT")
	CASE DAYEVENT_WAKEUP
		BASE:ムード = 300
		BASE:理性 = 900
		PALAM:好意 += 1000
		PALAM:優越 += 1300
		PALAM:恭順 += 700
		
		CALL KOJO_MESSAGE_SEND("SP_EVENT",5,TARGET,2)
	CASE DAYEVENT_COSLEEPING
		BASE:ムード = 100
		BASE:理性 = 300
		PALAM:好意 += 4000
		PALAM:恥情 += 2000
		PALAM:恭順 += 1400

		CALL KOJO_MESSAGE_SEND("SP_EVENT",5,TARGET,3)
	CASE DAYEVENT_STARE
		BASE:ムード = 800
		BASE:理性 = 900
		PALAM:好意 += 2700
		PALAM:恥情 += 3000
		PALAM:優越 += 700
		
		CALL KOJO_MESSAGE_SEND("SP_EVENT",5,TARGET,4)
	CASE DAYEVENT_PRANK
		BASE:ムード = 100
		BASE:理性 = 300
		PALAM:好意 += 1700
		PALAM:恥情 += 1500
		PALAM:優越 += 3000
		TALENT:機嫌 = 1 ;improve the mood
		
		CALL KOJO_MESSAGE_SEND("SP_EVENT",5,TARGET,5)
	CASE NIGHTEVENT_KISS
		CALL ADD_EXP_LESNIAN(5, TARGET, "PRINT")
		
		CALL KOJO_MESSAGE_SEND("SP_EVENT",6,TARGET,1)
		CALL MUTUAL_KISS
	CASE NIGHTEVENT_YOBAI
		nSexCount = MAX(MIN(ABL:欲望 / 2, 5) + CFLAG:溜まってる度 / 200,1)
		
		CALL KOJO_MESSAGE_SEND("SP_EVENT",6,TARGET,2,0)
		SIF RESULT
			PRINTL
		
		CALL KOJO_MESSAGE_SEND("SP_EVENT",6,TARGET,2,1)
		
		IF ( HAS_VAGINA(TARGET) && !HAS_VAGINA(MASTER) && HAS_PENIS(MASTER) ) ;player fucks nue
			nExp:10 += 2 * nSexCount ;orgasm
			nExp:6 += 2 * nSexCount ;v orgasm
			nExp:1 += 2 * nSexCount ;v exp
			
			LOCAL = MAX((ABL:膣 + 1) * 20 + BASE:MASTER:精力 / 25, 1)
			nExp:20 += nSexCount ;v sex exp
			nExp:16 += nSexCount ;v creampie
			FOR LOCAL:1,1,nSexCount
				LOCAL:2 += LOCAL + RAND:(LOCAL)
			NEXT

			LOCAL:2 = MIN(LOCAL:2, BASE:MASTER:精力)
			
			EXP:MASTER:挿入経験 += nSexCount
			EXP:MASTER:射精経験 += nSexCount
			EXP:MASTER:絶頂経験 += nSexCount
			
			CALL ADD_SAMEN_V(TARGET, LOCAL:2)

			CALL JUEL_UP_SPECIAL(TARGET,"快Ｖ","Ｖ感覚",15,1)
			CALL JUEL_UP_SPECIAL(MASTER,"快Ｃ","Ｃ感覚",10,1)
			
			BASE:MASTER:勃起 = 0
		ELSEIF ( HAS_PENIS(TARGET) && HAS_VAGINA(MASTER) ) ;nue fucks player
			nExp:10 += 2 * nSexCount ;orgasm
			nExp:29 += 2 * nSexCount ;insertion
			nExp:11 += 2 * nSexCount ;ejaculation
			
			LOCAL = MAX((ABL:MASTER:膣 + 1) * 20 + BASE:精力 / 25, 1)
			EXP:MASTER:Ｖ経験 += nSexCount
			EXP:MASTER:Ｖ絶頂経験 += nSexCount
			EXP:MASTER:Ｖ性交経験 += nSexCount
			EXP:MASTER:膣射経験 += nSexCount
			EXP:MASTER:絶頂経験 += nSexCount
			FOR LOCAL:1,1,nSexCount
				LOCAL:2 += LOCAL + RAND:(LOCAL)
			NEXT

			LOCAL:2 = MIN(LOCAL:2, BASE:精力)
			
			CALL SAMEN_SHOOT3(LOCAL:2, TARGET, MASTER)
			BASE:精力 -= LOCAL:2
			EX:MASTER:膣内精液 += LOCAL:2
			CFLAG:MASTER:累計膣内精液 += LOCAL:2

			STAIN:MASTER:Ｖ |= 4
			STAIN:MASTER:膣内 |= 4

			STAIN:Ｐ |= 1
			STAIN:Ｐ |= 4
			
			CALL JUEL_UP_SPECIAL(MASTER,"快Ｖ","Ｖ感覚",15,1)
			CALL JUEL_UP_SPECIAL(TARGET,"快Ｃ","Ｃ感覚",10,1)
			
			BASE:勃起 = 0
		ELSE ;lez
			nExp:10 += 2 * nSexCount ;orgasm
			nExp:5 += 2 * nSexCount ;c orgasm
			nExp:0 += 2 * nSexCount ;c exp
			
			EXP:MASTER:絶頂経験 += nSexCount
			EXP:MASTER:Ｃ絶頂経験 += nSexCount
			EXP:MASTER:Ｃ経験 += nSexCount
			
			SIF HAS_PENIS(MASTER)
				EXP:MASTER:射精経験 += nSexCount
			SIF HAS_PENIS(TARGET)
				nExp:11 += nSexCount
		
			CALL MUTUAL_KISS(TARGET, nSexCount)
			
			CALL JUEL_UP_SPECIAL(TARGET,"快Ｃ","Ｃ感覚",15,1)
			CALL JUEL_UP_SPECIAL(MASTER,"快Ｃ","Ｃ感覚",15,1)
		ENDIF
		
		CALL JUEL_UP_SPECIAL(TARGET,"欲情","欲望",20,1)
		CALL ADD_EXP_LESNIAN(5, TARGET, "PRINT")

		DOWNBASE:MASTER:体力 = 100
		DOWNBASE:MASTER:気力 = 150
		
		DOWNBASE:体力 = 100
		DOWNBASE:気力 = 150
		
		CFLAG:溜まってる度 = 0 ;nullify horny
		
		SETCOLOR C_YELLOW
		FOR LOCAL,0,58
			IF nExp:LOCAL
				PRINTFORML %EXP_TR(LOCAL)% ＋{nExp:LOCAL}
				EXP:LOCAL += nExp:LOCAL
			ENDIF
		NEXT
		RESETCOLOR
		CALL QOL_STAYOVER(TARGET,1,1)
		IF RESULT
			PRINTFORMW %CALLNAME:TARGET%会留在%NAME_FROM_PLACE(CFLAG:MASTER:初期位置)%过夜。
		ELSE
			PRINTFORMW 之后，%CALLNAME:TARGET%决定离开了……
		ENDIF
	CASE NIGHTEVENT_DRINK
		CALL RECOVER(TARGET, 500, "酒気", " ")
		CALL RECOVER(MASTER, 500, "酒気", " ")
		CALL KOJO_MESSAGE_SEND("SP_EVENT",7,TARGET,1)
	CASE NIGHTEVENT_MOONVIEWING
		CALL KOJO_MESSAGE_SEND("SP_EVENT",7,TARGET,2)
	CASE NIGHTEVENT_SLEEPOVER
		CALL KOJO_MESSAGE_SEND("SP_EVENT",7,TARGET,3)

		CALL QOL_STAYOVER(TARGET,,1)
		SIF !RESULT
			PRINTFORMW 不久之后，你再次转身，而%CALLNAME:TARGET%一下躲开了。
ENDSELECT

IF INRANGE(DAYEVENT_SCENARIO, DAYEVENT_ORAL, DAYEVENT_COWGIRL)
	BASE:ムード = 700 ;mood up
	BASE:理性 = 0 ;reason down
	CFLAG:ディレイ = 30 ;set delay so you don't get pushed down immediately
	CFLAG:溜まってる度 /= 2 ;halve the horny
		
	DOWNBASE:MASTER:体力 = 100
	DOWNBASE:MASTER:気力 = 150

	IF HAS_PENIS(MASTER) && !LOCAL:9 ;ignore sex event since it's already handled there
		EXP:MASTER:射精経験 ++
		DOWNBASE:MASTER:精力 = LOCAL
		BASE:MASTER:勃起 = 0
	ENDIF
	EXP:MASTER:絶頂経験 ++

	CALL JUEL_UP_SPECIAL(TARGET,"欲情","欲望",20,1)
	CALL ADD_EXP_LESNIAN(5, TARGET, "PRINT")
		
	SETCOLOR C_YELLOW
	FOR LOCAL,0,58
		IF nExp:LOCAL
			PRINTFORML %EXP_TR(LOCAL)% ＋{nExp:LOCAL}
			EXP:LOCAL += nExp:LOCAL
		ENDIF
	NEXT
	RESETCOLOR

	CALL CHANGE_CFLAG(2,TARGET,ABL:親密 * 3)
	cMorningGreeting:TARGET:0 = 1 ;quasi "met today" flag, still trigger first time greetings for the day but hide "haven't met her today" line
	LastHomeVisit:TARGET:LAST_HOME_VISIT_DAY = DAY
ELSEIF INRANGE(DAYEVENT_SCENARIO, DAYEVENT_KISS, DAYEVENT_PRANK)
	CALL CHANGE_CFLAG(2,TARGET,ABL:親密 * 5)
	cMorningGreeting:TARGET:0 = 1
	LastHomeVisit:TARGET:LAST_HOME_VISIT_DAY = DAY
ELSEIF INRANGE(DAYEVENT_SCENARIO, NIGHTEVENT_KISS, NIGHTEVENT_YOBAI)
	TCVAR:今日会ったか = DAY
	CALL CHANGE_CFLAG(2,TARGET,ABL:親密 * 3)
	LastHomeVisit:TARGET:LAST_HOME_VISIT_NIGHT = DAY
ELSEIF INRANGE(DAYEVENT_SCENARIO, NIGHTEVENT_DRINK, NIGHTEVENT_SLEEPOVER)
	TCVAR:今日会ったか = DAY
	CALL CHANGE_CFLAG(2,TARGET,ABL:親密 * 5)
	LastHomeVisit:TARGET:LAST_HOME_VISIT_NIGHT = DAY
ENDIF

TARGET = PREVTARGET

@DAYEVENT_COMMUTE(CHARA)
#DIM CHARA
CFLAG:CHARA:現在位置 = CFLAG:MASTER:現在位置
TCVAR:CHARA:なるべく動かない = 1
CFLAG:CHARA:同室 = 1
IF !CFLAG:CHARA:神社在住
	CFLAG:CHARA:拠点来訪 = 1
	CFLAG:CHARA:デート中 = MAIN_MAP
ENDIF

@DAYEVENT_ALLOWED_IN_ROOM(CHARA, LEWD)
#FUNCTION
#DIM CHARA
#DIM LEWD
#DIM DYNAMIC ROOMMATE
#DIM AFFINITY_REQ

;wives/concubines have privileges, but only if current roommates don't actively despise them
IF TALENT:CHARA:妻 == 2 || TALENT:CHARA:妾
	AFFINITY_REQ = 100
ELSE ;otherwise friends are allowed to stay over
	AFFINITY_REQ = 150
ENDIF

DO
    ROOMMATE = FINDCHARA(CFLAG:現在位置, CFLAG:MASTER:現在位置, ROOMMATE + 1, CHARANUM - 1)
	SIF ROOMMATE == CHARA
		CONTINUE
	IF ROOMMATE > 0
		SIF LEWD && GET_UWAKI_HISTORY(ROOMMATE, CHARA) < 2 ;not aware and accepting of relationship
			RETURNF 0
		LOCAL = RELATION:ROOMMATE:CHARA ? RELATION:ROOMMATE:CHARA # 100
		SIF LOCAL < AFFINITY_REQ
			RETURNF 0
	ENDIF
LOOP ROOMMATE > 0 && ROOMMATE < CHARANUM - 1
RETURNF 1

@DAYEVENT_VISIT(ARG, TYPE)
#FUNCTION
#DIMS TYPE
#DIM START_TIME
#DIM END_TIME

IF GET_MAPID(CFLAG:ARG:現在位置) == MAIN_MAP ;whole waking period
	START_TIME = CFLAG:ARG:起床時間
	END_TIME = CFLAG:ARG:就寝時間
ELSEIF TYPE == "day" ;wake-up until return
	START_TIME = CFLAG:ARG:起床時間
	END_TIME = CFLAG:ARG:帰宅時間
ELSE ;a little wider than bedtime
	START_TIME = CFLAG:ARG:来訪時間
	END_TIME = (CFLAG:ARG:就寝時間 + 120) % 1440
ENDIF

RETURNF BETWEENTIME(START_TIME,END_TIME)

@CUSTOM_OPTION_13
PRINTFORM [ 13] - 启用通用的早晨和夜晚事件：
SELECTCASE ENABLE_DAYEVENTS
	CASE 0
		PRINTL 禁用
	CASE 1
		PRINTL 仅允许有剧本的角色
	CASE 2
		PRINTL 允许所有角色  
	CASEELSE
		PRINTL ＊无效＊
ENDSELECT

@CUSTOM_OPTION_RESULT_13
ENABLE_DAYEVENTS = (ENABLE_DAYEVENTS+1) % 3

@EVENTTRAIN
#LATER
IF MORNING_SEX_CHARA
	CALL 脱衣処理(MORNING_SEX_CHARA, "全裸", "「表示無」")
	CALL 脱衣処理(MASTER, "全裸", "「表示無」")
ENDIF
