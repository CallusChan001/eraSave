
;Modified Japanese functions to have buttons with mouse over text also simplified the logic
;to serve our purposes

;Prints a button with the specified mouseover title, value and text
;ARGS Text for the button
;ARG value for the button
;TITLE mouse over text
;Padding for the number
@HTML_BUTTON_TITLE(ARGS, ARG, TITLE, digit=1)
#FUNCTIONS
#DIM DYNAMIC digit
#DIMS DYNAMIC TITLE
digit = MAX(digit, STRLENS(@"{ARG}"))
RETURNF @"<button value='{ARG}' title='%HTML_ESCAPE(TITLE)%'> [{ARG, digit}] %ARGS% </button>"


;Print an buttons based on a array of text choices and a array of mouse over 
@ASK_CHOICES_TITLE(refChoices, refTitles, size)
#DIMS REF refChoices, 0
#DIMS REF refTitles, 0
#DIM size
#DIMS DYNAMIC html

FOR LOCAL, 0, size
    html += HTML_SPACE(2) + HTML_BUTTON_TITLE(refChoices:LOCAL, LOCAL, refTitles:LOCAL, 3) + "<br>"
NEXT
;表示
HTML_PRINT @"<p align='left'><nobr><br>%html%<br></nobr></p>"
PRINTL 
PRINTFORML [999] 返回
$INPUT_LOOP
INPUT
SELECTCASE RESULT
	CASE 0 TO size - 1
		RETURN RESULT
	CASE 999
		RETURN -1
	CASEELSE
		CLEARLINE 1
		GOTO INPUT_LOOP
ENDSELECT
RETURN RESULT
