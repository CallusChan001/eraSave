@Update_Menu
#DIM nLineCnt_Head

LOADGLOBAL
FOR LOCAL, 0, 100
	PRINTL
NEXT
CUSTOMDRAWLINE -
nLineCnt_Head = LINECOUNT
REDRAW 0
DO
	SIF LINECOUNT - nLineCnt_Head > 0
		CLEARLINE LINECOUNT - nLineCnt_Head
	CALL UPDATE_STATE
	SELECTCASE RESULT
		CASE 0
			SAVEGLOBAL
			RETURN
	CASEELSE
	ENDSELECT
LOOP 1

@UPDATE_PAGE_0(P0_PAGE)
#DIM DYNAMIC P0_PAGE
#DIM C_ID
#DIM DYNAMIC DEBUG_MODE

[IF_DEBUG]
	DEBUG_MODE = 1
[ENDIF]

CALL COLORMESSAGE("//.Update", C_S_BLUE, 1, 1)
SIF SAVESTR:本体バージョン != eraTW_Version
	SETCOLOR C_RED
PRINTBUTTON "□ 存档更新", 501
PRINTL
SETCOLOR C_CREAM
PRINTFORML （选择剧本，更新存档数据，执行剧本更新函数）
RESETCOLOR
CALL PRINTBUTTON_OR_SPACE("□ 旧版UPDATE", 502, DEBUG_MODE)
IF DEBUG_MODE
	SETCOLOR C_CREAM
	PRINTFORML （旧有的更新功能，在常规游戏中不推荐）
	RESETCOLOR
ELSE
	PRINTL
ENDIF
PRINTL
PRINTL

CALL COLORMESSAGE("//.Settings", C_S_BLUE, 1, 1)
CALL PRINTBUTTON_OR_SPACE("□ 妊娠设定", 601, !UPDATE_REQUIRED_COND())
PRINTL

CALL COLORMESSAGE("//.Extra", C_S_BLUE, 1, 1)
;CALL PRINTBUTTON_OR_SPACE("□ Apply The Expanded Relations Patch", 504, !UPDATE_REQUIRED_COND())
CALL PRINTBUTTON_OR_SPACE("□ (Debug) 对白颜色列表·自定义", 507, DEBUG_MODE)
CALL PRINTBUTTON_OR_SPACE("□ (Debug) 生成委托", 602, DEBUG_MODE)
;CALL PRINTBUTTON_OR_SPACE("□ (Debug) Test Translations", 603, DEBUG_MODE)

@UPDATE_PAGE_1(P1_PAGE)
#DIM DYNAMIC P1_PAGE
#DIM CHARA
#DIM CHARA_DIALOGUE_COUNT
RETURN
IF (CHARANUM % UpdateP1_CharaPerPage == 0)
	UpdateMaxPages = (CHARANUM / UpdateP1_CharaPerPage)
ELSE 
	UpdateMaxPages = (CHARANUM / UpdateP1_CharaPerPage) + 1
ENDIF

PRINTFORML Page {P1_PAGE} ({((P1_PAGE - 1) * UpdateP1_CharaPerPage) + 1}～{MIN((P1_PAGE * UpdateP1_CharaPerPage), CHARANUM - 2)})
FOR CHARA, 1, CHARANUM - 1
	SIF CFLAG:CHARA:出禁
		CONTINUE

	RESULTS =
	IF INRANGE(CHARA, (((P1_PAGE - 1) * UpdateP1_CharaPerPage) + 1), (P1_PAGE * UpdateP1_CharaPerPage))
		CALL KOJO_COUNT_CHECK(CHARA, 1)
		CHARA_DIALOGUE_COUNT = RESULT
		IF !RESULT
			SETCOLOR C_GRAY
			LOCALS '= @"%NAME:CHARA, 25, LEFT% (无剧本)"
		ELSE
			LOCALS '= @"%NAME:CHARA, 25, LEFT%"
			SIF CHARA_DIALOGUE_COUNT > 1
				LOCALS '= @"%LOCALS% (有候选剧本)"
		ENDIF
		SIF HL_CHARA == CHARA
			SETCOLOR C_ORANGE
		PRINTFORML [{CHARA, 3}] - %LOCALS%
		RESETCOLOR
	ENDIF
NEXT
PRINTL
PRINTBUTTON @"%"上一页",15,LEFT%", 8102
PRINTBUTTON @"%"下一页",15,LEFT%", 8103
PRINTL

@UPDATE_PAGE_2(P2_PAGE)
#DIM DYNAMIC P2_PAGE
#DIM DYNAMIC P2_COUNT
#DIM DYNAMIC P2_LOOP, 2
#DIM DYNAMIC P2_LIST, 人物数量上限
#DIMS DYNAMIC P2_LISTS, 2
#DIMS DYNAMIC P2_STR_BUILD,2
#DIM DYNAMIC P2_KOJO_COUNT
#DIM CHARA
RETURN

PRINTFORML 点击剧本名即可切换，点击当前剧本来重置角色。
HTML_PRINT @"更改剧本也伴随着一部分角色数据重置。无须重置的例外情况以<font color='orange'>橙色</font>显示。"
HTML_PRINT @"当前剧本以<font color='lime'>绿色</font>显示"
PRINTL
;Get characters who has more than one dialogue
P2_COUNT = 1
FOR P2_LOOP, 0, CHARANUM
	P2_KOJO_COUNT = 0
	;normally IS_MOB would be used but ATW doesn't have that so check for the mob talent I guess
	SIF TALENT:P2_LOOP:300
		CONTINUE

	;check if characters have dialogues
	;skip if there's none or only one
	FOR P2_LOOP:1,0,5
		VARSET RESULT
		VARSET RESULTS:1
		VARSET RESULTS:2
		IF P2_LOOP:1
			TRYCALLFORM M_KOJO_K{NO:P2_LOOP}_{P2_LOOP:1}
		ELSE
			TRYCALLFORM M_KOJO_K{NO:P2_LOOP}
		ENDIF
		IF RESULT
			SIF !GETBIT(CFLAG:P2_LOOP:口上実装状況,P2_LOOP:1)
				SETBIT CFLAG:P2_LOOP:口上実装状況,P2_LOOP:1
			P2_KOJO_COUNT++
		ENDIF
	NEXT
	SIF P2_KOJO_COUNT < 2
		CONTINUE
	;if there's more than 1 then add to the list
	P2_LIST:P2_COUNT = P2_LOOP
	P2_COUNT++
NEXT

IF (P2_COUNT % UpdateP2_CharaPerPage == 0)
	UpdateMaxPages = (P2_COUNT / UpdateP2_CharaPerPage)
ELSE 
	UpdateMaxPages = (P2_COUNT / UpdateP2_CharaPerPage) + 1
ENDIF

PRINTFORML Page {P2_PAGE} ({((P2_PAGE - 1) * UpdateP2_CharaPerPage) + 1}～{MIN((P2_PAGE * UpdateP2_CharaPerPage), CHARANUM - 2)})
FOR P2_LOOP, 0, P2_COUNT
	CHARA = P2_LIST:P2_LOOP
	VARSET P2_STR_BUILD
	SIF CFLAG:CHARA:出禁
		CONTINUE

	RESULTS =
	IF INRANGE(P2_LOOP, (((P2_PAGE - 1) * UpdateP2_CharaPerPage) + 1), (P2_PAGE * UpdateP2_CharaPerPage))
		;has update
		IF CFLAG:CHARA:口上セレクタ
			TRYCALLFORM M_KOJO_K{NO:CHARA}_{CFLAG:CHARA:口上セレクタ}
		ELSE
			TRYCALLFORM M_KOJO_K{NO:CHARA}
		ENDIF
		LOCALS '= @"%NAME:CHARA, 25, LEFT%"
		SIF HL_CHARA == CHARA
			SETCOLOR C_ORANGE
		PRINTPLAINFORM [{CHARA, 3}] - %LOCALS%
		PRINTL
		RESETCOLOR
		P2_STR_BUILD '= " 	"
		P2_STR_BUILD:1 '= ""
		FOR P2_LOOP:1, 0, 5
			VARSET RESULT
			VARSET RESULTS:1
			VARSET RESULTS:2
			IF P2_LOOP:1
				TRYCALLFORM M_KOJO_K{NO:CHARA}_{P2_LOOP:1}
			ELSE
				TRYCALLFORM M_KOJO_K{NO:CHARA}
			ENDIF
			IF RESULT
				SIF RESULTS:1 == ""
					RESULTS:1 = 初始
				P2_LISTS:0 '= RESULTS:1
				P2_LISTS:1 '= @"\@RESULTS:2 != "" ? %RESULTS:2% # 无简介 \@"

				P2_STR_BUILD:1 '= @"<button value='{(10000*(P2_LOOP:1+1))+CHARA}' title='%HTML_ESCAPE(P2_LISTS:1)%'>%P2_LISTS:0%</button>   "
				IF CFLAG:CHARA:口上セレクタ == P2_LOOP:1
					P2_STR_BUILD:1 '= @"<font color=\"#9999ff\">%P2_STR_BUILD:1%</font>"
				ELSEIF GETBIT(RESULT:1, CFLAG:CHARA:口上セレクタ)
					P2_STR_BUILD:1 '= @"<font color=\"#99ff99\">%P2_STR_BUILD:1%</font>"
				ENDIF
			ENDIF
			P2_STR_BUILD += P2_STR_BUILD:1
			P2_STR_BUILD:1 '= ""
		NEXT
		HTML_PRINT P2_STR_BUILD
		PRINTL
	ENDIF
NEXT
PRINTL
PRINTBUTTON @"%"上一页",15,LEFT%", 8102
PRINTBUTTON @"%"下一页",15,LEFT%", 8103
PRINTL

@UPDATE_STATE(C_ID,OP)
#DIMS    OP         ;実行オプション
#DIM     C_ID       ;登録番号
#DIM	 D_ID
#DIM DYNAMIC    PAGE       ;表示中のページ
#DIM DYNAMIC	OPERATE_MODE
#DIM	 ACTIVE_SUBPAGE = 1
#DIM	 SUBPAGE_MEMO_1 = 1
#DIM	 SUBPAGE_MEMO_2 = 1
#DIMS CONST DISP_NAME = "主页", "个别UPDATE", "切换剧本&重置角色"
#DIM CREDRAW
#DIM FIRST_LINE
#DIM INPUT_RESULT

VARSET nCharaUpdate

CALL createAndPopulatePatchTable
CALL 更新未读信息

FIRST_LINE = LINECOUNT
$REPRINT
CLEARLINE LINECOUNT - FIRST_LINE
CREDRAW = CURRENTREDRAW()
REDRAW 0
IF PAGE == 0
	IF UPDATE_REQUIRED_COND()
		CALL COLORMESSAGE(@"正在从旧版本更新存档数据", C_RED, 1)
		PRINTFORML 更新可能会添加以下内容：新版本中包含的新角色、新特质、新标志
		PRINTFORML 在执行更新之前无法访问其他选项
	ELSE
		CALL COLORMESSAGE(@"存档数据已是最新版本", C_YELLOW, 1)
		PRINTFORML 执行存档更新通常能解决问题，不过某些变量或标志可能会被重置
	ENDIF
	PRINTL 
	IF News_Amount
		CALL PRINTBUTTON_EX(@"□ {News_Amount}条未读更新日志 □",508, , , ,)
		PRINTL
	ELSE
		CALL PRINTBUTTON_EX(@"□ 查看更新日志 □",508, , , ,)
		PRINTL
	ENDIF
	PRINTFORML 欢迎来到eraTW Ver.%eraTW_Version%
	PRINTL 
ELSEIF PAGE == 1
	CALL UPDATE_AND_SELECTION_OF_KOJO_MODDING_2(,ACTIVE_SUBPAGE - 1,OPERATE_MODE)
	;SIF UPDATE_KOJO_CUSTOMIZE
	;	SETCOLOR C_ORANGE 
	;PRINTBUTTON "[单独UPDATE]", 701
	;PRINTFORML 
	;RESETCOLOR
	;SIF Ask_SwitchDialogue
	;	SETCOLOR C_ORANGE 
	;PRINTBUTTON "[切换剧本]", 702
	;PRINTFORML 
	;RESETCOLOR
	;SIF ResetToggle
	;	SETCOLOR C_RED
	;PRINTBUTTON "[重置角色]", 703
	;PRINTFORML 
	;PRINTL
	;RESETCOLOR
	;CALL HIGHLIGHT_CHARA_DISPLAY(HL_CHARA)
ELSEIF PAGE == 2
	PRINTFORML 点击剧本名即可切换，点击当前剧本来重置角色。
	HTML_PRINT @"更改剧本也伴随着角色的部分数据重置。不重置的例外情况以<font color='orange'>橙色</font>显示。"
	HTML_PRINT @"当前剧本以<font color='lime'>绿色</font>显示"
	CALL UPDATE_AND_SELECTION_OF_KOJO_MODDING_2("SELECTION",ACTIVE_SUBPAGE - 1,OPERATE_MODE)
ENDIF
TRYCALLFORM UPDATE_PAGE_{PAGE}(ACTIVE_SUBPAGE)
DRAWLINE
FOR LOCAL, 0, VARSIZE("DISP_NAME")
	SIF PAGE == LOCAL
		SETCOLOR 0x00FFFF
	SIF UPDATE_MENU_PAGE_BANNED_COND(LOCAL)
		SETCOLOR 0x6A7783
	SIF DISP_NAME:LOCAL == "切换剧本&重置角色" && FINDELEMENT(剧本错误警告,1) > -1
		SETCOLOR C_RED
	PRINTBUTTON DISP_NAME:LOCAL, LOCAL + 8002
	RESETCOLOR
	PRINTFORM 　　
NEXT
PRINTFORML 
PRINTL
PRINTBUTTON " [返回] ", 0
RESETCOLOR
DO
	INPUT
	INPUT_RESULT = RESULT
	SELECTCASE INPUT_RESULT
	CASE 0
		VARSET HL_CHARA
		VARSET UPDATE_KOJO_CUSTOMIZE
		VARSET Ask_SwitchDialogue
		VARSET ResetToggle
		RETURN 0
	CASE 501
		CALL UPDATE_MAIN("full")
	CASE 502
		CALL UPDATE
	;CASE 504
		;PRINTFORML This option will set modified, expanded affinities for almost all characters.
		;PRINTFORML It will override all current relations between characters, confirm to proceed.
		;CALL ASK_YN
		;IF !INPUT_RESULT
			;FOR C_ID, 1, CHARANUM - 1
				;CALL ADD_SET_RELATION_PROC(C_ID)
			;NEXT
			;PRINTFORMW Applied.
		;ENDIF
	[IF_DEBUG]
	CASE 507
		CALL CHANGE_PT_KOJO_COLOR
	[ENDIF]
	CASE 508
		CALL patchNotes
	CASE 601
		CALL PREGNANCY_TYPE_OPTIONS
	CASE 602
		CALL DEBUG_IRAI_HAPPEN
	;CASE 603
		;CALL DEBUG_TRANSLATION_MENU
	;CASE 701
	;	ResetToggle = 0
	;	Ask_SwitchDialogue = 0
	;	UPDATE_KOJO_CUSTOMIZE = 1
	;CASE 702
	;	ResetToggle = 0
	;	Ask_SwitchDialogue = 1
	;	UPDATE_KOJO_CUSTOMIZE = 0
	;CASE 703
	;	ResetToggle = 1
	;	Ask_SwitchDialogue = 0
	;	UPDATE_KOJO_CUSTOMIZE = 0
	CASE 1004
		OPERATE_MODE += 1
		SIF OPERATE_MODE > 2
			OPERATE_MODE = 0
	CASE 8002 TO VARSIZE("DISP_NAME")+8002
		IF UPDATE_MENU_PAGE_BANNED_COND(INPUT_RESULT-8002)
			PRINTFORMW 现在不可用
			GOTO REPRINT
		ENDIF
		SELECTCASE PAGE
			CASE 1
				SUBPAGE_MEMO_1 = ACTIVE_SUBPAGE
			CASE 2
				SUBPAGE_MEMO_2 = ACTIVE_SUBPAGE
		ENDSELECT
		VARSET HL_CHARA
		PAGE = INPUT_RESULT-8002
		SELECTCASE PAGE
			CASE 1
				ACTIVE_SUBPAGE = SUBPAGE_MEMO_1
				OPERATE_MODE = 1
			CASE 2
				ACTIVE_SUBPAGE = SUBPAGE_MEMO_2
				OPERATE_MODE = 2
		ENDSELECT
	CASE 8101
		ACTIVE_SUBPAGE = 1
	CASE 8102
		ACTIVE_SUBPAGE--
		SIF ACTIVE_SUBPAGE <= 0
			ACTIVE_SUBPAGE = UpdateMaxPages
	CASE 8103
		ACTIVE_SUBPAGE++
		SIF ACTIVE_SUBPAGE > UpdateMaxPages
			ACTIVE_SUBPAGE = 1
	CASE 8104
		ACTIVE_SUBPAGE = UpdateMaxPages
	CASE 1 TO CHARANUM - 1
		;IF HL_CHARA == INPUT_RESULT
			C_ID = INPUT_RESULT
			IF Ask_SwitchDialogue
				CALL SELECT_NEW_KOJO(C_ID)
				SIF INPUT_RESULT == -1
					PRINTFORMW %CALLNAME:C_ID%没有其他剧本
			ELSEIF ResetToggle
				PRINTFORML 真的要重置%CALLNAME:C_ID%吗？
				PRINTFORML 关系、事件、能力、标志等都将被重置
				CALL ASK_YN
				IF !RESULT
					CALL RESET_KOJO_CHARA(C_ID)
				ELSE
					
				ENDIF
			ELSE
				CALL UPDATE_DIALOGUE_INDIVIDUAL(C_ID, 1, 1)
			ENDIF
		;ELSE
		;	HL_CHARA = INPUT_RESULT
		;ENDIF
	CASE 10000 TO 60000
		;character dialogue changer
		LOCAL = INPUT_RESULT
		C_ID = LOCAL%10000
		D_ID = (LOCAL/10000)-1

		IF PAGE != 2
			;don't do this when not in the dialogue selector
			PRINTW 输入错误
			CONTINUE
		ELSEIF CFLAG:C_ID:口上セレクタ == D_ID
			VARSET INPUT_RESULT
			PRINTFORML 真的要重置%CALLNAME:C_ID%吗？
			PRINTFORML 关系、事件、能力、标志等都将被重置
			CALL ASK_YN
			IF !RESULT
				CALL RESET_KOJO_CHARA(C_ID)
			ELSE
				
			ENDIF
		ELSE
			VARSET INPUT_RESULT
			IF D_ID
				TRYCALLFORM M_KOJO_K{NO:C_ID}_{D_ID}
			ELSE
				TRYCALLFORM M_KOJO_K{NO:C_ID}
			ENDIF
			CALL CHARACTER_GET_NEW_DIALOGUE(C_ID,D_ID,!GETBIT(RESULT:1,CFLAG:C_ID:口上セレクタ) && !剧本错误警告:C_ID)
		ENDIF
	CASEELSE
		$RETRY
		PRINTW 输入错误
		CONTINUE
	ENDSELECT
	BREAK
LOOP 1
GOTO REPRINT

@HIGHLIGHT_CHARA_DISPLAY(C_ID)
#DIM LAYER_COUNT
#DIM RESET_OFFSETS

#DIM CURRENT_INDEX      ;the current element index for the line
#DIM CURRENT_X_OFFSET   ;the current x offset for the line
#DIM X_OFFSETS, 人物数量上限 ;stores the offset per element
#DIM ELEMENT_WIDTH      ;the width of the current line+image
#DIM IMAGE_WIDTH        ;the width of the image
#DIM ELEMENT_HEIGHT       ;the height of the element
#DIM CHARACTER_WIDTH    ;estimated width in pixels per character

#DIM C_ID
#DIMS TITLE_TEXT
#DIM TITLE_COLOR

TITLE_TEXT '= @"[%CALLNAME:C_ID%]"

CALL SET_KOJO_COLOR(C_ID)
TITLE_COLOR = GETCOLOR()
RESETCOLOR

IF C_ID && FLAG:画像表示
	SIF !CHARACTER_WIDTH
    CHARACTER_WIDTH = CLIENTWIDTH() / HTML_STRINGLEN(DRAWLINESTR)

	IF TEMP_HTML:LAYER_COUNT == "" ;first element, reset
		CURRENT_X_OFFSET = 0
		CURRENT_INDEX = 0
	ENDIF

	SIF RESET_OFFSETS
		VARSET X_OFFSETS

	IMAGE_WIDTH = 180
	ELEMENT_HEIGHT = IMAGE_WIDTH+GETCONFIG("一行の高さ")
	ELEMENT_WIDTH = MAX(HTML_STRINGLEN(TITLE_TEXT) * CHARACTER_WIDTH, IMAGE_WIDTH, X_OFFSETS:CURRENT_INDEX)

	SIF !X_OFFSETS:CURRENT_INDEX
		X_OFFSETS:CURRENT_INDEX = ELEMENT_WIDTH

	LOCAL = FLAG:立ち絵種類
	FLAG:立ち絵種類 = 1

	TRYCALLFORM IMAGE_EX_I{NO:C_ID}_PREリソースチェック(C_ID, "顔絵", "服", "通常", "", "", "")
	CALL GET_BASE_RESOURCE(C_ID, "顔絵", "服", "通常", "", "")
	SIF !RESULT
		RESULTS = %GET_BASESTYLE(C_ID, "顔絵")%_服_通常_{C_ID}
	TEMP_HTML:LAYER_COUNT += @"<div width='{ELEMENT_WIDTH}px' height='{ELEMENT_HEIGHT}px' xpos='{CURRENT_X_OFFSET}px'>"
	TEMP_HTML:LAYER_COUNT += @"<p align='center'><font color='#%TOSTR(TITLE_COLOR,"X6")%'>%TITLE_TEXT%</font></p>"
	CALL 画像セット(RESULTS, 0, 0, 100, 0, 1, "", "")
	TEMP_HTML:LAYER_COUNT += "</div>"

	CURRENT_X_OFFSET += ELEMENT_WIDTH
	CURRENT_INDEX++

	CALL 画像一斉表示(0, 0, 1, -1)
	PRINTL

	FLAG:立ち絵種類 = LOCAL
ELSE
	PRINTL
	PRINTL
	PRINTL
	PRINTL
	PRINTL
	PRINTL
	PRINTL
	PRINTL
	PRINTL
	PRINTFORML %COND_STR(@"%TITLE_TEXT%", C_ID > 0)%
	PRINTL
ENDIF

@KOJO_RESET_CONFIRM
IF !Reset_NeverAskAgain
	PRINTFORM 为防止可能导致存档数据出现问题的情况，切换剧本需要
	CALL COLORMESSAGE("完全初始化该角色",C_YELLOW,0,0)
	PRINTFORML 
	PRINTFORML 除非明确注明无需重置，否则切换剧本时将进行重置
	PRINTFORML 亲密度、事件、能力、标志等都将被初始化
	PRINTFORML 
	PRINTFORML 请在理解的基础上做出决定。此后将不再进行确认
	CALL PRINTBUTTON_EX("[重置]",0,)
	$YESIAMOVER18
	INPUT
	IF RESULT == 0
		Reset_NeverAskAgain = 1
	ELSE
		GOTO YESIAMOVER18
	ENDIF
ENDIF

@剧本错误确认
IF !Reset_NeverAskAgain
	PRINTFORML 无法找到旧存档中游玩的剧本
	PRINTFORML 可能是手动进行了剧本的增删、或是剧本出现编号变动
	PRINTFORML 如果只是剧本的编号变动，那么该角色的进度数据完全可以继承到变更编号后的同一剧本中
	PRINTFORML 因此在此提供了跳过重置的机会，让玩家手动继承剧本进度
	CALL COLORMESSAGE,"注意：因为同一角色的不同剧本对标志的使用不同，请勿在此切换至非先前游玩剧本\n如果当前版本没有先前游玩的剧本，请同意重置角色而不要继承进度\n※若不重置标志就切换剧本可能导致剧本甚至游戏运行错误※",0xff130b,1
	PRINTFORML 请在理解本提示的基础上使用切换剧本&重置角色功能，此后将不再进行确认
	CALL PRINTBUTTON_EX("[我已知情]",0,)
	$YESIAMOVER18
	INPUT
	IF RESULT == 0
		Reset_NeverAskAgain = 1
	ELSE
		GOTO YESIAMOVER18
	ENDIF
ENDIF

@SELECT_NEW_KOJO(C_ID)
#DIM C_ID
#DIM DYNAMIC kojoIndex
#DIM DYNAMIC validKojoCount
#DIMS DYNAMIC kojoNameList, 4
#DIM DYNAMIC kojoIndexList, 4
#DIM DYNAMIC kojoListPointer
#DIMS kojoDescriptions, DESCRIPTIONS_SIZE
#DIM DYNAMIC kojoSelectResult

FOR kojoIndex,0,5
	VARSET RESULT
	VARSET RESULTS:1
	VARSET RESULTS:2
	IF kojoIndex
		TRYCALLFORM M_KOJO_K{NO:C_ID}_{kojoIndex}
	ELSE
		TRYCALLFORM M_KOJO_K{NO:C_ID}
	ENDIF
	IF RESULT
		kojoDescriptions:kojoListPointer = \@RESULTS:2 != "" ? %RESULTS:2% # 无简介 \@
		SIF RESULTS:1 == ""
			RESULTS:1 = 初始
		kojoNameList:kojoListPointer = %CALLNAME:C_ID%%RESULTS:1%剧本
		kojoIndexList:kojoListPointer = kojoIndex
		kojoListPointer ++
		IF !GETBIT(CFLAG:C_ID:口上実装状況,kojoIndex)
			validKojoCount += 1
			SETBIT CFLAG:C_ID:口上実装状況,kojoIndex
		ELSEIF GETBIT(CFLAG:C_ID:口上実装状況,kojoIndex)
			validKojoCount += 1
		ENDIF
	ENDIF
NEXT
SIF validKojoCount < 2
	RETURN -1
IF validKojoCount > 1
	PRINTFORML 请选择要使用的剧本 (鼠标悬停会显示简介)
	CALL KOJO_ACTIVE_INFO(C_ID)
	SIF RESULTS:1 == ""
		RESULTS:1 = 初始
	PRINTFORML %CALLNAME:C_ID%现在的版本: %RESULTS:1%剧本
	CALL ASK_CHOICES_TITLE(kojoNameList, kojoDescriptions, kojoListPointer)
	IF RESULT == -1 ;cancelled
		PRINTFORMW 未变更%CALLNAME:C_ID%的剧本
		RETURN -1
	ENDIF
	kojoSelectResult = kojoIndexList:RESULT
	IF CFLAG:C_ID:口上セレクタ == kojoSelectResult ;the dialogue is already in use
		PRINTFORMW 未变更%CALLNAME:C_ID%的剧本
	ELSE
		IF kojoSelectResult
			TRYCALLFORM M_KOJO_K{NO:C_ID}_{kojoSelectResult}
		ELSE
			TRYCALLFORM M_KOJO_K{NO:C_ID}
		ENDIF
		CALL CHARACTER_GET_NEW_DIALOGUE(C_ID,kojoSelectResult,!GETBIT(RESULT:1,CFLAG:C_ID:口上セレクタ))
	ENDIF
ENDIF

@CHARACTER_RESET_KOJO_SELECT(C_ID)
#DIM C_ID
CALL KOJO_RESET_CONFIRM
CALL RESET_KOJO_CHARA(C_ID)

;Unified resetting/dialogue change function
;C_ID: Character ID
;D_ID: Dialogue ID
;RequireReset: Whether to reset the character
;RequireConfirm: Whether to prompt the user to confirm a reset
@CHARACTER_GET_NEW_DIALOGUE(C_ID,D_ID,RequireReset=1)
#DIM C_ID
#DIM D_ID
#DIM RequireConfirm
#DIM RequireReset
IF RequireReset == 0
	IF !剧本错误警告:C_ID
		PRINTFORML 切换前后的剧本共用框架，因此无需重置进度
	ELSE
		CALL 剧本错误确认
	ENDIF
	PRINTFORML 将进行剧本切换，现在是否还需要重置角色进度？
	CALL ASK_M("重置",1,"不重置",1,"撤销，我要返回",1)
	IF !RESULT
		RequireReset = 1
	ELSEIF RESULT == 2
		RETURN		
	ENDIF
ENDIF
IF RequireReset
	CALL RESET_KOJO_CHARA(C_ID,1)
	CALL RESET_KOJO_CHARA_EXPABL(C_ID)
ENDIF
PRINTFORML 剧本切换中…
CFLAG:C_ID:口上セレクタ = D_ID
CALL KOJO_ACTIVE_INFO(C_ID)
SIF RESULTS:1 == ""
	RESULTS:1 = 初始
PRINTFORML 执行%RESULTS:1%剧本的UPDATE函数...
TRYCALLFORM M_KOJO%RESULTS%_UPDATE_K{NO:C_ID}
SIF UPDATE_KOJO_CUSTOMIZE
	CALL UPDATE_SHOW_SETTINGS(C_ID)
剧本错误警告:C_ID = 0
PRINTL
PRINTFORMW %CALLNAME:C_ID%现在的版本: %RESULTS:1%剧本

;print a button or leave an empty space when condition isn't met
@PRINTBUTTON_OR_SPACE(ARGS:0, ARG, CONDITION = 1)
#DIM CONDITION
IF CONDITION
	PRINTBUTTON @"%ARGS:0%", ARG
	PRINTL
ELSE
	PRINTL
ENDIF

;conditions for a page to be come inaccesible
@UPDATE_MENU_PAGE_BANNED_COND(PAGE)
#FUNCTION
#DIM PAGE
SELECTCASE PAGE
	CASE 1
		SIF UPDATE_REQUIRED_COND()
			RETURNF 1
	CASE 2
		SIF UPDATE_REQUIRED_COND()
			RETURNF 1
ENDSELECT

RETURNF 0


;conditions for the menu to prompt for update
@UPDATE_REQUIRED_COND()
#FUNCTION

SIF CHECK_UPDATE(eraTW_Version)
	RETURNF 1

RETURNF 0
