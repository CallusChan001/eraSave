;original function from eraERIN

;口上用文字色関係の処理

;色変更対象の選択
@CHANGE_PT_KOJO_COLOR
#DIMS DYNAMIC strDialog

PRINTFORML Dialogue Color List

FOR LOCAL:0, 1, CHARANUM
	CALL KOJO_ACTIVE_INFO(LOCAL)
	IF RESULT
		CALL SET_KOJO_COLOR(NO:LOCAL)
		SIF k_DialogueColorSet:(NO:LOCAL):0 != 0
			SETCOLOR k_DialogueColor:(NO:LOCAL):0
		LOCAL:1 = GETCOLOR()
		PRINTFORM [{LOCAL:0, 3}] %CALLNAME:LOCAL,14,LEFT%  Current Dialogue Color：
		PRINTFORM %UNICODE(0x2586)%%UNICODE(0x2586)%%UNICODE(0x2586)%%UNICODE(0x2586)%　(%TOUPPER(TOSTR(LOCAL:1, "X6"))%) (R:{LOCAL:1 / 0x10000,3} G:{(LOCAL:1 & 0xFF00) / 0x100,3} B:{LOCAL:1 & 0xFF,3})
		PRINTFORML 「Girls are speaking～!」
		RESETCOLOR
		strDialog += @"{LOCAL:0}:"
	ENDIF
NEXT
CALL COLOR_LINE
PRINTL [  0] Return

$INPUT_LOOP
INPUT

IF RESULT == 0
	RETURN
ELSE
	SIF !SPLIT_CHECK(strDialog, RESULT)
		GOTO INPUT_LOOP
	CALL PT_SET_KOJO_COLOR(RESULT)
ENDIF

RESTART



;指定キャラの文字色変更
;ARG:0	対象キャラID
@PT_SET_KOJO_COLOR(ARG:0)
;LOCAL
;0	LOOP
;1	今の文字色
;2	デフォルトカラー

PRINTFORML 
PRINTFORML Setting Dialogue Color for %CALLNAME:(ARG:0)% (demo purpose only).
SIF k_DialogueColorSet:ARG:0 != 0
	LOCAL:1 = k_DialogueColor:ARG:0
CALL SET_KOJO_COLOR(ARG:0)
LOCAL:2 = GETCOLOR()
SIF k_DialogueColorSet:ARG:0 == 0
	LOCAL:1 = LOCAL:2
	
SETCOLOR LOCAL:1
PRINTFORML Current：%UNICODE(0x2586)%%UNICODE(0x2586)%%UNICODE(0x2586)%%UNICODE(0x2586)%　(%TOUPPER(TOSTR(LOCAL:1, "X6"))%) (R:{LOCAL:1 / 0x10000,3} G:{(LOCAL:1 & 0xFF00) / 0x100,3} B:{LOCAL:1 & 0xFF,3})

PRINTFORML Example：「Girls are speaking～!」
RESETCOLOR

PRINTFORML 
PRINTFORML Change to what color?
SETCOLOR LOCAL:2
PRINTFORML [1] Dialogue Default %UNICODE(0x2586)%%UNICODE(0x2586)%%UNICODE(0x2586)%%UNICODE(0x2586)%　(%TOUPPER(TOSTR(LOCAL:2, "X6"))%) (R:{LOCAL:2 / 0x10000,3} G:{(LOCAL:2 & 0xFF00) / 0x100,3} B:{LOCAL:2 & 0xFF,3})
RESETCOLOR
PRINTFORML [2] Customize
PRINTFORML [3] Standard System Color
PRINTFORML [0] Cancel

$INPUT_LOOP
INPUT

IF RESULT == 0
	RETURN
ELSEIF RESULT == 1
	k_DialogueColor:ARG:0 = LOCAL:2
	k_DialogueColorSet:ARG:0 = 1
ELSEIF RESULT == 2
	CALL COLOR_RGBBAR,,LOCAL:1
	IF RESULT == -1
		;キャンセルされているので、何もしない
		RETURN
	ENDIF
	k_DialogueColor:ARG:0 = RESULT
	k_DialogueColorSet:ARG:0 = 1
ELSEIF RESULT == 3
	RESETCOLOR
	k_DialogueColor:ARG:0 = GETCOLOR()
	k_DialogueColorSet:ARG:0 = 1
ELSE
	GOTO INPUT_LOOP
ENDIF

SETCOLOR k_DialogueColor:ARG:0
PRINTFORMW Changed %CALLNAME:(ARG:0)%'s Dialogue color to %UNICODE(0x2586)%%UNICODE(0x2586)%%UNICODE(0x2586)%%UNICODE(0x2586)%　(%TOUPPER(TOSTR(k_DialogueColor:ARG:0, "X6"))%) (R:{k_DialogueColor:ARG:0 / 0x10000,3} G:{(k_DialogueColor:ARG:0 & 0xFF00) / 0x100,3} B:{k_DialogueColor:ARG:0 & 0xFF,3})
RESETCOLOR

RETURN

;Show RGB/HSV bar, from eraMeiQ Gaiden
@COLOR_RGBBAR, ARGS, ARG
#DIM nColorBuf

#DIM nInputResult
#DIM nStartLine
#DIM nSelectLine

#DIM nColorList = 0xCF0125, 0xEB5381, 0xF5A0BD, 0xF79101, 0xE49E61, 0xFFF352, 0x7FFF00, 0x2CB232, 0x00A6AF, 0x49BDF0, 0x4169E1, 0x0063A7, 0x7F1184, 0xBC64A4, 0x626063, 0x55461B

nColorBuf = ARG

SIF LINEISEMPTY() == 0
	PRINTFORML
nStartLine = LINECOUNT

$PRINT_LIST
SIF LINECOUNT - nStartLine > 0
	CLEARLINE (LINECOUNT - nStartLine)

PRINTFORML

PRINTFORM Selected Color : 
SETCOLOR nColorBuf
PRINTBUTTON @"■", nColorBuf
RESETCOLOR
PRINTFORM  ({nColorBuf}(0x%TOSTR(nColorBuf, "X6")%))
PRINTL 

SETCOLOR nColorBuf
PRINTFORML Example：「Girls are speaking～!」
RESETCOLOR
PRINTL

nSelectLine = LINECOUNT

; RGBスライダー（実際はボタンの集合体）を表示する
CALL PRINT_RGB_SLIDER( nColorBuf )
PRINTL 
; HSBスライダー（実際はボタンの集合体）を表示する
CALL PRINT_HSB_SLIDER( nColorBuf )
PRINTL 

PRINTFORM Color Presets : 

FOR LOCAL:0, 1, VARSIZE("nColorList")
	PRINT [
	SETCOLOR nColorList:LOCAL
	PRINTBUTTON "■", nColorList:LOCAL
	RESETCOLOR
	PRINT ]
NEXT
RESETCOLOR
PRINTL 

PRINTFORML You can change the color by clicking on the displayed color.

PRINTFORML [-100] Set RGB color
PRINTFORML [-200] Set HEX color
PRINTL 
PRINTFORML [-888] Apply
PRINTFORML [-999] Cancel

WHILE 1
	INPUT
	nInputResult = RESULT
	SELECTCASE  nInputResult
		CASE -888
			BREAK
		CASE -999
			RESULT = -1
			RESULT:1 = -1
			RESULT:2 = -1
			RESULT:3 = -1
			;RETURN RESULT, RESULT:1, RESULT:2, RESULT:3
			RETURN -1
		CASE -100
			FOR LOCAL, 0, 3
				LOCALS '= "Red", "Green", "Blue"
				PRINTFORML Input a value between 0～255 for %LOCALS:LOCAL% (Negative to cancel all)
				INPUT
				IF RESULT >= 0
					LOCAL:(LOCAL + 1) = LIMIT(RESULT, 0, 255)
				ELSE
					GOTO PRINT_LIST
				ENDIF
			NEXT
			nColorBuf = (LOCAL:1 << 16) + (LOCAL:2 << 8) + LOCAL:3
			GOTO PRINT_LIST
		CASE -200
			PRINTFORML Input a value between 000000～FFFFFF (Nothing to cancel)
			INPUTS
			IF RESULTS != ""
				SIF ISNUMERIC("0x" + RESULTS) && INRANGE(TOINT("0x" + RESULTS), 0, TOINT("0xFFFFFF"))
					nColorBuf = TOINT("0x" + RESULTS)
			ENDIF
			GOTO PRINT_LIST
		CASE 0x000000 TO 0xFFFFFF
			nColorBuf = nInputResult
			GOTO PRINT_LIST
	ENDSELECT
	CLEARLINE 1
WEND

RESETCOLOR

SIF LINECOUNT - nSelectLine > 0
	CLEARLINE (LINECOUNT - nSelectLine)
PRINTL 

RESULTS '= HEXTORGB(nColorBuf)
SPLIT RESULTS, "//", LOCALS
RESULT:1 = TOINT(LOCALS:0)
RESULT:2 = TOINT(LOCALS:1)
RESULT:3 = TOINT(LOCALS:2)

;RETURN RESULT, RESULT:1, RESULT:2, RESULT:3
RETURN nColorBuf

;to make this actually work in TW, it will require total rework of current kojo color application

;指定キャラのデフォルト文字色取得
;ARG:0	対象キャラID
; @GET_KOJO_DEFAULT_COLOR(ARG:0)
; TARGET = ARG:0
; TRYCCALLFORM KOJO_DEFAULT_COLOR_{NO:(ARG:0)}_{CFLAG:(ARG:0):701}
; CATCH
	; ;デフォルト色取得関数が無い場合、標準の文字色
	; RESULT = 0xC0C0C0
; ENDCATCH

; IF RESULT == 0
	; ;0が帰ってきている場合は、標準の文字色を使う
	; RESULT = 0xC0C0C0
; ENDIF

; RETURN RESULT



; ;全員の文字色初期設定
; @INIT_KOJO_DEFAULT_COLOR

; ;グローバルセーブから取得　念のためロードしなおしておく
; LOADGLOBAL

; FOR LOCAL:0, 1, CHARANUM
	; CFLAG:(LOCAL:0):701 = GLOBAL:(NO:(LOCAL:0) + 300)
	; IF CFLAG:(LOCAL:0):700 == 0
		; CALL GET_KOJO_DEFAULT_COLOR(LOCAL:0)
		; CFLAG:(LOCAL:0):700 = RESULT
	; ENDIF
; NEXT


;!!! ぱにめーしょんが標準搭載になったので、多分こっちは不要
;RGB形式の文字色指定を16進数に変換
;ARG:0	R
;ARG:1	G
;ARG:2	B
@CONVERT_RGB(ARG:0, ARG:1, ARG:2)
#FUNCTION

LOCAL:0 = (ARG:0 * 0x10000) + (ARG:1 * 0x100) + ARG:2

RETURNF LOCAL:0



