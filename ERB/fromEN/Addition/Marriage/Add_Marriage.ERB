@MARRIAGE_NEW_GAME
VARSET LEGAL_SPOUSE
VARSET MARRIAGE_STATE

;reset concubinage progress, but not completion
VARSET CONCUBINAGE_PROGRESS
VARSET CONCUBINAGE_SUB_PROGRESS
VARSET CONCUBINAGE_CONVINCED_AKYUU
VARSET CONCUBINAGE_CONVINCED_VILLAGERS
VARSET CONCUBINAGE_CONVINCED_EIKI
VARSET CONCUBINAGE_CONVINCED_TENGU
VARSET CONCUBINAGE_CONVINCED_KANAKO
VARSET CONCUBINAGE_CONVINCED_BYAKUREN
VARSET CONCUBINAGE_CONVINCED_MIKO
VARSET CONCUBINAGE_CONVINCED_OKINA
VARSET CONCUBINAGE_CONVINCED_KASEN
VARSET CONCUBINAGE_CONVINCED_REMILIA
VARSET CONCUBINAGE_CONVINCED_MAMIZOU
VARSET CONCUBINAGE_CONVINCED_REIMU
VARSET CONCUBINAGE_BYAKUREN_LABOR_REMAINING
VARSET CONCUBINAGE_BYAKUREN_LAST_WORK_DAY
VARSET CONCUBINAGE_TENGU_ETIQUETTE_LESSONS_REMAINING
VARSET CONCUBINAGE_TENGU_LAST_TRAINING_DAY
VARSET CONCUBINAGE_TENGU_PAYMENT_DONE
VARSET CONCUBINAGE_TENGU_DATE
VARSET CONCUBINAGE_TENGU_TIME
VARSET CONCUBINAGE_VILLAGERS_FOOD_REMAINING
VARSET CONCUBINAGE_REIMU_TRAINING_REMAINING
VARSET CONCUBINAGE_REIMU_LAST_TRAINING_DAY
VARSET CONCUBINAGE_MAMIZOU_EXPERIENCE
VARSET CONCUBINAGE_MAMIZOU_VIRGIN_CHECK
VARSET CONCUBINAGE_MAMIZOU_LAST_TRAINING_DAY
VARSET CONCUBINAGE_OKINA_PERFORMANCE_STATE
VARSET CONCUBINAGE_OKINA_PERFORMANCE_MUSICIANS
VARSET CONCUBINAGE_OKINA_PERFORMANCE_DANCERS
VARSET CONCUBINAGE_OKINA_PERFORMANCE_FAIRIES_RECRUITED
VARSET CONCUBINAGE_OKINA_PERFORMANCE_REHEARSING_REQUIRED

VARSET WEDDING_TYPE
VARSET WEDDING_SIZE
VARSET WEDDING_GUESTS
VARSET WEDDING_DATE
VARSET WEDDING_LOCATION
VARSET WEDDING_VENUE_AGREED

;player loses husband status
TALENT:MASTER:妻 = 0
;inherited wives do not count towards the lover limit
FLAG:追加恋人枠 = INHERITED_WIFE_COUNT()

;CVARSET CFLAG, GETNUM(CFLAG, "BANQUET"), 0
;CVARSET CFLAG, GETNUM(CFLAG, "BANQUET_ROLE"), 0
;CVARSET CFLAG, GETNUM(CFLAG, "BANQUET_CLEANUP"), 0

@WIFE_MOVE_IN
#DIM FIRST_LINE
#DIM DYNAMIC SELECTED_WIVES, 人物数量上限

FOR LOCAL, 1, CHARANUM
    CFLAG:LOCAL:LIVE_IN_WIFE = 0
NEXT

LOCAL = WIFE_COUNT()
IF !LOCAL
    RETURN
ELSEIF LOCAL == 1
    LOCAL = FINDCHARA(TALENT:妻, 2, 1)
    SIF CFLAG:LOCAL:神社在住
        RETURN
    
    PRINTFORML 想要%CALLNAME:LOCAL%搬来同居吗?
    CALL ASK_YN
    SIF !RESULT
        SELECTED_WIVES:LOCAL = 1
ELSE
    FIRST_LINE = LINECOUNT
    $OVERVIEW
    CLEARLINE LINECOUNT - FIRST_LINE
    REDRAW 0
    PRINTFORMDL 要让哪一位妻子住进来?

    DRAWLINE
    FOR LOCAL,1,CHARANUM-1
        IF TALENT:LOCAL:妻 < 2
            CONTINUE
        ELSEIF NO:LOCAL != LOCAL ;no clones
            CONTINUE
        ELSEIF CFLAG:LOCAL:神社在住 ;already lives here
            CONTINUE
        ELSEIF SELECTED_WIVES:LOCAL
            SETCOLOR C_YELLOW
        ELSE
            RESETCOLOR
        ENDIF
        PRINTFORMLC [{LOCAL}] %CALLNAME:LOCAL%
    NEXT
    RESETCOLOR
    PRINTFORML 
    DRAWLINE
    PRINTFORMDL [999] 结束
    $INPUT_LOOP
    INPUT
    LOCAL = RESULT
    IF LOCAL < 1 || (LOCAL >= CHARANUM && LOCAL != 999)
        CLEARLINE 1
        REUSELASTLINE 无效输入
        GOTO INPUT_LOOP
    ELSEIF LOCAL == 999 ;end
    ELSEIF TALENT:LOCAL:妻 < 2
        CLEARLINE 1
        REUSELASTLINE 不是你的妻子
        GOTO INPUT_LOOP
    ELSE
        SELECTED_WIVES:LOCAL = !SELECTED_WIVES:LOCAL
        GOTO OVERVIEW
    ENDIF
ENDIF

FOR LOCAL, 1, CHARANUM
    IF SELECTED_WIVES:LOCAL
        PRINTFORMW %CALLNAME:LOCAL%收拾好行李，搬来%GET_MAPNAME(GET_MAPID(CFLAG:MASTER:初期位置))%。
        CFLAG:LOCAL:LIVE_IN_WIFE = 1
        CALL MARRIAGE_MOVE_WIFE_IN(LOCAL)
    ENDIF
NEXT

@LOOK_RING(ARG)
IF TALENT:ARG:妻 > 0
    PRINTL
    PRINT 【戒指】
    PRINTFORM \@ TALENT:ARG:妻 > 1 ? 婚戒 # 订婚戒指 \@ 
ENDIF

@TRUE_LOVE_CONDITIONS(ARG)
#FUNCTION
SIF !TALENT:ARG:恋慕
	RETURNF 0
SIF TALENT:ARG:恋慕 > 1
    RETURNF 0
;SIF MARK:ARG:時姦刻印 && !CFLAG:ARG:時間停止バレ
;    RETURNF 0
;SIF EXP:ARG:睡眠姦経験
;    RETURNF 0
SIF CFLAG:ARG:好感度 < REQUIRED_FAVOR_TRUE_LOVE
	RETURNF 0
SIF CFLAG:ARG:信頼度 < REQUIRED_TRUST_TRUE_LOVE
	RETURNF 0
SIF ABL:ARG:親密 < REQUIRED_AFFECTION_TRUE_LOVE
	RETURNF 0
SIF EXP:ARG:デート経験 < REQUIRED_DATE_XP_TRUE_LOVE
    RETURNF 0
SIF EXP:ARG:愛情経験 < REQUIRED_LOVE_XP_TRUE_LOVE ;love xp somewhat restrained since only sex efficiently increases it
    RETURNF 0
SIF EXP:ARG:愛情経験 + EXP:ARG:デート経験 < REQUIRED_DATE_AND_LOVE_XP_TRUE_LOVE
    RETURNF 0

;no xp tied to restrained rape/hypnosis rape?

SIF MARK:ARG:反発刻印
	RETURNF 0
RETURNF 1

@WIFE_COUNT
#FUNCTION
RETURNF CMATCH(TALENT:妻, 2, 1)

@INHERITED_WIFE_COUNT()
#FUNCTION
RETURNF WIFE_COUNT() - (LEGAL_SPOUSE ? 1 # 0)

@MARRIAGE_MOVE_WIFE_IN(ARG)         
CFLAG:ARG:自宅位置 = CFLAG:MASTER:初期位置
CFLAG:ARG:初期位置 = CFLAG:MASTER:初期位置
CFLAG:ARG:神社在住 = CFLAG:MASTER:初期位置
CFLAG:ARG:現在位置 = CFLAG:MASTER:初期位置
CFLAG:ARG:本来の初期位置 = 0
CALL MARRIAGE_RESET_PRIVATEROOM()

@MARRIAGE_MOVE_WIFE_OUT(ARG)
IF MAP_住人(MAIN_MAP, ARG)
    CFLAG:ARG:初期位置 = CSVCFLAG(NO:ARG, GETNUM(CFLAG, "初期位置"))
    CFLAG:ARG:神社在住 = CFLAG:ARG:初期位置
ELSE
    PRIVATEROOM:(CFLAG:ARG:初期位置 % 100) = 0
    CFLAG:ARG:初期位置 = SUKIMA()
    CFLAG:ARG:神社在住 = 0
ENDIF
CFLAG:ARG:自宅位置 = CSVCFLAG(NO:ARG, GETNUM(CFLAG, "自宅位置"))
CFLAG:ARG:現在位置 = CFLAG:ARG:初期位置
CFLAG:ARG:デート中 = GET_MAPID(CFLAG:ARG:自宅位置)
CFLAG:ARG:本来の初期位置 = 0
CALL MARRIAGE_RESET_PRIVATEROOM()

@MARRIAGE_RESET_PRIVATEROOM()
LOCAL = CHARANUM + 1
DO
    LOCAL = FINDLASTCHARA(CFLAG:初期位置, CFLAG:MASTER:初期位置, 1, LOCAL-1)
LOOP LOCAL > 0 && CFLAG:LOCAL:本来の初期位置 == CFLAG:LOCAL:初期位置

IF LOCAL > 0
    PRIVATEROOM:(CFLAG:MASTER:初期位置 % 100) = LOCAL
ELSE
    PRIVATEROOM:(CFLAG:MASTER:初期位置 % 100) = 0
ENDIF
