@ADD_SHOW_TOUCH_TR
#DIM CHARA
#DIM TOUCH_INDEX

FOR LOCAL, 1, GET_TARGETNUM() + 1
    CHARA = TARGET:LOCAL
	SIF CHARA <= 0 || CHARA >= CHARANUM
		CONTINUE
	SIF !CFLAG:CHARA:うふふ ;only for those involved in sex
		CONTINUE
    FOR TOUCH_INDEX, 0, ADD_MAXTOUCH
        SIF !ADD_TOUCH_ACTIVE:TOUCH_INDEX
            BREAK
        SIF ADD_TOUCH_HISTORY:TOUCH_INDEX > 0
            BREAK
        IF ADD_TOUCH_TARGET:TOUCH_INDEX == CHARA
            SELECTCASE ADD_TOUCH_TARGET_PART:TOUCH_INDEX
                CASE SET_P
                    SELECTCASE ADD_TOUCH_ACTOR_PART:TOUCH_INDEX
                        CASE SET_YUBI
                            PRINT 给对方手交
                        CASE SET_M
                            PRINT 给对方口交
                        CASE SET_B
                            PRINT 给对方乳交
                        CASE SET_V
                            PRINT 主导V性交
                        CASE SET_A
                            PRINT 主导A性交
                        CASE SET_LEG
                            PRINT 给对方足交
                    ENDSELECT
                CASE SET_C
                    SELECTCASE ADD_TOUCH_ACTOR_PART:TOUCH_INDEX
                        CASE SET_P
                            PRINT 股间摩擦
                        CASE SET_YUBI
                            PRINT 抚摸阴蒂
                        CASE SET_M
                            PRINT 舔阴
                    ENDSELECT
                CASE SET_YUBI
                    SELECTCASE ADD_TOUCH_ACTOR_PART:TOUCH_INDEX
                		CASE SET_P
                			PRINT 被手交
                		CASE SET_B
                			PRINT 被抚摸胸部
                    ENDSELECT
                CASE SET_M
                    SELECTCASE ADD_TOUCH_ACTOR_PART:TOUCH_INDEX
                        CASE SET_P
                            PRINT 被口交
                        CASE SET_C
                            PRINT 被舔阴
                        CASE SET_M
                            PRINT 亲吻
                        CASE SET_A
                            PRINT 被舔肛
                    ENDSELECT
                CASE SET_B
                    SELECTCASE ADD_TOUCH_ACTOR_PART:TOUCH_INDEX
                        CASE SET_P
                            PRINT 乳交
                        CASE SET_YUBI
                            PRINT 抚摸乳房
                    ENDSELECT
                CASE SET_V
                    SELECTCASE ADD_TOUCH_ACTOR_PART:TOUCH_INDEX
                        CASE SET_P
                            PRINT 性交
                        CASE SET_YUBI
                            PRINT 指交
                    ENDSELECT
                CASE SET_A
                    SELECTCASE ADD_TOUCH_ACTOR_PART:TOUCH_INDEX
                        CASE SET_P
                            PRINT 肛交
                        CASE SET_YUBI
                            PRINT 抚摸肛门
                        CASE SET_M
                            PRINT 舔肛
                    ENDSELECT
                CASE SET_LEG
                    SELECTCASE ADD_TOUCH_ACTOR_PART:TOUCH_INDEX
                        CASE SET_P
                            PRINT 被足交
                    ENDSELECT
            ENDSELECT

            PRINTFORML (%CALLNAME:CHARA%)
        ENDIF
        
    NEXT
NEXT

@ADD_TOUCH_SUCCESSION_TR(ARG)
VARSET LOCAL
SELECTCASE SELECTCOM
	;愛撫　指と口をリセット
	CASE 0
		CALL TOUCH_RESET_M(SET_YUBI)
		SIF !TEQUIP:ARG:継続キス
			CALL TOUCH_RESET_M(SET_M)

        CALL ADD_TOUCH_SET_TR(PLAYER, SET_C, SET_YUBI, ARG,,)
        CALL ADD_TOUCH_SET_TR(PLAYER, SET_B, SET_YUBI, ARG,,,1)
	;クンニ　性交解除し対象のＣに口をセット
	CASE 1
		CALL 挿入解除(ARG)
		CALL TOUCH_RESET_M(SET_YUBI)
		IF HAS_PENIS(ARG)
			CALL TOUCH_SET(SET_FROM_P,SET_M,ARG)
		ELSE
			CALL TOUCH_SET(SET_FROM_C,SET_M,ARG)
		ENDIF
	;フェラする　性交解除し対象のＣに口をセット
	CASE 2
		;性交解除
		CALL 挿入解除(ARG)
		CALL TOUCH_SET(SET_FROM_P,SET_M,ARG)
	;指挿れ
	CASE 3
		IF TEQUIP:ARG:バイブ
		;指リセット
			CALL TOUCH_RESET_M(SET_YUBI)
		ELSE
			;指を対象のＶにセット
			CALL TOUCH_SET(SET_FROM_V,SET_YUBI,ARG)
		ENDIF
	;アナル舐め
	CASE 4
		;性交解除し口を対象のＡにセット
		CALL 挿入解除(ARG)
		CALL TOUCH_RESET_M(SET_YUBI)
		CALL TOUCH_SET(SET_FROM_A,SET_M,ARG)
	;アナル愛撫
	CASE 5
		IF TEQUIP:ARG:アナルバイブ || TEQUIP:ARG:アナルビーズ || TEQUIP:ARG:浣腸
			;指リセット
			CALL TOUCH_RESET_M(SET_YUBI)
		ELSE
			CALL TOUCH_SET(SET_FROM_A,SET_YUBI,ARG)
		ENDIF
	;胸愛撫
	CASE 6
		CALL TOUCH_SET(SET_FROM_B,SET_YUBI,ARG)
	;乳首責め
	CASE 7
		CALL TOUCH_SET(SET_FROM_B,SET_YUBI,ARG)
	;秘貝開帳
	CASE 8
        CALL ADD_TOUCH_RESET_T_PART_TR(ARG, SET_V)
        LOCAL = ADD_CHARA_WHO_INSERTED_IN(ARG, SET_A)
        LOCAL:1 = ADD_TOUCH_CURRENT_POSE(LOCAL, ARG)

        IF LOCAL == PLAYER
            SIF ADD_TOUCH_IS_REVERSE_POSE(LOCAL:1)
                CALL ADD_TOUCH_ALTER_POSE(PLAYER, ARG, ADD_TOUCH_MIRROR_POSE(LOCAL:1))
        ELSEIF LOCAL >= 0
            SIF ADD_TOUCH_IS_NORMAL_POSE(LOCAL:1)
                CALL ADD_TOUCH_ALTER_POSE(PLAYER, ARG, ADD_TOUCH_MIRROR_POSE(LOCAL:1))
        ENDIF

		SIF !SHIRAHU(ARG)
            CALL ADD_TOUCH_SET_TR(PLAYER, SET_V,SET_YUBI,ARG)
	;自慰
	CASE 9
	;乳首吸い
	CASE 10
		CALL TOUCH_SET(SET_FROM_B,SET_M,ARG)
    ;--------------------------------------
	CASE 11
		CALL TOUCH_SET(SET_FROM_B,SET_M,ARG)
    ;--------------------------------------
	;胸揉み
	CASE 12
		CALL TOUCH_SET(SET_FROM_B,SET_YUBI,ARG)
	;アナル開帳
	CASE 13
        CALL ADD_TOUCH_RESET_T_PART_TR(ARG, SET_A)
        LOCAL = ADD_CHARA_WHO_INSERTED_IN(ARG, SET_V)
        LOCAL:1 = ADD_TOUCH_CURRENT_POSE(LOCAL, ARG)

        IF LOCAL == PLAYER
            SIF ADD_TOUCH_IS_NORMAL_POSE(LOCAL:1)
                CALL ADD_TOUCH_ALTER_POSE(PLAYER, ARG, ADD_TOUCH_MIRROR_POSE(LOCAL:1))
        ELSEIF LOCAL >= 0
            SIF ADD_TOUCH_IS_REVERSE_POSE(LOCAL:1)
                CALL ADD_TOUCH_ALTER_POSE(PLAYER, ARG, ADD_TOUCH_MIRROR_POSE(LOCAL:1))
        ENDIF

		SIF !SHIRAHU(ARG)
            CALL ADD_TOUCH_SET_TR(PLAYER, SET_A,SET_YUBI,ARG)
	;クリ愛撫
	CASE 15
		;指を対象のＣにセット
		CALL TOUCH_SET(SET_FROM_C,SET_YUBI,ARG)
	;キスする
	CASE 19,20
		CALL TOUCH_SET(SET_FROM_M,SET_M,ARG)
	;何もしない
	CASE 21
	;誘い受け
	CASE 22
	;ローター
	CASE 40
		CALL TOUCH_RESET_M(SET_YUBI)        
		CALL ADD_TOUCH_RESET_T_PART_TR(ARG, SET_C)
		CALL ADD_TOUCH_RESET_T_PART_TR(ARG, SET_P)
	;Ｅマッサージャ
	CASE 41
		CALL TOUCH_SET(0,0,0,SET_P)        
		CALL ADD_TOUCH_RESET_T_PART_TR(ARG, SET_C)
		CALL ADD_TOUCH_RESET_T_PART_TR(ARG, SET_P)
	;クリキャップ
	CASE 42
		CALL TOUCH_RESET_M(SET_YUBI)        
		CALL ADD_TOUCH_RESET_T_PART_TR(ARG, SET_C)
		CALL ADD_TOUCH_RESET_T_PART_TR(ARG, SET_P)
	;オナホール
	CASE 43
		CALL TOUCH_RESET_M(SET_YUBI)        
		CALL ADD_TOUCH_RESET_T_PART_TR(ARG, SET_C)
		CALL ADD_TOUCH_RESET_T_PART_TR(ARG, SET_P)
	;バイブ
	CASE 44
		CALL TOUCH_RESET_M(SET_YUBI)        
		CALL ADD_TOUCH_RESET_T_PART_TR(ARG, SET_V)
	;アナルバイブ
	CASE 45
		CALL TOUCH_RESET_M(SET_YUBI)        
		CALL ADD_TOUCH_RESET_T_PART_TR(ARG, SET_A)
	;アナルビーズ
	CASE 46
		CALL TOUCH_RESET_M(SET_YUBI)
		CALL ADD_TOUCH_RESET_T_PART_TR(ARG, SET_A)
	;ニプルキャップ
	CASE 47
		CALL TOUCH_RESET_M(SET_YUBI)
		CALL ADD_TOUCH_RESET_T_PART_TR(ARG, SET_B)
	;搾乳機
	CASE 48
		CALL TOUCH_RESET_M(SET_YUBI)
		CALL ADD_TOUCH_RESET_T_PART_TR(ARG, SET_B)
	;乳首ローター
	CASE 49
		CALL TOUCH_RESET_M(SET_YUBI)
	;正常位
	CASE 60
		CALL ADD_TOUCH_SET_TR(PLAYER,SET_V,SET_P,ARG,NORMAL_OR_STANDING_POSE(SELECTCOM, POSE_MISSIONARY),1)
	;後背位
	CASE 61
		CALL ADD_TOUCH_SET_TR(PLAYER,SET_V,SET_P,ARG,NORMAL_OR_STANDING_POSE(SELECTCOM, POSE_DOGGYSTYLE),1)
	;正常位アナル
	CASE 62
		CALL ADD_TOUCH_SET_TR(PLAYER,SET_A,SET_P,ARG,NORMAL_OR_STANDING_POSE(SELECTCOM, POSE_MISSIONARY),1)
	;後背位アナル
	CASE 63, 323
		CALL ADD_TOUCH_SET_TR(PLAYER,SET_A,SET_P,ARG,NORMAL_OR_STANDING_POSE(SELECTCOM, POSE_DOGGYSTYLE),1)
	;逆レイプ
	CASE 64
		CALL ADD_TOUCH_SET_TR(PLAYER,SET_P,SET_V,ARG,POSE_COWGIRL,1)
	;騎乗位
	CASE 65
		CALL ADD_TOUCH_SET_TR(PLAYER,SET_V,SET_P,ARG,POSE_COWGIRL,1)
	;騎乗位アナル
	CASE 66
		CALL ADD_TOUCH_SET_TR(PLAYER,SET_A,SET_P,ARG,POSE_COWGIRL,1)
	;対面座位
	CASE 67
		CALL ADD_TOUCH_SET_TR(PLAYER,SET_V,SET_P,ARG,NORMAL_OR_STANDING_POSE(SELECTCOM, POSE_LOTUS),1)
	;背面座位
	CASE 68
		CALL ADD_TOUCH_SET_TR(PLAYER,SET_V,SET_P,ARG,NORMAL_OR_STANDING_POSE(SELECTCOM, POSE_REVLOTUS),1)
		CALL TOUCH_SET(SET_FROM_B,SET_YUBI,ARG)
	;対面座位アナル
	CASE 69
		CALL ADD_TOUCH_SET_TR(PLAYER,SET_A,SET_P,ARG,NORMAL_OR_STANDING_POSE(SELECTCOM, POSE_LOTUS),1)
	;背面座位アナル
	CASE 70
		CALL ADD_TOUCH_SET_TR(PLAYER,SET_A,SET_P,ARG,NORMAL_OR_STANDING_POSE(SELECTCOM, POSE_REVLOTUS),1)
		CALL TOUCH_SET(SET_FROM_B,SET_YUBI,ARG)
	;二穴挿し
	CASE 71
		LOCAL:1 = ADD_CHARA_WHO_TOUCHING(ARG, SET_V, SET_P, 1)
		LOCAL:2 = ADD_CHARA_WHO_TOUCHING(ARG, SET_A, SET_P, 1)

		IF LOCAL:1 == LOCAL:2 || (LOCAL:1 == PLAYER && LOCAL:2 == -1) || (LOCAL:2 == PLAYER && LOCAL:1 == -1)
            LOCAL:1 = PLAYER
            LOCAL:2 = PLAYER
            CALL ADD_TOUCH_SET_TR(LOCAL:1, SET_V, SET_P, ARG,ADD_TOUCH_CURRENT_POSE(LOCAL:1, ARG),,)
            CALL ADD_TOUCH_SET_TR(LOCAL:2, SET_A, SET_P, ARG,ADD_TOUCH_CURRENT_POSE(LOCAL:1, ARG),,1)
		ELSE
			IF LOCAL:1 == -1
				LOCAL:1 = PLAYER
                CALL ADD_TOUCH_SET_TR(LOCAL:1, SET_V, SET_P, ARG, ADD_TOUCH_MIRROR_POSE(ADD_TOUCH_CURRENT_POSE(LOCAL:2, ARG)))
			ENDIF
			IF LOCAL:2 == -1
				LOCAL:2 = PLAYER
                CALL ADD_TOUCH_SET_TR(LOCAL:2, SET_A, SET_P, ARG, ADD_TOUCH_MIRROR_POSE(ADD_TOUCH_CURRENT_POSE(LOCAL:1, ARG)))
			ENDIF
		ENDIF
	;挿入Ｇスポット責め
	CASE 72
		CALL TOUCH_SET(SET_FROM_V,SET_P,ARG)
	;挿入子宮
	CASE 73
		CALL TOUCH_SET(SET_FROM_V,SET_P,ARG)
	;Ｓ状結腸責め
	CASE 74
		CALL TOUCH_SET(SET_FROM_A,SET_P,ARG)
	;ＡＧスポ
	CASE 75
		CALL TOUCH_SET(SET_FROM_A,SET_P,ARG)
	;小人オナホ
	CASE 79
		CALL ADD_TOUCH_SET_TR(PLAYER,SET_V,SET_P,ARG,NORMAL_OR_STANDING_POSE(SELECTCOM, POSE_ONAHOLE),1)
	;手淫
	CASE 80
		CALL TOUCH_SET(SET_FROM_YUBI,SET_P,ARG)
	;フェラチオ
	CASE 81
		SIF ADD_CHARA_INSERTED(ARG, PLAYER)
			CALL 挿入解除(ARG)
		CALL TOUCH_SET(SET_FROM_M,SET_P,ARG)
	;パイズリ
	CASE 82
		SIF ADD_CHARA_INSERTED(ARG, PLAYER)
			CALL 挿入解除(ARG)
		CALL TOUCH_SET(SET_FROM_B,SET_P,ARG)
	;素股
	CASE 83
		SIF ADD_CHARA_INSERTED(ARG, PLAYER)
			CALL 挿入解除(ARG)
		CALL TOUCH_SET(SET_FROM_C,SET_P,ARG)
	;泡踊り
	CASE 84
		SIF ADD_CHARA_INSERTED(ARG, PLAYER)
			CALL 挿入解除(ARG)
		CALL TOUCH_SET(0,0,0,1)
	;足コキ
	CASE 85
		SIF ADD_CHARA_INSERTED(ARG, PLAYER)
			CALL 挿入解除(ARG)
		CALL TOUCH_SET(SET_FROM_LEG,SET_P,ARG)
	;バキュームフェラ
	CASE 88
		SIF ADD_CHARA_INSERTED(ARG, PLAYER)
			CALL 挿入解除(ARG)
		CALL TOUCH_SET(SET_FROM_M,SET_P,ARG)
	;アナル愛撫される
	CASE 90
		CALL TOUCH_SET(SET_FROM_YUBI,SET_A,ARG)
	;アナル舐めされる
	CASE 91
		SIF ADD_CHARA_INSERTED(ARG, PLAYER)
			CALL 挿入解除(ARG)
		CALL TOUCH_SET(SET_FROM_M,SET_A,ARG)
	;Ａ正常位される
	CASE 92
		CALL ADD_TOUCH_SET_TR(PLAYER,SET_P,SET_A,ARG,NORMAL_OR_STANDING_POSE(SELECTCOM, POSE_MISSIONARY))
		CALL ADD_TOUCH_RESET_T_PART_TR(ARG, SET_C)
		CALL ADD_TOUCH_RESET_T_PART_TR(ARG, SET_V)
	;Ａ後背位される
	CASE 93
		CALL ADD_TOUCH_SET_TR(PLAYER,SET_P,SET_A,ARG,NORMAL_OR_STANDING_POSE(SELECTCOM, POSE_DOGGYSTYLE))
		CALL ADD_TOUCH_RESET_T_PART_TR(ARG, SET_C)
		CALL ADD_TOUCH_RESET_T_PART_TR(ARG, SET_V)
	;Ａ騎乗位する
	CASE 94
		CALL ADD_TOUCH_SET_TR(PLAYER,SET_P,SET_A,ARG,POSE_COWGIRL)
		CALL ADD_TOUCH_RESET_T_PART_TR(ARG, SET_C)
		CALL ADD_TOUCH_RESET_T_PART_TR(ARG, SET_V)
	;スパンキング
	CASE 100
		CALL TOUCH_RESET_M(SET_YUBI)
	;鞭
	CASE 101
		CALL TOUCH_RESET_M(SET_YUBI)
	;蝋燭
	CASE 102
		CALL TOUCH_RESET_M(SET_YUBI)
	;針
	CASE 103
		CALL TOUCH_RESET_M(SET_YUBI)
	;アイマスク
	CASE 104
		CALL TOUCH_RESET_M(SET_YUBI)
	;乳スパンキング
	CASE 108
		CALL TOUCH_RESET_M(SET_YUBI)
	;クンニ強制
	CASE 120
		CALL 挿入解除(ARG) ;com custom code
		CALL TOUCH_SET(SET_FROM_M,SET_C,ARG)
	;貝合わせ
	CASE 121
		CALL 挿入解除(ARG) ;com custom code
		CALL TOUCH_SET(SET_FROM_C,SET_C,ARG)
	;手淫する
	CASE 124
		CALL TOUCH_SET(SET_FROM_P,SET_YUBI,ARG)
	;愛撫させる
	CASE 125
		CALL ADD_TOUCH_RESET_T_PART_TR(ARG, SET_YUBI)
		CALL ADD_TOUCH_RESET_T_PART_TR(ARG, SET_M)
	;乳の揉み合い
	CASE 126
		CALL 挿入解除(ARG) ;com custom code
		CALL TOUCH_RESET_M(SET_YUBI)
		CALL ADD_TOUCH_RESET_T_PART_TR(ARG, SET_YUBI)
		CALL TOUCH_SET(SET_FROM_B,SET_YUBI,ARG)
		CALL TOUCH_SET(SET_FROM_YUBI,5,ARG)
	;正常位される
	CASE 130
		CALL ADD_TOUCH_SET_TR(PLAYER,SET_P,SET_V,ARG,NORMAL_OR_STANDING_POSE(SELECTCOM, POSE_MISSIONARY),1)
	;後背位される
	CASE 131
		CALL ADD_TOUCH_SET_TR(PLAYER,SET_P,SET_V,ARG,NORMAL_OR_STANDING_POSE(SELECTCOM, POSE_DOGGYSTYLE),1)
	;対面座位される
	CASE 132
		CALL ADD_TOUCH_SET_TR(PLAYER,SET_P,SET_V,ARG,POSE_LOTUS,1)
	;背面座位される
	CASE 133
		CALL ADD_TOUCH_SET_TR(PLAYER,SET_P,SET_V,ARG,POSE_REVLOTUS,1)
	;イラマチオ
	CASE 140
		CALL TOUCH_SET(SET_FROM_M,SET_P,ARG)
	;フィストファック
	CASE 141
		CALL TOUCH_SET(SET_FROM_V,SET_YUBI,ARG)
	;アナルフィスト
	CASE 142
		CALL TOUCH_SET(SET_FROM_A,SET_YUBI,ARG)
	;両アナフィスト
	CASE 143
		CALL ADD_TOUCH_SET_TR(PLAYER,SET_YUBI,SET_V,ARG,POSE_REVLOTUS,1)
		CALL ADD_TOUCH_SET_TR(PLAYER,SET_YUBI,SET_A,ARG,POSE_REVLOTUS,1)
	;アナル奉仕
	CASE 145
		CALL TOUCH_SET(SET_FROM_M,SET_A,ARG)
	;浣腸
	CASE 146
		CALL TOUCH_RESET_M(SET_YUBI)
	;拡張バルーン
	CASE 147
		CALL TOUCH_RESET_M(SET_YUBI)
	;アナル電極
	CASE 148
		CALL TOUCH_RESET_M(SET_YUBI)
		CALL TOUCH_SET(SET_FROM_A,1,ARG)
	;乳を握り潰す
	CASE 149
		CALL TOUCH_SET(SET_FROM_B,SET_YUBI,ARG)
	;胸の谷間を犯す
	CASE 150
		CALL TOUCH_SET(SET_FROM_B,SET_P,ARG)

	;日常セクハラ
	;キスする
	CASE 312
		CALL TOUCH_SET(SET_FROM_M,SET_M,ARG)
	;胸愛撫
	CASE 313
		CALL TOUCH_SET(SET_FROM_B,SET_YUBI,ARG)
	;アナル愛撫
	CASE 314
		CALL TOUCH_SET(SET_FROM_A,SET_YUBI,ARG)
	;クリ愛撫
	CASE 315
		IF HAS_PENIS(ARG)
			CALL TOUCH_SET(SET_FROM_P,SET_YUBI,ARG)
		ELSE
			CALL TOUCH_SET(SET_FROM_C,SET_YUBI,ARG)
		ENDIF
	;指挿れ
	CASE 316
		CALL TOUCH_SET(SET_FROM_V,SET_YUBI,ARG)

	;相手主導正常位
	CASE 320
		IF ADD_CHARA_INSERTED(ARG)
			CALL 挿入解除(ARG)
		ENDIF
		CALL ADD_TOUCH_SET_TR(PLAYER,SET_V,SET_P,ARG,POSE_MISSIONARY,1)
	;相手主導後背位
	CASE 322
		IF ADD_CHARA_INSERTED(ARG)
			CALL 挿入解除(ARG)
		ENDIF
		CALL ADD_TOUCH_SET_TR(PLAYER,SET_V,SET_P,ARG,POSE_DOGGYSTYLE,1)
	;相手主導後背位Ａ
	CASE 323
		IF ADD_CHARA_INSERTED(ARG)
			CALL 挿入解除(ARG)
		ENDIF
		CALL ADD_TOUCH_SET_TR(PLAYER,SET_A,SET_P,ARG,POSE_DOGGYSTYLE,1)
	;愛撫(慰)
	CASE 700
		CALL TOUCH_RESET_M(SET_YUBI)
	;セルフフェラ(慰)
	CASE 702
	;指挿れ(慰)
	CASE 703
		IF TEQUIP:ARG:バイブ
		;指リセット
			CALL TOUCH_RESET_M(SET_YUBI)
		ELSE
			;指を対象のＶにセット
			CALL TOUCH_SET(SET_FROM_V,SET_YUBI,MASTER)
		ENDIF
	;アナル愛撫(慰)
	CASE 705
		IF TEQUIP:ARG:アナルバイブ || TEQUIP:ARG:アナルビーズ || TEQUIP:ARG:浣腸
			;指リセット
			CALL TOUCH_RESET_M(SET_YUBI)
		ELSE
			CALL TOUCH_SET(SET_FROM_A,SET_YUBI,MASTER)
		ENDIF
	;胸愛撫(慰)
	CASE 706
		CALL TOUCH_SET(SET_FROM_B,SET_YUBI,MASTER)
	;乳首責め(慰)
	CASE 707
		CALL TOUCH_SET(SET_FROM_B,SET_YUBI,MASTER)
	;秘貝開帳(慰)
	CASE 708
		CALL TOUCH_RESET_T(MASTER)
	;手淫(慰)
	CASE 709
		CALL TOUCH_SET(SET_FROM_P,SET_YUBI,MASTER)
	;胸揉み(慰)
	CASE 712
		CALL TOUCH_SET(SET_FROM_B,SET_YUBI,MASTER)
	;アナル開帳(慰)
	CASE 713
		CALL TOUCH_RESET_T(MASTER)
	;ローター(慰)
	CASE 740
		CALL TOUCH_RESET_M(SET_YUBI)
        CALL ADD_TOUCH_RESET_T_PART_TR(MASTER, SET_C)
        CALL ADD_TOUCH_RESET_T_PART_TR(MASTER, SET_P)
	;Ｅマッサージャ(慰)
	CASE 741
		CALL TOUCH_SET(0,0,0,1)
        CALL ADD_TOUCH_RESET_T_PART_TR(MASTER, SET_C)
        CALL ADD_TOUCH_RESET_T_PART_TR(MASTER, SET_P)
	;クリキャップ(慰)
	CASE 742
		CALL TOUCH_RESET_M(SET_YUBI)
        CALL ADD_TOUCH_RESET_T_PART_TR(MASTER, SET_C)
        CALL ADD_TOUCH_RESET_T_PART_TR(MASTER, SET_P)
	;オナホール(慰)
	CASE 743
		CALL TOUCH_RESET_M(SET_YUBI)
        CALL ADD_TOUCH_RESET_T_PART_TR(MASTER, SET_C)
        CALL ADD_TOUCH_RESET_T_PART_TR(MASTER, SET_P)
	;バイブ(慰)
	CASE 744
		CALL TOUCH_RESET_M(SET_YUBI)
        CALL ADD_TOUCH_RESET_T_PART_TR(MASTER, SET_V)
	;アナルバイブ(慰)
	CASE 745
		CALL TOUCH_RESET_M(SET_YUBI)
        CALL ADD_TOUCH_RESET_T_PART_TR(MASTER, SET_A)
	;アナルビーズ(慰)
	CASE 746
		CALL TOUCH_RESET_M(SET_YUBI)
        CALL ADD_TOUCH_RESET_T_PART_TR(MASTER, SET_A)
	;ニプルキャップ(慰)
	CASE 747
		CALL TOUCH_RESET_M(SET_YUBI)
        CALL ADD_TOUCH_RESET_T_PART_TR(MASTER, SET_B)
	;搾乳機(慰)
	CASE 748
		CALL TOUCH_RESET_M(SET_YUBI)
        CALL ADD_TOUCH_RESET_T_PART_TR(MASTER, SET_B)
	;乳首ローター(慰)
	CASE 749

    CASE 189 ;custom code, apron
		CALL ADD_TOUCH_RESET_T_PART_TR(ARG, SET_B)

	CASE IS < 200
		CALL TOUCH_SET(0,0,0,1)
	CASE IS > 500
		CALL TOUCH_SET(0,0,0,1)
	;掃除
	CASE 410
		CALL TOUCH_SET(0,0,0,1)

	;addition custom code
	CASE ADD_CUSTOM_COM
		SELECTCASE ADD_SELECTCUSTOMCOM
			CASE 15 ;pull out
                LOCAL:1 = ADD_CHARA_WHO_INSERTED_IN(ARG, SET_V)
                LOCAL:2 = ADD_CHARA_WHO_INSERTED_IN(ARG, SET_A)
                SIF LOCAL:1 == PLAYER
				    CALL ADD_TOUCH_RESET_T_PART_TR(ARG, SET_V)
                SIF LOCAL:2 == PLAYER
				    CALL ADD_TOUCH_RESET_T_PART_TR(ARG, SET_A)
            CASE 16 ;bellyjob
                SIF ADD_CHARA_INSERTED(ARG, PLAYER)
                    CALL 挿入解除(ARG)
                CALL ADD_TOUCH_SET_TR(PLAYER, SET_BELLY,SET_P, ARG)
            CASE 17 ;ball sucking
                SIF ADD_CHARA_INSERTED(ARG, PLAYER)
                    CALL 挿入解除(ARG)
                CALL ADD_TOUCH_SET_TR(PLAYER, SET_M,SET_BALLS, ARG)
            CASE 228 ;腋交
                SIF ADD_CHARA_INSERTED(ARG, PLAYER)
                    CALL 挿入解除(ARG)
                CALL ADD_TOUCH_SET_TR(PLAYER, SET_M,SET_ARMPIT, ARG)
		ENDSELECT
ENDSELECT

CALL ADD_TOUCH_SET_OLD_FIELDS(1)

@ADD_CHARA_POSE(ACTOR, CHARA, ACTOR_PART, CHARA_PART, HISTORY = 0)
#FUNCTION
#DIM ACTOR
#DIM CHARA
#DIM ACTOR_PART
#DIM CHARA_PART
#DIM HISTORY
#DIM TOUCH_INDEX

IF ADD_TEMPTOUCHMODE && HISTORY == 0
    LOCAL = ADD_TEMPTOUCH_CHARA_POSE(ACTOR, CHARA, ACTOR_PART, CHARA_PART)
    IF LOCAL == -2 ;value was reset, act as if there was no match
        RETURNF 0
    ELSEIF LOCAL > 0
        RETURNF 1
    ENDIF
ENDIF

FOR TOUCH_INDEX, 0, ADD_MAXTOUCH
    SIF !ADD_TOUCH_ACTIVE:TOUCH_INDEX
        BREAK
    SIF ADD_TOUCH_HISTORY:TOUCH_INDEX < HISTORY
        CONTINUE
    SIF ADD_TOUCH_HISTORY:TOUCH_INDEX > HISTORY
        BREAK
    SIF ADD_TOUCH_ACTOR:TOUCH_INDEX != ACTOR
        CONTINUE
    SIF ADD_TOUCH_TARGET:TOUCH_INDEX != CHARA
        CONTINUE
    SIF ADD_TOUCH_ACTOR_PART:TOUCH_INDEX != ACTOR_PART
        CONTINUE
    SIF ADD_TOUCH_TARGET_PART:TOUCH_INDEX != CHARA_PART
        CONTINUE

    RETURNF 1
NEXT
RETURNF 0

@ADD_SCOM_CHECKS(ARG)
#FUNCTION
#DIM DYNAMIC FINAL_CHOICE
IF SCOM12_CHECK(ARG)
	FINAL_CHOICE = 12
;シックスナイン
ELSEIF SCOM1_CHECK(ARG)
	FINAL_CHOICE = 1
;岩清水
ELSEIF TEQUIP:ARG:羞恥プレイ && SELECTCOM == 1
	FINAL_CHOICE = 2
;Gスポット刺激
ELSEIF SELECTCOM == 3 && PREVCOM == SELECTCOM && TFLAG:103 == ARG
	FINAL_CHOICE = 3
;乱れ牡丹
ELSEIF TEQUIP:ARG:羞恥プレイ && SELECTCOM == 68
	FINAL_CHOICE = 4
;手淫フェラ
ELSEIF SCOM5_CHECK(ARG)
	IF FLAG:70 == 1
	ELSE
		;フェラ-手淫
		IF ADD_CHARA_POSE(PLAYER, ARG, SET_P, SET_YUBI)
			TFLAG:194 = 0
		;手淫-フェラ
		ELSE
			TFLAG:194 = 1
		ENDIF
		FINAL_CHOICE = 5
	ENDIF
;挿入Ｇスポット責め、子宮口責め、交互挿入
ELSEIF (SELECTCOM == 60 || SELECTCOM == 61 || SELECTCOM == 65 || SELECTCOM == 67 || SELECTCOM == 68) 
	;交互挿入
    LOCAL = ADD_MASTER_POSE_TR(PLAYER, SET_V,SET_P,1)
    {
	IF ADD_CHARA_POSE(PLAYER, ARG, SET_P, SET_V) && LOCAL && LOCAL != ARG
        && GROUPMATCH(ADD_TOUCH_CURRENT_POSE(PLAYER, ARG, 0), POSE_MISSIONARY, POSE_DOGGYSTYLE, POSE_COWGIRL, POSE_REV_COWGIRL)
        && GROUPMATCH(ADD_TOUCH_CURRENT_POSE(PLAYER, LOCAL, 1), POSE_MISSIONARY, POSE_DOGGYSTYLE, POSE_COWGIRL, POSE_REV_COWGIRL)
    }
		FINAL_CHOICE = 13
	ENDIF
;アナル交互挿入
ELSEIF (SELECTCOM == 62 || SELECTCOM == 63 || SELECTCOM == 66 || SELECTCOM == 69 || SELECTCOM == 70) 
	;アナル交互挿入
    LOCAL = ADD_MASTER_POSE_TR(PLAYER, SET_A,SET_P,1)
    {
	IF ADD_CHARA_POSE(PLAYER, ARG, SET_P, SET_A) && LOCAL && LOCAL != ARG
        && GROUPMATCH(ADD_TOUCH_CURRENT_POSE(PLAYER, ARG, 0), POSE_MISSIONARY, POSE_DOGGYSTYLE, POSE_COWGIRL, POSE_REV_COWGIRL)
        && GROUPMATCH(ADD_TOUCH_CURRENT_POSE(PLAYER, LOCAL, 1), POSE_MISSIONARY, POSE_DOGGYSTYLE, POSE_COWGIRL, POSE_REV_COWGIRL)
    }
		FINAL_CHOICE = 16
	ENDIF
ELSEIF SELECTCOM == 72
	FINAL_CHOICE = 6
ELSEIF SELECTCOM == 73
	FINAL_CHOICE = 7
;シックスナインパイズリ
ELSEIF SCOM8_CHECK(ARG)
	IF FLAG:70 == 1
	ELSE
	    FINAL_CHOICE = 8
	ENDIF
;ダブルフェラ
ELSEIF SCOM9_CHECK(ARG)
	FINAL_CHOICE = 9
;ダブル素股
ELSEIF SCOM10_CHECK(ARG)
	FINAL_CHOICE = 10
;ダブルパイズリ
ELSEIF SCOM11_CHECK(ARG)
	IF FLAG:70 == 1
	ELSE
	    FINAL_CHOICE = 11
	ENDIF
;Ｓ状結腸責め、アナルＧスポット刺激
ELSEIF (SELECTCOM == 62 || SELECTCOM == 63 || SELECTCOM == 66 || SELECTCOM == 69 || SELECTCOM == 70) && PREVCOM == SELECTCOM && TFLAG:103 == ARG && TFLAG:前回のコマンド結果 > 0
	IF SELECTCOM == 66 || RAND:2
		FINAL_CHOICE = 14
	ELSE
		FINAL_CHOICE = 15
	ENDIF
ELSEIF SELECTCOM == 74
	FINAL_CHOICE = 14
ELSEIF SELECTCOM == 75
	FINAL_CHOICE = 15
ELSEIF SCOM19_CHECK(ARG)
	IF !FLAG:70
		;乳首吸い-手淫
		IF TEQUIP:ARG:指接触部位 == 1
			TFLAG:194 = 0
		;手淫-乳首吸い
		ELSE
			TFLAG:194 = 1
		ENDIF
		FINAL_CHOICE = 19
	ENDIF
;乳首同時吸い
ELSEIF SELECTCOM == 11 && TFLAG:103 == ARG && TEQUIP:ARG:乳首吸い
	FINAL_CHOICE = 17
ENDIF

;=== 日常系 ===
;耳かき
IF SELECTCOM == 302 && PREVCOM == 305 && TFLAG:前回のコマンド結果 > 0 ;only when last command is successfull 
	FINAL_CHOICE = 60
;抱きまくら
ELSEIF SELECTCOM == 402 && PREVCOM == 311 && TFLAG:前回のコマンド結果 > 0
	FINAL_CHOICE = 61
;デコちゅー
ELSEIF SELECTCOM == 312 && PREVCOM == 309 && !FLAG:70 && TFLAG:前回のコマンド結果 > 0
	FINAL_CHOICE = 62
ENDIF
;もっとキスする
SIF SELECTCOM == 312 && PREVCOM == 312 && !TFLAG:151 && !(REJECT_CHECK(SELECTCOM) && !CFLAG:睡眠) ;com custom code, don't trigger in case of failure
	FINAL_CHOICE = 63

;=== デート系 ===
IF CHK_DATENOW(CFLAG:TARGET:デート中)
	;手を引く
	IF SELECTCOM == 603 && PREVCOM == 603 && TFLAG:前回のコマンド結果 > 0
		FINAL_CHOICE = 80
	ENDIF
ENDIF

RETURNF FINAL_CHOICE

;Sets the contact area
@ADD_TOUCH_SET_TR(ACTOR,CHARA_PART,ACTOR_PART,CHARA,POSE=0,RESET,ALLOW_DUPLICATE_ACTOR_PART)
#DIM ACTOR
#DIM CHARA
#DIM ACTOR_PART
#DIM CHARA_PART
#DIM POSE
#DIM RESET
#DIM TOUCH_INDEX
#DIM ALLOW_DUPLICATE_ACTOR_PART

IF ADD_TEMPTOUCHMODE
    CALL ADD_TEMPTOUCH_SET_TR(ACTOR,CHARA_PART,ACTOR_PART,CHARA,POSE,RESET)
    RETURN
ENDIF

VARSET ADD_TOUCH_MARK_REMOVE

IF RESET
    FOR TOUCH_INDEX, 0, ADD_MAXTOUCH
        SIF !ADD_TOUCH_ACTIVE:TOUCH_INDEX
            BREAK
        SIF ADD_TOUCH_HISTORY:TOUCH_INDEX > 0
            BREAK
        SIF ADD_TOUCH_TARGET:TOUCH_INDEX == CHARA && ADD_TOUCH_TARGET_PART:TOUCH_INDEX == SET_M && TFLAG:3 >= 60 && TFLAG:3 <= 70
            CONTINUE

        ADD_TOUCH_MARK_REMOVE:TOUCH_INDEX = 1
    NEXT

	FOR LOCAL,1,CHARANUM
		TEQUIP:LOCAL:体位 = 0
	NEXT
ENDIF

IF !ALLOW_DUPLICATE_ACTOR_PART
    FOR TOUCH_INDEX, 0, ADD_MAXTOUCH
        SIF !ADD_TOUCH_ACTIVE:TOUCH_INDEX
            BREAK
        SIF ADD_TOUCH_HISTORY:TOUCH_INDEX > 0
            BREAK
        SIF ADD_TOUCH_ACTOR:TOUCH_INDEX != ACTOR
            CONTINUE
        SIF ADD_TOUCH_ACTOR_PART:TOUCH_INDEX == ACTOR_PART
            ADD_TOUCH_MARK_REMOVE:TOUCH_INDEX = 1
    NEXT
ENDIF
FOR TOUCH_INDEX, 0, ADD_MAXTOUCH
    SIF !ADD_TOUCH_ACTIVE:TOUCH_INDEX
        BREAK
    SIF ADD_TOUCH_HISTORY:TOUCH_INDEX > 0
        BREAK
    SIF ADD_TOUCH_TARGET:TOUCH_INDEX != CHARA
        CONTINUE
    SIF ADD_TOUCH_TARGET_PART:TOUCH_INDEX == CHARA_PART
        ADD_TOUCH_MARK_REMOVE:TOUCH_INDEX = 1
NEXT

FOR TOUCH_INDEX, ADD_MAXTOUCH-1, -1, -1
    SIF ADD_TOUCH_MARK_REMOVE:TOUCH_INDEX
        CALL ADD_TOUCH_REMOVE(TOUCH_INDEX)
NEXT

SIF !POSE && GROUPMATCH(CHARA_PART, SET_V, SET_A)
    POSE = MAX(ADD_TOUCH_CURRENT_POSE(ACTOR, CHARA),0)

SIF ACTOR_PART && CHARA_PART
    CALL ADD_TOUCH_SET(ACTOR, CHARA, ACTOR_PART, CHARA_PART, POSE)

VARSET ADD_TOUCH_MARK_REMOVE

IF TEQUIP:CHARA:継続キス && !ADD_CHARA_POSE(PLAYER, CHARA, SET_M, SET_M)
	PRINTL ＜结束接吻＞
    TEQUIP:CHARA:継続キス = 0
ENDIF
IF TEQUIP:CHARA:胸揉み && !ADD_CHARA_POSE(PLAYER, CHARA, SET_YUBI, SET_B)
    PRINTL ＜结束揉胸＞
    TEQUIP:CHARA:胸揉み = 0
ENDIF
IF TEQUIP:CHARA:乳首吸い && !ADD_CHARA_POSE(PLAYER, CHARA, SET_M, SET_B)
    PRINTL ＜结束吸乳头＞
    TEQUIP:CHARA:乳首吸い = 0
ENDIF

;MASTERの接触部位をリセットする
@ADD_TOUCH_RESET_M_TR(PART, ACTOR)
#DIM ACTOR
#DIM PART
#DIM TOUCH_INDEX

IF ADD_TEMPTOUCHMODE
    CALL ADD_TEMPTOUCH_RESET_M_TR(PART, ACTOR)
    RETURN
ENDIF

VARSET ADD_TOUCH_MARK_REMOVE

FOR TOUCH_INDEX, 0, ADD_MAXTOUCH
    SIF !ADD_TOUCH_ACTIVE:TOUCH_INDEX
        BREAK
    SIF ADD_TOUCH_HISTORY:TOUCH_INDEX > 0
        BREAK
    SIF ADD_TOUCH_ACTOR:TOUCH_INDEX == ACTOR && ADD_TOUCH_ACTOR_PART:TOUCH_INDEX == PART
        ADD_TOUCH_MARK_REMOVE:TOUCH_INDEX = 1
NEXT
FOR TOUCH_INDEX, ADD_MAXTOUCH-1, -1, -1
    SIF ADD_TOUCH_MARK_REMOVE:TOUCH_INDEX
        CALL ADD_TOUCH_REMOVE(TOUCH_INDEX)
NEXT

;TARGETの接触部位をリセットする
@ADD_TOUCH_RESET_T_TR(ARG)
#DIM TOUCH_INDEX

IF ADD_TEMPTOUCHMODE
    CALL ADD_TEMPTOUCH_RESET_T_TR(ARG)
    RETURN
ENDIF

VARSET ADD_TOUCH_MARK_REMOVE

FOR TOUCH_INDEX, 0, ADD_MAXTOUCH
    SIF !ADD_TOUCH_ACTIVE:TOUCH_INDEX
        BREAK
    SIF ADD_TOUCH_HISTORY:TOUCH_INDEX > 0
        BREAK
    SIF ADD_TOUCH_TARGET:TOUCH_INDEX == ARG
        ADD_TOUCH_MARK_REMOVE:TOUCH_INDEX = 1
NEXT
FOR TOUCH_INDEX, ADD_MAXTOUCH-1, -1, -1
    SIF ADD_TOUCH_MARK_REMOVE:TOUCH_INDEX
        CALL ADD_TOUCH_REMOVE(TOUCH_INDEX)
NEXT

@ADD_TOUCH_RESET_T_PART_TR(ARG, PART)
#DIM TOUCH_INDEX
#DIM PART

IF ADD_TEMPTOUCHMODE
    CALL ADD_TEMPTOUCH_RESET_T_PART_TR(ARG, PART)
    RETURN
ENDIF

IF PART == SET_M && TEQUIP:ARG:継続キス && ADD_CHARA_POSE(PLAYER, ARG, SET_M, SET_M)
	PRINTL ＜结束接吻＞
    TEQUIP:ARG:継続キス = 0
ENDIF
IF PART == SET_B && TEQUIP:ARG:胸揉み && ADD_CHARA_POSE(PLAYER, ARG, SET_YUBI, SET_B)
    PRINTL ＜结束揉胸＞
    TEQUIP:ARG:胸揉み = 0
ENDIF
IF PART == SET_B && TEQUIP:ARG:乳首吸い && ADD_CHARA_POSE(PLAYER, ARG, SET_M, SET_B)
    PRINTL ＜结束吸乳头＞
    TEQUIP:ARG:乳首吸い = 0
ENDIF

VARSET ADD_TOUCH_MARK_REMOVE

FOR TOUCH_INDEX, 0, ADD_MAXTOUCH
    SIF !ADD_TOUCH_ACTIVE:TOUCH_INDEX
        BREAK
    SIF ADD_TOUCH_HISTORY:TOUCH_INDEX > 0
        BREAK
    SIF ADD_TOUCH_TARGET:TOUCH_INDEX == ARG && ADD_TOUCH_TARGET_PART:TOUCH_INDEX == PART
        ADD_TOUCH_MARK_REMOVE:TOUCH_INDEX = 1
NEXT
FOR TOUCH_INDEX, ADD_MAXTOUCH-1, -1, -1
    SIF ADD_TOUCH_MARK_REMOVE:TOUCH_INDEX
        CALL ADD_TOUCH_REMOVE(TOUCH_INDEX)
NEXT

@ADD_CHARA_INSERTED(ARG, ACTOR = -1)
#FUNCTION
#DIM TOUCH_INDEX
#DIM ACTOR

IF ADD_TEMPTOUCHMODE
    LOCAL = ADD_TEMPTOUCH_CHARA_INSERTED(ARG)
    IF LOCAL == -2 ;value was reset, act as if there was no match
        RETURNF 0
    ELSEIF LOCAL > 0
        RETURNF 1
    ENDIF
ENDIF

FOR TOUCH_INDEX, 0, ADD_MAXTOUCH
    SIF !ADD_TOUCH_ACTIVE:TOUCH_INDEX
        BREAK
    SIF ADD_TOUCH_HISTORY:TOUCH_INDEX > 0
        BREAK
    SIF ADD_TOUCH_TARGET_PART:TOUCH_INDEX != SET_P
        CONTINUE
    SIF ACTOR >= 0 && ADD_TOUCH_ACTOR:TOUCH_INDEX != ACTOR
        CONTINUE    
    SIF GROUPMATCH(ADD_TOUCH_ACTOR_PART:TOUCH_INDEX, SET_V, SET_A)
        RETURNF 1
NEXT
RETURNF 0

@ADD_CHARA_WHO_INSERTED_IN(CHARA, PART)
#FUNCTION
#DIM CHARA
#DIM PART
RETURNF ADD_CHARA_WHO_TOUCHING(CHARA, PART, SET_P)

@ADD_CHARA_WHO_TOUCHING_ACTOR(ACTOR, CHARA_PART, ACTOR_PART, HISTORY = 0)
#FUNCTION
#DIM ACTOR
#DIM TOUCH_INDEX
#DIM CHARA_PART
#DIM ACTOR_PART
#DIM HISTORY

IF ADD_TEMPTOUCHMODE && HISTORY == 0
    LOCAL = ADD_TEMPTOUCH_CHARA_WHO_TOUCHING_ACTOR(ACTOR, CHARA_PART, ACTOR_PART)
    IF LOCAL == -2 ;value was reset, act as if there was no match
        RETURNF -1
    ELSEIF LOCAL >= 0
        RETURNF LOCAL
    ENDIF
ENDIF

FOR TOUCH_INDEX, 0, ADD_MAXTOUCH
    SIF !ADD_TOUCH_ACTIVE:TOUCH_INDEX
        BREAK
    SIF ADD_TOUCH_HISTORY:TOUCH_INDEX < HISTORY
        CONTINUE
    SIF ADD_TOUCH_HISTORY:TOUCH_INDEX > HISTORY
        BREAK
    SIF ADD_TOUCH_ACTOR:TOUCH_INDEX != ACTOR
        CONTINUE
    SIF ADD_TOUCH_TARGET_PART:TOUCH_INDEX != CHARA_PART
        CONTINUE
    SIF ADD_TOUCH_ACTOR_PART:TOUCH_INDEX != ACTOR_PART
        CONTINUE
    RETURNF ADD_TOUCH_TARGET:TOUCH_INDEX
NEXT
RETURNF -1

@ADD_CHARA_WHO_TOUCHING(CHARA, CHARA_PART, ACTOR_PART, HISTORY = 0)
#FUNCTION
#DIM CHARA
#DIM TOUCH_INDEX
#DIM CHARA_PART
#DIM ACTOR_PART
#DIM HISTORY

IF ADD_TEMPTOUCHMODE && HISTORY == 0
    LOCAL = ADD_TEMPTOUCH_CHARA_WHO_TOUCHING(CHARA, CHARA_PART, ACTOR_PART)
    IF LOCAL == -2 ;value was reset, act as if there was no match
        RETURNF -1
    ELSEIF LOCAL >= 0
        RETURNF LOCAL
    ENDIF
ENDIF

FOR TOUCH_INDEX, 0, ADD_MAXTOUCH
    SIF !ADD_TOUCH_ACTIVE:TOUCH_INDEX
        BREAK
    SIF ADD_TOUCH_HISTORY:TOUCH_INDEX < HISTORY
        CONTINUE
    SIF ADD_TOUCH_HISTORY:TOUCH_INDEX > HISTORY
        BREAK
    SIF ADD_TOUCH_TARGET:TOUCH_INDEX != CHARA
        CONTINUE
    SIF ADD_TOUCH_TARGET_PART:TOUCH_INDEX != CHARA_PART
        CONTINUE
    SIF ADD_TOUCH_ACTOR_PART:TOUCH_INDEX != ACTOR_PART
        CONTINUE
    RETURNF ADD_TOUCH_ACTOR:TOUCH_INDEX
NEXT
RETURNF -1

@ADD_TOUCH_PART_INSERTED_IN(CHARA, CHARA_PART, HISTORY = 0)
#FUNCTION
#DIM CHARA
#DIM CHARA_PART
#DIM HISTORY
#DIM TOUCH_INDEX

IF ADD_TEMPTOUCHMODE && HISTORY == 0
    LOCAL = ADD_TEMPTOUCH_PART_INSERTED_IN(CHARA, CHARA_PART)
    IF LOCAL == -2 ;value was reset, act as if there was no match
        RETURNF -1
    ELSEIF LOCAL >= 0
        RETURNF LOCAL
    ENDIF
ENDIF

FOR TOUCH_INDEX, 0, ADD_MAXTOUCH
    SIF !ADD_TOUCH_ACTIVE:TOUCH_INDEX
        BREAK
    SIF ADD_TOUCH_HISTORY:TOUCH_INDEX < HISTORY
        CONTINUE
    SIF ADD_TOUCH_HISTORY:TOUCH_INDEX > HISTORY
        BREAK
    SIF ADD_TOUCH_TARGET:TOUCH_INDEX != CHARA
        CONTINUE
    SIF ADD_TOUCH_TARGET_PART:TOUCH_INDEX != CHARA_PART
        CONTINUE
    RETURNF ADD_TOUCH_ACTOR_PART:TOUCH_INDEX
NEXT
RETURNF -1

@ADD_MASTER_POSE_TR(ACTOR, CHARA_PART, ACTOR_PART,HISTORY)
#FUNCTION
#DIM ACTOR
#DIM CHARA_PART
#DIM ACTOR_PART
#DIM HISTORY
#DIM TOUCH_INDEX

IF ADD_TEMPTOUCHMODE && HISTORY == 0
    LOCAL = ADD_TEMPTOUCH_MASTER_POSE_TR(ACTOR, CHARA_PART, ACTOR_PART)
    IF LOCAL == -2 ;value was reset, act as if there was no match
        RETURNF 0
    ELSEIF LOCAL >= 0
        RETURNF LOCAL
    ENDIF
ENDIF

FOR TOUCH_INDEX, 0, ADD_MAXTOUCH
    SIF !ADD_TOUCH_ACTIVE:TOUCH_INDEX
        BREAK
    SIF ADD_TOUCH_HISTORY:TOUCH_INDEX < HISTORY
        CONTINUE
    SIF ADD_TOUCH_HISTORY:TOUCH_INDEX > HISTORY
        BREAK
    SIF ADD_TOUCH_ACTOR:TOUCH_INDEX != ACTOR
        CONTINUE

    IF !CHARA_PART
        SIF ADD_TOUCH_ACTOR_PART:TOUCH_INDEX == ACTOR_PART
            RETURNF ADD_TOUCH_TARGET:TOUCH_INDEX
    ELSE
        SIF ADD_TOUCH_ACTOR_PART:TOUCH_INDEX == ACTOR_PART && ADD_TOUCH_TARGET_PART:TOUCH_INDEX == CHARA_PART
            RETURNF ADD_TOUCH_TARGET:TOUCH_INDEX
    ENDIF
NEXT

@ADD_挿入解除_TR(ARG)
#DIM TOUCH_INDEX
IF ADD_TEMPTOUCHMODE
    CALL ADD_TEMPTOUCH_挿入解除_TR(ARG)
    RETURN
ENDIF

VARSET ADD_TOUCH_MARK_REMOVE

FOR TOUCH_INDEX, 0, ADD_MAXTOUCH
    SIF !ADD_TOUCH_ACTIVE:TOUCH_INDEX
        BREAK
    SIF ADD_TOUCH_HISTORY:TOUCH_INDEX > 0
        BREAK

    SIF !GROUPMATCH(ADD_TOUCH_TARGET_PART:TOUCH_INDEX, SET_P, SET_C, SET_M, SET_V, SET_A)
        CONTINUE
    SIF ADD_TOUCH_TARGET:TOUCH_INDEX == ARG
        ADD_TOUCH_MARK_REMOVE:TOUCH_INDEX = 1
NEXT

FOR TOUCH_INDEX, ADD_MAXTOUCH-1, -1, -1
    SIF ADD_TOUCH_MARK_REMOVE:TOUCH_INDEX
        CALL ADD_TOUCH_REMOVE(TOUCH_INDEX)
NEXT
TEQUIP:ARG:Ｖセックス = -1
TEQUIP:ARG:Ａセックス = -1
TEQUIP:ARG:体位 = 0

@ADD_TOUCH_SHIFT(INDEX)
#DIM INDEX

ARRAYSHIFT ADD_TOUCH_ACTIVE, 1, INDEX
ARRAYSHIFT ADD_TOUCH_HISTORY, 1, INDEX
ARRAYSHIFT ADD_TOUCH_ACTOR, 1, INDEX
ARRAYSHIFT ADD_TOUCH_TARGET, 1, INDEX
ARRAYSHIFT ADD_TOUCH_ACTOR_PART, 1, INDEX
ARRAYSHIFT ADD_TOUCH_TARGET_PART, 1, INDEX
ARRAYSHIFT ADD_TOUCH_POSE, 1, INDEX

@ADD_TOUCH_SET(ACTOR, CHARA, ACTOR_PART, TARGET_PART, POSE)
#DIM ACTOR
#DIM CHARA
#DIM ACTOR_PART
#DIM TARGET_PART
#DIM POSE
#DIM TOUCH_INDEX

IF ADD_TEMPTOUCHMODE
    CALL ADD_TEMPTOUCH_SET(ACTOR, CHARA, ACTOR_PART, TARGET_PART, POSE)
    RETURN
ENDIF

FOR TOUCH_INDEX, 0, ADD_MAXTOUCH
    SIF !ADD_TOUCH_ACTIVE:TOUCH_INDEX
        BREAK
    SIF ADD_TOUCH_HISTORY:TOUCH_INDEX > 0
        BREAK

    SIF ADD_TOUCH_ACTOR:TOUCH_INDEX != ACTOR
        CONTINUE
    SIF ADD_TOUCH_TARGET:TOUCH_INDEX != CHARA
        CONTINUE
    SIF ADD_TOUCH_ACTOR_PART:TOUCH_INDEX != ACTOR_PART
        CONTINUE
    SIF ADD_TOUCH_TARGET_PART:TOUCH_INDEX != TARGET_PART
        CONTINUE
    IF ADD_TOUCH_POSE:TOUCH_INDEX != POSE
        ;only need to change the pose
        ADD_TOUCH_POSE:TOUCH_INDEX = POSE
        RETURN
    ENDIF

    RETURN ;already present
NEXT

CALL ADD_TOUCH_SHIFT(0)

ADD_TOUCH_ACTIVE:0 = 1
ADD_TOUCH_HISTORY:0 = 0
ADD_TOUCH_ACTOR:0 = ACTOR
ADD_TOUCH_TARGET:0 = CHARA
ADD_TOUCH_ACTOR_PART:0 = ACTOR_PART
ADD_TOUCH_TARGET_PART:0 = TARGET_PART
ADD_TOUCH_POSE:0 = POSE

@ADD_TOUCH_ALTER_POSE(ACTOR, CHARA, NEW_POSE)
#DIM ACTOR
#DIM CHARA
#DIM NEW_POSE
#DIM TOUCH_INDEX

IF ADD_TEMPTOUCHMODE
    CALL ADD_TEMPTOUCH_ALTER_POSE(ACTOR, CHARA, NEW_POSE)
    RETURN
ENDIF

FOR TOUCH_INDEX, 0, ADD_MAXTOUCH
    SIF !ADD_TOUCH_ACTIVE:TOUCH_INDEX
        BREAK
    SIF ADD_TOUCH_HISTORY:TOUCH_INDEX > 0
        BREAK
    SIF ADD_TOUCH_ACTOR:TOUCH_INDEX != ACTOR
        CONTINUE
    SIF ADD_TOUCH_TARGET:TOUCH_INDEX != CHARA
        CONTINUE
    SIF ADD_TOUCH_ACTOR_PART:TOUCH_INDEX != SET_P
        CONTINUE
    SIF !GROUPMATCH(ADD_TOUCH_TARGET_PART:TOUCH_INDEX, SET_V, SET_A)
        CONTINUE

    ADD_TOUCH_POSE:TOUCH_INDEX = NEW_POSE
NEXT

@ADD_TOUCH_REMOVE(INDEX)
#DIM INDEX
ARRAYREMOVE ADD_TOUCH_ACTIVE, INDEX, 1
ARRAYREMOVE ADD_TOUCH_HISTORY, INDEX, 1
ARRAYREMOVE ADD_TOUCH_ACTOR, INDEX, 1
ARRAYREMOVE ADD_TOUCH_TARGET, INDEX, 1
ARRAYREMOVE ADD_TOUCH_ACTOR_PART, INDEX, 1
ARRAYREMOVE ADD_TOUCH_TARGET_PART, INDEX, 1
ARRAYREMOVE ADD_TOUCH_POSE, INDEX, 1

@ADD_TOUCH_PRINT_DEBUG()
#FUNCTION
#DIM TOUCH_INDEX

DEBUGPRINTFORML Sex action history:
FOR TOUCH_INDEX, 0, ADD_MAXTOUCH
    SIF !ADD_TOUCH_ACTIVE:TOUCH_INDEX
        BREAK
    
    DEBUGPRINTFORML [{ADD_TOUCH_HISTORY:TOUCH_INDEX}] %CALLNAME:(ADD_TOUCH_ACTOR:TOUCH_INDEX)%'s %ADD_SEX_MODULE_PARTNAME(ADD_TOUCH_ACTOR_PART:TOUCH_INDEX)% on %CALLNAME:(ADD_TOUCH_TARGET:TOUCH_INDEX)%'s %ADD_SEX_MODULE_PARTNAME(ADD_TOUCH_TARGET_PART:TOUCH_INDEX)%%COND_STR(@" (%ADD_SEX_MODULE_POSE(ADD_TOUCH_POSE:TOUCH_INDEX)%)", ADD_TOUCH_POSE:TOUCH_INDEX)%.
NEXT

@ADD_TOUCH_INCREMENT
#DIM TOUCH_INDEX
FOR TOUCH_INDEX, ADD_MAXTOUCH-1, -1, -1
    SIF !ADD_TOUCH_ACTIVE:TOUCH_INDEX
        CONTINUE
    
    ADD_TOUCH_HISTORY:TOUCH_INDEX++
    IF ADD_TOUCH_HISTORY:TOUCH_INDEX == 1
        CALL ADD_TOUCH_SHIFT(0)
        TOUCH_INDEX++
        ADD_TOUCH_ACTIVE:0 = 1
        ADD_TOUCH_HISTORY:0 = -1
        ADD_TOUCH_ACTOR:0 = ADD_TOUCH_ACTOR:TOUCH_INDEX
        ADD_TOUCH_TARGET:0 = ADD_TOUCH_TARGET:TOUCH_INDEX
        ADD_TOUCH_ACTOR_PART:0 = ADD_TOUCH_ACTOR_PART:TOUCH_INDEX
        ADD_TOUCH_TARGET_PART:0 = ADD_TOUCH_TARGET_PART:TOUCH_INDEX
        ADD_TOUCH_POSE:0 = ADD_TOUCH_POSE:TOUCH_INDEX
    ENDIF
NEXT
CALL ADD_TOUCH_SET_OLD_FIELDS

@ADD_TOUCH_DECREMENT
#DIM TOUCH_INDEX

VARSET ADD_TOUCH_MARK_REMOVE
FOR TOUCH_INDEX, ADD_MAXTOUCH-1, -1, -1
    SIF !ADD_TOUCH_ACTIVE:TOUCH_INDEX
        CONTINUE
    
    ADD_TOUCH_HISTORY:TOUCH_INDEX--
    IF ADD_TOUCH_HISTORY:TOUCH_INDEX < 0
        ADD_TOUCH_MARK_REMOVE:TOUCH_INDEX = 1
    ENDIF
NEXT

FOR TOUCH_INDEX, ADD_MAXTOUCH-1, -1, -1
    SIF ADD_TOUCH_MARK_REMOVE:TOUCH_INDEX
        CALL ADD_TOUCH_REMOVE(TOUCH_INDEX)
NEXT

CALL ADD_TOUCH_SET_OLD_FIELDS

@ADD_TOUCH_SET_OLD_FIELDS(ONLY_NOW)
#DIM TOUCH_INDEX
#DIM ONLY_NOW
;set old fields for compatibility
FOR LOCAL,0,CHARANUM
    VARSET TEQUIP:LOCAL:0, 0, 101, 111 + (ONLY_NOW ? 0 # 90)
NEXT

SIF ONLY_NOW && ADD_TEMPTOUCHMODE
    GOTO SETTEMPTOUCH

FOR TOUCH_INDEX, ADD_MAXTOUCH-1, -1, -1
    SIF !ADD_TOUCH_ACTIVE:TOUCH_INDEX
        CONTINUE
    SIF ONLY_NOW && ADD_TOUCH_HISTORY:TOUCH_INDEX > 0
        CONTINUE
    SIF ADD_TOUCH_HISTORY:TOUCH_INDEX > 9
        CONTINUE

    SIF ADD_TEMPTOUCHMODE && ADD_TOUCH_HISTORY:TOUCH_INDEX == 0
        BREAK

    SIF !INRANGE(ADD_TOUCH_TARGET_PART:TOUCH_INDEX, 1, 9) || !INRANGE(ADD_TOUCH_ACTOR_PART:TOUCH_INDEX, 1, 9)
        CONTINUE

    IF ADD_TOUCH_TARGET_PART:TOUCH_INDEX == SET_P ;inserted in player
        TEQUIP:(ADD_TOUCH_ACTOR:TOUCH_INDEX):(100 + (10 * ADD_TOUCH_HISTORY:TOUCH_INDEX) + ADD_TOUCH_ACTOR_PART:TOUCH_INDEX) = ADD_TOUCH_TARGET_PART:TOUCH_INDEX
        SIF ADD_TOUCH_POSE:TOUCH_INDEX > 0
            TEQUIP:(ADD_TOUCH_ACTOR:TOUCH_INDEX):(110 + (10 * ADD_TOUCH_HISTORY:TOUCH_INDEX)) = ADD_TOUCH_POSE:TOUCH_INDEX
    ELSE
        TEQUIP:(ADD_TOUCH_TARGET:TOUCH_INDEX):(100 + (10 * ADD_TOUCH_HISTORY:TOUCH_INDEX) + ADD_TOUCH_TARGET_PART:TOUCH_INDEX) = ADD_TOUCH_ACTOR_PART:TOUCH_INDEX
        SIF ADD_TOUCH_POSE:TOUCH_INDEX > 0
            TEQUIP:(ADD_TOUCH_TARGET:TOUCH_INDEX):(110 + (10 * ADD_TOUCH_HISTORY:TOUCH_INDEX)) = ADD_TOUCH_POSE:TOUCH_INDEX
    ENDIF
NEXT

IF ADD_TEMPTOUCHMODE
    $SETTEMPTOUCH
    FOR TOUCH_INDEX, ADD_MAXTEMPTOUCH-1, -1, -1
        SIF !ADD_TEMPTOUCH_ACTIVE:TOUCH_INDEX
            CONTINUE

        SIF !INRANGE(ADD_TEMPTOUCH_TARGET_PART:TOUCH_INDEX, 1, 9) || !INRANGE(ADD_TEMPTOUCH_ACTOR_PART:TOUCH_INDEX, 1, 9)
            CONTINUE

        IF ADD_TEMPTOUCH_TARGET_PART:TOUCH_INDEX == SET_P ;inserted in player
            TEQUIP:(ADD_TEMPTOUCH_ACTOR:TOUCH_INDEX):(100 + ADD_TEMPTOUCH_ACTOR_PART:TOUCH_INDEX) = ADD_TEMPTOUCH_TARGET_PART:TOUCH_INDEX
            SIF ADD_TEMPTOUCH_POSE:TOUCH_INDEX > 0
                TEQUIP:(ADD_TEMPTOUCH_ACTOR:TOUCH_INDEX):110 = ADD_TEMPTOUCH_POSE:TOUCH_INDEX
        ELSE
            TEQUIP:(ADD_TEMPTOUCH_TARGET:TOUCH_INDEX):(100 + ADD_TEMPTOUCH_TARGET_PART:TOUCH_INDEX) = ADD_TEMPTOUCH_ACTOR_PART:TOUCH_INDEX
            SIF ADD_TEMPTOUCH_POSE:TOUCH_INDEX > 0
                TEQUIP:(ADD_TEMPTOUCH_TARGET:TOUCH_INDEX):110 = ADD_TEMPTOUCH_POSE:TOUCH_INDEX
        ENDIF
    NEXT
ENDIF

@ADD_TOUCH_MIRROR_POSE(POSE)
#FUNCTION
#DIM POSE
SELECTCASE POSE
    CASE POSE_MISSIONARY
        RETURNF POSE_DOGGYSTYLE
    CASE POSE_DOGGYSTYLE
        RETURNF POSE_MISSIONARY
    CASE POSE_LOTUS
        RETURNF POSE_REVLOTUS
    CASE POSE_REVLOTUS
        RETURNF POSE_LOTUS
ENDSELECT
RETURNF POSE ;no mirror

@ADD_TOUCH_IS_NORMAL_POSE(POSE)
#FUNCTION
#DIM POSE
RETURNF GROUPMATCH(POSE, POSE_MISSIONARY, POSE_LOTUS)

@ADD_TOUCH_IS_REVERSE_POSE(POSE)
#FUNCTION
#DIM POSE
RETURNF GROUPMATCH(POSE, POSE_DOGGYSTYLE, POSE_REVLOTUS)

@ADD_TOUCH_CURRENT_POSE(ACTOR, CHARA, HISTORY = -1)
#FUNCTION
#DIM ACTOR
#DIM CHARA
#DIM HISTORY
#DIM TOUCH_INDEX

IF ADD_TEMPTOUCHMODE && GROUPMATCH(HISTORY, -1, 0)
    LOCAL = ADD_TEMPTOUCH_CURRENT_POSE_TR(ACTOR, CHARA)
    IF LOCAL == -2 ;value was reset, act as if there was no match
        RETURNF 0
    ELSEIF LOCAL >= 0
        RETURNF LOCAL
    ENDIF
ENDIF

FOR TOUCH_INDEX, 0, ADD_MAXTOUCH
    SIF !ADD_TOUCH_ACTIVE:TOUCH_INDEX
        BREAK
    IF HISTORY >= 0
        SIF ADD_TOUCH_HISTORY:TOUCH_INDEX < HISTORY
            CONTINUE
        SIF ADD_TOUCH_HISTORY:TOUCH_INDEX > HISTORY
            BREAK
    ENDIF
    SIF HISTORY == -1 && ADD_TOUCH_HISTORY:TOUCH_INDEX > 1
        BREAK
    SIF ADD_TOUCH_ACTOR:TOUCH_INDEX != ACTOR
        CONTINUE
    SIF ADD_TOUCH_TARGET:TOUCH_INDEX != CHARA
        CONTINUE
    
    SIF ADD_TOUCH_ACTOR_PART:TOUCH_INDEX == SET_P && !GROUPMATCH(ADD_TOUCH_TARGET_PART:TOUCH_INDEX, SET_V, SET_A)
        CONTINUE
    SIF ADD_TOUCH_TARGET_PART:TOUCH_INDEX == SET_P && !GROUPMATCH(ADD_TOUCH_ACTOR_PART:TOUCH_INDEX, SET_V, SET_A)
        CONTINUE
    SIF !ADD_TOUCH_POSE:TOUCH_INDEX
        CONTINUE

    RETURNF ADD_TOUCH_POSE:TOUCH_INDEX
NEXT

@ADD_TOUCH_CLEAR
VARSET ADD_TOUCH_ACTIVE
VARSET ADD_TOUCH_HISTORY
VARSET ADD_TOUCH_ACTOR
VARSET ADD_TOUCH_TARGET
VARSET ADD_TOUCH_ACTOR_PART
VARSET ADD_TOUCH_TARGET_PART
VARSET ADD_TOUCH_POSE

@ADD_TOUCH_CLEAR_CHARA(ARG)
#DIM TOUCH_INDEX
VARSET ADD_TOUCH_MARK_REMOVE

FOR TOUCH_INDEX, 0, ADD_MAXTOUCH
    SIF !ADD_TOUCH_ACTIVE:TOUCH_INDEX
        BREAK
    SIF ADD_TOUCH_TARGET:TOUCH_INDEX == ARG
        ADD_TOUCH_MARK_REMOVE:TOUCH_INDEX = 1
NEXT
FOR TOUCH_INDEX, ADD_MAXTOUCH-1, -1, -1
    SIF ADD_TOUCH_MARK_REMOVE:TOUCH_INDEX
        CALL ADD_TOUCH_REMOVE(TOUCH_INDEX)
NEXT
TFLAG:前回の特殊COM = -1 ;clear out this field to prevent continue last action command to show up if someone leaves


@ADD_TEMPTOUCH_CLEAR
VARSET ADD_TEMPTOUCH_ACTIVE
VARSET ADD_TEMPTOUCH_ACTOR
VARSET ADD_TEMPTOUCH_TARGET
VARSET ADD_TEMPTOUCH_ACTOR_PART
VARSET ADD_TEMPTOUCH_TARGET_PART
VARSET ADD_TEMPTOUCH_POSE
VARSET ADD_TEMPTOUCH_RESET

@ADD_TEMPTOUCH_SHIFT
ARRAYSHIFT ADD_TEMPTOUCH_ACTIVE, 1, 0
ARRAYSHIFT ADD_TEMPTOUCH_ACTOR, 1, 0
ARRAYSHIFT ADD_TEMPTOUCH_TARGET, 1, 0
ARRAYSHIFT ADD_TEMPTOUCH_ACTOR_PART, 1, 0
ARRAYSHIFT ADD_TEMPTOUCH_TARGET_PART, 1, 0
ARRAYSHIFT ADD_TEMPTOUCH_POSE, 1, 0
ARRAYSHIFT ADD_TEMPTOUCH_RESET, 1, 0

@ADD_TEMPTOUCH_SET(ACTOR, CHARA, ACTOR_PART, TARGET_PART, POSE)
#DIM ACTOR
#DIM CHARA
#DIM ACTOR_PART
#DIM TARGET_PART
#DIM POSE
#DIM TOUCH_INDEX

CALL ADD_TEMPTOUCH_SHIFT

ADD_TEMPTOUCH_ACTIVE:0 = 1
ADD_TEMPTOUCH_ACTOR:0 = ACTOR
ADD_TEMPTOUCH_TARGET:0 = CHARA
ADD_TEMPTOUCH_ACTOR_PART:0 = ACTOR_PART
ADD_TEMPTOUCH_TARGET_PART:0 = TARGET_PART
ADD_TEMPTOUCH_POSE:0 = POSE

@ADD_TEMPTOUCH_SET_TR(ACTOR,CHARA_PART,ACTOR_PART,CHARA,POSE,RESET)
#DIM ACTOR
#DIM CHARA
#DIM ACTOR_PART
#DIM CHARA_PART
#DIM POSE
#DIM RESET
#DIM TOUCH_INDEX

IF RESET
    FOR TOUCH_INDEX, 0, ADD_MAXTOUCH
        SIF !ADD_TOUCH_ACTIVE:TOUCH_INDEX
            BREAK
        SIF ADD_TOUCH_HISTORY:TOUCH_INDEX > 0
            BREAK
        SIF ADD_TOUCH_TARGET:TOUCH_INDEX == CHARA && ADD_TOUCH_TARGET_PART:TOUCH_INDEX == SET_M && TFLAG:3 >= 60 && TFLAG:3 <= 70
            CONTINUE

        CALL ADD_TEMPTOUCH_RESET(TOUCH_INDEX)
    NEXT

    TEQUIP:CHARA:体位 = 0
ENDIF

SIF !POSE && GROUPMATCH(CHARA_PART, SET_V, SET_A)
    POSE = MAX(ADD_TOUCH_CURRENT_POSE(ACTOR, CHARA),0)

SIF ACTOR_PART && CHARA_PART
    CALL ADD_TEMPTOUCH_SET(ACTOR, CHARA, ACTOR_PART, CHARA_PART, POSE)

@ADD_TEMPTOUCH_CHARA_POSE(ACTOR, CHARA, ACTOR_PART, CHARA_PART)
#FUNCTION
#DIM ACTOR
#DIM CHARA
#DIM ACTOR_PART
#DIM CHARA_PART
#DIM TOUCH_INDEX

FOR TOUCH_INDEX, 0, ADD_MAXTEMPTOUCH
    SIF !ADD_TEMPTOUCH_ACTIVE:TOUCH_INDEX
        BREAK
    SIF ADD_TEMPTOUCH_ACTOR:TOUCH_INDEX != ACTOR
        CONTINUE
    SIF ADD_TEMPTOUCH_TARGET:TOUCH_INDEX != CHARA
        CONTINUE
    SIF ADD_TEMPTOUCH_ACTOR_PART:TOUCH_INDEX != ACTOR_PART
        CONTINUE
    SIF ADD_TEMPTOUCH_TARGET_PART:TOUCH_INDEX != CHARA_PART
        CONTINUE
    SIF ADD_TEMPTOUCH_RESET:TOUCH_INDEX
        RETURNF -2

    RETURNF 1
NEXT
RETURNF 0

@ADD_TEMPTOUCH_CHARA_INSERTED(ARG)
#FUNCTION
#DIM TOUCH_INDEX

FOR TOUCH_INDEX, 0, ADD_MAXTEMPTOUCH
    SIF !ADD_TEMPTOUCH_ACTIVE:TOUCH_INDEX
        BREAK
    SIF ADD_TEMPTOUCH_TARGET_PART:TOUCH_INDEX != SET_P
        CONTINUE
    SIF !GROUPMATCH(ADD_TEMPTOUCH_ACTOR_PART:TOUCH_INDEX, SET_V, SET_A)
        CONTINUE        
    SIF ADD_TEMPTOUCH_RESET:TOUCH_INDEX
        RETURNF -2
    RETURNF 1
NEXT
RETURNF 0

@ADD_TEMPTOUCH_CHARA_WHO_TOUCHING_ACTOR(ACTOR, CHARA_PART, ACTOR_PART)
#FUNCTION
#DIM ACTOR
#DIM TOUCH_INDEX
#DIM CHARA_PART
#DIM ACTOR_PART
#DIM HISTORY
FOR TOUCH_INDEX, 0, ADD_MAXTEMPTOUCH
    SIF !ADD_TEMPTOUCH_ACTIVE:TOUCH_INDEX
        BREAK
    SIF ADD_TEMPTOUCH_ACTOR:TOUCH_INDEX != ACTOR
        CONTINUE
    SIF ADD_TEMPTOUCH_TARGET_PART:TOUCH_INDEX != CHARA_PART
        CONTINUE
    SIF ADD_TEMPTOUCH_ACTOR_PART:TOUCH_INDEX != ACTOR_PART
        CONTINUE
    SIF ADD_TEMPTOUCH_RESET:TOUCH_INDEX
        RETURNF -2
    RETURNF ADD_TEMPTOUCH_ACTOR:TOUCH_INDEX
NEXT
RETURNF -1

@ADD_TEMPTOUCH_CHARA_WHO_TOUCHING(CHARA, CHARA_PART, ACTOR_PART)
#FUNCTION
#DIM CHARA
#DIM TOUCH_INDEX
#DIM CHARA_PART
#DIM ACTOR_PART

FOR TOUCH_INDEX, 0, ADD_MAXTEMPTOUCH
    SIF !ADD_TEMPTOUCH_ACTIVE:TOUCH_INDEX
        BREAK
    SIF ADD_TEMPTOUCH_TARGET:TOUCH_INDEX != CHARA
        CONTINUE
    SIF ADD_TEMPTOUCH_TARGET_PART:TOUCH_INDEX != CHARA_PART
        CONTINUE
    SIF ADD_TEMPTOUCH_ACTOR_PART:TOUCH_INDEX != ACTOR_PART
        CONTINUE
    SIF ADD_TEMPTOUCH_RESET:TOUCH_INDEX
        RETURNF -2
    RETURNF ADD_TEMPTOUCH_ACTOR:TOUCH_INDEX
NEXT
RETURNF -1

@ADD_TEMPTOUCH_PART_INSERTED_IN(CHARA, CHARA_PART)
#FUNCTION
#DIM CHARA
#DIM CHARA_PART
#DIM TOUCH_INDEX

FOR TOUCH_INDEX, 0, ADD_MAXTEMPTOUCH
    SIF !ADD_TEMPTOUCH_ACTIVE:TOUCH_INDEX
        BREAK
    SIF ADD_TEMPTOUCH_TARGET:TOUCH_INDEX != CHARA
        CONTINUE
    SIF ADD_TEMPTOUCH_TARGET_PART:TOUCH_INDEX != CHARA_PART
        CONTINUE
    SIF ADD_TEMPTOUCH_RESET:TOUCH_INDEX
        RETURNF -2
    RETURNF ADD_TEMPTOUCH_ACTOR_PART:TOUCH_INDEX
NEXT
RETURNF -1


@ADD_TEMPTOUCH_MASTER_POSE_TR(ACTOR, CHARA_PART, ACTOR_PART)
#FUNCTION
#DIM ACTOR
#DIM CHARA_PART
#DIM ACTOR_PART
#DIM TOUCH_INDEX

FOR TOUCH_INDEX, 0, ADD_MAXTEMPTOUCH
    SIF !ADD_TEMPTOUCH_ACTIVE:TOUCH_INDEX
        BREAK
    SIF ADD_TEMPTOUCH_ACTOR:TOUCH_INDEX != ACTOR
        CONTINUE

    IF !CHARA_PART
        IF ADD_TEMPTOUCH_ACTOR_PART:TOUCH_INDEX == ACTOR_PART
            SIF ADD_TEMPTOUCH_RESET:TOUCH_INDEX
                RETURNF -2
            RETURNF ADD_TEMPTOUCH_TARGET:TOUCH_INDEX
        ENDIF
    ELSE
        IF ADD_TEMPTOUCH_ACTOR_PART:TOUCH_INDEX == ACTOR_PART && ADD_TEMPTOUCH_TARGET_PART:TOUCH_INDEX == CHARA_PART
            SIF ADD_TEMPTOUCH_RESET:TOUCH_INDEX
                RETURNF -2
            RETURNF ADD_TEMPTOUCH_TARGET:TOUCH_INDEX
        ENDIF
    ENDIF
NEXT
RETURNF -1

@ADD_TEMPTOUCH_CURRENT_POSE_TR(ACTOR, CHARA)
#FUNCTION
#DIM ACTOR
#DIM CHARA
#DIM TOUCH_INDEX

FOR TOUCH_INDEX, 0, ADD_MAXTEMPTOUCH
    SIF !ADD_TEMPTOUCH_ACTIVE:TOUCH_INDEX
        BREAK
    SIF ADD_TEMPTOUCH_ACTOR:TOUCH_INDEX != ACTOR
        CONTINUE
    SIF ADD_TEMPTOUCH_TARGET:TOUCH_INDEX != CHARA
        CONTINUE
    SIF ADD_TEMPTOUCH_ACTOR_PART:TOUCH_INDEX == SET_P && !GROUPMATCH(ADD_TEMPTOUCH_TARGET_PART:TOUCH_INDEX, SET_V, SET_A)
        CONTINUE
    SIF ADD_TEMPTOUCH_TARGET_PART:TOUCH_INDEX == SET_P && !GROUPMATCH(ADD_TEMPTOUCH_ACTOR_PART:TOUCH_INDEX, SET_V, SET_A)
        CONTINUE
    SIF !ADD_TEMPTOUCH_POSE:TOUCH_INDEX
        CONTINUE
    SIF ADD_TEMPTOUCH_RESET:TOUCH_INDEX
        RETURNF -2
    RETURNF ADD_TEMPTOUCH_POSE:TOUCH_INDEX
NEXT
RETURNF -1

@ADD_TEMPTOUCH_RESET(TOUCH_INDEX)
#DIM TOUCH_INDEX
CALL ADD_TEMPTOUCH_SHIFT
ADD_TEMPTOUCH_ACTIVE:0 = 1
ADD_TEMPTOUCH_ACTOR:0 = ADD_TOUCH_ACTOR:TOUCH_INDEX
ADD_TEMPTOUCH_TARGET:0 = ADD_TOUCH_TARGET:TOUCH_INDEX
ADD_TEMPTOUCH_ACTOR_PART:0 = ADD_TOUCH_ACTOR_PART:TOUCH_INDEX
ADD_TEMPTOUCH_TARGET_PART:0 = ADD_TOUCH_TARGET_PART:TOUCH_INDEX
ADD_TEMPTOUCH_POSE:0 = ADD_TOUCH_POSE:TOUCH_INDEX
ADD_TEMPTOUCH_RESET:0 = 1

@ADD_TEMPTOUCH_RESET_M_TR(PART, ACTOR)
#DIM PART
#DIM ACTOR
#DIM TOUCH_INDEX

FOR TOUCH_INDEX, 0, ADD_MAXTOUCH
    SIF !ADD_TOUCH_ACTIVE:TOUCH_INDEX
        BREAK
    SIF ADD_TOUCH_HISTORY:TOUCH_INDEX > 0
        BREAK
    SIF ADD_TOUCH_ACTOR:TOUCH_INDEX == ACTOR && ADD_TOUCH_ACTOR_PART:TOUCH_INDEX == PART
        CALL ADD_TEMPTOUCH_RESET(TOUCH_INDEX)
NEXT

@ADD_TEMPTOUCH_RESET_T_TR(ARG)
#DIM TOUCH_INDEX
FOR TOUCH_INDEX, 0, ADD_MAXTOUCH
    SIF !ADD_TOUCH_ACTIVE:TOUCH_INDEX
        BREAK
    SIF ADD_TOUCH_HISTORY:TOUCH_INDEX > 0
        BREAK
    SIF ADD_TOUCH_TARGET:TOUCH_INDEX == ARG
        CALL ADD_TEMPTOUCH_RESET(TOUCH_INDEX)
NEXT

@ADD_TEMPTOUCH_RESET_T_PART_TR(ARG, PART)
#DIM TOUCH_INDEX
#DIM PART

FOR TOUCH_INDEX, 0, ADD_MAXTOUCH
    SIF !ADD_TOUCH_ACTIVE:TOUCH_INDEX
        BREAK
    SIF ADD_TOUCH_HISTORY:TOUCH_INDEX > 0
        BREAK
    SIF ADD_TOUCH_TARGET:TOUCH_INDEX == ARG && ADD_TOUCH_TARGET_PART:TOUCH_INDEX == PART
        CALL ADD_TEMPTOUCH_RESET(TOUCH_INDEX)
NEXT

@ADD_TEMPTOUCH_挿入解除_TR(ARG)
#DIM TOUCH_INDEX

FOR TOUCH_INDEX, 0, ADD_MAXTOUCH
    SIF !ADD_TOUCH_ACTIVE:TOUCH_INDEX
        BREAK
    SIF ADD_TOUCH_HISTORY:TOUCH_INDEX > 0
        BREAK

    SIF !GROUPMATCH(ADD_TOUCH_TARGET_PART:TOUCH_INDEX, SET_P, SET_C, SET_M, SET_V, SET_A)
        CONTINUE
    SIF ADD_TOUCH_TARGET:TOUCH_INDEX == ARG
        CALL ADD_TEMPTOUCH_RESET(TOUCH_INDEX)
NEXT

@ADD_TEMPTOUCH_ALTER_POSE(ACTOR, CHARA, NEW_POSE)
#DIM ACTOR
#DIM CHARA
#DIM NEW_POSE
#DIM TOUCH_INDEX

FOR TOUCH_INDEX, 0, ADD_MAXTOUCH
    SIF !ADD_TOUCH_ACTIVE:TOUCH_INDEX
        BREAK
    SIF ADD_TOUCH_HISTORY:TOUCH_INDEX > 0
        BREAK

    SIF ADD_TOUCH_ACTOR:TOUCH_INDEX != ACTOR
        CONTINUE
    SIF ADD_TOUCH_TARGET:TOUCH_INDEX != CHARA
        CONTINUE
    SIF ADD_TOUCH_ACTOR_PART:TOUCH_INDEX != SET_P
        CONTINUE
    SIF !GROUPMATCH(ADD_TOUCH_TARGET_PART:TOUCH_INDEX, SET_V, SET_A)
        CONTINUE

    CALL ADD_TEMPTOUCH_SET(ACTOR, CHARA, ADD_TOUCH_ACTOR_PART:TOUCH_INDEX, ADD_TOUCH_TARGET_PART:TOUCH_INDEX, NEW_POSE)
NEXT

@ADD_TEMPTOUCH_PRINT_DEBUG()
#FUNCTION
#DIM TOUCH_INDEX

DEBUGPRINTFORML Temptouch history:
FOR TOUCH_INDEX, 0, ADD_MAXTEMPTOUCH
    SIF !ADD_TEMPTOUCH_ACTIVE:TOUCH_INDEX
        BREAK
    
    DEBUGPRINTFORML {ADD_TEMPTOUCH_RESET:TOUCH_INDEX} %CALLNAME:(ADD_TEMPTOUCH_ACTOR:TOUCH_INDEX)%'s %ADD_SEX_MODULE_PARTNAME(ADD_TEMPTOUCH_ACTOR_PART:TOUCH_INDEX)% on %CALLNAME:(ADD_TEMPTOUCH_TARGET:TOUCH_INDEX)%'s %ADD_SEX_MODULE_PARTNAME(ADD_TEMPTOUCH_TARGET_PART:TOUCH_INDEX)%%COND_STR(@" (%ADD_SEX_MODULE_POSE(ADD_TEMPTOUCH_POSE:TOUCH_INDEX)%)", ADD_TEMPTOUCH_POSE:TOUCH_INDEX)%.
NEXT
