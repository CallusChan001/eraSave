;-------------------------------
;MORE DATES REPLACEMENT FUNCTIONS
;-------------------------------
[SKIPSTART]
@MOREDATES_CABLEWAY(FEE)
#DIM FEE
#DIM RandomResponse, 8 ; same lenght as below array
#DIM ResponseCharacters = 10, 29, 31, 32, 33, 42, 64, 65
#DIM MoriyaCount
#DIM RandomCount

RandomResponse = 0
MoriyaCount = 0
RandomCount = 0

FOR LOCAL:10, 0, 8
	IF IS_IN_GROUP_DATE(ResponseCharacters:(LOCAL:10))
		SIF GROUPMATCH((ResponseCharacters:(LOCAL:10)),31,32,33)
			MoriyaCount += 1
		RandomResponse:RandomCount = ResponseCharacters:(LOCAL:10)
		RandomCount += 1
	ENDIF
NEXT
; check for all 3 moriyas
IF MoriyaCount == 3
	LOCAL = 12 + ((CFLAG:MASTER:現在位置 / 100 == 8) * 4)
	CALL SPTALK, 0, "通常", LOCAL, @"「Huh? %CALLNAME:32%-san, %CALLNAME:33%-san and %CALLNAME:31%-san? What are they all doing so close to that \@ IS_MALE(MASTER) ? guy # girl\@?」",1
	CALL SPTALK, 0, "笑顔", LOCAL, @" [Err, I mean, are you all going on a family trip?]", 1
	PRINTFORM Although taken aback, the 
	;人里側に河童が常駐してるのもアレかなと
	IF CFLAG:MASTER:現在位置 / 100 == 2
		PRINTFORM Kappa
	ELSE
		PRINTFORM receptionist
	ENDIF
	PRINTFORML  quickly puts on a forced smile.
; check for any character with response
ELSEIF RandomCount > 0
	LOCAL:5 = RandomResponse:(RAND:RandomCount)
	LOCAL = 12 + ((CFLAG:MASTER:現在位置 / 100 == 8) * 4)
	CALL SPTALK, 0, "笑顔", LOCAL, @"「Well if it isn't %CALLNAME:(LOCAL:5)%-san, are you using it too?」",1
	PRINTFORM As soon as the 
	;人里側に河童が常駐してるのもアレかなと
	IF CFLAG:MASTER:現在位置 / 100 == 2
		PRINTFORM Kappa
	ELSE
		PRINTFORM receptionist
	ENDIF
	PRINTFORML  sees %CALLNAME:(LOCAL:5)%, %HE_SHE(LOCAL:5)% puts on a forced smile.
; default
ELSE
	;人里側では一応丁寧対応に
	IF CFLAG:MASTER:現在位置 / 100 == 2
		CALL SPTALK, 0, "通常", 12, @"「Going as a big group I see!」/「Sorry, but we don't offer discounts, it'll be ¥{FEE * 200}! {FEE} Charisma works as well.」",1
	ELSE
		CALL SPTALK, 0, "憤怒", 16, @"「Got a real Casanova here, huh?」/「No discounts. {FEE} Charisma. ¥{FEE * 200} works as well.」",1
	ENDIF
	SIF NumberOfDates > 9
		CALL SPTALK, 0, "通常", CFLAG:MASTER:現在位置 / 100 == 2 ? 12 # 16, @"「...Some of you might want to take turns, just to be safe.」",1	
	;qol custom code
	SIF TIME >= 1020 && FEE != 1
		CALL SPTALK, 0, "笑顔", CFLAG:MASTER:現在位置 / 100 == 2 ? 12 # 16, @"「Yaawn... Working this late makes me want to charge you more.」",1	
ENDIF

@MOREDATES_HOUSE_EXIT(ARG)
#DIM OTHERMOVE
;remove everyone else
FOR LOCAL, 1, CHARANUM
	IF CFLAG:LOCAL:お招き
		;restore original locations (where they start the day)
		IF CFLAG:LOCAL:神社在住
			CFLAG:LOCAL:初期位置 = CFLAG:LOCAL:神社在住
		ELSE
			CFLAG:LOCAL:初期位置 = SUKIMA()
		ENDIF
		;check if character belongs to this map, else send them back home
		IF GET_MAPID(ARG) != CFLAG:LOCAL:よく行く地域 && !IS_IN_GROUP_DATE(LOCAL)
			IF CFLAG:LOCAL:よく行く地域 != -1
				OTHERMOVE = GET_ENTRANCE(CFLAG:LOCAL:よく行く地域)
			ELSE
				OTHERMOVE = CFLAG:LOCAL:初期位置
			ENDIF
		ELSE
			OTHERMOVE = ARG
		ENDIF

		;three different messages/outcomes:
		;exit with you (if dating or they live in that map)
		;go back home (else they just loiter there all day due to movement soft-breaking being in a map that's not theirs)
		;stay sleeping inside - incidentally, this could be changed so they don't go to sleep in another character's home
		;the check for this would be in SLEEP.ERB and we can make sure with お招き and HouseInvitationStatus
		IF !CFLAG:LOCAL:睡眠 ; character moves, either to their home map or outside
			CFLAG:LOCAL:現在位置 = OTHERMOVE
			IF OTHERMOVE == ARG  
				PRINTFORML %CALLNAME:LOCAL% comes out with you.
			ELSE
				PRINTFORML %CALLNAME:LOCAL% returns home.
			ENDIF
		ELSE ; stay sleeping inside, default behavior on the original game is to keep the dating status so we copy that
			CFLAG:LOCAL:現在位置 = SUKIMA()
			PRINTFORML %CALLNAME:LOCAL% stays asleep inside.
		ENDIF
		CFLAG:LOCAL:遭遇位置 = OTHERMOVE ; set moving location so meeting dialogues can take place
		CFLAG:LOCAL:お招き = 0	; remove staying at place flag
	ENDIF
NEXT

@MOREDATES_HOUSE_VISIT(ARG)
; ask
PRINTFORML Should the rest of your dates come along?
CALL ASK_YN("Go with everyone","Go alone")
; maybe add some sort of consent check
IF !RESULT ; 0 - Go with everyone
	PRINTFORMW You bring %GET_DATEES_NAMES(ARG,1)% along with you.
	FOR LOCAL, 0, NumberOfDates
		; physically move (and prevent wandering, asking to leave, etc.)
		CFLAG:(Datees:LOCAL):お招き = ARG
		CFLAG:(Datees:LOCAL):初期位置 = OMANEKIBEYA()
		CFLAG:(Datees:LOCAL):現在位置 = OMANEKIBEYA()
		;SIF (Datees:LOCAL) != ARG
			;CALL KOJO_MESSAGE_SEND("EVENT",1,(Datees:LOCAL),1010,ARG)
	NEXT
	;CALL KOJO_MESSAGE_SEND("EVENT",1,ARG,1012,GET_RANDOM_DATE)
	SIF HouseInvitationStatus ; if at date end, remove date status from everyone
		CALL CLEAN_GROUP_DATES
ELSE
	PRINTFORMW You part from your dates to have some private time with %CALLNAME:ARG%.
	;FOR LOCAL, 0, NumberOfDates
		;SIF (Datees:LOCAL) != ARG 
			;CALL KOJO_MESSAGE_SEND("EVENT",1,(Datees:LOCAL),1011,ARG)
	;NEXT
	CALL CLEAN_GROUP_DATES
	SIF !HouseInvitationStatus ; add house owner back to the date if it's at the start/middle of a date
		CALL ADD_TO_GROUP_DATE(ARG)
ENDIF
;set focus on room owner
IF !HouseInvitationStatus
	FLAG:デート相手 = ARG
	FOR LOCAL, 0, NumberOfDates
		IF Datees:LOCAL == ARG
			TFLAG:デート前好感度 = DatesPrevLike:LOCAL
			BREAK
		ENDIF
	NEXT
ENDIF
TARGET = ARG


@MOREDATES_SOUVENIR
#DIM CANBUY
#DIM TEMP
; Check if everyone already has something
CANBUY = 0
FOR LOCAL, 0, NumberOfDates
	IF !TCVAR:(Datees:LOCAL):今日の贈り物
		CANBUY = 1
	ENDIF
NEXT
IF CANBUY
	PRINTFORML 
	PRINTFORML Buy something for your other dates?
	PRINTFORML 
	FOR LOCAL, 0, NumberOfDates
		IF Datees:LOCAL != TARGET && !TCVAR:(Datees:LOCAL):今日の贈り物 ; can only buy to people who haven't gotten something today
			PRINTFORML [{(Datees:LOCAL), 3}] - Buy something for %CALLNAME:(Datees:LOCAL)% as well.
		ENDIF
	NEXT
	PRINTFORML 
	PRINTFORML [999] - No.
	$INPUT_LOOP
	INPUT

	IF RESULT != 999 && (!IS_IN_GROUP_DATE(RESULT) || TCVAR:RESULT:今日の贈り物)
		GOTO INPUT_LOOP
	ENDIF

	IF RESULT != 999 && IS_IN_GROUP_DATE(RESULT) && !TCVAR:RESULT:今日の贈り物
		TEMP = RESULT ; we're about to call another function which will replace our RESULT value
		; simulate end of turn since we're switching targets, do gems/favor/trust and reaction
		CALL SET_KOJO_COLOR(TARGET)
		TRYCALLFORM M_KOJO_MESSAGE_COM_K{TARGET}_626
		RESETCOLOR
		CALL FAVOR_CALC(TARGET)
		CALL TRUST_CALC(TARGET)
		CALL SOURCE_歓楽(TARGET)
		CALL SOURCE_受動(TARGET)
		CALL SOURCE_征服(TARGET)
		; reset to avoid infinite exponential gains 
		TFLAG:97 = 0 
		TFLAG:98 = 0
		SOURCE:歓楽 = 0
		SOURCE:受動 = 0
		SOURCE:征服 = 0
		TARGET = TEMP
		; 30/10 minutes per buy seems a little harsh so let's reduce it by 80% as bulk purchases
		TIME -= !FLAG:TIME_COMPRESSION ? 24 # 8
		; there's gotta be a better way to call a COM right? DOTRAIN doesn't work either since we're technically in the middle of a command still
		CALL COM626
		CALL MESSAGE_COM626
	ENDIF
ENDIF

@MOREDATES_HUD_INFO
#DIM Date
CALL COLORMESSAGE(@"　[On a group date with {NumberOfDates} people] ", 0xFF3399)
FOR LOCAL, 0, NumberOfDates
	Date = Datees:LOCAL
	IF FLAG:守矢くじデート相手
		CALL COLORMESSAGE(@"　<Moriya Date With %CALLNAME:(FLAG:守矢くじデート相手)%>",C_YELLOW)
	; only show visiting for focused date
	ELSEIF CFLAG:(Date):現在位置 == OMANEKIBEYA() && Date == FLAG:デート相手
		CALL COLORMESSAGE(@"　<\@ TFLAG:泡風呂 ? Getting Service In # Visiting \@ %CALLNAME:Date%'s Room\@ TFLAG:泡風呂 - TIME:1 > 0 ? %" "%(%PRINT_PLUR("Minute", TFLAG:泡風呂 - TIME:1)% left)#\@>", C_PINK)
	ELSE
		; show focused date in a different color 
		IF Date == FLAG:デート相手 
			CALL COLORMESSAGE(@"　<%CALLNAME:Date%", C_PINK)
			SIF nDateScoreDisplay || nSpoilerMode
				CALL COLORMESSAGE(@" - Score: {DateEvent_Checksum(Date)}", C_PINK)
			CALL COLORMESSAGE(@"> ", C_PINK)
		ELSE
			CALL COLORMESSAGE(@"　<%CALLNAME:Date%", 0xFF3399)
			SIF nDateScoreDisplay || nSpoilerMode
				CALL COLORMESSAGE(@" - Score: {DateEvent_Checksum(Date)}", 0xFF3399)
			CALL COLORMESSAGE(@"> ", 0xFF3399)
		ENDIF
	ENDIF
	;don't display tired messages when in a private room
	SIF INRANGE(TIME, CFLAG:(Date):就寝時間 - 60, CFLAG:(Date):就寝時間) && CFLAG:(Date):現在位置 != OMANEKIBEYA()
		CALL COLORMESSAGE(@"　<%CALLNAME:(Date)% Looks Tired> ", C_D_ORANGE)
NEXT

@MOREDATES_LOVEHOTEL_ASK(ARG)
#DIM Date
FOR LOCAL, 0, NumberOfDates
	Date = Datees:LOCAL
	SIF !GETBIT(CFLAG:(Date):既成事実 , 1) && !TALENT:(Date):合意誤認 ;hypnosis patch
		CALL 抱き寄せ(Date)
	IF TCVAR:(Date):抱き寄せ初回 >= 6 && !TCVAR:(Date):泥酔 && !CFLAG:(Date):負け犬
		PRINTFORMW %CALLNAME:(Date)% rejects the proposal.
		SIF TCVAR:(Date):抱き寄せ初回 == 6
			PRINTFORMW But %HE_SHE(Date)% doesn't look all that annoyed.
		; since we called 抱き寄せ before it might have initiated the sex flags so we remove them
		CFLAG:MASTER:うふふ = 0
		FOR LOCAL:2, 0, NumberOfDates
			CFLAG:(LOCAL:2):うふふ = 0
		NEXT
		RETURN 0
	ENDIF
NEXT
PRINTFORML It costs %金額表示((1500 + (NumberOfDates * 1500)))%. Is this okay?
CALL ASK_YN
IF RESULT == 0
	MONEY -= 1500 + (NumberOfDates * 1500)
	CFLAG:MASTER:うふふ = 0
	FOR LOCAL, 0, NumberOfDates
		Date = Datees:LOCAL
		CFLAG:(Date):うふふ = 0
		IF TCVAR:(Date):泥酔
			;custom code
			PRINTFORMW You bring the dead-drunk %CALLNAME:(Date)% to %NAME_FROM_PLACE_THE(ARG)%.
		ELSEIF CFLAG:(Date):負け犬
			;custom code
			PRINTFORMW You bring the resisting %CALLNAME:(Date)% to %NAME_FROM_PLACE_THE(ARG)%.
		ELSE
			;custom code
			PRINTFORMW You take %CALLNAME:(Date)% to %NAME_FROM_PLACE_THE(ARG)%.
		ENDIF
	NEXT
		RETURN 1
ELSE
	RETURN 0
ENDIF

@MOREDATES_COM610
#DIM 回復前体力１
#DIM 回復前気力１
#DIM 回復後体力１
#DIM 回復後気力１
#DIM 回復前体力２
#DIM 回復前気力２
#DIM 回復後体力２
#DIM 回復後気力２
#DIM TEMP
TEMP = TARGET
FOR LOCAL:10, 0, NumberOfDates
	TARGET = Datees:(LOCAL:10)
	回復前体力２ = BASE:TARGET:体力
	回復前気力２ = BASE:TARGET:気力
	LOCAL = RAND:100
	LOCAL:1 = 90 + GET_SUCCESS_RATE() / 5
	IF LOCAL:1 > 99
		LOCAL:1 = 99
	ELSEIF LOCAL <= 9
		EXP:TARGET:デート経験 ++
	ELSEIF LOCAL >= LOCAL:1
		;10～1％で失敗
		TFLAG:193 = -1
	ELSE
		;残りは成功
		TFLAG:193 = 0
	ENDIF
	TFLAG:98 += 2

	;固定で獲得するソース
	SOURCE:歓楽 = 800
	SOURCE:情愛 = 300
	
	;ABL:欲望をみる
	IF ABL:欲望 <= 1
		SOURCE:歓楽 += (ABL:欲望 * 60)
	ELSEIF ABL:欲望 <= 3
		SOURCE:歓楽 += 300 + (ABL:欲望 * 60)
	ELSEIF ABL:欲望 <= 5
		SOURCE:歓楽 += 600 + (ABL:欲望 * 60)
	ELSEIF ABL:欲望 <= 8
		SOURCE:歓楽 += 900 + (ABL:欲望 * 75)
	ELSEIF ABL:欲望 <= 11
		SOURCE:歓楽 += 1500 + (ABL:欲望 * 75)
	ELSE
		SOURCE:歓楽 += 2500 + (ABL:欲望 * 30)
	ENDIF
	
	;ABL:奉仕精神をみる
	IF ABL:奉仕精神 <= 1
		SOURCE:情愛 += (ABL:奉仕精神 * 30)
	ELSEIF ABL:奉仕精神 <= 3
		SOURCE:情愛 += 200 + (ABL:奉仕精神 * 30)
	ELSEIF ABL:奉仕精神 <= 5
		SOURCE:情愛 += 500 + (ABL:奉仕精神 * 30)
	ELSEIF ABL:奉仕精神 <= 8
		SOURCE:情愛 += 700 + (ABL:奉仕精神 * 45)
	ELSEIF ABL:奉仕精神 <= 11
		SOURCE:情愛 += 1200 + (ABL:奉仕精神 * 45)
	ELSE
		SOURCE:情愛 += 1800 + (ABL:奉仕精神 * 20)
	ENDIF
	
	;好感度をみる
	IF CFLAG:2 <= 500
		SOURCE:歓楽 += CFLAG:2
	ELSEIF CFLAG:2 <= 5000
		SOURCE:歓楽 += 750 + (CFLAG:2 - 500) / 3
	ELSE
		SOURCE:歓楽 += 2000 + (CFLAG:2 - 5000) / 4
	ENDIF
	SIF SOURCE:歓楽 < 0
		SOURCE:歓楽 = 0
	
	
	SOURCE:受動 = 200 + 100 * ABL:従順
	SOURCE:征服 = 200 + 100 * ABL:サドっ気
	
	IF TFLAG:193 == -1
		TIMES SOURCE:歓楽 , 0.10
		TIMES SOURCE:情愛 , 0.10
		TIMES SOURCE:征服 , 0.50
		TIMES SOURCE:受動 , 0.50
	ELSEIF TFLAG:193 == 0
		TIMES SOURCE:歓楽 , 1.00
		TIMES SOURCE:情愛 , 1.00
		TIMES SOURCE:征服 , 1.00
		TIMES SOURCE:受動 , 1.00
	ELSE
		TIMES SOURCE:歓楽 , 2.00
		TIMES SOURCE:情愛 , 1.50
		TIMES SOURCE:征服 , 2.00
		TIMES SOURCE:受動 , 2.00
	ENDIF
	SIF TALENT:健啖
		TIMES SOURCE:歓楽 , 1.50

	SIF TFLAG:193 >= 0
		addDateActionsDone:TARGET:date_EatOut ++ ;addition custom code

	EXP:TARGET:デート経験 ++
	CALL 満腹度上昇(TARGET,"主食")
	回復後体力２ = BASE:TARGET:体力
	回復後気力２ = BASE:TARGET:気力

	PRINTFORML %CALLNAME:TARGET%
	CALL FAVOR_CALC(TARGET)
	CALL TRUST_CALC(TARGET)
	CALL SOURCE_歓楽(TARGET)
	CALL SOURCE_征服(TARGET)
	CALL SOURCE_情愛(TARGET)
	CALL SOURCE_受動(TARGET)
	TFLAG:97 = 0 
	TFLAG:98 = 0
	SOURCE:歓楽 = 0
	SOURCE:征服 = 0
	SOURCE:情愛 = 0
	SOURCE:受動 = 0

	CALL RECOVER_PERMIL(TARGET, 150, "体力", 0, "Recover via meal")
	CALL RECOVER_PERMIL(TARGET, 300, "気力", 0, "Recover via meal")
	CALL RECOVER_PERMIL(TARGET, 100, "精力", 0, "Recover via meal")
NEXT

;Do MASTER stuff only once

回復前体力１ = BASE:MASTER:体力
回復前気力１ = BASE:MASTER:気力
LOCAL = RAND:100
LOCAL:1 = 90 + GET_SUCCESS_RATE() / 5
SIF LOCAL:1 > 99
	LOCAL:1 = 99
IF LOCAL <= 9
	;10％で大成功
	EXP:MASTER:会話経験 ++
	EXP:MASTER:デート経験 ++
	TFLAG:193 = 1
ELSEIF LOCAL >= LOCAL:1
	;10～1％で失敗
	TFLAG:193 = -1
ELSE
	;残りは成功
	TFLAG:193 = 0
ENDIF

DOWNBASE:気力 += 10

CALL RECOVER_PERMIL(MASTER, 150, "体力", 0, "Recover via meal")
CALL RECOVER_PERMIL(MASTER, 300, "気力", 0, "Recover via meal")
CALL RECOVER_PERMIL(MASTER, 100, "精力", 0, "Recover via meal")

TIME += 60
EXP:MASTER:デート経験 ++
CALL 満腹度上昇(MASTER,"主食")
MONEY -= 1500 + (NumberOfDates * 1500)
回復後体力１ = BASE:MASTER:体力
回復後気力１ = BASE:MASTER:気力
TARGET = TEMP

@MOREDATES_COM617
#DIM 回復前体力１
#DIM 回復前気力１
#DIM 回復後体力１
#DIM 回復後気力１
#DIM 回復前体力２
#DIM 回復前気力２
#DIM 回復後体力２
#DIM 回復後気力２
#DIM TEMP
TEMP = TARGET
FOR LOCAL:10, 0, NumberOfDates
	TARGET =  Datees:(LOCAL:10)
	回復前体力２ = BASE:TARGET:体力
	回復前気力２ = BASE:TARGET:気力
	LOCAL = RAND:100
	LOCAL:1 = 90 + GET_SUCCESS_RATE() / 5
	SIF LOCAL:1 > 99
		LOCAL:1 = 99
	IF LOCAL <= 9
		;10％で大成功
		EXP:TARGET:デート経験 ++
		TFLAG:193 = 1
	ELSEIF LOCAL >= LOCAL:1
		;10～1％で失敗
		TFLAG:193 = -1
	ELSE
		;残りは成功
		TFLAG:193 = 0
	ENDIF
		TFLAG:98 += 1	

	;固定で獲得するソース
	SOURCE:歓楽 = 800
	SOURCE:情愛 = 300

	;ABL:欲望をみる
	IF ABL:欲望 <= 1
		SOURCE:歓楽 += (ABL:欲望 * 60)
	ELSEIF ABL:欲望 <= 3
		SOURCE:歓楽 += 300 + (ABL:欲望 * 60)
	ELSEIF ABL:欲望 <= 5
		SOURCE:歓楽 += 600 + (ABL:欲望 * 60)
	ELSEIF ABL:欲望 <= 8
		SOURCE:歓楽 += 900 + (ABL:欲望 * 75)
	ELSEIF ABL:欲望 <= 11
		SOURCE:歓楽 += 1500 + (ABL:欲望 * 75)
	ELSE
		SOURCE:歓楽 += 2500 + (ABL:欲望 * 30)
	ENDIF

	;ABL:奉仕精神をみる
	IF ABL:奉仕精神 <= 1
		SOURCE:情愛 += (ABL:奉仕精神 * 30)
	ELSEIF ABL:奉仕精神 <= 3
		SOURCE:情愛 += 200 + (ABL:奉仕精神 * 30)
	ELSEIF ABL:奉仕精神 <= 5
		SOURCE:情愛 += 500 + (ABL:奉仕精神 * 30)
	ELSEIF ABL:奉仕精神 <= 8
		SOURCE:情愛 += 700 + (ABL:奉仕精神 * 45)
	ELSEIF ABL:奉仕精神 <= 11
		SOURCE:情愛 += 1200 + (ABL:奉仕精神 * 45)
	ELSE
		SOURCE:情愛 += 1800 + (ABL:奉仕精神 * 20)
	ENDIF

	;好感度をみる
	IF CFLAG:2 <= 500
		SOURCE:歓楽 += CFLAG:2
	ELSEIF CFLAG:2 <= 5000
		SOURCE:歓楽 += 750 + (CFLAG:2 - 500) / 3
	ELSE
		SOURCE:歓楽 += 2000 + (CFLAG:2 - 5000) / 4
	ENDIF
	SIF SOURCE:歓楽 < 0
		SOURCE:歓楽 = 0


	SOURCE:受動 = 200 + 100 * ABL:従順
	SOURCE:征服 = 200 + 100 * ABL:サドっ気

	IF TFLAG:193 == -1
		TIMES SOURCE:歓楽 , 0.10
		TIMES SOURCE:情愛 , 0.10
		TIMES SOURCE:征服 , 0.50
		TIMES SOURCE:受動 , 0.50
	ELSEIF TFLAG:193 == 0
		TIMES SOURCE:歓楽 , 1.00
		TIMES SOURCE:情愛 , 1.00
		TIMES SOURCE:征服 , 1.00
		TIMES SOURCE:受動 , 1.00
		TFLAG:ムードボーナス = 30
	ELSE
		TIMES SOURCE:歓楽 , 2.00
		TIMES SOURCE:情愛 , 1.50
		TIMES SOURCE:征服 , 2.00
		TIMES SOURCE:受動 , 2.00
		SIF TALENT:15 < 1
			TALENT:15 = 1
		TFLAG:ムードボーナス = 50
	ENDIF

	EXP:TARGET:デート経験 ++
	CALL 満腹度上昇(TARGET,"デザート")
	回復後体力２ = BASE:TARGET:体力
	回復後気力２ = BASE:TARGET:気力
			
	PRINTFORML %CALLNAME:TARGET%
	CALL FAVOR_CALC(TARGET)
	CALL TRUST_CALC(TARGET)
	CALL SOURCE_歓楽(TARGET)
	CALL SOURCE_征服(TARGET)
	CALL SOURCE_情愛(TARGET)
	CALL SOURCE_受動(TARGET)
	TFLAG:97 = 0 
	TFLAG:98 = 0
	SOURCE:歓楽 = 0
	SOURCE:征服 = 0
	SOURCE:情愛 = 0
	SOURCE:受動 = 0
	IF TFLAG:193 == 1
		addDateActionsDone:TARGET:date_EatOut ++ ;addition custom code
	ENDIF
	
	CALL RECOVER_PERMIL(TARGET, 100, "体力", 0, "Recover via meal")
	CALL RECOVER_PERMIL(TARGET, 400, "気力", 0, "Recover via meal")
	CALL RECOVER_PERMIL(TARGET, 50, "精力", 0, "Recover via meal")
NEXT

;Do MASTER stuff only once

回復前体力１ = BASE:MASTER:体力
回復前気力１ = BASE:MASTER:気力

LOCAL = RAND:100
LOCAL:1 = 90 + GET_SUCCESS_RATE() / 5
SIF LOCAL:1 > 99
	LOCAL:1 = 99
IF LOCAL <= 9
	;10％で大成功
	EXP:MASTER:会話経験 ++
	EXP:MASTER:デート経験 ++
	TFLAG:193 = 1
ELSEIF LOCAL >= LOCAL:1
	;10～1％で失敗
	TFLAG:193 = -1
ELSE
	;残りは成功
	TFLAG:193 = 0
ENDIF

DOWNBASE:気力 += 10
TIME += 60
EXP:MASTER:デート経験 ++

CALL RECOVER_PERMIL(MASTER, 100, "体力", 0, "Recover via meal")
CALL RECOVER_PERMIL(MASTER, 400, "気力", 0, "Recover via meal")
CALL RECOVER_PERMIL(MASTER, 50, "精力", 0, "Recover via meal")

CALL 満腹度上昇(MASTER,"デザート")
MONEY -= 1500 + (NumberOfDates * 1500)
回復後体力１ = BASE:MASTER:体力
回復後気力１ = BASE:MASTER:気力
TARGET = TEMP

@MOREDATES_COM615
#DIM 回復前体力１
#DIM 回復前気力１
#DIM 回復後体力１
#DIM 回復後気力１
#DIM 回復前体力２
#DIM 回復前気力２
#DIM 回復後体力２
#DIM 回復後気力２
#DIM TEMP
TEMP = TARGET
FOR LOCAL:10, 0, NumberOfDates
	TARGET = Datees:(LOCAL:10)
	回復前体力２ = BASE:TARGET:体力
	回復前気力２ = BASE:TARGET:気力
	LOCAL = RAND:3
	LOCAL:1 = RAND:10
	IF LOCAL > ABL:料理技能
		;料理が下手だと失敗
		TFLAG:193 = -1
		TFLAG:98 = -1
	ELSEIF LOCAL:1 <= ABL:料理技能 - 2
		;料理が上手いと大成功
		EXP:TARGET:デート経験 ++
		TFLAG:193 = 1
		TFLAG:98 = 5
	ELSE
		;残りは成功
		TFLAG:193 = 0
		TFLAG:98 = 3
	ENDIF

	SIF TFLAG:193 >= 0 ;addition custom code
		addDateActionsDone:TARGET:date_Lunch ++

	;固定で獲得するソース
	SOURCE:歓楽 = 300
	SOURCE:情愛 = 300
	SOURCE:露出 = 100
	SOURCE:反感 = 50

	IF (ABL:親密 + ABL:奉仕精神) < 10
		LOCAL:2 = 1
		SOURCE:歓楽 += 1000 + (ABL:親密 * 100)
		SOURCE:達成 += 500 + (ABL:親密 * 50)
		SOURCE:情愛 += (ABL:親密 * 30)
	ELSEIF (ABL:親密 + ABL:奉仕精神) < 15
		LOCAL:2 = 2
		SOURCE:歓楽 += 2000 + (ABL:親密 * 150)
		SOURCE:達成 += 1500 + (ABL:親密 * 80)
		SOURCE:情愛 += 400 + (ABL:親密 * 30)
	ELSEIF (ABL:親密 + ABL:奉仕精神) < 25
		LOCAL:2 = 3
		SOURCE:歓楽 += 3000 + (ABL:親密 * 200)
		SOURCE:達成 += 2000 + (ABL:親密 * 100)
		SOURCE:情愛 += 600 + (ABL:親密 * 40)
	ELSE
		LOCAL:2 = 5
		SOURCE:歓楽 += 5000 + (ABL:親密 * 250)
		SOURCE:達成 += 3000 + (ABL:親密 * 150)
		SOURCE:情愛 += 1800 + (ABL:親密 * 30)
	ENDIF

	EXP:料理経験 += LOCAL:2
	SIF TALENT:恋慕
		EXP:愛情経験 += LOCAL:2

	IF TFLAG:193 == -1
		TIMES SOURCE:歓楽 , 0.50
		TIMES SOURCE:達成 , 0.50
		TIMES SOURCE:情愛 , 0.50
	ELSEIF TFLAG:193 == 0
		TIMES SOURCE:歓楽 , 1.00
		TIMES SOURCE:達成 , 1.00
		TIMES SOURCE:情愛 , 1.00
	ELSE
		TIMES SOURCE:歓楽 , 2.00
		TIMES SOURCE:達成 , 2.00
		TIMES SOURCE:情愛 , 2.00
	ENDIF
	EXP:TARGET:デート経験 ++
	CALL 満腹度上昇(TARGET,"主食")

	SETBIT TFLAG:一日一回,1
	回復後体力２ = BASE:TARGET:体力
	回復後気力２ = BASE:TARGET:気力			
	
	PRINTFORML %CALLNAME:TARGET%
	CALL FAVOR_CALC(TARGET)
	CALL TRUST_CALC(TARGET)
	CALL SOURCE_歓楽(TARGET)
	CALL SOURCE_達成(TARGET)
	CALL SOURCE_情愛(TARGET)
	TFLAG:97 = 0 
	TFLAG:98 = 0
	SOURCE:歓楽 = 0
	SOURCE:達成 = 0
	SOURCE:情愛 = 0
	SIF TFLAG:193 == 1
		addDateActionsDone:TARGET:date_Lunch ++ 

	IF ABL:料理技能 < 2
		CALL RECOVER_PERMIL(TARGET, 200, "体力", 0, "Recover via meal")
		CALL RECOVER_PERMIL(TARGET, 300, "気力", 0, "Recover via meal")
		CALL RECOVER_PERMIL(TARGET, 100, "精力", 0, "Recover via meal")
	ELSEIF ABL:料理技能 < 4
		CALL RECOVER_PERMIL(TARGET, 250, "体力", 0, "Recover via meal")
		CALL RECOVER_PERMIL(TARGET, 350, "気力", 0, "Recover via meal")
		CALL RECOVER_PERMIL(TARGET, 150, "精力", 0, "Recover via meal")
	ELSE
		CALL RECOVER_PERMIL(TARGET, 300, "体力", 0, "Recover via meal")
		CALL RECOVER_PERMIL(TARGET, 400, "気力", 0, "Recover via meal")
		CALL RECOVER_PERMIL(TARGET, 200, "精力", 0, "Recover via meal")
	ENDIF
NEXT

;Do MASTER stuff only once
回復前体力１ = BASE:MASTER:体力
回復前気力１ = BASE:MASTER:気力
LOCAL = RAND:3
LOCAL:1 = RAND:10
IF LOCAL > ABL:料理技能
	;料理が下手だと失敗
	TFLAG:193 = -1
ELSEIF LOCAL:1 <= ABL:料理技能 - 2
	;料理が上手いと大成功
	EXP:MASTER:会話経験 ++
	EXP:MASTER:デート経験 ++
	TFLAG:193 = 1
ELSE
	;残りは成功
	TFLAG:193 = 0
ENDIF

DOWNBASE:気力 += 50

; use original target for player's stats
TARGET = TEMP
IF ABL:料理技能 < 2
	CALL RECOVER_PERMIL(MASTER, 200, "体力", 0, "Recover via meal")
	CALL RECOVER_PERMIL(MASTER, 300, "気力", 0, "Recover via meal")
	CALL RECOVER_PERMIL(MASTER, 100, "精力", 0, "Recover via meal")
ELSEIF ABL:料理技能 < 4
	CALL RECOVER_PERMIL(MASTER, 250, "体力", 0, "Recover via meal")
	CALL RECOVER_PERMIL(MASTER, 350, "気力", 0, "Recover via meal")
	CALL RECOVER_PERMIL(MASTER, 150, "精力", 0, "Recover via meal")
ELSE
	CALL RECOVER_PERMIL(MASTER, 300, "体力", 0, "Recover via meal")
	CALL RECOVER_PERMIL(MASTER, 400, "気力", 0, "Recover via meal")
	CALL RECOVER_PERMIL(MASTER, 200, "精力", 0, "Recover via meal")
ENDIF
TIME += 60
EXP:MASTER:デート経験 ++
CALL 満腹度上昇(MASTER,"主食")

SETBIT TFLAG:一日一回,1
回復後体力１ = BASE:MASTER:体力
回復後気力１ = BASE:MASTER:気力
[SKIPEND]