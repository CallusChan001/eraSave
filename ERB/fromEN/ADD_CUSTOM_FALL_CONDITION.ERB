@ADD_CHECK_FALL_STATE(STATE, CHARA, ALLOWED_STATES, BIT_INDEX, DEFAULT_RESULT)
#DIMS STATE
#DIM CHARA
#DIM REF ALLOWED_STATES
#DIM BIT_INDEX
#DIM DEFAULT_RESULT
#DIM DYNAMIC VALID = 1
VARSET RESULT

CALL KOJO_CHECK(CHARA, "FALL_STATE_OVERRIDE", STATE)
IF RESULT == -1
    CLEARBIT ALLOWED_STATES, BIT_INDEX
    RETURN
ENDIF

SIF !RESULT
    VALID = DEFAULT_RESULT

IF VALID
    VARSET RESULT
    VARSET RESULTS
    CALL KOJO_CHECK(CHARA, "FALL_STATE", STATE)
    FOR LOCAL, 1, RESULT + 1
        VALID &= RESULT:LOCAL
    NEXT
ENDIF

SIF VALID
    SETBIT ALLOWED_STATES, BIT_INDEX

;Returns bitwise value containing which states are permitted
;0=Yearning,1=Love,2=Deep Love,3=Fuck Buddy,4=Lust
@ADD_CHECK_FALL_STATES(CHARA)
#DIM CHARA
#DIM DYNAMIC FALL_STATES_ALLOWED

SIF !(TALENT:CHARA:愛欲 || TALENT:CHARA:恋慕 || TALENT:CHARA:思慕); && !(TALENT:CHARA:セフレ && !FLAG:陥落路線変化)
    CALL ADD_CHECK_FALL_STATE("YEARNING", CHARA, FALL_STATES_ALLOWED, 0, 思慕取得条件(CHARA))
SIF !(TALENT:CHARA:恋慕 || (TALENT:CHARA:愛欲 && !FLAG:陥落路線変化))
    CALL ADD_CHECK_FALL_STATE("LOVE", CHARA, FALL_STATES_ALLOWED, 1, 恋慕取得条件(CHARA))
SIF TALENT:CHARA:恋慕 == 1
    CALL ADD_CHECK_FALL_STATE("DEEP LOVE", CHARA, FALL_STATES_ALLOWED, 2, TRUE_LOVE_CONDITIONS(CHARA))
SIF !(TALENT:CHARA:恋人 || TALENT:CHARA:恋慕 || TALENT:CHARA:セフレ == 2)    
    CALL ADD_CHECK_FALL_STATE("FUCK BUDDY", CHARA, FALL_STATES_ALLOWED, 3, 炮友取得条件(CHARA))
SIF !(TALENT:CHARA:恋慕 || TALENT:CHARA:愛欲 || TALENT:CHARA:セフレ == 0)
    CALL ADD_CHECK_FALL_STATE("LUST", CHARA, FALL_STATES_ALLOWED, 4, 愛欲取得条件(CHARA))

RETURN FALL_STATES_ALLOWED

@ADD_PRINT_FALL_STATES(STATE, CHARA)
#DIMS STATE
#DIM CHARA
#LOCALSIZE 1
#LOCALSSIZE 10
VARSET RESULT
VARSET RESULTS

CALL KOJO_CHECK(CHARA, "FALL_STATE", STATE)
FOR LOCAL, 1, RESULT + 1
    VARSET LOCALS
	SPLIT RESULTS:LOCAL, "/", LOCALS
   ; CALL COND_COLORMESSAGE(RESULT:LOCAL, RESULTS:LOCAL)
	HTML_PRINT @"<p align='left'><nonbutton title='%LOCALS:1%'><font color = '\@RESULT:LOCAL? yellow # #C0C0C0 \@'>　%LOCALS%</font></nonbutton>"
NEXT

@陥落条件表示_QOL(ARG)
#DIM 自覚絶頂
#DIM DYNAMIC AS_COLUMNS
#DIM EXPECTED_WIDTH
#DIM START_LINECOUNT
#DIMS DYNAMIC FALL_STATE_COLUMN, 60
#DIMS DYNAMIC SKILL_ACQUISITION_COLUMN, 60

自覚絶頂 = EXP:ARG:絶頂経験 - EXP:ARG:無自覚絶頂経験
PRINTL

IF MAXWIDTH() > CHAR_PER_LINE && !TFLAG_HideFallingStates && !TFLAG_HideSkillAcquisition
	AS_COLUMNS = 1
	EXPECTED_WIDTH = MAXWIDTH() / 2
	START_LINECOUNT = LINECOUNT
    CALL 陥落条件表示(ARG, EXPECTED_WIDTH-2)
    CALL HTML_READ_AS_COLUMN(LINECOUNT - START_LINECOUNT)
	ARRAYCOPY "RESULTS", "FALL_STATE_COLUMN"
    ;CALL QoL_UnlockExtraTraitsInfo(ARG, EXPECTED_WIDTH-2)
    CALL SHOW_FALL_STATE_REV_REQUIREMENTS(ARG,, EXPECTED_WIDTH-2)
    CALL HTML_READ_AS_COLUMN(LINECOUNT - START_LINECOUNT)
    ARRAYCOPY "RESULTS", "SKILL_ACQUISITION_COLUMN"

    FOR LOCAL, 0, VARSIZE("FALL_STATE_COLUMN")
        SIF FALL_STATE_COLUMN:LOCAL == "" && SKILL_ACQUISITION_COLUMN:LOCAL == ""
            BREAK
        LOCAL:1 = STRLENS(HTML_TOPLAINTEXT(FALL_STATE_COLUMN:LOCAL))

        HTML_PRINT @"<p align='left'>%FALL_STATE_COLUMN:LOCAL%%" "*MAX((EXPECTED_WIDTH-LOCAL:1),0)%%SKILL_ACQUISITION_COLUMN:LOCAL%</p>"
    NEXT
ELSE
	EXPECTED_WIDTH = MAXWIDTH()
	CALL 陥落条件表示(ARG, EXPECTED_WIDTH)
    PRINTL
	;CALL QoL_UnlockExtraTraitsInfo(ARG, EXPECTED_WIDTH)
    CALL SHOW_FALL_STATE_REV_REQUIREMENTS(ARG,, EXPECTED_WIDTH)
ENDIF

@HTML_READ_AS_COLUMN(LINE_AMOUNT)
#DIM LINE_AMOUNT
#DIM DYNAMIC LINE_INDEX

VARSET RESULTS
FOR LOCAL, 0, LINE_AMOUNT
    LOCALS '= HTML_GETPRINTEDSTR(LINE_AMOUNT - LOCAL - 1)
    LOCALS '= REPLACE(LOCALS, "<p align='left'>", "")
    LOCALS '= REPLACE(LOCALS, "</p>", "")
    LOCALS '= REPLACE(LOCALS, "<nobr>", "")
    LOCALS '= REPLACE(LOCALS, "</nobr>", "")
	RESULTS:(LINE_INDEX++) '= LOCALS
NEXT
CLEARLINE LINE_AMOUNT
RETURN LOCAL
