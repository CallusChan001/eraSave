[SKIPSTART]
@DEBUG_TRANSLATION_MENU
LOCAL = LINECOUNT
$MENUSTART
CLEARLINE LINECOUNT - LOCAL
PRINTFORMDL [ 0] - Generate Random Gift List
;PRINTFORMDL [10] - Untranslated Gift Nouns and Adjectives      [11] - List All Gift Nouns and Adjectives
PRINTFORMDL [20] - Untranslated Taste Translations             [21] - Configure Taste Translation Test
;PRINTFORMDL [30] - Untranslated Clothes Translations           [31] - List All Clothes Translations
;PRINTFORMDL [40] - Untranslated Dishes
;PRINTFORMDL [50] - Panty Info
PRINTFORMDL [-1] - Return

INPUT
SELECTCASE RESULT
	CASE 0
		CALL GIFT_TEST_LIST
	;CASE 10
	;    CALL GIFT_NOUN_ADJECTIVE_TEST(0)
	;CASE 11
	;    CALL GIFT_NOUN_ADJECTIVE_TEST(1)
	CASE 20
		CALL TASTE_TRANSLATION_TEST(0, 0)
	CASE 21
		LOCAL:1 = 0
		LOCAL:2 = 0
		LOCAL:10 = LINECOUNT
		$TASTEMENUSTART      
		CLEARLINE LINECOUNT - LOCAL:10
		PRINTFORMD Show All Tastes:                         
		PRINTBUTTON @"[1] \@ LOCAL:1 ? All Tastes # Only Untranslated \@", 1
		PRINTL
		PRINTFORMD Include Tastes From Assistants (Slow):   
		PRINTBUTTON @"[2] \@ LOCAL:2 ? Enabled # Disabled \@", 2
		PRINTL
		PRINTFORMDL [3] Execute
		PRINTFORMDL [4] Cancel
		INPUT
		SELECTCASE RESULT
			CASE 1
				LOCAL:1 = !LOCAL:1
			CASE 2
				LOCAL:2 = !LOCAL:2
			CASE 3
				CALL TASTE_TRANSLATION_TEST(LOCAL:1, LOCAL:2)
				FORCEWAIT
				GOTO MENUSTART
			CASE 4
				GOTO MENUSTART
		ENDSELECT
		GOTO TASTEMENUSTART
	;CASE 30
	;	CALL CLOTHES_TRANSLATION_TEST(0)
	;CASE 31
	;	CALL CLOTHES_TRANSLATION_TEST(1)
	;CASE 40
	;	CALL DISH_TRANSLATION_TEST
	;CASE 50
	;	TRYCALL PANTIES_INFO_TEST
	CASE -1
		RETURN
	CASEELSE
		GOTO MENUSTART
ENDSELECT
FORCEWAIT
GOTO MENUSTART

;create a list of gifts, test names and translation
@GIFT_TEST_LIST
FOR LOCAL, 0, 60
	CALL CREATE_GIFT(99)
	PRINTFORML [{LOCAL, 2}] - %RESULTS, 42, LEFT%\\{ RESULT:1}
	CALL REVIEW_GIFT(1, RESULT:0)
	CALL SHOW_TASTE(RESULTS, "GIFT")
	PRINTFORML
	PRINTFORML
NEXT

;if SHOW_ALL is true, list all translations, otherwise only show those without a translation
@GIFT_NOUN_ADJECTIVE_TEST(SHOW_ALL)
#DIM SHOW_ALL
PRINTFORMDL %COND_STR("Untranslated ", !SHOW_ALL)%Gift Adjectives:
LOCAL:1 = LINECOUNT
FOR LOCAL, 100, 2113
	CALL GIFTDATA_ADJECT(LOCAL)
	IF STRLENS(RESULTS)
		LOCALS '= RESULTS
		LOCALS:1 '= TASTE_TR(LOCALS)

		IF SHOW_ALL
			PRINTFORML %LOCALS% => %LOCALS:1%
		ELSEIF LOCALS == LOCALS:1
			PRINTFORML %LOCALS%
		ENDIF
	ENDIF
NEXT
SIF LINECOUNT == LOCAL:1
	PRINTFORMDL All adjectives have a translation.

PRINTL
PRINTFORMDL %COND_STR("Untranslated ", !SHOW_ALL)%Gift Nouns:
LOCAL:1 = LINECOUNT
FOR LOCAL, 11, 529
	CALL GIFTDATA_MAIN(LOCAL)
	IF STRLENS(RESULTS)
		LOCALS '= RESULTS
		LOCALS:1 '= TASTE_TR(LOCALS)

		IF SHOW_ALL
			PRINTFORML %LOCALS% => %LOCALS:1%
		ELSEIF LOCALS == LOCALS:1
			PRINTFORML %LOCALS%
		ENDIF
	ENDIF
NEXT
SIF LINECOUNT == LOCAL:1
	PRINTFORMDL All nouns have a translation.
RETURN -1

@TASTE_TRANSLATION_TEST(SHOW_ALL, CHECK_ASSISTANTS)
#DIM SHOW_ALL
#DIM CHECK_ASSISTANTS
#DIMS TRANSLATED_TASTE
#DIMS DYNAMIC CHECKED_TASTES = "/"
#DIM CHARA
#DIMS CONST CHARADATA_TYPES = "料理：喜欢的味道", "料理：讨厌的味道", "料理：助手効果", "感性：好き", "感性：嫌い"
#DIMS CONST CHARADATA_TYPES_TR = "Favorite Flavor", "料理：讨厌的味道", "料理：助手效果", "喜好", "讨厌"

PRINTFORMDL Checking if character taste info is set correctly:
LOCAL:10 = LINECOUNT
FOR CHARA, 1, CHARANUM - 1
	SIF CFLAG:CHARA:出禁 == 2
		CONTINUE
	FOR LOCAL:1, 0, 5
		LOCALS '= GET_STR(, "キャラデータ", CHARA, CHARADATA_TYPES:(LOCAL:1))
		SIF LOCALS == "" || LOCALS == "/"
			PRINTFORMDL %CALLNAME:CHARA% ({CHARA}): %CHARADATA_TYPES_TR:(LOCAL:1)% (%CHARADATA_TYPES:(LOCAL:1)%) not set.

		SIF LOCALS != "" && !STRING_ENDS_IN(LOCALS, "/")
			PRINTFORMDL %CALLNAME:CHARA% ({CHARA}): %CHARADATA_TYPES_TR:(LOCAL:1)% (%CHARADATA_TYPES:(LOCAL:1)%) does not end with "/".
	NEXT
NEXT

IF LOCAL:10 == LINECOUNT
	PRINTFORMDL Taste info is correctly configured for all characters.
ELSE
	WAIT
ENDIF

SIF CHECK_ASSISTANTS
	PRINTFORMDW (Warning: long execution time, please wait patiently. Click to start.)

PRINTFORMDL %COND_STR("Untranslated ", !SHOW_ALL)%Taste Translations:
LOCAL:10 = LINECOUNT

;check every possible dish with every possible assistant for every possible dish
;may take a little while
FOR LOCAL, 101, 400 + CHARANUM
	FOR CHARA, 0, CHECK_ASSISTANTS ? CHARANUM - 1 # 1
		VARSET LOCALS
		CALL DISHDATA(LOCAL, "", 1)
		SIF !RESULT
			CONTINUE

		FOR LOCAL:1, 0, VARSIZE("DISH_ING")
			SIF DISH_ING:(LOCAL:1) > 0
				CALL DISH_TASTE_ADD(DISH_ING:(LOCAL:1))
		NEXT

		SIF CHARA
			CALL DISH_TASTE_FROM_CHARA(CHARA)

		SPLIT DISH_TASTE, "/", LOCALS
		LOCAL:1 = RESULT

		SIF SHOW_ALL
			PRINTFORMDL {LOCAL}: %DISHNAME_TR(DISH_NAME)%: (%DISH_TASTE%)

		FOR LOCAL:2, 0, LOCAL:1
			SIF !STRLENS(LOCALS:(LOCAL:2))
				CONTINUE
			SIF INRANGE(LOCAL, 400, 400 + CHARANUM) && LOCALS:(LOCAL:2) == @"%CALLNAME:(LOCAL - 400)%的特别食谱"
				CONTINUE
			TRANSLATED_TASTE '= TASTE_TR(LOCALS:(LOCAL:2))
			IF SHOW_ALL
				PRINTFORML %LOCALS:(LOCAL:2)% => %TRANSLATED_TASTE%
			ELSEIF LOCALS:(LOCAL:2) == TRANSLATED_TASTE
				SIF STRFIND(CHECKED_TASTES, @"/%LOCALS:(LOCAL:2)%/") >= 0
					CONTINUE
				PRINTFORML %LOCALS:(LOCAL:2)%

				CHECKED_TASTES += @"%LOCALS:(LOCAL:2)%/"
			ENDIF
		NEXT

		CALL RESET_DISH
	NEXT
NEXT

SIF LINECOUNT == LOCAL:10
	PRINTFORMDL All tastes have a translation.

PRINTFORMDL Finished.

@CLOTHES_TRANSLATION_TEST(SHOW_ALL)
#DIM SHOW_ALL
#DIM 装備部位
#DIM PRINTED_HEADER
LOCAL:10 = LINECOUNT
FOR LOCAL, 0, VARSIZE("表示部位")
	SIF 表示部位:LOCAL == ""
		CONTINUE

	SIF 表示部位:LOCAL == "上半身下着１"
		CONTINUE
	SIF 表示部位:LOCAL == "下半身下着２"
		CONTINUE
	SIF 表示部位:LOCAL == "下半身上着"
		CONTINUE
	SIF 表示部位:LOCAL == "その他２"
		CONTINUE
	SIF 表示部位:LOCAL == "その他３"
		CONTINUE
	SIF 装備部位 < 0
		CONTINUE
	PRINTED_HEADER = 0
	
	FOR LOCAL:1, 0, OBJ_ID_LAST
		LOCALS '= GET_STR(, CLOTHES_PARTS_TO_CATEGORY(表示部位:LOCAL), LOCAL:1, "名前") ;CLOTHNAME(LOCAL, EQUIP:C_ID:装備部位)
		SIF LOCALS == ""
			CONTINUE
		LOCALS:2 '= CLOTHES_TR(LOCALS)
		SIF LOCALS != LOCALS:2 && !SHOW_ALL
			CONTINUE

		IF !PRINTED_HEADER
			PRINTFORMDL %表示ラベル:LOCAL%:
			PRINTED_HEADER = 1
		ENDIF

		PRINTFORMDL (%LOCALS%) -> %(LOCALS:2)%
	NEXT
	SIF PRINTED_HEADER
		PRINTL

NEXT

SIF LINECOUNT == LOCAL:10
	PRINTFORMDL All clothes are translated.

@DISH_TRANSLATION_TEST()
PRINTFORMDL Untranslated dishes:
LOCAL:10 = LINECOUNT
FOR LOCAL, 0, 400 + CHARANUM
	CALL DISHDATA(LOCAL, "", 1)
	SIF DISH_NAME == ""
		CONTINUE
	
	IF DISH_NAME == DISHNAME_TR(DISH_NAME) || DISH_COMMENT == DISHCOMMENT_TR(DISH_COMMENT)
		PRINTFORMD Dish {LOCAL}
		IF LOCAL > 400
			PRINTFORMDL (%CALLNAME:(LOCAL-400)%)
		ELSE
			PRINTL
		ENDIF
		IF DISH_NAME == DISHNAME_TR(DISH_NAME)
			PRINTFORMDL %DISH_NAME%
		ENDIF
		IF DISH_COMMENT == DISHCOMMENT_TR(DISH_COMMENT)
			PRINTFORMDL %DISH_COMMENT%
		ENDIF
	ENDIF
NEXT

SIF LINECOUNT == LOCAL:10
	PRINTFORMDL All dishes are translated.

@PANTIES_INFO_TEST
#DIM ROWS_ON_SCREEN
#DIM DYNAMIC CURRENT_PAGE
#DIM DYNAMIC PAGE_COUNT
#DIM DYNAMIC ROW_COUNT
#DIM DYNAMIC CHARA_INDEX
#DIM DYNAMIC PANTY_INDEX
#DIM STARTLINECOUNT
#DIM DYNAMIC PAGE_INDEXES, 100, 2 ;start/end
#DIM DYNAMIC PAGE_INDEXES_INDEX
#DIM DYNAMIC START_INDEX = -1
#DIM DYNAMIC END_INDEX = -1
#DIM CONST UI_ROWS = 12
#DIM CONST TABLE_WIDTH = 40
ROWS_ON_SCREEN = MAX(CLIENTHEIGHT() / GETCONFIG("フォントサイズ")-9, 30) - UI_ROWS
;round off to the nearest clean value (4x+3)
ROWS_ON_SCREEN = ROWS_ON_SCREEN - ((ROWS_ON_SCREEN-3)%4)

;generic panties
ROW_COUNT = 2
FOR LOCAL, 1, OBJ_ID_LAST
	SIF GROUPMATCH(LOCAL, 18, 35, 36, 37, 38, 39) ;character-dependent panties
		CONTINUE
	SIF GROUPMATCH(LOCAL, 27, 28, 29, 30) ;unused panties
		CONTINUE
	LOCALS '= GET_STR(0, "下半身下着_ずらし可能", LOCAL, "名前")
	SIF LOCALS == ""
		CONTINUE

	PANTY_INDEX = LOCAL

	SIF START_INDEX < 0
		START_INDEX = PANTY_INDEX

	ROW_COUNT++
	IF ROW_COUNT > ROWS_ON_SCREEN
		PAGE_INDEXES:PAGE_INDEXES_INDEX:0 = START_INDEX
		PAGE_INDEXES:PAGE_INDEXES_INDEX:1 = END_INDEX
		PAGE_INDEXES_INDEX++
		ROW_COUNT = 3
		PAGE_COUNT++
		VARSET END_INDEX
		START_INDEX = PANTY_INDEX
	ENDIF
	END_INDEX = PANTY_INDEX
NEXT

FOR LOCAL, 1, CHARANUM-1
	SIF CFLAG:LOCAL:出禁 ;skip disabled characters
		CONTINUE
	LOCALS '= GET_STR(LOCAL, "下半身下着_ずらし可能", 18, "名前")
	IF LOCALS != ""
		PANTY_INDEX = LOCAL * 1000 + 18
		
		SIF START_INDEX < 0
			START_INDEX = PANTY_INDEX

		ROW_COUNT += 4
		IF ROW_COUNT > ROWS_ON_SCREEN
			PAGE_INDEXES:PAGE_INDEXES_INDEX:0 = START_INDEX
			PAGE_INDEXES:PAGE_INDEXES_INDEX:1 = END_INDEX
			PAGE_INDEXES_INDEX++
			ROW_COUNT = 3
			PAGE_COUNT++
			VARSET END_INDEX
			START_INDEX = PANTY_INDEX
		ENDIF

		END_INDEX = PANTY_INDEX
	ENDIF

	;check every possible dialogue for custom panties
	IF CFLAG:LOCAL:口上実装状況
		LOCAL:3 = CFLAG:LOCAL:口上セレクタ
		FOR LOCAL:1,0,3
			CFLAG:LOCAL:口上セレクタ = LOCAL:1
			CALL KOJO_ACTIVE_INFO(LOCAL)
			SIF !RESULT
				CONTINUE
			VARSET CUSTOM_PANTY_EXISTS
			CALL KOJO_CHECK(LOCAL, "PANTIES")

			FOR LOCAL:2, 0, 5
				IF CUSTOM_PANTY_EXISTS:LOCAL:(LOCAL:2)
					SIF LOCAL:2 == 0
						ROW_COUNT++
					PANTY_INDEX = LOCAL * 1000 + LOCAL:1 * 100 + LOCAL:2 + CUSTOM_PANTY_START_INDEX
					ROW_COUNT++

					IF ROW_COUNT > ROWS_ON_SCREEN
						PAGE_INDEXES:PAGE_INDEXES_INDEX:0 = START_INDEX
						PAGE_INDEXES:PAGE_INDEXES_INDEX:1 = END_INDEX
						PAGE_INDEXES_INDEX++
						ROW_COUNT = 4
						PAGE_COUNT++
						VARSET END_INDEX
						START_INDEX = PANTY_INDEX
					ENDIF
					END_INDEX = PANTY_INDEX
				ENDIF
			NEXT
		NEXT
		CFLAG:LOCAL:口上セレクタ = LOCAL:3
	ENDIF
	VARSET CUSTOM_PANTY_EXISTS
	CALL KOJO_CHECK(LOCAL, "PANTIES")
NEXT

IF END_INDEX
	PAGE_INDEXES:PAGE_INDEXES_INDEX:0 = START_INDEX
	PAGE_INDEXES:PAGE_INDEXES_INDEX:1 = END_INDEX
	PAGE_COUNT++
ENDIF

STARTLINECOUNT = LINECOUNT
$SHOW_OVERVIEW
CLEARLINE LINECOUNT - STARTLINECOUNT

PRINTFORMDL ┌─┯%"─"*20%┯─┯─┯─┯─┯─┯─────┯─┐
PRINTFORMDL ｜　｜%"　"*20%｜　｜　｜　｜　｜　｜ ＯＰＥＮ ｜　｜
PRINTFORMDL ┝─┿%"─"*20%┿─┿─┿─┿─┿─┿─┯─┯─┿─┥
PRINTFORMDL ｜Ｉ｜%"　"*8%ＮＡＭＥ%"　"*8%｜Ｃ｜Ｓ｜Ｐ｜Ｐ｜Ｚ｜Ｈ｜Ｓ｜Ｎ｜Ｔ｜
PRINTFORMDL ｜Ｄ｜%"　"*9%　%"　"*10%｜Ｏ｜Ｈ｜Ｕ｜Ａ｜Ｉ｜Ｏ｜Ｔ｜Ｏ｜Ｉ｜
PRINTFORMDL ｜　｜%"　"*9%　%"　"*10%｜Ｖ｜Ｉ｜Ｌ｜Ｓ｜Ｐ｜Ｌ｜Ｒ｜Ｔ｜Ｅ｜
PRINTFORMDL ｜　｜%"　"*9%　%"　"*10%｜Ｅ｜Ｆ｜Ｌ｜Ｔ｜Ｐ｜Ｅ｜Ｉ｜Ｈ｜Ｄ｜
PRINTFORMDL ｜　｜%"　"*9%　%"　"*10%｜Ｒ｜Ｔ｜　｜Ｙ｜Ｅ｜　｜Ｎ｜Ｉ｜　｜
PRINTFORMDL ｜　｜%"　"*9%　%"　"*10%｜　｜　｜　｜　｜Ｒ｜　｜Ｇ｜Ｎ｜　｜
PRINTFORMDL ｜　｜%"　"*9%　%"　"*10%｜　｜　｜　｜　｜　｜　｜　｜Ｇ｜　｜
PRINTFORMDL ┝─┴%"─"*20%┴─┴─┴─┴─┴─┴─┴─┴─┴─┥

CALL PANTIES_INFO_TEST_PAGE(PAGE_INDEXES:CURRENT_PAGE:0, PAGE_INDEXES:CURRENT_PAGE:1, TABLE_WIDTH)

LOCAL = 0
REPEAT (ROWS_ON_SCREEN + UI_ROWS - 1) - (LINECOUNT - STARTLINECOUNT)
	IF COUNT == 0
		PRINTFORMDL ┝─┴%"─"*20%┴─┴─┴─┴─┴─┴─┴─┴─┴─┥
	ELSE
		PRINTFORMDL ｜%"　"*TABLE_WIDTH%｜
	ENDIF
	LOCAL = 1
REND
IF LOCAL
	PRINTFORMDL └%"─"*TABLE_WIDTH%┘
ELSE
	PRINTFORMDL └─┴%"─"*20%┴─┴─┴─┴─┴─┴─┴─┴─┴─┘
ENDIF

PRINTFORML
PRINTBUTTON @"%"Prev page",15,LEFT%", 888
PRINTFORM (Page {CURRENT_PAGE+1,2}/{PAGE_COUNT,2})%" "*6%
PRINTBUTTON @"%"Next page",15,LEFT%", 777
PRINTFORML

PRINTLC [999] Return
INPUT

SELECTCASE RESULT
	CASE 999
		RETURN 0
	CASE 777, 888
		CURRENT_PAGE = PAGE_HANDLE(RESULT, 888, CURRENT_PAGE, 0, PAGE_COUNT-1)
		GOTO SHOW_OVERVIEW
ENDSELECT
RETURN RESULT

@PANTIES_INFO_TEST_PAGE(START_PANTY, END_PANTY, TABLE_WIDTH)
#DIM START_PANTY
#DIM END_PANTY
#DIM TABLE_WIDTH
#DIM PANTY_INDEX
#DIM CHARA
#DIM DIALOGUE
#DIM ORIGINAL_DIALOGUE
#DIM DYNAMIC LAST_CHARA = -1
#DIM DYNAMIC LAST_DIALOGUE = -1
#DIM PANTY
#DIM DYNAMIC PANTY_INDEXES, 75
#DIM DYNAMIC INDEX
PANTY_INDEX = START_PANTY
;load everything for this page
IF PANTY_INDEX < 100
	FOR LOCAL, PANTY_INDEX, OBJ_ID_LAST
		SIF GROUPMATCH(LOCAL, 18, 35, 36, 37, 38, 39) ;character-dependent panties
			CONTINUE
		SIF GROUPMATCH(LOCAL, 27, 28, 29, 30) ;unused panties
			CONTINUE
		SIF LOCAL > END_PANTY
			BREAK

		LOCALS '= GET_STR(0, "下半身下着_ずらし可能", LOCAL, "名前")
		SIF LOCALS == ""
			CONTINUE

		PANTY_INDEXES:(INDEX++) = LOCAL
	NEXT
ENDIF

IF END_PANTY >= 100
	PANTY_INDEX = MAX(START_PANTY, 1000)
	;loop per character
	FOR LOCAL, PANTY_INDEX / 1000, END_PANTY / 1000 + 1
		SIF CFLAG:LOCAL:出禁 ;skip disabled characters
			CONTINUE
		LOCALS '= GET_STR(LOCAL, "下半身下着_ずらし可能", 18, "名前")
		IF LOCALS != ""
			PANTY_INDEX = LOCAL * 1000 + 18
			SIF PANTY_INDEX > END_PANTY
				BREAK
				
			SIF PANTY_INDEX >= START_PANTY
				PANTY_INDEXES:(INDEX++) = PANTY_INDEX
		ENDIF
		IF CFLAG:LOCAL:口上実装状況
			LOCAL:3 = CFLAG:LOCAL:口上セレクタ
			FOR LOCAL:1,0,3
				CFLAG:LOCAL:口上セレクタ = LOCAL:1
				CALL KOJO_ACTIVE_INFO(LOCAL)
				SIF !RESULT
					CONTINUE
				VARSET CUSTOM_PANTY_EXISTS
				CALL KOJO_CHECK(LOCAL, "PANTIES")

				FOR LOCAL:2, 0, 5
					IF CUSTOM_PANTY_EXISTS:LOCAL:(LOCAL:2)
						PANTY_INDEX = LOCAL * 1000 + LOCAL:1 * 100 + LOCAL:2 + CUSTOM_PANTY_START_INDEX
						
						SIF PANTY_INDEX < START_PANTY
							CONTINUE
						SIF PANTY_INDEX > END_PANTY
							BREAK
						PANTY_INDEXES:(INDEX++) = PANTY_INDEX
					ENDIF
				NEXT
			NEXT
			CFLAG:LOCAL:口上セレクタ = LOCAL:3
		ENDIF
		VARSET CUSTOM_PANTY_EXISTS
		CALL KOJO_CHECK(LOCAL, "PANTIES")
	NEXT
ENDIF

FOR LOCAL, 0, INDEX
	CHARA = PANTY_INDEXES:LOCAL / 1000
	DIALOGUE = PANTY_INDEXES:LOCAL / 100 % 10
	PANTY = PANTY_INDEXES:LOCAL % 100
	IF LAST_CHARA != CHARA
		IF !CHARA
			PRINTFORMDL ｜General Panties:%"　"*(TABLE_WIDTH-8)%｜
			PRINTFORMDL ┝─┯%"─"*20%┯─┯─┯─┯─┯─┯─┯─┯─┯─┥
		ELSE
			SIF LAST_CHARA != -1
				PRINTFORMDL ┝─┴%"─"*20%┴─┴─┴─┴─┴─┴─┴─┴─┴─┥
			PRINTD ｜
			LOCALS = %NAME:CHARA%'s Panties:
			CALL COLORMESSAGE(NAME:CHARA, C_GREEN)
			PRINTD 's Panties:
			IF STRLENS(LOCALS) % 2 == 1
				PRINTD  
				LOCALS += " "
			ENDIF
			PRINTFORMDL %"　"*(TABLE_WIDTH-STRLENS(LOCALS)/2)%｜
			PRINTFORMDL ┝─┯%"─"*20%┯─┯─┯─┯─┯─┯─┯─┯─┯─┥
		ENDIF
		ORIGINAL_DIALOGUE = CFLAG:CHARA:口上セレクタ
		LAST_DIALOGUE = -1
	ENDIF
	IF LAST_DIALOGUE != DIALOGUE && INRANGE(PANTY, 35, 39)
		VARSET RESULTS
		IF DIALOGUE
			TRYCALLFORM M_KOJO_K{NO:CHARA}_{DIALOGUE}
		ELSE
			TRYCALLFORM M_KOJO_K{NO:CHARA}
		ENDIF
		IF RESULTS:1 == ""
			LOCALS = Default Dialogue
			LOCALS:1 = Default Dialogue Custom Panties:
		ELSE
			LOCALS = %RESULTS:1%
			LOCALS:1 = %RESULTS:1% Custom Panties:
		ENDIF
		SIF STRLENS(LOCALS) % 2 == 1
			LOCALS:1 += " "
		PRINTD ｜　｜
		CALL COLORMESSAGE(LOCALS, C_ORANGE)
		PRINTD  Custom Panties:
		PRINTFORMDL %"　"*(20-STRLENS(LOCALS:1)/2)%｜　｜　｜　｜　｜　｜　｜　｜　｜　｜
	ENDIF

	CFLAG:CHARA:口上セレクタ = DIALOGUE
	IF INRANGE(PANTY, 35, 39)
		VARSET CUSTOM_PANTY_EXISTS
		CALL KOJO_CHECK(LOCAL, "PANTIES")
	ENDIF
	CALL PANTIES_INFO_PRINT_PANTY_LINE(CHARA, PANTY)
	LAST_CHARA = CHARA
	SIF INRANGE(PANTY, 35, 39)
		LAST_DIALOGUE = DIALOGUE
	CFLAG:CHARA:口上セレクタ = ORIGINAL_DIALOGUE
	VARSET CUSTOM_PANTY_EXISTS
	CALL KOJO_CHECK(LOCAL, "PANTIES")
NEXT

@PANTIES_INFO_PRINT_PANTY_LINE(CHARA, PANTY)
#DIM CHARA
#DIM PANTY

PRINTFORMD ｜
;index
IF PANTY == 18
	CALL COLORMESSAGE("Ｕ", C_L_GREEN)
ELSEIF INRANGE(PANTY, 35, 39)
	CALL COLORMESSAGE(@"#{PANTY-34}", C_L_GREEN)
ELSE
	PRINTFORMD {PANTY, 2, LEFT}
ENDIF
PRINTFORMD ｜
;name
LOCALS:1 '= GET_STR(CHARA, "下半身下着_ずらし可能", PANTY, "名前")
LOCALS:2 '= CLOTHES_TR(LOCALS:1, 1)
SIF STRLENS(LOCALS:2) % 2 == 1
	LOCALS:2 += " "
PRINTFORMD %LOCALS:2, 40, LEFT%
PRINTFORMD ｜

;cover
F = GET_INT(CHARA, "下半身下着_ずらし可能", PANTY, "透ける(前)")
R = GET_INT(CHARA, "下半身下着_ずらし可能", PANTY, "透ける(後)")
CALL PANTIES_INFO_PRINT_PANTY_VALUE(F, R)
PRINTFORMD ｜
;shiftable
CALL PANTIES_INFO_PRINT_PANTY_BOOL(GET_INT(CHARA, "下半身下着_ずらし可能", PANTY, "SHIFTABLE"))
PRINTFORMD ｜
;pullable
CALL PANTIES_INFO_PRINT_PANTY_BOOL(!GET_INT(CHARA, "下半身下着_ずらし可能", PANTY, "PASTIES"))
PRINTFORMD ｜
;pasties
CALL PANTIES_INFO_PRINT_PANTY_BOOL(GET_INT(CHARA, "下半身下着_ずらし可能", PANTY, "PASTIES"))
PRINTFORMD ｜
;zippable
F = GET_INT(CHARA, "下半身下着_ずらし可能", PANTY, "ZIPPABLE_FRONT")
R = GET_INT(CHARA, "下半身下着_ずらし可能", PANTY, "ZIPPABLE_BACK")
CALL PANTIES_INFO_PRINT_PANTY_VALUE(F, R)
PRINTFORMD ｜

;holes
F = GET_INT(CHARA, "下半身下着_ずらし可能", PANTY, "HOLE_FRONT")
R = GET_INT(CHARA, "下半身下着_ずらし可能", PANTY, "HOLE_BACK")
CALL PANTIES_INFO_PRINT_PANTY_VALUE(F, R)
PRINTFORMD ｜
F = GET_INT(CHARA, "下半身下着_ずらし可能", PANTY, "STRING_FRONT")
R = GET_INT(CHARA, "下半身下着_ずらし可能", PANTY, "STRING_BACK")
CALL PANTIES_INFO_PRINT_PANTY_VALUE(F, R)
PRINTFORMD ｜
F = GET_INT(CHARA, "下半身下着_ずらし可能", PANTY, "NOTHING_FRONT")
R = GET_INT(CHARA, "下半身下着_ずらし可能", PANTY, "NOTHING_BACK")
CALL PANTIES_INFO_PRINT_PANTY_VALUE(F, R)
PRINTFORMD ｜

;tied
CALL PANTIES_INFO_PRINT_PANTY_BOOL(GET_INT(CHARA, "下半身下着_ずらし可能", PANTY, "TIED"))
PRINTFORMD ｜
PRINTL

@PANTIES_INFO_PRINT_PANTY_VALUE(FRONT, REAR)
#DIM FRONT
#DIM REAR
IF FRONT && REAR
	CALL COLORMESSAGE("Ｂ", C_M_GREEN)
ELSEIF FRONT
	CALL COLORMESSAGE("Ｆ", C_BLUE)
ELSEIF REAR
	CALL COLORMESSAGE("Ｒ", C_ORANGE)
ELSE
	PRINT 　
ENDIF

@PANTIES_INFO_PRINT_PANTY_BOOL(STATE)
#DIM STATE
IF STATE == 1
	CALL COLORMESSAGE("Ｏ", C_GREEN)
ELSEIF STATE == 0
	CALL COLORMESSAGE("Ｘ", C_RED)
ELSE
	PRINT 　
ENDIF
[SKIPEND]