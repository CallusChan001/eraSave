;-----------------------------------------------------------
;处理据点内角色行动的函数
;ARG   要判定的角色
;停留率   相对于当前位置的移动率，值越高越倾向于停留，值越低越容易移动
;移动目的地 移动先   进行移动判定的地点ID
;移动决定   实际移动的地点ID
;无差别移动   如果目的地可进入，则暂时移动过去
;强制移动   无论如何都要移动到目的地（当无差别移动无法到达任何地点时触发）
;END   结束标志
;N_ID   ARG的当前位置
;G_POINT   临时引力点
;引力点   逐渐移动到的目的地
;保持不动   除非是仅持续一回合的无差别移动，否则不进行移动的状态。通常在移动后或采取特定行动时进入此状态
;-----------------------------------------------------------
@角色移動処理(ARG)
#DIM 停留率
#DIM 移動先
#DIM 移動決定
#DIM 無差別移動
#DIM 強制移動
#DIM END
#DIM N_ID
#DIM G_POINT
VARSET LOCAL
END = 0
;你の拠点物件にいる角色のみが対象    仅限身处你据点的人员
SIF !AT_HOME(ARG)
	END = 1
;行動不能
SIF FLAG:時間停止 || CFLAG:ARG:衰弱 || CFLAG:ARG:招待 || CFLAG:ARG:睡眠
	END = 1
;仕事、延迟、添い寝、诶嘿嘿、同行中は移動処理リセット    工作、延迟、陪睡、诶嘿嘿、同行时不处理
SIF CFLAG:ARG:行動 == 5 || CFLAG:ARG:延迟 || CFLAG:ARG:陪睡中 || CFLAG:ARG:诶嘿嘿 || CFLAG:ARG:同行
	END = 1

;終了処置
IF END
	TCVAR:ARG:なるべく動かない = 0
	RETURN
ENDIF

N_ID = CFLAG:ARG:現在位置

;停留率
;新增剧本侧接口，调用剧本侧设定的角色移动数值
CALL KOJO_CHECK(ARG, "CHARADATA", "MOVE", 4, N_ID)
IF RESULT == -1
	;剧本侧禁用该机制，但是请作者务必清楚自己在做什么
	VARSET RESULT
ELSEIF RESULT == 1
	;采用剧本设定的值
	RESULT = RESULT:1
ELSE 
	;剧本没有采用CHECK函数或者剧本需要是结果为0，从而采用原版移动数值
	CALLFORM CHARAMOVE_DATA_{NO:ARG}(4, N_ID)
ENDIF 
停留率 = RESULT
;待機場所は固定    最大房（待机/地图外等）停留率固定降低
IF N_ID == MAXROOM
	停留率 = -10
;引力点も固定    引力点停留率固定提升
ELSEIF N_ID == TCVAR:ARG:引力点
	停留率 = 5
ELSE
	;留まりにくい場所    不易停留的地方，包括地图入口或不停留标记
	SIF N_ID == GET_ENTRANCE(MAIN_MAP) || ROOMDATA:(N_ID % 100) & 場所_留まらない
		停留率 += -5
	;你と同室だったら態度で留まる    要是和你同地点就看态度决定是否留下
	SIF N_ID == CFLAG:MASTER:現在位置
		停留率 += CFLAG:ARG:態度 * 2
ENDIF

無差別移動 = 0
強制移動 = 0
;お風呂にいる
IF BATHROOM(N_ID)
	;お風呂なう    正在洗澡
	IF CFLAG:ARG:風呂
		TCVAR:ARG:なるべく動かない = 0
		RETURN
	;あがる
	ELSE
		無差別移動 = 1
	ENDIF
ENDIF
;廁所は10分経過したら出てくる,利尿剤が効いてると出てこない    上厕所的话，10分钟后就会出来，如果利尿剂起作用的话就不会出来
IF IN_TOILET(N_ID) && TIME_PROGRESS(TIME:1) >= 10
	IF TCVAR:ARG:利尿剤
		TCVAR:ARG:なるべく動かない = 1
	ELSEIF TIME_PROGRESS(TIME:1) >= 10 && !TCVAR:ARG:利尿剤
		無差別移動 = 1
	ENDIF
ENDIF
;天候ダメージが痛い    天气因素影响太严重了
SIF 天候造成的影響(ARG, N_ID) <= -3
	無差別移動 = 1
;現在位置の移動率を判定して、本来入らない場所（なおかつ你が居ない）ならG_POINTを付けて無差別移動    判断当前位置的移动率，如果是本不该进入的地方（而且你不在那里）则附加G_POINT进行无差别移动
CALL 角色移動率(ARG, N_ID)
IF RESULT <= 0 && N_ID != CFLAG:MASTER:現在位置
	無差別移動 = 1
	;引力点が未設定ならとりあえず外に出ようとする    如果引力点未设定的话，就以地图出入口为G_POINT
	SIF !TCVAR:ARG:引力点
		G_POINT = ENTRANCE
ELSE
	G_POINT = 0
ENDIF
;なるべく動かないなら終了    如果不动的话就结束
IF TCVAR:ARG:なるべく動かない && !無差別移動
	TCVAR:ARG:なるべく動かない = 0
	RETURN
ENDIF

CALL FISHER_YATES_SHAFFLE(MAXROOM - MINROOM())
移動決定 = 0
WHILE 1
	;引力点の設定を行う    进行引力点的设置
	TCVAR:ARG:引力点 = 0
	;新增剧本侧接口，调用剧本侧设定的角色移动数值
	CALL KOJO_CHECK(ARG, "CHARADATA", "MOVE", 8)
	IF RESULT == -1
		;剧本侧禁用该机制，但是请作者务必清楚自己在做什么
		;对于引力点机制，这等于TCVAR:ARG:引力点 = 0
	ELSEIF RESULT
		;等于其他正值时，表明口上侧有自己的值
		;对于引力点机制，TCVAR:ARG:引力点在函数中赋值
	ELSE 
		;剧本没有采用CHECK函数或者剧本需要是结果为0，从而采用原版移动数值
		CALLFORM CHARAMOVE_DATA_{NO:ARG}(8)
	ENDIF
	CALL KOJO_MESSAGE_SEND("GRAVITY", 1, ARG)
	;引力点または脱出用のG_POINTがある場合は引力点に向かって移動する,G_POINTは条件無し    若有引力点或离开用的G_POINT，则向引力点移动，G_POINT无条件
	IF 引力点発動可(ARG) || G_POINT
		;引力点かG_POINTを目指す    以引力点或G_POINT为目标
		移動先 = TCVAR:ARG:引力点 ? TCVAR:ARG:引力点 # G_POINT
		;スキップムーブでルート検索    无法直连移动时搜索路线
		;fix 引力点可能是N_ID，而一个地点无法移动到自己。原版的处置是按照静态的疏散路径图强制移动到疏散位置，然后在下一次移动处理时再返回引力点，表现为反复进出引力点。优化后的寻路则是呆在引力点不动。
		WHILE !(CAN_MOVE(N_ID, 移動先) & 1) && N_ID != 移動先
			;TRYCALLFORM SKIP_MOVE_{MAIN_MAP}(移動先, N_ID)
			TRYCALLFORM QOL_SKIP_MOVE_INFO(移動先, N_ID,MAIN_MAP)
			移動先 = RESULT
		WEND
		;移動先が主人公私室であり、その角色の私室でなく、そこで你がウフフ中の場合は入ってこれない    如果移动先是主人公的房间，而非其他角色的房间，且你在其中时则无法进入
		IF 移動先 == CFLAG:MASTER:初期位置 && 移動先 != CFLAG:ARG:初期位置 && 移動先 == CFLAG:MASTER:現在位置 && CFLAG:MASTER:诶嘿嘿
		;結界発動中
		ELSEIF TEQUIP:MASTER:結界発動 && 移動先 == CFLAG:MASTER:現在位置
		ELSE
			;停留率に補正をかけて動きやすくする（要調整,-100だとほぼ運動の）    对停留率进行修正以使其更易移动（需调整，若为-100则几乎无运动）
			CALL 角色移動率(ARG, 移動先, 停留率 - 100)
			;移動判定
			IF RESULT * TIME_PROGRESS(TIME:1) > RAND:1000
				移動決定 = 移動先
			;無差別移動時はなるべく移動,強制移動時は無条件に移動    在无差别移动时尽量移动，在强制移动时无条件移动
			ELSEIF (RESULT && 無差別移動) || 強制移動
				移動決定 = 移動先
			ENDIF
		ENDIF
	;通常の移動
	ELSE
		FOR LOCAL, 0, MAXROOM - MINROOM()
			移動先 = SHAFFLE_ARRAY:LOCAL + MAIN_MAP * 100 + 1
			;現在位置
			SIF 移動先 == N_ID
				CONTINUE
			;移動できない    无法移动（无地图路径）
			SIF !(CAN_MOVE(N_ID, 移動先) & 1)
				CONTINUE
			;移動できない２,ワンルーム制限等、移動に制限がある場所    无法移动2（无移动条件）、单间等有移动限制的场所
			SIF !MAP_CAN_MOVE(移動先, ARG)
				CONTINUE
			;移動先が主人公私室であり、その角色の私室でなく、そこで你がウフフ中の場合は入ってこれない    目的地是你私室非她私室且你在里面涩涩的时候不能进入
			SIF 移動先 == CFLAG:MASTER:初期位置 && 移動先 != CFLAG:ARG:初期位置 && 移動先 == CFLAG:MASTER:現在位置 && CFLAG:MASTER:诶嘿嘿
				CONTINUE
			;強制移動時は以下の条件はパスする    强制移动时以下条件可忽略
			IF !強制移動
				;天候が今いる場所より痛い    天气造成的巨大影响
				SIF 天候造成的影響(ARG, 移動先) <= -3 && N_ID != MAXROOM && 天候造成的影響(ARG, N_ID) >= -2
					CONTINUE
				;追い出された先    被赶走后的去处
				SIF 移動先 == TCVAR:ARG:追い出され地点
					CONTINUE
				;結界発動中
				SIF TEQUIP:MASTER:結界発動 && 移動先 == CFLAG:MASTER:現在位置
					CONTINUE
			ENDIF
			;最初にヒットした移動先で判定してBREAKする（分岐が多いほど移動確率が上がらないようにするため）    在最初命中目标处进行判定并跳出（为避免分支过多导致移动概率上升）
			CALL 角色移動率(ARG, 移動先, 停留率)
			;通常の移動判定
			IF RESULT * TIME_PROGRESS(TIME:1) > RAND:1000
				移動決定 = 移動先
			;無差別移動時はなるべく移動,強制移動時は無条件に移動    在无差别移动时尽量移动（即有概率就移动），在强制移动时无条件移动
			ELSEIF (RESULT && 無差別移動) || 強制移動
				移動決定 = 移動先
			;移動率が0%じゃないなら判定終了（减少でも終了）    若移动率不是0%则判定结束确定不移动（减少时也结束），这指的是未通过移动判定且无强制移动
			ELSEIF RESULT
				;ただし無差別移動時は強制移動を付けて再判定    但如果有无差别移动，则需附加强制移动后重新判定，以直到可以移动
				IF 無差別移動 && !強制移動
					強制移動 = 1
				ELSE
					TCVAR:ARG:なるべく動かない = 0
					RETURN
				ENDIF
			;0%なら再判定    移动率0时再判定
			ELSE
				CONTINUE
			ENDIF
			BREAK
		NEXT
	ENDIF
	;強制移動時は移動決定するまでループ    强制移动时需要有移动决定
	IF 強制移動 && !移動決定
		CONTINUE
	ELSEIF 移動決定
		CALL 角色移動処理２(ARG, 移動先, N_ID)
		RETURN
	ELSE
		TCVAR:ARG:なるべく動かない = 0
		RETURN
	ENDIF
WEND

;-----------------------------------------------------------
;移動が決定したあとの処理    移动决定后的处理
;ARG     移動する角色
;移動先  移動する地点ID
;N_ID    ARGの現在位置
;-----------------------------------------------------------
@角色移動処理２(ARG, 移動先, N_ID)
#DIM 移動先
#DIM N_ID
#DIM 移動メッセージ
#DIM ワンルーム人数カウント
#DIM SKIP
移動メッセージ = 0
SKIP = 0
SETCOLOR C_YELLOW
IF CAN_SEE(ARG) && !CFLAG:MASTER:睡眠
	IF N_ID == MAXROOM
		PRINTFORML %CALLNAME:ARG%来到了【%NAME_FROM_PLACE(移動先)%】
	ELSE
		PRINTFORML %CALLNAME:ARG%从【%NAME_FROM_PLACE(N_ID)%】往【%NAME_FROM_PLACE(移動先)%】移动了
	ENDIF
	移動メッセージ = 1
ENDIF
;ワンルーム処理    私室处理
IF ROOMDATA:(N_ID % 100) & 場所_ワンルーム
	;ワンルーム家主が外出する場合、相性が良くない角色は一緒に外に出される    当私室家主外出时，相性不足的角色会被一同带出
	IF N_ID == CFLAG:ARG:初期位置
		ワンルーム人数カウント = 0
		FOR LOCAL, 1, CHARANUM
			IF CFLAG:LOCAL:現在位置 == N_ID && LOCAL != ARG
				;別の同居している角色がまだいる場合は逐出が発生しない    如果还有其他同住的人，则不会发生驱逐
				IF CFLAG:LOCAL:現在位置 == CFLAG:LOCAL:初期位置
					SKIP = 1
					BREAK
				;相性の良い角色は居ても良い    有足够相性的角色也不会，这里的逻辑是查找不足的人
				ELSEIF RELATION:ARG:LOCAL <= 100
					ワンルーム人数カウント ++
					CFLAG:LOCAL:現在位置 = 移動先
					TCVAR:LOCAL:なるべく動かない = 1
				ENDIF
			ENDIF
		NEXT
		;你は恋慕ならオッケー,你＋相性のいい角色が留守番、他の角色が締め出しだと稍微地の文と齟齬があるのは仕様    如果你是恋慕的话就没问题，你+相性好的角色留守，其他角色被赶走的话，稍微有点和原文不符也是没办法的事
		IF 移動メッセージ && !SKIP
			IF !TALENT:ARG:恋慕
				PRINTFORML %CALLNAME:MASTER%\@ ワンルーム人数カウント ? 他们 #  \@、在%CALLNAME:ARG%的催促下从【%NAME_FROM_PLACE(N_ID)%】往【%NAME_FROM_PLACE(移動先)%】移动了
				CFLAG:MASTER:現在位置 = 移動先
			ELSEIF ワンルーム人数カウント
				PRINTFORML 在%CALLNAME:ARG%的催促下、%CALLNAME:MASTER%以外的人从【%NAME_FROM_PLACE(N_ID)%】往【%NAME_FROM_PLACE(移動先)%】移动了
			ELSE
				PRINTFORML %CALLNAME:ARG%拜托%CALLNAME:MASTER%留下来看家
			ENDIF
		ENDIF
	ENDIF
ENDIF
RESETCOLOR
SIF 移動メッセージ
	WAIT
;移動処理
CFLAG:ARG:現在位置 = 移動先
TCVAR:ARG:なるべく動かない = 1

;--------------------------------------------------------
;角色(ARG)が地点(移動先)へ移動する確率／分    角色（ARG）移动至地点（目的地）的概率/分钟
;停留率は减少補正    停留率进行减少修正
;実数値は10倍したもの    数值为实际值的10倍
;式中関数にしたかったが…    本想把它做成公式函数的……
;--------------------------------------------------------
@角色移動率(ARG, 移動先, 停留率)
#DIM 移動率
#DIM 停留率
#DIM 移動先
#DIM N_ID
#DIM お風呂入りたい
#DIM 部屋主
;お風呂入りたい度を設定    设定想洗澡的程度，应该指的是阈值，需要大于该值才会去？
IF TALENT:ARG:氷精
	お風呂入りたい = 2000
;お風呂が嫌いな角色(虎は泳げるけど)    讨厌洗澡的角色（虎能游泳）
ELSEIF GROUPMATCH(ARG, [[橙]], [[星]], [[正邪]], [[阿燐]], [[三花]])
	お風呂入りたい = 2000
ELSEIF TALENT:ARG:汚臭耐性 == -2
	お風呂入りたい = 600
ELSEIF TALENT:ARG:汚臭耐性 == -1
	お風呂入りたい = 800
ELSEIF TALENT:ARG:汚臭耐性 == 0
	お風呂入りたい = 1000
ELSEIF TALENT:ARG:汚臭耐性 == 1
	お風呂入りたい = 1500
ELSEIF TALENT:ARG:汚臭耐性 == 2
	お風呂入りたい = 2000
ENDIF

N_ID = CFLAG:ARG:現在位置
;基本移動率,1.5% / 分 = 45.0% / 30分
移動率 = 15 + CFLAG:ARG:移動率補正
;你が見えていたら態度でUP
SIF CFLAG:MASTER:現在位置 == 移動先 && CAN_MOVE(N_ID, 移動先) == 3
	移動率 += CFLAG:ARG:態度 * 2
;掃除係は掃除中汚れに影響される
SIF TALENT:ARG:掃除係 && CFLAG:ARG:神社在住 && BETWEENTIME(720, 1080)
	移動率 += YOGORE:移動先 / 300
;角色ごとの移動率設定
;新增剧本侧接口，调用剧本侧设定的角色移动数值
CALL KOJO_CHECK(ARG, "CHARADATA", "MOVE", 4, 移動先)
IF RESULT == -1
	;剧本侧禁用该机制，但是请作者务必清楚自己在做什么
	VARSET RESULT
ELSEIF RESULT == 1
	;采用剧本设定的值
	RESULT = RESULT:1
	SIF RESULT:2
		RESULT = RESULT:2
ELSE 
	;剧本没有采用CHECK函数或者剧本需要是结果为0，从而采用原版移动数值
	CALLFORM CHARAMOVE_DATA_{NO:ARG}(4, 移動先)
ENDIF 
移動率 = 移動率 + RESULT

;お風呂に入りたい時は入りに行く    想洗澡时就去洗
IF BATHCHECK(移動先) & 場所_風呂
	IF CFLAG:ARG:風呂 >= お風呂入りたい
		移動率 = 移動率 * 10
	ELSE
		移動率 = 0
	ENDIF
ENDIF
;引きこもり対策として、天候ダメージがない状況で屋内にいる場合屋外へ行きたがるように    作为应对宅家对策，在没有天气影响的情况下，要引导人们从室内走向室外
SIF INROOM(N_ID) && !INROOM(移動先) && 天候造成的影響(ARG, 移動先) >= 0 
	移動率 += 15

;-----------------------------------
;根据移动节制设定不去的地方
;据点居民的私人场所请单独设置
 ;0以上 = 不进入私人场所
;     1 = 不移动到仓库、小巷、地下室等偏僻地点
;    -1 = 不太在意私人场所，也会移动
;    -2 = 移动到所有地方
;-----------------------------------
部屋主 = PRIVATEROOM:(移動先 % 100)
IF 移動先 != CFLAG:ARG:初期位置 && N_ID != MAXROOM
	IF CFLAG:ARG:移動節度 >= 1
		SIF ROOMDATA:(移動先 % 100) & 場所_過疎
			移動率 = 0
	ENDIF
	IF CFLAG:ARG:移動節度 >= 0 && !CFLAG:ARG:神社在住
		SIF ROOMDATA:(移動先 % 100) & 場所_プライベート && RELATION:部屋主:ARG <= 100
			移動率 = 0
	ENDIF
	IF CFLAG:ARG:移動節度 >= -1
		SIF 寝てる子がいる(移動先)
			移動率 = 移動率 * 1 / 3
		SIF ROOMDATA:(移動先 % 100) & 場所_プライベート && !CFLAG:ARG:神社在住 && RELATION:部屋主:ARG <= 100
			移動率 = 移動率 * 2 / 3
		SIF 天候造成的影響(ARG, 移動先) <= -1
			移動率 = 移動率 * 1 / 3
		SIF ROOMDATA:(移動先 % 100) & 場所_過疎
			移動率 = 移動率 * 1 / 3
	ENDIF
ENDIF
;停留率を差し引く    减去停留率
移動率 = MAX(移動率 - 停留率, 0)
RETURN 移動率

@寝てる子がいる(ARG)
#FUNCTION
FOR LOCAL, 1, CHARANUM
	SIF CFLAG:LOCAL:現在位置 == ARG && CFLAG:LOCAL:睡眠
		RETURNF 1
NEXT

;ARG  判定角色
@引力点発動可(ARG)
#FUNCTION
;引力点が無い
SIF !TCVAR:ARG:引力点
	RETURNF 0
;現在位置と同じ
SIF CFLAG:ARG:現在位置 == TCVAR:ARG:引力点
	RETURNF 0
;引力点が拠点の外
SIF IN_HOME(TCVAR:ARG:引力点) != 1
	RETURNF 0
;現在位置が拠点の外
SIF IN_HOME(CFLAG:ARG:現在位置) != 1
	RETURNF 0
;天候ダメージが痛い
SIF 天候造成的影響(ARG, TCVAR:ARG:引力点) < 0
	RETURNF 0
RETURNF 1
