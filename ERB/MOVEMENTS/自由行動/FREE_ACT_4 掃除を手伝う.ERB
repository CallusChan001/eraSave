;幫忙掃除
@EXIST_FreeAct4
#LOCALSIZE 1
#LOCALSSIZE 1

;オブジェクト本体
@FreeAct4(ARG, O_DATA, V_NAME)
#FUNCTION
#LOCALSIZE 1
#LOCALSSIZE 1
#DIMS O_DATA
#DIMS V_NAME
#DIM 経過時間
経過時間 = Activity_Duration:TARGET
SELECTCASE O_DATA
CASE "名称"
	CALLF MAKE_STR(V_NAME, "帮忙扫除")
CASE "CHAIN"
;他角色に連鎖する場合コメントアウト外すこと
	CALLF MAKE_INT(V_NAME, 1)
CASE "経過時間"
;陪同行動指令での経過時間
;未入力だと最大30分
	;CALLF MAKE_INT(V_NAME, 経過時間)
ENDSELECT

@自由行動内容_4(CHARA, ARGS, 時間係数, CHAIN)
#LOCALSIZE 1
#LOCALSSIZE 1
#DIM CHARA
#DIM CHAIN
#DIM 現在地
#DIM メッセージ表示
#DIM 時間係数
#DIM 経過時間
現在地 = CFLAG:CHARA:現在位置
メッセージ表示 = 現在地 == CFLAG:MASTER:現在位置 ? 1 # 0
SELECTCASE ARGS
	CASE "発生"
		;个室にいる時
		SIF !BEDROOM(現在地)
			RETURN 0
		SIF BASE:CHARA:気力 <= 500
			RETURN 0
		;汚臭耐性（掃除係）によって積極性が変わる
		IF TALENT:CHARA:汚臭耐性 < 0 || TALENT:CHARA:掃除係
			SIF YOGORE:(現在地) < 2000
				RETURN 0
		ELSEIF TALENT:CHARA:汚臭耐性 == 0
			SIF YOGORE:(現在地) < 3500
				RETURN 0
		ELSEIF TALENT:CHARA:汚臭耐性 > 0
			SIF YOGORE:(現在地) < 5000
				RETURN 0
		ENDIF
		SIF メッセージ表示
			PRINTFORMW %CALLNAME:CHARA%\@ CHAIN ? 决定帮%CALLNAME:CHAIN%一起扫除了。# 似乎在意房间脏污、决定打扫除了。\@
		RETURN 30 + RAND:20
	CASE "行動中"
		;実際に掃除
		YOGORE:(現在地) = MAX(YOGORE:(現在地) - 250 * TIME_PROGRESS(TIME:1) / 5, 0)
		IF メッセージ表示
			IF 充填率(CHARA,1) >= 10 || 充填率(CHARA,2) >= 10
				PRINTFORMW %CALLNAME:CHARA%满脸通红的%TEXTR("清扫着垃圾/整理和服品/打扫着灰尘/收拾着房间")%。
			ELSE
				PRINTFORMW %CALLNAME:CHARA%%TEXTR("清扫着垃圾/整理和服品/打扫着灰尘/在手脚利落地收拾着房间")%。
			ENDIF	
		ENDIF
	CASE "完了"
		;清掃経験が入る
		EXP:CHARA:清掃経験 += 3
		IF 充填率(CHARA,1) >= 10 || 充填率(CHARA,2) >= 10
			SIF メッセージ表示
				PRINTFORMW %CALLNAME:CHARA%吐着甜美的喘息扫除完毕了。
		ELSE
			SIF メッセージ表示
				PRINTFORMW %CALLNAME:CHARA%似乎扫除完毕了。
		ENDIF		
			
	CASE "中断"
		;中断されても清掃経験は入る
		EXP:CHARA:清掃経験 += 1
	CASE "付き合う"
		IF 充填率(CHARA,1) >= 150 || 充填率(CHARA,2) >= 150
				PRINTFORML %CALLNAME:CHARA%捂着嘴忍耐着体内\@ TALENT:MASTER:濃厚精液? 浓厚的 # 的\@精子的爱抚…香汗淋漓…
				PRINTFORML 脸涨得通红并且夹杂着甜美的喘息%TEXTR("整理垃圾/擦灰尘/打扫")%中
				PRINTFORML 经过的地方地上有奇怪的水迹……
			ELSEIF 充填率(CHARA,1) >= 100 || 充填率(CHARA,2) >= 100
				PRINTFORML %CALLNAME:CHARA%正享受着体内满满\@ TALENT:MASTER:濃厚精液? 浓厚的 # 的\@精子的刺激轻轻娇喘……
				PRINTFORML 欣赏着一边娇喘一边%TEXTR("整理垃圾/擦灰尘/打扫")%的%CALLNAME:CHARA%…
			ELSEIF 充填率(CHARA,1) >= 70 || 充填率(CHARA,2) >= 70
				PRINTFORML %CALLNAME:CHARA%正感受着体内大量\@ TALENT:MASTER:濃厚精液? 浓厚的 # 的\@精子的刺激……
				PRINTFORML 断断续续打扫并时不时夹杂着甜美娇喘的%CALLNAME:CHARA%%TEXTR("整理垃圾的动作慢了下来/用抹布擦灰尘的动作慢了下来/打扫的动作慢了下来")%…
			ELSEIF 充填率(CHARA,1) >= 40 || 充填率(CHARA,2) >= 40
				PRINTFORML %CALLNAME:CHARA%正被体内许多\@ TALENT:MASTER:濃厚精液? 浓厚的 # 的\@精子刺激着……
				PRINTFORML 红着脸的%CALLNAME:CHARA%%TEXTR("整理垃圾的动作慢了下来/用抹布擦灰尘的动作慢了下来/打扫的动作慢了下来")%
			ELSEIF 充填率(CHARA,1) >= 10 || 充填率(CHARA,2) >= 10
				PRINTFORML %CALLNAME:CHARA%正被体内残存\@ TALENT:MASTER:濃厚精液? 浓厚的 # 的\@精子刺激着……
				PRINTFORML %CALLNAME:PLAYER%帮助红着脸的%CALLNAME:CHARA%%TEXTR("将整理好的垃圾一起拿出去扔掉了/将占满灰尘的地方一起用抹布擦干净了/一起把一个人伸手够不到的地方打扫干净了")%
			ELSE
				PRINTFORML %CALLNAME:PLAYER%帮助%CALLNAME:CHARA%%TEXTR("将整理好的垃圾一起拿出去扔掉了/将占满灰尘的地方一起用抹布擦干净了/一起把一个人伸手够不到的地方打扫干净了")%
			ENDIF		
	CASE "ソース"
		LOCAL = YOGORE:(CFLAG:MASTER:現在位置)
		YOGORE:(CFLAG:MASTER:現在位置) = MAX(YOGORE:(CFLAG:MASTER:現在位置) -100 * 経過時間 / 5, 0)
		EX:MASTER:今日的掃除回数 += LOCAL - YOGORE:(CFLAG:MASTER:現在位置)
		DOWNBASE:MASTER:体力 += 20 * 時間係数
		DOWNBASE:MASTER:気力 += 40 * 時間係数
		DOWNBASE:CHARA:体力 += 10 * 時間係数
		DOWNBASE:CHARA:気力 += 20 * 時間係数
		SOURCE:CHARA:歓楽 = (100 + ((ABL:CHARA:従順 + ABL:MASTER:清掃技能) * 50)) * 時間係数
		TFLAG:98 = 2
		EXP:MASTER:清掃経験 ++
		EXP:CHARA:清掃経験 ++
ENDSELECT
