@UPDATE
#DIM C_ID
#DIM 更新能力番号
#DIM 最低能力値
#DIM 最低経験数
#DIM 育児中の兒童
#DIM 兒童設定初回
#DIM 口上実装チェック, 人物数量上限
#DIM リセット画面
#DIM 技能素質更新用
#DIMS rname

PRINTL 旧版本不存在的人物・初期素质・FLAG等可能会产生混乱。

VARSET 口上実装チェック
リセット画面 = 0

;まずは路人を削除
DELCHARA CHARANUM - 1

FOR C_ID,1,CHARANUM ;CSVに名前を参照してNOを書き直す
	VARSET LOCAL
	FOR LOCAL,1,人物数量上限
		SIF !EXISTCSV(LOCAL)
			CONTINUE
		IF NAME:C_ID == CSVNAME(LOCAL)
			NO:C_ID = LOCAL
			BREAK
		ENDIF
	NEXT
NEXT

FOR C_ID,1,人物数量上限 ;終わったら新角色追加
	IF GETCHARA(C_ID) == -1 && EXISTCSV(C_ID)
		ADDCHARA C_ID
		TARGET = C_ID
		RESULT = 0
	ENDIF
	IF TALENT:C_ID:路人
		RANDOM_CHARANUM = C_ID
		BREAK
	ENDIF
NEXT

FOR C_ID, 1, CHARANUM
	IF NO:C_ID != C_ID && NO:C_ID != 0 ;もしずれたら並べなおす
		IF NO:C_ID > C_ID ;CSVNOが今のNOより後ろなら、SWAPして続く
			SWAPCHARA C_ID,NO:C_ID
			FOR LOCAL,0,CHARANUM
				SWAP RELATION:LOCAL:C_ID,RELATION:LOCAL:(NO:C_ID)
			NEXT
		ELSE
			SWAPCHARA C_ID,NO:C_ID ;そうじゃなかったらSWAPして一から並べなおす
			FOR LOCAL,0,CHARANUM
				SWAP RELATION:LOCAL:C_ID,RELATION:LOCAL:(NO:C_ID)
			NEXT
			C_ID = 1
		ENDIF
	ELSEIF NO:C_ID == C_ID && NO:C_ID != 0
		CALLNAME:C_ID = %CSVCALLNAME(C_ID)%
		NAME:C_ID = %CSVNAME(C_ID)%
	ENDIF
	IF TALENT:C_ID:路人
		RANDOM_CHARANUM = C_ID
		BREAK
	ENDIF
NEXT
FOR C_ID, 1, CHARANUM
	SIF TALENT:C_ID:路人
		CONTINUE
	;CSTR:工作情報をCSTR:3に移行する手続き
	IF CSTR:C_ID:3 == "" && CSVCSTR(C_ID, 3) != ""
		CSTR:C_ID:3 = %CSVCSTR(C_ID, 3)%
		CSTR:C_ID:1 = 
	ENDIF
	;获得CSTR:职场
	IF CSTR:C_ID:2 == "" && CSVCSTR(C_ID, 2) != ""
		CSTR:C_ID:2 = %CSVCSTR(C_ID, 2)%
	ENDIF
	IF TALENT:C_ID:恋慕
		TALENT:C_ID:思慕 = 0
		TALENT:C_ID:炮友 = 0
		TALENT:C_ID:愛欲 = 0
	ENDIF
	
	FOR 更新能力番号, 0, 111
		SIF EXP:C_ID:更新能力番号 < CSVEXP (C_ID,更新能力番号)
			EXP:C_ID:更新能力番号 = (EXP:C_ID:更新能力番号 + CSVEXP (C_ID,更新能力番号))
	NEXT
	;キャラデータのエラーチェック
	LOCAL:4 = 0
NEXT

SIF FLAG:3 == 0
	PRINTL 宴会的有无设定改为了OFF

LOCAL = TARGET
FOR C_ID, 1, CHARANUM
	RESULT = 0
	TARGET = C_ID
	CALL KOJO_MULTIPLE(C_ID)
	IF RESULT
		;新規口上追加時のリセット処理のスイッチ
		IF RESULT == 2
			口上実装チェック:C_ID = 1
			リセット画面 = 1
		ENDIF
		;CALL KOJO_ACTIVE_INFO(C_ID)
		;PRINTFORML 正在确认%CALLNAME:C_ID%%RESULTS:1%的剧本是否存在UPDATE
		;TRYCALLFORM M_KOJO%RESULTS%_UPDATE_K{C_ID}
		;PRINTL 
	ENDIF
NEXT
CALL UPDATE_AND_SELECTION_OF_KOJO_MODDING

$UPDATE_END

PRINTW 更新完毕

;アプデでCSV読み込みによる更新されない素質
@TALENT_NOT_UPDATED(ARG)
#FUNCTION
SELECTCASE ARG
	CASE 15,28,29,35 TO 39, 42 TO 49, 58, 59, 63 TO 69, 72 TO 79, 87 TO 91, 95 TO 99, 107 TO 120, 122 TO 129
		RETURNF 1
	CASEELSE
		RETURNF 0
ENDSELECT

@履歴文字列変換(ARG)
#DIM 日期
VARSET LOCALS
IF CFLAG:ARG:初吻喪失記念日 && CSTR:ARG:初吻喪失履歴 == ""
	日期 = CFLAG:ARG:初吻喪失記念日
	LOCALS = %PRINT_DATE_F(日期)%、%CALLNAME:(CFLAG:ARG:初吻喪失对手)%
	SIF CSTR:ARG:初吻位置 != ""
		LOCALS += @"在%CSTR:ARG:初吻位置%"
	IF CFLAG:ARG:初吻喪失対象 & (喪失_自己 | 喪失_恋慕 | 喪失_思慕)
		LOCALS += "献上了初吻"
	ELSE
		SIF CFLAG:ARG:初吻喪失対象 & 喪失_無崩し
			LOCALS += "随波逐流地"
		LOCALS += "被夺走初吻了"
	ENDIF
	CSTR:ARG:初吻喪失履歴 = %LOCALS%
	PRINTFORML %CALLNAME:ARG%的初吻成为了历史
ENDIF

IF CFLAG:ARG:尻穴処女喪失記念日 && CSTR:ARG:尻穴処女喪失履歴 == ""
	日期 = CFLAG:ARG:尻穴処女喪失記念日
	LOCALS:1 = %PRINT_DATE_F(日期)%、\@ CFLAG:ARG:尻穴処女喪失対象 & 喪失_時間停止? 时间停止中# \@
	SIF CSTR:ARG:Ａ処女喪失位置 != ""
		LOCALS:1 += @"在%CSTR:ARG:Ａ処女喪失位置%"
	IF GETBIT (CFLAG:ARG:尻穴処女喪失対象,3)
		LOCALS:1 += @"被%CALLNAME:(CFLAG:ARG:尻穴処女喪失对手)%用穿戴式假阴茎"
	ELSEIF GETBIT (CFLAG:ARG:尻穴処女喪失対象,2)
		LOCALS:1 += @"被%CALLNAME:(CFLAG:ARG:尻穴処女喪失对手)%用肛用振动棒"
	ELSEIF GETBIT (CFLAG:ARG:尻穴処女喪失対象,1)
		LOCALS:1 += @"被%CALLNAME:(CFLAG:ARG:尻穴処女喪失对手)%的阴茎"
	ENDIF
	IF CFLAG:ARG:尻穴処女喪失対象 & 喪失_時間停止
		LOCALS:1 += "夺走了肛门处女"
	ELSE
		LOCALS:1 += "奉献了肛门处女"
	ENDIF
	CSTR:ARG:尻穴処女喪失履歴 = %LOCALS:1%
	PRINTFORML %CALLNAME:ARG%的肛门处女成为了历史
ENDIF

IF CFLAG:ARG:処女喪失記念日 && CSTR:ARG:処女喪失履歴 == ""
	日期 = CFLAG:ARG:処女喪失記念日
	LOCAL = CFLAG:ARG:処女喪失対象
	LOCALS:2 = %PRINT_DATE_F(日期)%、
	SIF LOCAL & 喪失_時間停止
		LOCALS:2 += "时间停止中"
	SIF GETBIT (LOCAL,9)
		LOCALS:2 += "喝得酩酊大醉的时候烂醉如泥的时候"
	SIF GETBIT (LOCAL,16)
		LOCALS:2 += "在睡觉的时候"
	SIF CSTR:ARG:処女喪失位置 != ""
		LOCALS:2 += @"在%CSTR:ARG:処女喪失位置%\@ LOCAL & 喪失_恋慕 ? 心爱的# \@%CALLNAME:(CFLAG:ARG:処女喪失对手)%"
	IF GETBIT (LOCAL,3)
		LOCALS:2 += "用穿戴式假阴茎"
	ELSEIF GETBIT (LOCAL,2)
		LOCALS:2 += "用振动棒"
	ELSEIF GETBIT (LOCAL,1)
		LOCALS:2 += "的阴茎"
	ENDIF
	SIF LOCAL & 喪失_無崩し
		LOCALS:2 += "破坏性地"
	IF LOCAL & ( 喪失_恋慕 | 喪失_自己 ) || ( LOCAL & 喪失_思慕 && ! ( LOCAL & ( 喪失_烂醉 | 喪失_强行 | 喪失_睡眠 ) ) )
		LOCALS:2 += "献上处女"
	ELSE
		SIF LOCAL & 喪失_强行
			LOCALS:2 += "用力地"
		LOCALS:2 += @"就那样\@ARG == MASTER ? 献上了处女# 被夺走了处女\@"
	ENDIF
	CSTR:ARG:処女喪失履歴 = %LOCALS:2%
	PRINTFORML %CALLNAME:ARG%的处女成为了历史
ENDIF

;CSV更新用
@SET_MINIMUM_EXP_ALL(C_ID, ARG) ;custom code, arg to check current level instead of checking csv
#DIM C_ID
#DIM 更新能力番号
#DIM 最低能力値

FOR 更新能力番号,0,100
	最低能力値 = CSVABL(C_ID, 更新能力番号)
	SIF ARG ;custom code
		最低能力値 = ABL:C_ID:更新能力番号
	SIF 最低能力値 == 0
		CONTINUE
	;戦闘能力
	IF ABLNAME:更新能力番号 == "戦闘能力"
		;戦闘能力修正
		SIF ABL:C_ID:更新能力番号 < 最低能力値 && EXP:C_ID:戦闘経験 == CSVEXP(C_ID, GETNUM(EXP, "戦闘経験"))
			ABL:C_ID:更新能力番号 = 最低能力値
		CALL SET_MINIMUM_EXP(C_ID, GETNUM(EXP, "戦闘経験"), 最低能力値)
		CONTINUE
	ENDIF
	ABL:C_ID:更新能力番号 = MAX(最低能力値, ABL:C_ID:更新能力番号)
	SELECTCASE ABLNAME:更新能力番号
	CASE "清掃技能"
		CALL SET_MINIMUM_EXP(C_ID, GETNUM(EXP, "清掃経験"), 最低能力値)
	CASE "話術技能"
		CALL SET_MINIMUM_EXP(C_ID, GETNUM(EXP, "会話経験"), 最低能力値)
	CASE "教養"
		CALL SET_MINIMUM_EXP(C_ID, GETNUM(EXP, "学習経験"), 最低能力値)
	CASE "料理技能"
		CALL SET_MINIMUM_EXP(C_ID, GETNUM(EXP, "料理経験"), 最低能力値)
	CASE "音楽技能"
		SELECTCASE TALENT:C_ID:音楽知識
		CASE 1
			CALL SET_MINIMUM_EXP(C_ID, GETNUM(EXP, "演奏経験"), 最低能力値)
		CASE 2
			CALL SET_MINIMUM_EXP(C_ID, GETNUM(EXP, "歌唱経験"), 最低能力値)
		CASE 3
			CALL SET_MINIMUM_EXP(C_ID, GETNUM(EXP, "舞踏経験"), 最低能力値)
		CASEELSE ;bug fix, set generic singing experience otherwise
			CALL SET_MINIMUM_EXP(C_ID, GETNUM(EXP, "歌唱経験"), 最低能力値)
		ENDSELECT
	;移行完了
;	CASE "釣魚技能"
;		ABL:C_ID:更新能力番号 = MAX(ABL:C_ID:更新能力番号, CSVTALENT(C_ID, 47))
;		CALL SET_MINIMUM_EXP(C_ID, GETNUM(EXP, "釣魚経験"), 最低能力値)
;		TALENT:C_ID:釣魚Lv = MAX(TALENT:C_ID:釣魚Lv, ABL:C_ID:釣魚技能)
;	CASE "採集技能"
;		ABL:C_ID:更新能力番号 = MAX(ABL:C_ID:更新能力番号, CSVTALENT(C_ID, 48))
;		CALL SET_MINIMUM_EXP(C_ID, GETNUM(EXP, "採集経験"), 最低能力値)
;		TALENT:C_ID:採集Lv = MAX(TALENT:C_ID:採集Lv, ABL:C_ID:採集技能)
;	CASE "調合技能"
;		ABL:C_ID:更新能力番号 = MAX(ABL:C_ID:更新能力番号, CSVTALENT(C_ID, 49))
;		CALL SET_MINIMUM_EXP(C_ID, GETNUM(EXP, "調合経験"), 最低能力値)
;		TALENT:C_ID:調合Lv = MAX(TALENT:C_ID:調合Lv, ABL:C_ID:調合技能)
	ENDSELECT
NEXT
