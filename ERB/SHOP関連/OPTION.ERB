;オプション設定処理
@OPTION(IN_TRAINING = 0) ;ARG = 1: during training (not in the shop)
#DIM IN_TRAINING
#DIM L_COUNT
#DIM L_FEED
#DIM TEMP
VARSET LOCAL
L_FEED = LINECOUNT
TOOLTIP_SETDELAY 100
TEMP = CURRENTREDRAW()
REDRAW 0
DRAWLINEFORM =
FOR L_COUNT, 0, 91
	CALL OPTION_SETNAME(L_COUNT)
	SIF !RESULT
		CONTINUE
	LOCAL:L_COUNT = 1
	IF RESULTS:2 == "" 
		PRINTFORML [{L_COUNT, 2}] - %RESULTS:0, 30, RIGHT%：%RESULTS:1%
	ELSE
		HTML_PRINT @"<button value='{L_COUNT}' title='%RESULTS:2%'>[{L_COUNT, 2}] - %RESULTS:0, 30, RIGHT%：%RESULTS:1%</button>"
	ENDIF
NEXT
DRAWLINEFORM －
PRINTFORML [91] - 各种补丁的启动设定
PRINTFORML [92] - 自动喘息批量设定
PRINTFORML [93] - 简易过滤的总设定
PRINTFORML [94] - 画像表示设定
PRINTFORML [95] - 登场人物的切换
PRINTFORML [96] - 指令过滤器
;#;PRINTFORML [97] - Counter行动过滤器（DEBUG用）
PRINTFORML [98] - 状态上升倍率
;PRINTFORML [98] - 料理操作的設定
DRAWLINE

SIF DAY > 124 && !IN_TRAINING
	HTML_PRINT @"<button value='200' title='【警告】 此处之后的内容不宜阅读'>[200] - 败者·食尘</button>"
PRINTFORML [999] - 设定结束
INPUT
SELECTCASE RESULT
CASE 0 TO 90
	SIF LOCAL:RESULT
	CALL OPTION_SETTING(RESULT)
	CLEARLINE LINECOUNT - L_FEED
CASE 91
	CALL OPTION_DLC
CASE 92
	CALL OPTION_自動喘息一括設定
CASE 93
	CALL 簡易过滤器一括設定
CASE  94
	CALL OPTION_画像表示詳細設定
CASE 95
	CALL 登場人物的切换
CASE 96
	CALL OPTION_COMMAND_FILTER
[IF_DEBUG]
CASE 97
	CALL DEBUG_COUNTER_FILTER
[ENDIF]
CASE 98
	CALL OPTION_ABL_UP_RAIT
CASE 200
	TOOLTIP_SETDELAY 500
	IF DAY <= 124
		REDRAW TEMP
		RETURN
	ENDIF
	PRINTFORML 要返回到第一天么？？
	PRINTL 
	PRINTL [0]不要按下「开关」啊ーー
	PRINTL [1]不！到极限了、按下它！就现在！
	INPUT
	IF RESULT == 1
		CALL 強行開始新的游戏
	ELSE
;		PRINTW 虽然没能停止按下“开关”，但幸运的是只回到了选项选择，当然你也可以回头。
	ENDIF
CASEELSE
	TOOLTIP_SETDELAY 500
	REDRAW TEMP
	RETURN
ENDSELECT
REDRAW TEMP
RESTART

@OPTION_SETNAME(ARG)
;RESULT:2	ツールチップ表記内容
#DIM 係数
VARSET RESULTS 
RESULT = 1
SELECTCASE ARG
CASE 0
	RESULTS:0 = 怀孕的有无
	IF FLAG:妊娠開关
		RESULTS:1 = 会怀孕
		RESULTS:2 = ※自机孕在魔改功能里※
	ELSE
		RESULTS:1 = 不会怀孕
	ENDIF
CASE 1
	RESULTS:0 = 儿童的性别
	SELECTCASE FLAG:兒童の性別
	CASE 0
		RESULTS:1 = 随机
	CASE 1
		RESULTS:1 = 女孩子
	CASE 2
		RESULTS:1 = 男孩子
	ENDSELECT
	SIF !FLAG:妊娠開关
		RESULT = 0
CASE 2
	RESULTS:0 = 儿童的描写
	IF FLAG:兒童描写
		RESULTS:1 = 有
	ELSE
		RESULTS:1 = 无
	ENDIF
	SIF !FLAG:妊娠開关
		RESULT = 0
CASE 3
	RESULTS:0 = 搬家事件
	IF FLAG:住宿事件
		RESULTS:1 = 无
	ELSE
		RESULTS:1 = 有
	ENDIF
	RESULTS:2 = 亲密的角色会想要和你搬到一块住
CASE 4
	RESULTS:0 = 宴会的发生
	IF FLAG:宴会的有無
		RESULTS:1 = 有
	ELSE
		RESULTS:1 = 无
	ENDIF
CASE 5
	RESULTS:0 = 主动告白事件
	IF FLAG:告白禁止
		RESULTS:1 = 禁止角色向你告白
	ELSE
		RESULTS:1 = 允许角色向你告白
	ENDIF
CASE 6
	RESULTS:0 = 从炮友系陷落向恋慕系陷落变化
	IF FLAG:陥落路線変化
		RESULTS:1 = 会变化
	ELSE
		RESULTS:1 = 不会变化
	ENDIF
CASE 7
	RESULTS:0 = [无知]因成长而失去
	IF FLAG:無知自動喪失禁止
		RESULTS:1 = 不会失去
		RESULTS:2 = 无论经验如何，角色都会保留[无知]属性
	ELSE
		RESULTS:1 = 会失去
		RESULTS:2 = 角色因为经验的增长而失去[无知]属性
	ENDIF
CASE 8
	RESULTS:0 = 文本设定
	IF FLAG:情景文本設定 == 1 && FLAG:口上文本設定 == 2
		RESULTS:1 = 剧本台词＋情景文本
	ELSEIF FLAG:情景文本設定 == 1 && FLAG:口上文本設定 == -1
		RESULTS:1 = 仅情景文本
	ELSEIF FLAG:情景文本設定 == 0 && FLAG:口上文本設定 == -1
		RESULTS:1 = 不显示文本
	ENDIF
CASE 9
	RESULTS:0 = 对白颜色
	IF FLAG:口上色
		RESULTS:1 = 有
	ELSE
		RESULTS:1 = 无
	ENDIF
CASE 10
	RESULTS:0 = 显示所在地的清洁程度
	IF FLAG:垃圾显示OFF
		RESULTS:1 = 否
	ELSE
		RESULTS:1 = 是
	ENDIF
CASE 11
	RESULTS:0 = 对方行动时的描述
	IF FLAG:Counter地文控制
		RESULTS:1 = 个性化文本
		RESULTS:2 = 描述会根据角色的素质发生变化
	ELSE
		RESULTS:1 = 固定文本
		RESULTS:2 = 仅显示通用描述
	ENDIF
CASE 12
	RESULTS:0 = 描写胸部的文本
	IF FLAG:Look表示用乳房 == 1 && FLAG:Look表示用乳頭 == 1
		RESULTS:1 = 全部显示固定描述
	ELSEIF FLAG:Look表示用乳房 == 1 && FLAG:Look表示用乳頭 == 0
		RESULTS:1 = 仅固定乳房描写
	ELSEIF FLAG:Look表示用乳房 == 0 && FLAG:Look表示用乳頭 == 1
		RESULTS:1 = 仅固定乳头描写
	ELSEIF FLAG:Look表示用乳房 == 0 && FLAG:Look表示用乳頭 == 0
		RESULTS:1 = 全部显示个性化描述
	ENDIF
;CASE 13
;	RESULTS:0 = 好意/理性之壁の表示
;	IF FLAG:好意理性短縮
;		RESULTS:1 = 縮短
;	ELSE
;		RESULTS:1 = 不縮短
;	ENDIF
;	RESULTS:2 = ※現在未機能※
;CASE 14
;	RESULTS:0 = 能力表示的標記
;	IF FLAG:能力表記
;		RESULTS:1 = 数字標記
;	ELSE
;		RESULTS:1 = 記号標記
;	ENDIF
;	RESULTS:2 = ※現在未機能※
CASE 15
	RESULTS:0 = 每回合的ABLUP
	IF FLAG:ABLUP省略
		RESULTS:1 = 省略
	ELSE
		RESULTS:1 = 不省略
	ENDIF
CASE 16
	RESULTS:0 = 初吻的记录
	SELECTCASE FLAG:ＦＫ判定
	CASE 0
		RESULTS:1 = 仅在亲吻时・仅在有意识时
	CASE 1
		RESULTS:1 = 仅在亲吻时・无意识时也算
	CASE 2
		RESULTS:1 = 口交时也算・仅在有意识时
	CASE 3
		RESULTS:1 = 口交时也算・无意识时也算
	ENDSELECT
CASE 17
	RESULTS:0 = 是否会在移动时停下
	SELECTCASE FLAG:停下脚步
	CASE 0
		RESULTS:1 = 无条件停下脚步
	CASE 1
		RESULTS:1 = 只有在剧本存在的情况下会停下
	CASE 2
		RESULTS:1 = 不会停下脚步
	ENDSELECT
CASE 18
	RESULTS:0 = 遭遇时显示委托
	IF FLAG:依頼スルー
		RESULTS:1 = 不显示
	ELSE
		RESULTS:1 = 显示
	ENDIF
CASE 19
	RESULTS:0 = 消耗品自动购入
	IF FLAG:消耗品自動購入
		RESULTS:1 = 是
		RESULTS:2 = 只买润滑液
	ELSE
		RESULTS:1 = 否
	ENDIF
CASE 20
	RESULTS:0 = 自动时停移动
	SELECTCASE FLAG:瞬間移動
	CASE 0
		RESULTS:1 = ＯＦＦ
	CASE 1
		RESULTS:1 = 仅在外出散步时
	CASE 2
		RESULTS:1 = 仅在据点内移动时
	CASE 3
		RESULTS:1 = 外出/据点内均启用
	ENDSELECT
CASE 21
	RESULTS:0 = 逆推的机制
	SELECTCASE FLAG:推倒
	CASE 2
		RESULTS:1 = ＯＦＦ
		RESULTS:2 = 逆推不会发生
	CASE 1
		RESULTS:1 = 允许玩家通过力量拒绝逆推
		RESULTS:2 = 进行双方能力的比较，允许玩家拒绝逆推
	CASE 0
		RESULTS:1 = 不拒绝
		RESULTS:2 = 无法拒绝逆推
	ENDSELECT
CASE 22
	RESULTS:0 = 警告牌使用
	IF FLAG:警告牌使用
		RESULTS:1 = 有
	ELSE
		RESULTS:1 = 无
	ENDIF
	RESULTS:2 = 因为过分的性骚扰等而被警告，累积到一定程度游戏就会结束。
CASE 23
	RESULTS:0 = 来访模式
	SELECTCASE FLAG:交流来訪模式
	CASE 0
		RESULTS:1 = 正常
	CASE 1
		RESULTS:1 = 好感度50以下不会来访
	CASE 2
		RESULTS:1 = 仅限陷落的角色
	CASE 3
		RESULTS:1 = 不会来访
	ENDSELECT
	RESULTS:2 = 角色造访自家地区的条件
CASE 24
	RESULTS:0 = 脱衣指令的选择
	SELECTCASE FLAG:脱衣選択
	CASE 0
		RESULTS:1 = 每次都由自己决定
	CASE 1
		RESULTS:1 = 默认选择全脱
		RESULTS:2 = 不出现选择，每次都会脱光衣服
	CASE 2
		RESULTS:1 = 默认选择敞开衣服
		RESULTS:2 = 默认脱下内衣敞开衣物，只在没有内衣时询问是否脱光
	ENDSELECT
CASE 25
	RESULTS:0 = 时间停止时脱下内裤的选择
	SELECTCASE FLAG:胖次泥棒
	CASE 0
		RESULTS:1 = 每次都由自己决定
	CASE 1
		RESULTS:1 = 总是占为己有
	CASE 2
		RESULTS:1 = 总是单单脱掉
	ENDSELECT
CASE 26
	RESULTS:0 = 部分角色的长筒袜穿戴
	IF FLAG:長筒襪着用
		RESULTS:1 = 有
	ELSE
		RESULTS:1 = 无
	ENDIF
CASE 27
	RESULTS:0 = 增加色色的内衣
	IF FLAG:セクシー胖次追加
		RESULTS:1 = 增加
	ELSE
		RESULTS:1 = 不增加
	ENDIF
CASE 28
	RESULTS:0 = 胖次收集状况表示
	IF FLAG:胖次収集状況タイプB
		RESULTS:1 = 未入手的胖次也会显示
	ELSE
		RESULTS:1 = 仅显示入手的胖次
	ENDIF
CASE 29
	RESULTS:0 = 地图的显示模式
	IF RECOLOREDMAPS
		IF ANIMATERECOLOREDMAPS
			RESULTS:1 = 颜色地图 + 动画
		ELSE
			RESULTS:1 = 颜色地图
		ENDIF
	ELSE
		RESULTS:1 = 经典
	ENDIF
CASE 30
	RESULTS:0 = 角色列表表示顺序
	IF FLAG:並び替え
		RESULTS:1 = 原作顺
	ELSE
		RESULTS:1 = ＴＷ顺
	ENDIF
CASE 31
	RESULTS:0 = 放尿和漏尿癖的有效化
	IF FLAG:おもらし有効
		RESULTS:1 = 有效
	ELSE
		RESULTS:1 = 无效
	ENDIF
CASE 32
	RESULTS:0 = 采集时的再抽选功能
	IF FLAG:採集再抽選
		RESULTS:1 = 有效
	ELSE
		RESULTS:1 = 无效
	ENDIF
CASE 33
	RESULTS:0 = 根据日期和天气改变背景颜色
	IF FLAG:背景色推移
		RESULTS:1 = 有效
	ELSE
		RESULTS:1 = 无效
	ENDIF
CASE 34
	RESULTS:0 = 角色对你的制裁行动
	SELECTCASE FLAG:カウンター制裁
	CASE 0
		RESULTS:1 = 允许过激的惩罚
		RESULTS:2 = 一旦被抓住把柄就会偶尔发生
	CASE 1
		RESULTS:1 = 允许但不采取过激的惩罚
		RESULTS:2 = 不会使用道具进行惩罚
	CASE 2
		RESULTS:1 = 没有
	ENDSELECT
CASE 35
	RESULTS:0 = 肛门开发许可
	SELECTCASE FLAG:アナル開発無効
	CASE 0
		RESULTS:1 = 允许
	CASE 1
		RESULTS:1 = 禁止开发你
		RESULTS:2 = 禁止角色对玩家使用获得快A的Counter行动
	CASE 2
		RESULTS:1 = 禁止自我开发
		RESULTS:2 = 禁止角色对自身使用获得快A的Counter行动
	CASE 3
		RESULTS:1 = 都禁止
		RESULTS:2 = 禁止角色使用任何获得快A的Counter行动
	ENDSELECT
CASE 36
	RESULTS:0 = 日常的走光事件
	IF FLAG:風チラ発生
		RESULTS:1 = 有效
	ELSE
		RESULTS:1 = 无效
	ENDIF
CASE 37
	RESULTS:0 = 误爆插入的发生
	IF FLAG:誤爆無効
		RESULTS:1 = 无效
		RESULTS:2 = 推倒中对方不会不小心插入
	ELSE
		RESULTS:1 = 有效
		RESULTS:2 = 推倒中对方可能会不小心插入
	ENDIF
CASE 38
	RESULTS:0 = 高级角色信息列表
	IF 高级列表功能
		RESULTS:1 = 开启
	ELSE
		RESULTS:1 = 关闭
	ENDIF
	RESULTS:2 = 有着高级编辑功能的角色信息UI，可选替代原先的能力表示界面
CASE 41
	RESULTS:0 = 妊娠期间设定
	係数 = 1 + FLAG:妊娠日程倍率
	SELECTCASE 係数
		CASE 1
			RESULTS:1 = 通常
		CASEELSE
			RESULTS:1 '= @"%TOSTR(係数)%倍速"
	ENDSELECT

CASE 50
	RESULTS:0 = 好感度上升率
	IF FLAG:好感度上昇率 > 0
		RESULTS:1 = X {FLAG:好感度上昇率}
	ELSEIF FLAG:好感度上昇率 == 0
		RESULTS:1 = X 1
	ENDIF
CASE 51
	RESULTS:0 = 信赖度上升率
	IF FLAG:信頼度上昇率 > 0
		RESULTS:1 = X {FLAG:信頼度上昇率}
	ELSEIF FLAG:信頼度上昇率 == 0
		RESULTS:1 = X 1
	ENDIF

CASEELSE
	RESULT = 0
ENDSELECT
RETURN RESULT

@OPTION_SETTING(ARG)
CALL OPTION_SETNAME(ARG)
SELECTCASE RESULTS:0
CASE "怀孕的有无"
	FLAG:妊娠開关 = !FLAG:妊娠開关
CASE "文本设定"
	IF FLAG:情景文本設定 == 1 && FLAG:口上文本設定 == 2
		FLAG:情景文本設定 = 1
		FLAG:口上文本設定 = -1
	ELSEIF FLAG:情景文本設定 == 1 && FLAG:口上文本設定 == -1
		FLAG:情景文本設定 = 0
		FLAG:口上文本設定 = -1
	ELSEIF FLAG:情景文本設定 == 0 && FLAG:口上文本設定 == -1
		FLAG:情景文本設定 = 1
		FLAG:口上文本設定 = 2
	ENDIF
CASE "消耗品自动购入"
	FLAG:消耗品自動購入 = !FLAG:消耗品自動購入
CASE "显示所在地的清洁程度"
	FLAG:垃圾显示OFF = !FLAG:垃圾显示OFF
CASE "是否会在移动时停下"
	CALL オプション切り替え("停下脚步", 2)
CASE "逆推的机制"
	CALL オプション切り替え("推倒", 2)
CASE "自动时停移动"
	CALL オプション切り替え("瞬間移動", 3)
CASE "搬家事件"
	FLAG:住宿事件 = !FLAG:住宿事件
CASE "警告牌使用"
	FLAG:警告牌使用 = !FLAG:警告牌使用
CASE "部分角色的长筒袜穿戴"
	FLAG:長筒襪着用 = !FLAG:長筒襪着用
CASE "宴会的发生"
	FLAG:宴会的有無 = !FLAG:宴会的有無
CASE "来访模式"
	CALL オプション切り替え("交流来訪模式", 3)
CASE "脱衣指令的选择"
	CALL オプション切り替え("脱衣選択", 2)
CASE "时间停止时脱下内裤的选择"
	CALL オプション切り替え("胖次泥棒", 2)
CASE "对白颜色"
	FLAG:口上色 = !FLAG:口上色
CASE "描写胸部的文本"
	IF FLAG:Look表示用乳房 == 1 && FLAG:Look表示用乳頭 == 1
		FLAG:Look表示用乳房 = 1
		FLAG:Look表示用乳頭 = 0
	ELSEIF FLAG:Look表示用乳房 == 1 && FLAG:Look表示用乳頭 == 0
		FLAG:Look表示用乳房 = 0
		FLAG:Look表示用乳頭 = 1
	ELSEIF FLAG:Look表示用乳房 == 0 && FLAG:Look表示用乳頭 == 1
		FLAG:Look表示用乳房 = 0
		FLAG:Look表示用乳頭 = 0
	ELSEIF FLAG:Look表示用乳房 == 0 && FLAG:Look表示用乳頭 == 0
		FLAG:Look表示用乳房 = 1
		FLAG:Look表示用乳頭 = 1
	ENDIF
CASE "能力表示的标记"
	FLAG:能力表記 = !FLAG:能力表記
CASE "对方行动时的描述"
	FLAG:Counter地文控制 = !FLAG:Counter地文控制
CASE "增加色色的内衣"
	FLAG:セクシー胖次追加 = !FLAG:セクシー胖次追加
	IF !FLAG:セクシー胖次追加
		PRINTFORMW 席卷幻想乡的性感内衣热潮结束了
		PRINTL 
		PRINTFORML 要消除掉所拥有的性感内衣吗？
		PRINTFORML 削除对象：超短内裤、兜裆布、T型裤、透明女式短内裤、开放型女式短内裤、创可贴、衬裤（开档）
		PRINTL 
		PRINTL [0] 已经不需要了
		PRINTL [1] 珍藏起来
		INPUT
		IF RESULT == 0
			CALL DELETE_SEXYPANTS
			CLEARLINE 10
		ELSE
			CLEARLINE 8
		ENDIF
	ENDIF
CASE "从炮友系陷落向恋慕系陷落变化"
	FLAG:陥落路線変化 = !FLAG:陥落路線変化
CASE "主动告白事件"
	FLAG:告白禁止 = !FLAG:告白禁止
CASE "每回合的ABLUP"
	FLAG:ABLUP省略 = !FLAG:ABLUP省略
CASE "[无知]因成长而失去"
	FLAG:無知自動喪失禁止 = !FLAG:無知自動喪失禁止
CASE "好意/理性之壁の表示"
	FLAG:好意理性短縮 = !FLAG:好意理性短縮
CASE "儿童的描写"
	FLAG:兒童描写 = !FLAG:兒童描写
CASE "儿童的性别"
	CALL オプション切り替え("兒童の性別", 2)
CASE "胖次收集状况表示"
	FLAG:胖次収集状況タイプB = !FLAG:胖次収集状況タイプB
CASE "初吻的记录"
	CALL オプション切り替え("ＦＫ判定", 3)
CASE "遭遇时显示委托"
	FLAG:依頼スルー = !FLAG:依頼スルー
CASE "地图的显示模式"
	IF RECOLOREDMAPS
		IF ANIMATERECOLOREDMAPS
			RECOLOREDMAPS = 0
			ANIMATERECOLOREDMAPS = 0
		ELSE
			ANIMATERECOLOREDMAPS = 1000
		ENDIF
	ELSE
		RECOLOREDMAPS = 1
	ENDIF
CASE "角色列表表示顺序"
	FLAG:並び替え = !FLAG:並び替え
CASE "放尿和漏尿癖的有效化"
	FLAG:おもらし有効 = !FLAG:おもらし有効
CASE "采集时的再抽选功能"
	FLAG:採集再抽選 = !FLAG:採集再抽選
CASE "根据日期和天气改变背景颜色"
	FLAG:背景色推移 = !FLAG:背景色推移
CASE "角色对你的制裁行动"
	CALL オプション切り替え("カウンター制裁", 2)
CASE "肛门开发许可"
	CALL オプション切り替え("アナル開発無効", 3)
CASE "日常的走光事件"
	FLAG:風チラ発生 = !FLAG:風チラ発生
CASE "误爆插入的发生"
	FLAG:誤爆無効 = !FLAG:誤爆無効
CASE "高级角色信息列表"
	高级列表功能 = !高级列表功能
CASE "妊娠期间设定"
	IF GETBIT(ACCELERATE_PREGANCY_AND_RAISING_CHILD, 0) && !GETBIT(ACCELERATE_PREGANCY_AND_RAISING_CHILD, 10)
		SETCOLOR C_YELLOW
		FONTBOLD
		PRINTFORML 注意：4.943版本以后eratw更新的自带的妊娠育儿加速与“加速怀孕和育儿”补丁部分功能相冲突
		FONTREGULAR
		RESETCOLOR
		PRINTL
		PRINTFORML 检测到“加速怀孕和育儿”补丁开启
		PRINTFORML 若需应用“妊娠期间设定”功能，建议关闭“加速怀孕和育儿”补丁
		PRINTFORML 是否需要关闭补丁？
		VARSET RESULT
		PRINTFORML [0] 是
		PRINTFORML [1] 否
		PRINTFORML [2] 取消
		INPUT
		IF RESULT == 0
			VARSET ACCELERATE_PREGANCY_AND_RAISING_CHILD
			VARSET RATE_OF_PREGNANCY_ACCELERATION
			VARSET RATE_OF_CHILD_RAISING_ACCELERATION
			VARSET DEBUG_INGNOREANCE_CHARACTER_VALUE_SAVE
			PRINTFORMW 补丁已关闭
		ELSEIF RESULT == 1
			SETCOLOR C_YELLOW
			FONTBOLD
			PRINTFORMW 未关闭补丁，可能会出现意料之外的运行状况
			FONTREGULAR
			RESETCOLOR
			PRINTFORML 是否关闭本冲突提示？（若需重置本设定，请点击加速怀孕和育儿补丁设置页面的“关闭”按键）
			VARSET RESULT
			CALL ASK_YN
			IF RESULT == 0
				SETBIT ACCELERATE_PREGANCY_AND_RAISING_CHILD, 10
				SETCOLOR C_YELLOW
				FONTBOLD
				PRINTFORMW 已设置不再提醒“妊娠期间设定”与“加速怀孕和育儿”补丁存在冲突
				FONTREGULAR
				RESETCOLOR
			ELSE
				PRINTFORMW 未关闭“妊娠期间设定”与“加速怀孕和育儿”补丁冲突提示
			ENDIF
		ELSE
			RETURN
		ENDIF
	ENDIF
	CALL オプション切り替え("妊娠日程倍率", 3)

CASE "好感度上升率"
	INPUT
	FLAG:好感度上昇率 = LIMIT(RESULT, 0, 20)
CASE "信赖度上升率"
	INPUT
	FLAG:信頼度上昇率 = LIMIT(RESULT, 0, 10)

ENDSELECT

@オプション切り替え(ARGS, ARG)
FLAG:ARGS ++
SIF FLAG:ARGS > ARG
	FLAG:ARGS = 0

@OPTION_自動喘息一括設定
CALL ASK_M("全员无效",1,"全员有效（无下流）",1,"全员有效（有下流）",1, "中止",1)
IF INRANGE(RESULT, 0, 2)
	FOR LOCAL,1,CHARANUM
		CFLAG:LOCAL:自動喘息 = RESULT
	NEXT
	PRINTFORMW 统一设定了
ENDIF

@登場人物的切换
PRINTL 　被指定的人物将不会登场。（"*"为有剧本的角色）
DRAWLINE
SIF FLAG:並び替え
	CALL CHARASORT_EX
FOR LOCAL:1, 1, EX_CHARANUM
	IF FLAG:並び替え
		LOCAL = RESULT:(LOCAL:1)
		SIF LOCAL < 0
			CONTINUE
	ELSE
		LOCAL = LOCAL:1
		SIF LOCAL >= CHARANUM
			BREAK
	ENDIF
	SIF TALENT:LOCAL:路人
		CONTINUE
	SIF CFLAG:LOCAL:出禁
		SETCOLOR C_D_RED
	RESULT = 0
	TRYCALLFORM M_KOJO_K{NO:LOCAL}
	PRINTFORMLC [{LOCAL, 3}] \@ RESULT ? * # \@%CALLNAME:LOCAL%
	RESETCOLOR
NEXT
PRINTL 　
DRAWLINE
PRINTFORML [999] 设定结束　出禁人物：{FLAG:出禁人数}人　　[997]无剧本角色批量禁用　[998]重置
$LOOP
INPUT
IF RESULT >= 1 && RESULT <= CHARANUM
	IF CFLAG:RESULT:出禁 && FLAG:扮演 != RESULT
		CFLAG:RESULT:出禁 = 0
		SIF CFLAG:RESULT:現在位置 == CFLAG:RESULT:初期位置 && CFLAG:RESULT:睡眠
			LOCK:(CFLAG:RESULT:初期位置) = 1
		FLAG:出禁人数 --
	ELSEIF TALENT:RESULT:路人
		PRINTFORMW 这个角色不能解除出禁
		GOTO LOOP
	ELSEIF FLAG:扮演 == RESULT
		IF CFLAG:(FLAG:扮演):出禁 == 1
			PRINTFORML 要扮演现在这个人物的话、将会变成两个%CALLNAME:RESULT%同时存在的情况…
			PRINTL 即使这样也要登场吗？
			CALL ASK_YN
			IF RESULT == 0
				CFLAG:(FLAG:扮演):出禁 = 0
				SIF CFLAG:(FLAG:扮演):現在位置 == CFLAG:(FLAG:扮演):初期位置 && CFLAG:(FLAG:扮演):睡眠
					LOCK:(CFLAG:(FLAG:扮演):初期位置) = 1
				FLAG:出禁人数 --
				PRINTFORMW %CALLNAME:(FLAG:扮演)%将登场
			ELSE
				GOTO LOOP
			ENDIF
		ELSE
			CFLAG:(FLAG:扮演):出禁 = 1
			SIF LOCK:(CFLAG:(FLAG:扮演):初期位置) && CFLAG:(FLAG:扮演):初期位置 < 100
				LOCK:(CFLAG:(FLAG:扮演):初期位置) = 0
			FLAG:出禁人数 ++
		ENDIF
	ELSE
		CFLAG:RESULT:出禁 = 1
		SIF LOCK:(CFLAG:RESULT:初期位置) && CFLAG:RESULT:初期位置 < 100
			LOCK:(CFLAG:RESULT:初期位置) = 0
		FLAG:出禁人数 ++
	ENDIF
	RESTART
ELSEIF RESULT == 999
	PRINTW 设定完毕
ELSEIF RESULT == 998
	FOR LOCAL,1,CHARANUM
		SIF FLAG:扮演 == LOCAL
			CONTINUE
		SIF TALENT:LOCAL:路人
			CONTINUE
		CFLAG:LOCAL:出禁 = 0
	NEXT
	;FLAG:出禁人数 = 0
	FLAG:出禁人数 = CMATCH(CFLAG:出禁, 2, 1, CHARANUM-1)
	RESTART
ELSEIF RESULT == 997
	FOR LOCAL,1,CHARANUM
		SIF FLAG:扮演 == LOCAL
			CONTINUE
		SIF TALENT:LOCAL:路人
			CONTINUE
		IF !CFLAG:LOCAL:口上セレクタ
			TRYCCALLFORM M_KOJO_K{NO:LOCAL}
			CATCH
				IF CFLAG:LOCAL:出禁 == 0
					CFLAG:LOCAL:出禁 = 1
					FLAG:出禁人数 ++
				ENDIF
			ENDCATCH
		ELSE
			TRYCCALLFORM M_KOJO_K{NO:LOCAL}_{CFLAG:LOCAL:口上セレクタ}
			CATCH
				IF CFLAG:LOCAL:出禁 == 0
					CFLAG:LOCAL:出禁 = 1
					FLAG:出禁人数 ++
				ENDIF
			ENDCATCH
		ENDIF
;			PRINTFORM %CALLNAME:LOCAL% {FLAG:出禁人数} ／
	NEXT
	RESTART
ELSE
	GOTO LOOP
ENDIF

;簡易过滤器制御関数
;仕様上の問題として、設定項目が増えすぎると設計をしなおす必要がある
@簡易过滤器一括設定
#DIM OP_CFLAG_Ｖ過濾器
#DIM OP_CFLAG_Ａ過濾器
#DIM OP_CFLAG_ＳＭ過濾器
#DIM OP_CFLAG_奉仕過濾器
#DIM OP_CFLAG_性騷擾過濾器
#DIM OP_SETTING
IF !LOCAL
	OP_CFLAG_Ｖ過濾器       = GETNUM(CFLAG, "Ｖ過濾器")
	OP_CFLAG_Ａ過濾器       = GETNUM(CFLAG, "Ａ過濾器")
	OP_CFLAG_ＳＭ過濾器     = GETNUM(CFLAG, "ＳＭ過濾器")
	OP_CFLAG_奉仕過濾器     = GETNUM(CFLAG, "奉仕過濾器")
	OP_CFLAG_性騷擾過濾器 = GETNUM(CFLAG, "性騷擾過濾器")
	LOCAL ++
ENDIF
PRINTFORML ～简易过滤设定～
PRINTFORML ※这里和单独个别指令的设定是不一样的
PRINTFORML 
PRINTFORML [29] 全角色的全简易过滤OFF
PRINTFORML [28] 开放全角色的[Ｖ]
PRINTFORML [27] 开放全角色的[Ａ]
PRINTFORML [26] 开放全角色的[ＳＭ]
PRINTFORML [25] 开放全角色的[侍奉]
PRINTFORML [24] 开放全角色的[性骚扰]
PRINTFORML [23] 开放[家事]
PRINTFORML 
PRINTFORML [19] 全角色的全简易过滤ON
PRINTFORML [18] 限制全角色的[Ｖ]
PRINTFORML [17] 限制全角色的[Ａ]
PRINTFORML [16] 限制全角色的[ＳＭ]
PRINTFORML [15] 限制全角色的[侍奉]
PRINTFORML [14] 限制全角色的[性骚扰]
PRINTFORML [13] 限制[家事]
PRINTFORML 
PRINTFORML [99] 设定结束
DO
	INPUT
	OP_SETTING = (RESULT / 10) - 1
	;20で1、10で0
	SELECTCASE RESULT
	CASE 29, 19
		CVARSET CFLAG, OP_CFLAG_Ｖ過濾器, OP_SETTING
		CVARSET CFLAG, OP_CFLAG_Ａ過濾器, OP_SETTING
		CVARSET CFLAG, OP_CFLAG_ＳＭ過濾器, OP_SETTING
		CVARSET CFLAG, OP_CFLAG_奉仕過濾器, OP_SETTING
		CVARSET CFLAG, OP_CFLAG_性騷擾過濾器, OP_SETTING
		FLAG:家事過濾器 = OP_SETTING
		IF OP_SETTING
			PRINTFORMW 全角色的所有簡易过滤都设为＜OFF＞了
		ELSE
			PRINTFORMW 全角色的所有簡易过滤都设为＜ON＞了
		ENDIF
	CASE 28, 18
		CVARSET CFLAG, OP_CFLAG_Ｖ過濾器, OP_SETTING
		IF OP_SETTING
			PRINTFORMW 全角色的[Ｖ]指令设为＜开放＞了
		ELSE
			PRINTFORMW 全角色的[Ｖ]指令设为＜限制＞了
		ENDIF
	CASE 27, 17
		CVARSET CFLAG, OP_CFLAG_Ａ過濾器, OP_SETTING
		IF OP_SETTING
			PRINTFORMW 全角色的[Ａ]指令设为＜开放＞了
		ELSE
			PRINTFORMW 全角色的[Ａ]指令设为＜限制＞了
		ENDIF
	CASE 26, 16
		CVARSET CFLAG, OP_CFLAG_ＳＭ過濾器, OP_SETTING
		IF OP_SETTING
			PRINTFORMW 全角色的[ＳＭ]指令设为＜开放＞了
		ELSE
			PRINTFORMW 全角色的[ＳＭ]指令设为＜限制＞了
		ENDIF
	CASE 25, 15
		CVARSET CFLAG, OP_CFLAG_奉仕過濾器, OP_SETTING
		IF OP_SETTING
			PRINTFORMW 全角色的[侍奉]指令设为＜开放＞了
		ELSE
			PRINTFORMW 全角色的[侍奉]指令设为＜限制＞了
		ENDIF
	CASE 24, 14
		CVARSET CFLAG, OP_CFLAG_性騷擾過濾器, OP_SETTING
		IF OP_SETTING
			PRINTFORMW 全角色的[性骚扰]指令设为＜开放＞了
		ELSE
			PRINTFORMW 全角色的[性骚扰]指令设为＜限制＞了
		ENDIF
	CASE 23, 13
		FLAG:家事過濾器 = OP_SETTING
		IF OP_SETTING
			PRINTFORMW 家事指令设为＜开放＞了
		ELSE
			PRINTFORMW 家事指令设为＜限制＞了
		ENDIF
	CASE 99
		PRINTFORMW 简易过滤的设定完成了
		RETURN
	CASEELSE
		PRINTFORML 无效输入
		CONTINUE
	ENDSELECT
	BREAK
LOOP 1
RESTART

;---------------------------------------------------------------
;	画像詳細設定
;	設定が増えたので新設
;---------------------------------------------------------------
@OPTION_画像表示詳細設定
#DIM nLineCnt_Head ;for correct clearline
nLineCnt_Head = LINECOUNT
REDRAW 0
DO ;no spacing for inside the loop to make merging easier

SIF LINECOUNT - nLineCnt_Head > 0
	CLEARLINE LINECOUNT - nLineCnt_Head
TARGET = [[霊夢]]
FOR LOCAL, 1, 11
	TARGET:LOCAL = LOCAL
NEXT
PRINTL ▼示例▼
CALL PRINT_TARGET_IMAGE(TARGET)
PRINTL 
PRINTFORML [ 0] - 画像\@ FLAG:画像表示 ? 显示 # 不显示 \@
IF FLAG:画像表示
	PRINTFORML [ *]┬ 现在的\@ FLAG:立絵種類 ? 颜绘 # 立绘 \@的显示人数 {FLAG:画像表示}人
	PRINTFORML [ 1]├ 增加
	PRINTFORML [ 2]└ 减少
	PRINTFORML [ 3] - 角色画像的类型 \@ FLAG:立絵種類 ? [颜] # [立绘] \@
	PRINTFORML [ 4]┬ 图片尺寸：[\@ FLAG:多尺寸? 保持图片原尺寸（※可能出现大小不一） # 统一调整至{默认角色画像横幅}px \@]
	PRINTFORML [ 5]├ 扩大：[缩放比例：{100 + isModifier}％]
	PRINTFORML [ 6]└ 缩小：[缩放比例：{100 + isModifier}％]
	PRINTFORML [ 7] - \@ FLAG:立絵種類 ? 颜绘 # 立绘 \@的显示位置 [\@ FLAG:画像表示位置 ? 左对齐 # 中央对齐 \@]
	PRINTFORML [ 8] - 图像边框显示 \@ FLAG:画像枠表示 ? ON # OFF \@
	PRINTFORML [ 9] - \@ FLAG:巨乳差分表示? 显示可用的巨乳差分# 不显示巨乳差分\@
	PRINTFORML [10] - \@ FLAG:追加エフェクト? 显示追加特效# 不显示追加特效\@
	PRINTFORML [11] - 就寝后的自慰立绘 \@ FLAG:自慰画像非表示? 不显示# 显示\@
	PRINTFORML [12] - 对话或事件中的配图 \@ 顔絵付きメッセージ ? 显示 # 关闭\@
	PRINTL 
	PRINTFORML [13]\@ FLAG:挿絵表示 ? ┬ # %" "%- \@ 显示插入配图 \@ FLAG:挿絵表示 ? ON # OFF \@
	IF FLAG:挿絵表示
		PRINT [14]└ 射精画像的种类
		SELECTCASE FLAG:射精画像種類
			CASE 0
				PRINTL  [断面＋效果音]
			CASE 1
				PRINTL  [只有断面]
			CASE 2
				PRINTL  [只有效果音]
		ENDSELECT
	ELSE
		PRINTL 
	ENDIF
	PRINTFORML [15]\@ FLAG:アニメーション ? ┬ # %" "%- \@动画设定 \@ FLAG:アニメーション ? 重复 {FLAG:アニメーション}回 # OFF \@
	IF FLAG:アニメーション
		PRINTFORML [16]└插入中每回合的动画显示：\@FLAG:挿入アニメ过滤器 & アニメ过滤器_挿入 ? [×]# [〇]\@
	ELSE
		PRINTL 
	ENDIF
ELSE
	FOR LOCAL, 1, 13
		PRINTL 
	NEXT
ENDIF
PRINTFORML [999] - 返回
INPUT
IF RESULT == 999
	VARSET TARGET
	SETANIMETIMER 32627
	RETURN 0
ELSEIF RESULT == 0
	;SIF FLAG:画像表示 ;store previous value if it's going to be disabled
	;	nStoreImageCount = FLAG:画像表示
		
	FLAG:画像表示 = !FLAG:画像表示
	
	;SIF FLAG:画像表示 && nStoreImageCount > 0 ;restore previous value if enabled
	;	FLAG:画像表示 = nStoreImageCount
ELSEIF RESULT == 1
	FLAG:画像表示++
	SIF FLAG:画像表示 > 10
		FLAG:画像表示 = 10
ELSEIF RESULT == 2
	FLAG:画像表示--
	SIF FLAG:画像表示 < 1
		FLAG:画像表示 = 1
ELSEIF RESULT == 3
	FLAG:立絵種類 = !FLAG:立絵種類
ELSEIF RESULT == 4
	IF FLAG:多尺寸
		FLAG:多尺寸 = !FLAG:多尺寸
		默认角色画像横幅 = 180
	ELSEIF 默认角色画像横幅 == 180
		默认角色画像横幅 = 270
	ELSEIF 默认角色画像横幅 == 270
		默认角色画像横幅 = 360
	;ELSEIF 默认角色画像横幅 == 360
	;	默认角色画像横幅 = 540
	;ELSEIF 默认角色画像横幅 == 540
	;	默认角色画像横幅 = 720
	ELSE
		;FLAG:多尺寸 = !FLAG:多尺寸
		默认角色画像横幅 = 180
	ENDIF
ELSEIF RESULT == 5
	isModifier = MIN(isModifier + 10, 50)
ELSEIF RESULT == 6
	isModifier = MAX(isModifier - 10, -50)
ELSEIF RESULT == 7
	FLAG:画像表示位置 = !FLAG:画像表示位置
ELSEIF RESULT == 8
	FLAG:画像枠表示 = !FLAG:画像枠表示
ELSEIF RESULT == 9
	FLAG:巨乳差分表示 = !FLAG:巨乳差分表示
ELSEIF RESULT == 10
	FLAG:追加エフェクト = !FLAG:追加エフェクト
ELSEIF RESULT == 11
	FLAG:自慰画像非表示 = !FLAG:自慰画像非表示
ELSEIF RESULT == 12
	INVERTBIT 顔絵付きメッセージ, 0
ELSEIF RESULT == 13
	FLAG:挿絵表示 = !FLAG:挿絵表示
ELSEIF RESULT == 14 && FLAG:挿絵表示
	FLAG:射精画像種類++
	SIF FLAG:射精画像種類 >= 3
		FLAG:射精画像種類 = 0
ELSEIF RESULT == 15
	FLAG:アニメーション++
	SIF FLAG:アニメーション >= 6
		FLAG:アニメーション = 0
ELSEIF RESULT == 16
	INVERTBIT FLAG:挿入アニメ过滤器, 0
	;CALL ASK_M("挿入表示切替",1,"絶頂表示切替",1,"射精表示切替",1,"射精表示切替",1,"返回",1)
	;SIF RESULT == 4
	;	RESTART
	;INVERTBIT FLAG:挿入アニメ过滤器, RESULT
	RESTART
ENDIF
LOOP 1

;カウンター行動フィルタ
@DEBUG_COUNTER_FILTER
#DIM CCOM_ID
#DIM L_FEED
#DIM TEMP
TEMP = CURRENTREDRAW()
REDRAW 0
LOADGLOBAL
CALL COLORMESSAGE, "※ 过滤过度可能会导致不良现象。 ※", C_YELLOW, 2
DRAWLINE
DO
	L_FEED = LINECOUNT
	FOR CCOM_ID, 0, CCOM_MAXNUM
		SIF STR:(4000 + CCOM_ID) == ""
			CONTINUE
		CALL PRINTBUTTON_EXC(@"%STR_TR(4000 + CCOM_ID)%[{CCOM_ID,3}]", CCOM_ID, CCOM_FILTER:CCOM_ID)
	NEXT
	PRINTL 
	DRAWLINE
	PRINTL [500] - 全部无效
	PRINTL [501] - 复位
	PRINTL 
	PRINTL [999] - 决定
	INPUT
	SELECTCASE RESULT
	CASE 0 TO CCOM_MAXNUM
		SIF STR:(4000 + RESULT) != ""
			CCOM_FILTER:RESULT = !(CCOM_FILTER:RESULT)
	CASE 500, 501
		VARSET CCOM_FILTER, (RESULT == 500)
	CASE 999
		SAVEGLOBAL
		REDRAW TEMP
		RETURN
	ENDSELECT
	CLEARLINE LINECOUNT - L_FEED
LOOP 1

;指定依頼発生
@DEBUG_IRAI_HAPPEN
#DIM CNT
#DIM CHR
#DIM RDW
#DIM HAPPEN
#DIM TEMP
RDW = CURRENTREDRAW()
REDRAW 0
DRAWLINE
SIF FLAG:並び替え
	CALL CHARASORT_EX
FOR CNT, 1, EX_CHARANUM
	IF FLAG:並び替え
		LOCAL = RESULT:CNT
		SIF LOCAL < 0
			CONTINUE
	ELSE
		LOCAL = CNT
		SIF LOCAL >= CHARANUM
			BREAK
	ENDIF
	SIF TALENT:LOCAL:行きずり
		CONTINUE
	SIF !CAN_MEET(LOCAL)
		CONTINUE
	RESULT = 0
	PRINTFORMLC [{LOCAL, 3}] %CALLNAME:LOCAL%
NEXT
PRINTFORMLC [999] 取消
PRINTL 　
DRAWLINE
PRINTFORML 请选择发起委托的角色
CALL COLORMESSAGE, "※ 当前正在进行的委托将会全部丢失 ※", C_YELLOW, 1
DO
	INPUT
	SELECTCASE RESULT
		CASE 1 TO RANDOM_CHARANUM -1
			IF CAN_MEET(RESULT)
				CHR = RESULT
				BREAK
			ENDIF
		CASE 999
			REDRAW RDW
			RETURN
	ENDSELECT
	CLEARLINE 1
LOOP 1

DRAWLINE
CVARSET CFLAG, 403
CVARSET CFLAG, 404
VARSET IRAI_SLOT
VARSET IRAI_DATA
VARSET IRAI_SUB
VARSET IRAI_DEADLINE
VARSET IRAI_CLIENT
VARSET IRAI_EX
VARSET LOCAL
TEMP = 0

PRINTFORML 请选择要发生的委托
PRINTFORML 无法选择不满足发生条件的委托
FOR CNT, 1, 100
	SIF !INT_DATA_IRAI(MAKE_IRAI_ID(CHR, CNT), "依頼発生条件", CHR)
		CONTINUE
	CALL KOJO_CHECK(CHR, "IRAI_BLOCKED", , CNT)
	SIF RESULT == 1
		CONTINUE
	PRINTFORML [{CNT, 3}] %STR_DATA_IRAI(CNT, "依頼内容表示", CHR)%
	LOCAL:TEMP = CNT
	TEMP ++
NEXT
IF GET_INT(0, "キャラデータ", CHR, "固有依頼最終要素")
	FOR CNT, 101, GET_INT(0, "キャラデータ", CHR, "固有依頼最終要素") + 1
		SIF !INT_DATA_IRAI(MAKE_IRAI_ID(CHR, CNT), "依頼発生条件", CHR)
			CONTINUE
		CALL KOJO_CHECK(CHR, "IRAI_BLOCKED", , CNT)
		SIF RESULT == 1
			CONTINUE
		PRINTFORML [{CNT}] %STR_DATA_IRAI(CNT, "依頼内容表示", CHR)%
		LOCAL:TEMP = CNT
		TEMP ++
	NEXT
ENDIF
DO
	INPUT
	IF FINDELEMENT(LOCAL, RESULT) > -1 && RESULT
		HAPPEN = RESULT
		BREAK
	ENDIF
	CLEARLINE 1
LOOP 1
CALL SET_IRAI_PROGRESS(CHR, "依頼発生直後")
CFLAG:CHR:依頼内容 = MAKE_IRAI_ID(CHR, HAPPEN)

REDRAW RDW
RETURN 1

[SKIPSTART]
;;料理オプション設定関数
;;0:手動決定,1:異物不可,2:薬物不可
;@SET_OPTION_COOKING
;#DIM CONST OP_BIT_手動決定 = 0
;#DIM CONST OP_BIT_異物不可 = 1
;#DIM CONST OP_BIT_薬物不可 = 2
;#DIM CONST OP_BIT_打分オン = 3
;PRINTFORML 設定料理関連的選項
;PRINTFORML 現在的設定
;IF GET_OPTION_COOKING("料理内容手動決定")
;	PRINTFORML 　料理内容：手動決定
;ELSE
;	PRINTFORML 　料理内容：自動決定
;ENDIF
;IF GET_OPTION_COOKING("異物混入不可")
;	PRINTFORML 　異物混入：否
;ELSE
;	PRINTFORML 　異物混入：是
;ENDIF
;IF GET_OPTION_COOKING("薬物混入不可")
;	PRINTFORML 　薬物混入：否
;ELSE
;	PRINTFORML 　薬物混入：是
;ENDIF
;SELECTCASE GET_OPTION_COOKING("自動味付け設定")
;CASE 0
;	PRINTFORML 　自動味付：否
;CASE 10
;	PRINTFORML 　自動味付：自動设为普通口味
;CASE 11
;	PRINTFORML 　自動味付：自動设为浓厚口味
;CASE 12
;	PRINTFORML 　自動味付：自動设为清淡口味
;ENDSELECT
;IF GET_OPTION_COOKING("打分機能オフ")
;	PRINTFORML 　打分機能：OFF
;ELSE
;	PRINTFORML 　打分機能：ON
;ENDIF
;DO
;	PRINTFORML [9] 料理内容の設定変更
;	PRINTFORML [8] 異物混入の設定変更
;	PRINTFORML [7] 薬物混入の設定変更
;	PRINTFORML [6] 自動味付の設定変更
;	PRINTFORML [5] 打分機能の設定変更
;	PRINTFORML 
;	PRINTFORML 
;	PRINTFORML [0] 決定
;	INPUT
;	SELECTCASE RESULT
;	CASE 9
;		INVERTBIT FLAG:料理オプション, OP_BIT_手動決定
;	CASE 8
;		INVERTBIT FLAG:料理オプション, OP_BIT_異物不可
;	CASE 7
;		INVERTBIT FLAG:料理オプション, OP_BIT_薬物不可
;	CASE 6
;		PRINTFORML 自動調味的機能設定
;		PRINTFORML [12] 自動調味＜薄味＞
;		PRINTFORML [11] 自動調味＜浓郁＞
;		PRINTFORML [10] 自動調味＜普通の味＞
;		PRINTFORML [ 0] 不使用自動調味
;		DO
;			INPUT
;			SELECTCASE RESULT
;			CASE 10, 11, 12
;				CLEARBIT FLAG:料理オプション, 10, 11, 12
;				SETBIT   FLAG:料理オプション, RESULT
;			CASE 0
;				CLEARBIT FLAG:料理オプション, 10, 11, 12
;			CASEELSE
;			PRINTFORML 无效输入
;			CONTINUE
;			ENDSELECT
;			BREAK
;		LOOP 1
;	CASE 5
;		INVERTBIT FLAG:料理オプション, OP_BIT_打分オン
;	CASE 0
;		RETURN 
;	CASEELSE
;		PRINTFORML 无效输入
;		CONTINUE
;	ENDSELECT
;	BREAK
;LOOP 1
;RESTART
;
;
;;料理オプションの内容取得関数
;@GET_OPTION_COOKING(TYPE)
;#FUNCTION
;#DIMS DYNAMIC TYPE
;#DIM CONST OP_BIT_手動決定 = 0
;#DIM CONST OP_BIT_異物混入不可 = 1
;#DIM CONST OP_BIT_薬物混入不可 = 2
;#DIM CONST OP_BIT_打分機能オン = 3
;#DIM CONST OP_BIT_自動味付け_普通 = 10
;#DIM CONST OP_BIT_自動味付け_濃い = 11
;#DIM CONST OP_BIT_自動味付け_单薄的 = 12
;SELECTCASE TYPE
;CASE "料理内容手動決定"
;	SIF GETBIT(FLAG:料理オプション, OP_BIT_手動決定)
;		RETURNF 1
;CASE "料理内容自動決定"
;	RETURNF !GET_OPTION_COOKING("料理内容手動決定")
;
;CASE "異物混入不可"
;	SIF GETBIT(FLAG:料理オプション, OP_BIT_異物混入不可)
;		RETURNF 1
;CASE "異物混入可能"
;	RETURNF !GET_OPTION_COOKING("異物混入不可")
;
;CASE "薬物混入不可"
;	SIF GETBIT(FLAG:料理オプション, OP_BIT_薬物混入不可)
;		RETURNF 1
;CASE "薬物混入可能"
;	RETURNF !GET_OPTION_COOKING("薬物混入不可")
;CASE "自動味付け設定"
;	SIF GETBIT(FLAG:料理オプション, OP_BIT_自動味付け_普通)
;		RETURNF OP_BIT_自動味付け_普通
;	SIF GETBIT(FLAG:料理オプション, OP_BIT_自動味付け_濃い)
;		RETURNF OP_BIT_自動味付け_濃い
;	SIF GETBIT(FLAG:料理オプション, OP_BIT_自動味付け_单薄的)
;		RETURNF OP_BIT_自動味付け_单薄的
;CASE "打分機能オン"
;	SIF GETBIT(FLAG:料理オプション, OP_BIT_打分機能オン)
;		RETURNF 1
;CASE "打分機能オフ"
;	RETURNF !GET_OPTION_COOKING("打分機能オン")
;CASEELSE
;	THROW 不明なTYPE%TYPE%
;ENDSELECT
;RETURNF 0
[SKIPEND]


@SIMATTYAUOJISAN
;TW414_移動パッチにて右クリックやマクロによるスキップもしまっちゃうように修正
;PRINTでのウェイトをなくすかわりにFORCEWAITでスキップ解除するように
;
FORCEWAIT
PRINT 「输入画面上不可能显示的数字的孩子就在这吗？」
FORCEWAIT
PRINT 没收叔叔出现了！
FORCEWAIT
PRINT 「那么、输入画面上不可能显示的数字的孩子的钱、就这样没收了哦」
FORCEWAIT
PRINTFORM 没收叔叔把%CALLNAME:MASTER%的现金没收了
MONEY = 0
MONEY:2 = 0
FORCEWAIT
PRINT 「输入画面上不可能显示的数字的孩子的道具也、就这样没收了哦」
FORCEWAIT
PRINTFORM %CALLNAME:MASTER%的道具被没收了
;FOR LOCAL,0,1000
;	ITEM:LOCAL = 0
;NEXT
FORCEWAIT
PRINT 「输入画面上不可能显示的数字的孩子的技巧也、就这样没收了哦」
FORCEWAIT
PRINTFORM %CALLNAME:MASTER%的技巧被没收了
FORCEWAIT
;FOR LOCAL,0,100
;	ABL:0:LOCAL = 0
;NEXT
;FOR LOCAL,46,51
;	TALENT:0:LOCAL = 0
;NEXT
PRINT 「输入画面上不可能显示的数字的孩子的经验也、就这样没收了哦」
FORCEWAIT
PRINTFORM %CALLNAME:MASTER%的经验被没收了
FORCEWAIT
;FOR LOCAL,0,100
;	EXP:0:LOCAL = 0
;NEXT
;せっかくだし收藏もしまっちゃいましょう（eraTW4.15胖次対策パッチにて追加）
;
PRINT 「输入画面上不可能显示的数字的孩子的内裤藏品也、就这样没收了哦」
FORCEWAIT
PRINTFORM %CALLNAME:MASTER%殚精竭智收集到的宝贝般的内裤藏品被没收了
FORCEWAIT
;FLAG:700 = 0
;FOR LOCAL,1,CHARANUM
;	FOR LOCAL:1,0,MAXPANTS
;		CFLAG:LOCAL:(LOCAL:1 + 胖次管理) = 0
;	NEXT
;NEXT
PRINT 「输入画面上不可能显示的数字的孩子的信赖也、就这样没收了哦」
FORCEWAIT
PRINTFORM %CALLNAME:MASTER%获得的少女们的信赖被没收了
;FOR LOCAL,1,CHARANUM
;	CFLAG:LOCAL:信賴度 = 0
;NEXT
FORCEWAIT

PRINT 「输入画面上不可能显示的数字的孩子的能力值也、就这样没收了哦」
FORCEWAIT
PRINTFORM %CALLNAME:MASTER%的能力被没收了
;FOR LOCAL,0,9
	;最大体力算了,这样只能读档了
	; MAXBASE:0:LOCAL = 99
;	BASE:0:LOCAL = min(99,BASE:0:LOCAL)
;NEXT
; さすがにやり過ぎは良くないと思ったのでクリック連打で許してみた
; 作者様へ　勝手に当てたパッチですので不愉快でしたらこの部分だけパッチ前に戻してくださって構いません。
;
;
FORCEWAIT
PRINTW 不要再这样啦～～～～


;セクシー胖次削除
@DELETE_SEXYPANTS
#DIM 対象
#DIM 種類
#DIM 所有数

VARSET RESULT

FOR 対象,1,CHARANUM
	FOR 種類,20,26
		所有数 = CFLAG:対象:(種類 + 胖次管理)
		IF 所有数 >= 1
			RESULT:0 += (所有数)
			CFLAG:対象:(種類 + 胖次管理) = 0
		ENDIF
	NEXT
NEXT

;振動棒胖次削除
@DELETE_VIBEPANTS
#DIM 対象
#DIM 種類
#DIM 所有数

VARSET RESULT

FOR 対象,1,CHARANUM
	FOR 種類,27,30
		所有数 = CFLAG:対象:(種類 + 胖次管理)
		IF 所有数 >= 1
			RESULT:0 += (所有数)
			CFLAG:対象:(種類 + 胖次管理) = 0
		ENDIF
	NEXT
NEXT


