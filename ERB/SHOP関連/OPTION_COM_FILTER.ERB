;FILTER用のCOMNAME取得関数
;とりあえず0～599の範囲で使って下さい
@GET_COMNAME(ID_COM)
#FUNCTIONS
#LOCALSIZE 1
#LOCALSSIZE 1
#DIM ID_COM
;派生指令用
SIF INRANGE(ID_COM, 500, 599)
	RETURNF STR_TR(1000 + ID_COM)
SIF INRANGE(ID_COM, 0, 499)
	RETURNF TRAINNAME_TR(ID_COM)
THROW 传递了错误的COM番号 {ID_COM}



@QUICKFILTER(ID_COM)
#FUNCTION
#LOCALSIZE 1
#LOCALSSIZE 1
#DIM ID_COM
#DIM CONST BIT_Ｖ系       = 1
#DIM CONST BIT_Ａ系       = 2
#DIM CONST BIT_奉仕系     = 5
#DIM CONST BIT_SM系       = 6
#DIM CONST BIT_性騷擾系 = 7
#DIM CONST BIT_家事系     = 9
#DIM CONST BIT_自慰系     = 13  ;たぶんつかってない
SIF QUICKFILTER_UNIQUE(ID_COM)
	RETURNF 0

SIF IS_COM_VAGINA(ID_COM)   && !CFLAG:TARGET:Ｖ過濾器
	RETURNF 0
SIF IS_COM_ANAL(ID_COM)     && !CFLAG:TARGET:Ａ過濾器
	RETURNF 0
SIF IS_COM_HOUSI(ID_COM)    && !CFLAG:TARGET:奉仕過濾器
	RETURNF 0
SIF IS_COM_SM(ID_COM)       && !CFLAG:TARGET:ＳＭ過濾器
	RETURNF 0
SIF IS_COM_KAJI(ID_COM)     && !FLAG:家事過濾器
	RETURNF 0
SIF IS_COM_SEKUHARA(ID_COM) && !CFLAG:TARGET:性騷擾過濾器 && (!FLAG:70 || CFLAG:TARGET:344 == 1)
	RETURNF 0
;SIF IS_COM_ONANISM(ID_COM)  && !CFLAG:TARGET:自慰過濾器
;	RETURNF 0
SIF IS_COM_KOJO(ID_COM) && !CFLAG:TARGET:allowKojoCom
	RETURNF 0
RETURNF 1

@QUICKFILTER_UNIQUE(ID_COM)
;複数のフィルタに関係するコマンド
;非表示にする状況の場合1を返す（呼び出し元で非表示処理）
#FUNCTION
#LOCALSIZE 1
#LOCALSSIZE 1
#DIM ID_COM
;アナル愛撫
;指挿入れ
IF ID_COM == 314 || ID_COM == 316
	IF (CFLAG:うふふ || CFLAG:MASTER:イタズラ)
 		;SEX系のフィルタが設定可能なときはそちらのフィルタを参照
		SIF IS_COM_VAGINA(ID_COM)   && !CFLAG:TARGET:Ｖアンフィルタ
			RETURNF 1
		SIF IS_COM_ANAL(ID_COM)     && !CFLAG:TARGET:Ａアンフィルタ
			RETURNF 1
		;以下アナル愛撫・指挿入れ時は関係ないけど一応入れとく
		SIF IS_COM_HOUSI(ID_COM)    && !CFLAG:TARGET:奉仕アンフィルタ
			RETURNF 1
		SIF IS_COM_SM(ID_COM)       && !CFLAG:TARGET:ＳＭアンフィルタ
			RETURNF 1
	ELSE
 		;SEX系のフィルタが設定不可能なときはセクハラアンフィルタを参照
		SIF !CFLAG:TARGET:セクハラアンフィルタ && (!FLAG:70 || CFLAG:TARGET:344 == 1)
			RETURNF 1
	ENDIF
ENDIF
RETURNF 0

@过滤器表示
;Act_COMのラインと合体
#LOCALSIZE 1
#LOCALSSIZE 1
#DIM DYNAMIC ShowSex
SIF CFLAG:うふふ || CFLAG:MASTER:イタズラ
	ShowSex = 1
PRINTFORM %"=" * 6% Act_COM ==
SIF STALKED == TARGET && STALKED
	SETCOLOR C_YELLOW
PRINTBUTTON "【★】", TARGET + 4000
RESETCOLOR
IF ShowSex
	CALL PRINTBUTTON_EX("[Ｖ]",831,!CFLAG:TARGET:Ｖ過濾器)
	PRINTPLAIN =
	CALL PRINTBUTTON_EX("[Ａ]",832,!CFLAG:TARGET:Ａ過濾器)
	PRINTPLAIN =
	CALL PRINTBUTTON_EX("[ＳＭ]",836,!CFLAG:TARGET:ＳＭ過濾器)
	PRINTPLAIN =
	CALL PRINTBUTTON_EX("[侍奉]",835,!CFLAG:TARGET:奉仕過濾器)
	PRINTPLAIN =
ENDIF
;CALL PRINTBUTTON_EX("[自慰]",843,!CFLAG:TARGET:自慰过滤器)
;PRINTPLAIN =
CALL PRINTBUTTON_EX("[性骚扰]",837,!CFLAG:TARGET:性騷擾過濾器)
PRINTPLAIN =
CALL PRINTBUTTON_EX("[家务]",839,!FLAG:家事過濾器)
PRINTPLAIN =
CALL PRINTBUTTON_EX("[剧本]",840,!CFLAG:TARGET:allowKojoCom)
PRINTPLAIN =
CALL PRINTBUTTON_EX("[自定义]",841,!ADD_CUSTOM_COM_SWITCH)
PRINTPLAIN =
HTML_POPPRINTINGSTR
HTML_PRINT RESULTS + @"%"=" * MAX(MAXWIDTH() - HTML_STRINGLEN(RESULTS), 0)%"

;文字列変数に保存してみた
@OPTION_COMMAND_FILTER
#LOCALSIZE 1
#LOCALSSIZE 1
#DIM ID_COM
;CALL LOAD_COM_FILTER(LOCAL)
DRAWLINE
FOR ID_COM ,0, 600
	SIF !STRLENS(GET_COMNAME(ID_COM))
		CONTINUE
	CALL PRINTBUTTON_EXC(@"%GET_COMNAME(ID_COM)%[{ID_COM,3}]", ID_COM, GET_COM_FILTER(ID_COM))
NEXT
PRINTL 
DRAWLINE
PRINTFORML [1101] - Ｖ过滤器     [1102] - Ａ过滤器       [1103] - 侍奉过滤器 
PRINTFORML [1104] - ＳＭ过滤器   [1105] - 性骚扰过滤器   [1106] - 家务过滤器 
;com custom code
PRINTFORML [1109] - 逆侍奉过滤器 [1110] - 被插入过滤器
PRINTFORML [1111] - ALL过滤器
PRINTFORML [2000] - USERCOM开关
PRINTFORML [3000] - 保存过滤器   [3001] - 读取过滤器
PRINTFORML [1000] - 决定
PRINTL  
PRINTFORML [9999] - 复位
PRINTL  
INPUT
SELECTCASE RESULT
CASE 1000
	RETURN 0
CASE 9999
	SAVESTR:0 = 
CASE 3000
	CALL SAVE_GLOBAL_COM_FILTER
CASE 3001
	CALL LOAD_GLOBAL_COM_FILTER
CASE 2000
	CALL SETUP_USERCOM_FILTER
CASE 1101
	;CALL SET_FILTER_NG_VAGINA
	CALL QOL_SET_FILTER_NG("VAGINA")
CASE 1102
	;CALL SET_FILTER_NG_ANAL
	CALL QOL_SET_FILTER_NG("ANAL")
CASE 1103
	;CALL SET_FILTER_NG_HOUSI
	CALL QOL_SET_FILTER_NG("HOUSI")
CASE 1104
	;CALL SET_FILTER_NG_SM
	CALL QOL_SET_FILTER_NG("SM")
CASE 1105
	;CALL SET_FILTER_NG_SEKUHARA
	CALL QOL_SET_FILTER_NG("SEKUHARA")
CASE 1106
	;CALL SET_FILTER_NG_KAJI
	CALL QOL_SET_FILTER_NG("KAJI")
CASE 1111
	;CALL SET_FILTER_NG_ALL
	CALL QOL_SET_FILTER_NG("ALL")
;com custom code
CASE 1109
	;CALL SET_FILTER_NG_REVSERVICE
	CALL QOL_SET_FILTER_NG("REVSERVICE")
CASE 1110
	;CALL SET_FILTER_NG_REVSEX
	CALL QOL_SET_FILTER_NG("REVSEX")
CASEELSE
	IF STRLENS(GET_COMNAME(RESULT))
		IF !GET_COM_FILTER(RESULT)
			CALL ADD_COM_FILTER(RESULT)
		ELSE
			CALL DEL_COM_FILTER(RESULT)
			;CALL SAVE_COM_FILTER(LOCAL)
		ENDIF
	ENDIF
ENDSELECT
RESTART

;qol
@GET_COM_FILTER(NG_ID)
#LOCALSIZE 1
#LOCALSSIZE 1
#DIM DYNAMIC NG_ID
#FUNCTION
RETURNF STRCOUNT(SAVESTR:0, @"/{NG_ID}/")

;过滤器ーにNGなCOM追加する関数
@ADD_COM_FILTER(NG_ID)
#LOCALSIZE 1
#LOCALSSIZE 1
#DIM DYNAMIC NG_ID
SIF SAVESTR:0 == ""
	SAVESTR:0 = /
SIF STRCOUNT(SAVESTR:0, @"/{NG_ID}/")
	RETURN
SAVESTR:0 += @"{NG_ID}/"

;过滤器ーからNGなCOM削除する関数
;正規表現とかろくに触ったことないんだがこれで運動のん？
@DEL_COM_FILTER(NG_ID)
#LOCALSIZE 1
#LOCALSSIZE 1
#DIM DYNAMIC NG_ID
SIF !STRCOUNT(SAVESTR:0, @"/{NG_ID}/")
	RETURN
SAVESTR:0 '= REPLACE(SAVESTR:0, @"%ESCAPE("/")%{NG_ID}%ESCAPE("/")%", ESCAPE("/"))


[SKIPSTART]
;REF使用COM_FILTER保存関数
;配列→文字列
;是一个使用和指令数量相当长度的数组的高端函数，至少需要600长度
@SAVE_COM_FILTER(NG_LIST)
#LOCALSIZE 1
#LOCALSSIZE 1
#DIM REF NG_LIST
#DIM ID_COM
SAVESTR:0 = /
FOR ID_COM, 0, 600
	SIF !NG_LIST:ID_COM
		CONTINUE
	SIF STRCOUNT(SAVESTR:0, @"/{ID_COM}/")
		CONTINUE
	SAVESTR:0 += @"{ID_COM}/"
NEXT
;#;PRINTFORML SAVE_COM_FILTER：%SAVESTR:0%


;REF使用COM_FILTER読込関数
;文字列→配列
;配列の要素数が600未満だと怒られる
@LOAD_COM_FILTER(NG_LIST)
#LOCALSIZE 1
#LOCALSSIZE 1
#DIM REF NG_LIST
#DIM ID_COM
VARSET NG_LIST
IF SAVESTR:0 == ""
	SAVESTR:0 = /
ELSE
	FOR ID_COM, 0, 600
		SIF STRCOUNT(SAVESTR:0, @"/{ID_COM}/")
			NG_LIST:ID_COM = 1
	NEXT
ENDIF
;#;PRINTFORML LOAD_COM_FILTER：%SAVESTR:0%
[SKIPEND]

;NG文字列をGLOBALに保存
@SAVE_GLOBAL_COM_FILTER
#LOCALSIZE 1
#LOCALSSIZE 1
LOADGLOBAL
FOR LOCAL, 0, 3
	PRINTFORM [{LOCAL}] - 
	IF STRLENS(GLOBALS:(LOCAL * 2 + 1))
		PRINTFORML 【%GLOBALS:(LOCAL * 2 + 1)%】
	ELSE
		PRINTL ----------
	ENDIF
NEXT
PRINTL [100] - 返回
INPUT
DO
	LOCAL = RESULT
	SIF RESULT == 100
		RETURN
	SIF !INRANGE(RESULT, 0, 2)
		CONTINUE
	SIF STRLENS(GLOBALS:(RESULT * 2 + 1))
		PRINTFORML 【%GLOBALS:(RESULT * 2 + 1)%】 这一设置将被保存
	PRINTL 请登录指令过滤器的名字
	INPUTS
	GLOBALS:(LOCAL * 2 + 1) = %RESULTS%
	GLOBALS:(LOCAL * 2) = %SAVESTR:0%
	PRINTFORMW 指令过滤器【%GLOBALS:(LOCAL * 2 + 1)%】已登录完成
	SAVEGLOBAL
	RETURN
LOOP 1



;NG文字列をGLOBALから読込
@LOAD_GLOBAL_COM_FILTER
#LOCALSIZE 1
#LOCALSSIZE 1
LOADGLOBAL
FOR LOCAL, 0, 3
	PRINTFORM [{LOCAL}] - 
	IF STRLENS(GLOBALS:(LOCAL * 2 + 1))
		PRINTFORML 【%GLOBALS:(LOCAL * 2 + 1)%】
	ELSE
		PRINTL ----------
	ENDIF
NEXT
PRINTL [100] - 返回
INPUT
DO
	SIF RESULT == 100 
		RETURN
	SIF !INRANGE(RESULT, 0, 2)
		CONTINUE
	PRINTFORMW 指令过滤器【%GLOBALS:(RESULT * 2 + 1)%】已加载
	SAVESTR:0 = %GLOBALS:(RESULT * 2)%
	RETURN
LOOP 1



;ぶっちゃけ不使用気がする
@SETUP_USERCOM_FILTER
#LOCALSIZE 1
#LOCALSSIZE 1
CALL PRINTBUTTON_EXC("履历表示[800]",             800, GETBIT(FLAG:USERCOM開关, 12))
CALL PRINTBUTTON_EXC("污渍显示[801]",             801, GETBIT(FLAG:USERCOM開关,  0))
CALL PRINTBUTTON_EXC("变更对象[802]",         802, GETBIT(FLAG:USERCOM開关,  1))
CALL PRINTBUTTON_EXC("能力显示[803]",             803, GETBIT(FLAG:USERCOM開关,  2))
CALL PRINTBUTTON_EXC("能力显示（Master）[804]", 804, GETBIT(FLAG:USERCOM開关,  3))
CALL PRINTBUTTON_EXC("道具确认[805]", 805, GETBIT(FLAG:USERCOM開关,  4))
CALL PRINTBUTTON_EXC("自动时停移动[995]", 995, GETBIT(FLAG:USERCOM開关,  13))

;自动扫除
CALL PRINTBUTTON_EXC("自动扫除[996]", 996, GETBIT(FLAG:USERCOM開关,  14))

CALL PRINTBUTTON_EXC("指令过滤器[809]",     809, GETBIT(FLAG:USERCOM開关, 11))
PRINTL 
PRINTC 返回 [0]
INPUT
SELECTCASE RESULT
CASE 0
	RETURN
CASE 801
	INVERTBIT FLAG:USERCOM開关, 0
CASE 802
	INVERTBIT FLAG:USERCOM開关, 1
CASE 803
	INVERTBIT FLAG:USERCOM開关, 2
CASE 804
	INVERTBIT FLAG:USERCOM開关, 3
CASE 805
	INVERTBIT FLAG:USERCOM開关, 4
CASE 809
	INVERTBIT FLAG:USERCOM開关, 11
	IF GETBIT (FLAG:USERCOM開关, 11)
		PRINTW 不先决定或手动输入[809]的OPTION就无法退出
		CLEARLINE 1
	ENDIF
CASE 800
	INVERTBIT FLAG:USERCOM開关, 12
CASE 995
	INVERTBIT FLAG:USERCOM開关, 13
;自動掃除
CASE 996
	INVERTBIT FLAG:USERCOM開关, 14
ENDSELECT
CLEARLINE 3
RESTART


;V使用指令判定
;FLAG:1013, 1
@IS_COM_VAGINA(ID_COM)
#LOCALSIZE 1
#LOCALSSIZE 1
#FUNCTION
#DIM ID_COM
SIF GROUPMATCH(ID_COM, 3, 44, 60, 61, 65, 67, 68, 71, 122, 141, 143, 185, 316, 369, 503, 504, 506, 507, 513)
	RETURNF 1
RETURNF 0

@SET_FILTER_NG_VAGINA
#LOCALSIZE 1
#LOCALSSIZE 1
#DIM ID_COM
FOR ID_COM, 0, 600
	;V
	SIF !IS_COM_VAGINA(ID_COM)
		CONTINUE
	CALL ADD_COM_FILTER(ID_COM)
NEXT


;A使用指令判定
;FLAG:1013, 2
@IS_COM_ANAL(ID_COM)
#LOCALSIZE 1
#LOCALSSIZE 1
#FUNCTION
#DIM ID_COM
SIF GROUPMATCH(ID_COM, 4, 5, 45, 62, 63, 66, 69, 70, 314, 142, 143, 146, 147, 148, 314, 369, 514, 515)
	RETURNF 1
RETURNF 0

@SET_FILTER_NG_ANAL
#LOCALSIZE 1
#LOCALSSIZE 1
#DIM ID_COM
FOR ID_COM, 0, 600
	;A
	SIF !IS_COM_ANAL(ID_COM)
		CONTINUE
	CALL ADD_COM_FILTER(ID_COM)
NEXT




;奉仕指令判定
;FLAG:1013, 5
@IS_COM_HOUSI(ID_COM)
#LOCALSIZE 1
#LOCALSSIZE 1
#FUNCTION
#DIM ID_COM
SIF INRANGE(ID_COM, 80, 85) || GROUPMATCH(ID_COM, 120, 121, 140, 501, 505, 508, 509, 510, 511, 512)
	RETURNF 1
RETURNF 0

@SET_FILTER_NG_HOUSI
#LOCALSIZE 1
#LOCALSSIZE 1
#DIM ID_COM
FOR ID_COM, 0, 600
	;HOUSI
	SIF !IS_COM_HOUSI(ID_COM)
		CONTINUE
	CALL ADD_COM_FILTER(ID_COM)
NEXT



;SM指令判定
;FLAG:1013, 6
@IS_COM_SM(ID_COM)
#LOCALSIZE 1
#LOCALSSIZE 1
#FUNCTION
#DIM ID_COM
SIF INRANGE(ID_COM, 100, 119) || INRANGE(ID_COM, 140, 159)
	RETURNF 1
RETURNF 0

@SET_FILTER_NG_SM
#LOCALSIZE 1
#LOCALSSIZE 1
#DIM ID_COM
FOR ID_COM, 0, 600
	;SM
	SIF !IS_COM_SM(ID_COM)
		CONTINUE
	CALL ADD_COM_FILTER(ID_COM)
NEXT



;性騷擾指令判定
;FLAG:1013, 7
@IS_COM_SEKUHARA(ID_COM)
#LOCALSIZE 1
#LOCALSSIZE 1
#FUNCTION
#DIM ID_COM
SIF INRANGE(ID_COM, 305, 316) || INRANGE(ID_COM, 560, 563) || GROUPMATCH(ID_COM, 330, 350, 352, 429)
	RETURNF 1
RETURNF 0

@SET_FILTER_NG_SEKUHARA
#LOCALSIZE 1
#LOCALSSIZE 1
#DIM ID_COM
FOR ID_COM, 0, 600
	;性騷擾
	SIF !IS_COM_SEKUHARA(ID_COM)
		CONTINUE
	CALL ADD_COM_FILTER(ID_COM)
NEXT



;家事指令判定
;FLAG:1013, 9
@IS_COM_KAJI(ID_COM)
#LOCALSIZE 1
#LOCALSSIZE 1
#FUNCTION
#DIM ID_COM
SIF INRANGE(ID_COM, 410, 419) || GROUPMATCH(ID_COM, 403, 450)
	RETURNF 1
RETURNF 0

;kojo custom com
@IS_COM_KOJO(ID_COM)
#LOCALSIZE 1
#LOCALSSIZE 1
#FUNCTION
#DIM ID_COM
SIF INRANGE(ID_COM, 270, 299)
	RETURNF 1
RETURNF 0

@SET_FILTER_NG_KAJI
#LOCALSIZE 1
#LOCALSSIZE 1
#DIM ID_COM
FOR ID_COM, 0, 600
	SIF !IS_COM_KAJI(ID_COM)
		CONTINUE
	CALL ADD_COM_FILTER(ID_COM)
NEXT



;自慰指令判定
;FLAG:1013, 13
@IS_COM_ONANISM(ID_COM)
#LOCALSIZE 1
#LOCALSSIZE 1
#FUNCTION
#DIM ID_COM
SIF INRANGE(ID_COM, 700, 712) || INRANGE(ID_COM, 740, 752) || GROUPMATCH(ID_COM, 433)
	RETURNF 1
RETURNF 0

@SET_FILTER_NG_ONANISM
#LOCALSIZE 1
#LOCALSSIZE 1
#DIM ID_COM
FOR ID_COM, 0, 600
	SIF !IS_COM_ONANISM(ID_COM)
		CONTINUE
	CALL ADD_COM_FILTER(ID_COM)
NEXT



;ＡＬＬ指令判定
@SET_FILTER_NG_ALL
#LOCALSIZE 1
#LOCALSSIZE 1
#DIM ID_COM
FOR ID_COM, 0, 600
	CALL ADD_COM_FILTER(ID_COM)
NEXT

@CLEAR_FILTER_NG_ALL
#LOCALSIZE 1
#LOCALSSIZE 1
#DIM ID_COM
FOR ID_COM, 0, 600
	CALL DEL_COM_FILTER(ID_COM)
NEXT

@QOL_SET_FILTER_NG(FILTER_CLASS)
#LOCALSIZE 1
#LOCALSSIZE 1
#DIMS DYNAMIC FILTER_CLASS
SELECTCASE FILTER_CLASS
	CASE "ALL"
		IF GET_COM_FILTER(0)
			CALL CLEAR_FILTER_NG_ALL
		ELSE
			CALL SET_FILTER_NG_ALL
		ENDIF
	CASEELSE
		IF !GET_COM_FILTER(GETVAR(@"FILTER_NG_%FILTER_CLASS%"))
			FOR LOCAL, 0, VARSIZE(@"FILTER_NG_%FILTER_CLASS%")
				CALL ADD_COM_FILTER(GETVAR(@"FILTER_NG_%FILTER_CLASS%:LOCAL"))
			NEXT
		ELSE
			FOR LOCAL, 0, VARSIZE(@"FILTER_NG_%FILTER_CLASS%")
				CALL DEL_COM_FILTER(GETVAR(@"FILTER_NG_%FILTER_CLASS%:LOCAL"))
			NEXT
		ENDIF
ENDSELECT
