@UPDATE_OF_KOJO_ORIGIN

;口上の存在判定 and RESULTSに文字列代入
FOR LOCAL, 1, CHARANUM
	VARSET RESULTS
	TARGET = LOCAL
	IF CFLAG:LOCAL:口上セレクタ
		TRYCALLFORM M_KOJO_K{LOCAL}_{CFLAG:LOCAL:口上セレクタ}
	ELSE
		TRYCALLFORM M_KOJO_K{LOCAL}
	ENDIF
	PRINTFORML 正在确认%CALLNAME:LOCAL%%RESULTS:1%的剧本是否存在UPDATE
	TRYCALLFORM M_KOJO%RESULTS%_UPDATE_K{LOCAL}
	WAIT
NEXT

@UPDATE_AND_SELECTION_OF_KOJO_MODDING(WORK_MODE)
#DIM DYNAMIC MAXPAGE_MODE,3
#DIM DYNAMIC MAXPAGE
#DIM DYNAMIC PAGE
#DIM DYNAMIC MARK_KOJO_AVAILABLE, 人物数量上限
#DIM DYNAMIC NUM_OF_CHARA_WITH_KOJO,2
#DIM DYNAMIC LIST_OF_CHARA_WITH_KOJO, 人物数量上限
#DIM DYNAMIC LIST_OF_CHARA_WITH_KOJOS, 人物数量上限
#DIM DYNAMIC LIST_OF_CHARA_ALTERED_BY_TOUHOU, 人物数量上限
#DIM DYNAMIC C_ID
#DIM DYNAMIC LINE_RECORD
#DIM DYNAMIC CHOOSE_COMMAND
#DIMS DYNAMIC HTML
#DIMS DYNAMIC COLOR
#DIM DYNAMIC REDRAW_RECORD
#DIM OPERATE_MODE = 2
#DIMS DYNAMIC WORK_MODE
#DIM DYNAMIC NUM_OF_KOJO
#DIM DYNAMIC CHARACTER_PER_PAGE
CURRENTREDRAW
REDRAW_RECORD = RESULT
SIF WORK_MODE != "SELECTION"
	OPERATE_MODE = 1
REDRAW 0
;确认角色口上存在情况
VARSET RESULT
FOR LOCAL, 0, CHARANUM
	IF CFLAG:LOCAL:口上セレクタ
		TRYCCALLFORM M_KOJO_K{LOCAL}_{CFLAG:LOCAL:口上セレクタ}
		CATCH
			VARSET RESULT
		ENDCATCH
	ELSE
		TRYCCALLFORM M_KOJO_K{LOCAL}
		CATCH
			VARSET RESULT
		ENDCATCH
	ENDIF
	;有口上角色标记
	IF RESULT
		NUM_OF_CHARA_WITH_KOJO ++ 
		MARK_KOJO_AVAILABLE:LOCAL++
		SIF CFLAG:LOCAL:36>1
			NUM_OF_CHARA_WITH_KOJO:1++
	ENDIF
NEXT
IF WORK_MODE == "SELECTION"
	CHARACTER_PER_PAGE = 10
ELSE
	CHARACTER_PER_PAGE = 20
ENDIF
;不过滤无口上角色时的最多页数
MAXPAGE_MODE = (CHARANUM - 1) / CHARACTER_PER_PAGE + 1
;过滤无口上角色时的最多页数
MAXPAGE_MODE:1 = (NUM_OF_CHARA_WITH_KOJO - 1) / CHARACTER_PER_PAGE + 1
;过滤无/单口上角色时的最多页数
MAXPAGE_MODE:2 = (NUM_OF_CHARA_WITH_KOJO:1 - 1) / CHARACTER_PER_PAGE + 1
;制作以原作顺序排列的人物列表
VARSET RESULT
IF FLAG:並び替え
	CALL CHARASORT_EX
	LOCAL:1 = 0
	FOR LOCAL, 0, EX_CHARANUM
		SIF RESULT:LOCAL < 0 
			CONTINUE
		LIST_OF_CHARA_ALTERED_BY_TOUHOU:(LOCAL:1) = RESULT:LOCAL
		LOCAL:1 ++
	NEXT
ENDIF
;制作过滤无口上角色时的角色列表
LOCAL:2 = 0
FOR LOCAL, 0, CHARANUM
	SIF !MARK_KOJO_AVAILABLE:LOCAL
		CONTINUE
	IF FLAG:並び替え
		LIST_OF_CHARA_WITH_KOJO:(FINDELEMENT(LIST_OF_CHARA_ALTERED_BY_TOUHOU, LOCAL, 1, CHARANUM)) = LOCAL
	ELSE
		LIST_OF_CHARA_WITH_KOJO:(LOCAL:2) = LOCAL
	ENDIF
	LOCAL:2 ++
NEXT
;多个剧本列表
LOCAL:2 = 0
FOR LOCAL, 0, CHARANUM	
	SIF CFLAG:LOCAL:36<=1
		CONTINUE
	IF FLAG:並び替え
		LIST_OF_CHARA_WITH_KOJOS:(FINDELEMENT(LIST_OF_CHARA_ALTERED_BY_TOUHOU, LOCAL, 1, CHARANUM)) = LOCAL
	ELSE
		LIST_OF_CHARA_WITH_KOJOS:(LOCAL:2) = LOCAL
	ENDIF
	LOCAL:2 ++
NEXT

IF FLAG:並び替え
	FOR LOCAL, 0, NUM_OF_CHARA_WITH_KOJO
		IF !LIST_OF_CHARA_WITH_KOJO:LOCAL
			ARRAYSHIFT LIST_OF_CHARA_WITH_KOJO, -1, 0, LOCAL
			LOCAL --
		ENDIF		
	NEXT
	FOR LOCAL, 0, NUM_OF_CHARA_WITH_KOJO:1
		IF !LIST_OF_CHARA_WITH_KOJOS:LOCAL
			ARRAYSHIFT LIST_OF_CHARA_WITH_KOJOS, -1, 0, LOCAL
			LOCAL --
		ENDIF		
	NEXT
ENDIF

LINE_RECORD = LINECOUNT
WHILE 1
	DRAWLINE
	DRAWLINE
	MAXPAGE = MAXPAGE_MODE:OPERATE_MODE
	IF PAGE >= MAXPAGE
		PAGE = MAXPAGE - 1
	ELSEIF PAGE < 0 
		PAGE = 0
	ENDIF
	IF WORK_MODE == "SELECTION"
		RESETCOLOR
		PRINTFORML %UNICODE(0x3000) * 4%角色剧本选择界面%UNICODE(0x3000) * 4%
		PRINTFORML ＜　ＰＡＧＥ　．　%TOFULL(TOSTR(PAGE + 1))%　／　ＡＬＬ．ＰＡＧＥ．%TOFULL(TOSTR(MAXPAGE))%＞
	ELSE
		HTML_PRINT @"<i><b><u>　　　　角色剧本ＵＰＤＡＴＥ选择界面　　　　</u></b></i>"
		HTML_PRINT @"<i><font color = '#0x00FF00'>＜　ＰＡＧＥ　．　%TOFULL(TOSTR(PAGE + 1))%　／　ＡＬＬ．ＰＡＧＥ．%TOFULL(TOSTR(MAXPAGE))%＞</font></i>"
	ENDIF
	IF OPERATE_MODE == 2
		HTML_PRINT @"<i><nonbutton title = '当前显示角色均为有多个剧本角色'><font color='#0x90EE90'>　　过滤无/单剧本角色功能开启　　</font></nonbutton></i>"
	ELSEIF OPERATE_MODE == 1
		HTML_PRINT @"<i><nonbutton title = '当前显示角色均为有剧本角色'><font color='#0x90EE90'>　　过滤无剧本角色功能开启　　</font></nonbutton></i>"
	ELSE
		HTML_PRINT @"<i><nonbutton title = '已显示全部角色，包括无剧本角色'><font color='#0x404040'>　　过滤无剧本角色功能关闭　　</font></nonbutton></i>"
	ENDIF
	LOCAL:1=0
	FOR LOCAL, PAGE * CHARACTER_PER_PAGE, (PAGE + 1) * CHARACTER_PER_PAGE
		VARSET HTML
		VARSET RESULT
		VARSET RESULTS
		VARSET C_ID
		IF OPERATE_MODE
			IF OPERATE_MODE == 2
				SIF LIST_OF_CHARA_WITH_KOJOS:LOCAL && CFLAG:(LIST_OF_CHARA_WITH_KOJOS:LOCAL):36>1
					C_ID = LIST_OF_CHARA_WITH_KOJOS:LOCAL
				IF !C_ID
					CONTINUE
				ELSEIF LOCAL >= NUM_OF_CHARA_WITH_KOJO:1
					BREAK
				ENDIF
			ELSEIF OPERATE_MODE == 1
				SIF LIST_OF_CHARA_WITH_KOJO:LOCAL
					C_ID = LIST_OF_CHARA_WITH_KOJO:LOCAL
				SIF LOCAL >= NUM_OF_CHARA_WITH_KOJO
					BREAK
			ENDIF
		ELSE 
			SIF !LOCAL
				CONTINUE
			IF LOCAL >= CHARANUM-1
				BREAK
			ELSEIF FLAG:並び替え
				C_ID = LIST_OF_CHARA_ALTERED_BY_TOUHOU:LOCAL
			ELSE
				C_ID = LOCAL
			ENDIF
		ENDIF
		IF WORK_MODE == "SELECTION"
			RESETCOLOR
			PRINTPLAINFORM [{C_ID, 3, RIGHT}] - %CALLNAME:C_ID, 25, LEFT%
			IF FINDELEMENT(LIST_OF_CHARA_WITH_KOJO, C_ID) > -1
				VARSET RESULT
				VARSET RESULTS
				IF CFLAG:C_ID:口上セレクタ
					TRYCALLFORM M_KOJO_K{C_ID}_{CFLAG:C_ID:口上セレクタ}
				ELSE
					TRYCALLFORM M_KOJO_K{C_ID}
				ENDIF
				SIF !STRLENS(RESULTS:1) && RESULT == 1 && CFLAG:C_ID:口上セレクタ == 0
					RESULTS:1 = 初始
				SIF STRLENS(RESULTS:1)
					PRINTFORM %UNICODE(0x3000) * 4%%RESULTS:1%剧本
			ENDIF
			PRINTL
			VARSET NUM_OF_KOJO
			FOR LOCAL:3, 0, 10
				VARSET RESULT
				VARSET RESULTS
				IF LOCAL:3 > 0
					TRYCALLFORM M_KOJO_K{C_ID}_{LOCAL:3}
				ELSE
					TRYCALLFORM M_KOJO_K{C_ID}
				ENDIF
				SIF RESULT == 1
					NUM_OF_KOJO ++
				SIF !STRLENS(RESULTS:1) && RESULT == 1 ;&& CFLAG:C_ID:口上セレクタ == 0
					RESULTS:1 '= LOCAL:3 ? @"第%TOKANJI(LOCAL:3)%" # "初始"
				IF RESULT == 1 && CFLAG:C_ID:口上セレクタ == LOCAL:3
					; SETCOLOR C_GREEN
					; PRINTPLAINFORM %UNICODE(0x3000) * 3%└ %RESULTS:1%剧本
					SIF RESULTS:2 == ""
						RESULTS:2 = 无简介
					HTML_PRINT @"<p align='left'><nobr><button value='{2000 + C_ID * 10 + LOCAL:3}' title='%RESULTS:2%'><font color = 'lime'>%UNICODE(0x3000) * 3%└ %RESULTS:1%剧本</font></button><br>"
					; RESETCOLOR
					; PRINTL
				ELSEIF RESULT == 1
					; SETCOLOR C_ORANGE
					; PRINTBUTTON @"%UNICODE(0x3000) * 3%└ %RESULTS:1%剧本", 2000 + C_ID * 10 + LOCAL:3
					SIF RESULTS:2 == ""
						RESULTS:2 = 无简介
					HTML_PRINT @"<p align='left'><nobr><button value='{2000 + C_ID * 10 + LOCAL:3}' title='%RESULTS:2%'><font color = 'orange'>%UNICODE(0x3000) * 3%└ %RESULTS:1%剧本</font></button><br>"
					; RESETCOLOR
					; PRINTL
				ENDIF
			NEXT
			SIF NUM_OF_KOJO == 1
				CLEARLINE 1
		ELSE
			VARSET RESULT
			IF CFLAG:C_ID:口上セレクタ
				TRYCALLFORM M_KOJO_K{C_ID}_{CFLAG:C_ID:口上セレクタ}
			ELSE
				TRYCALLFORM M_KOJO_K{C_ID}
			ENDIF
			SIF !STRLENS(RESULTS:1) && RESULT == 1 && CFLAG:C_ID:口上セレクタ == 0
				RESULTS:1 = 初始
			RESETCOLOR
			VARSET COLOR
			TRYCALLFORM M_KOJO%RESULTS%_COLOR_K{C_ID}
			COLOR '= "0x" + hex_to_str(GETCOLOR())
			HTML += @"<button value='{C_ID}'><font color='#%COLOR%'>[{C_ID, 3, RIGHT}"
			HTML += @"]  -  %CALLNAME:C_ID, 16, LEFT%    %CSVCSTR(C_ID, 10), 100, LEFT%"
			IF FINDELEMENT(LIST_OF_CHARA_WITH_KOJO, C_ID) > -1
				HTML += @"----------------%CALLNAME:C_ID%%RESULTS:1%剧本</font></button>"
			ELSE
				HTML += @"----------------无剧本</font></button>"
			ENDIF
			HTML_PRINT HTML
			RESETCOLOR
		ENDIF
	NEXT
	IF WORK_MODE == "SELECTION"
		PRINTBUTTON @"%UNICODE(0x3000) * 2%［跳转首页］%UNICODE(0x3000) * 2%", 1000
		PRINTBUTTON	@"%UNICODE(0x3000) * 2%［上一页］%UNICODE(0x3000) * 2%", 1001
		PRINTBUTTON @"%UNICODE(0x3000) * 2%［跳转指定页］%UNICODE(0x3000) * 2%", 1010
		PRINTBUTTON @"%UNICODE(0x3000) * 2%［下一页］%UNICODE(0x3000) * 2%", 1002
		PRINTBUTTON @"%UNICODE(0x3000) * 2%［跳转尾页］%UNICODE(0x3000) * 2%", 1003
	ELSE
		{
		HTML_PRINT @"<button value = '1000' title = '跳转至首页'>　　［　＜＜　］　　</button>
		<button value = '1001' title = '上一页'>　　［　＜　］　　</button>
		<button value = '1010' title = '跳转至指定页'>　　［　□　］　　</button>
		<button value = '1002' title = '下一页'>　　［　＞　］　　</button>
		<button value = '1003' title = '跳转至尾页'>　　［　＞＞　］　　</button>"
		}
	ENDIF
	;"
	IF OPERATE_MODE == 2 && WORK_MODE == "SELECTION"
		HTML_PRINT @"<button value = '1004' title = '当前显示角色均为有多个剧本角色'>　　［过滤功能：开启］　　</button>"
	ELSEIF OPERATE_MODE == 1
		HTML_PRINT @"<button value = '1004' title = '当前显示角色均为有剧本角色'>　　［过滤功能：开启］　　</button>"
	ELSE
		HTML_PRINT @"<button value = '1004' title = '已显示全部角色，包括无剧本角色'>　　［过滤功能：关闭］　　</button>"
	ENDIF
	SIF !STRLENS(WORK_MODE)
		HTML_PRINT @"<button value = '1005' title = '经典模式，一次性全过一遍'>　　［ＵＰＤＡＴＥ全体进行］　　</button>"
	SIF WORK_MODE == "SELECTION"
		HTML_PRINT @"<button value = '1008' title = '还原为全部选择初期剧本'>　　［全部初期］　　</button><button value = '1009' title = '输入角色编号与剧本编号完成设定'>　　［批量设置］　　</button>"
	HTML_PRINT @"<button value = '1099'>　　［查找角色］　　</button>"
	HTML_PRINT @"<button value = '1100'>　　［退出］　　</button>"
	DRAWLINE
	DRAWLINE
	VARSET RESULT
	INPUT
	CHOOSE_COMMAND = RESULT
	SELECTCASE CHOOSE_COMMAND
		CASE 1000
			PAGE = 0
		CASE 1001
			PAGE=(PAGE-1+MAXPAGE)%MAXPAGE
		CASE 1002
			PAGE=(PAGE+1)%MAXPAGE
		CASE 1003
			PAGE = MAXPAGE - 1
		CASE 1004
			OPERATE_MODE+=1
			SIF OPERATE_MODE>1+(WORK_MODE == "SELECTION")
				OPERATE_MODE=0
		CASE 1 TO 500
			VARSET RESULTS
			TARGET = CHOOSE_COMMAND
			IF CFLAG:CHOOSE_COMMAND:口上セレクタ
				TRYCALLFORM M_KOJO_K{CHOOSE_COMMAND}_{CFLAG:CHOOSE_COMMAND:口上セレクタ}
			ELSE
				TRYCALLFORM M_KOJO_K{CHOOSE_COMMAND}
			ENDIF
			PRINTFORML 正在确认%CALLNAME:CHOOSE_COMMAND%%RESULTS:1%的剧本是否存在UPDATE
			TRYCALLFORM M_KOJO%RESULTS%_UPDATE_K{CHOOSE_COMMAND}
			WAIT
			PRINTL
		CASE 1005
			CALL UPDATE_OF_KOJO_ORIGIN
		CASE 1008
			SIF WORK_MODE != "SELECTION"
				CONTINUE
			CVARSET CFLAG,998,0
			CFLAG:60:998=1;初期不存在
		CASE 1009
			SIF WORK_MODE != "SELECTION"
				CONTINUE
			;RESTART后清行用存行
			CALL Reline(,1)
			CALL MultiKojoSet
		CASE 1010
			PRINTFORMDL 请输入页码（范围1～{MAXPAGE}）
			INPUT
			RESULT-=1
			IF !INRANGE(RESULT,0,MAXPAGE-1)
				RESULT=LIMIT(RESULT,0,MAXPAGE-1)
				PRINTFORMW 输入值超过范围，现在修正为{RESULT+1}
			ENDIF
			PAGE=RESULT
		CASE 1099
			PRINTDL 请输入与上表形式一致的角色称呼（例：阿燐而非火焰猫燐）
			PRINTDL 魔界白魔法使 舞（マイ）编号105，后户绿童子 丁礼田 舞 编号122
			LOCAL:1=0
			INPUTS
			FOR LOCAL,1,CHARANUM-1
				SIF RESULTS!=CALLNAME:LOCAL
					CONTINUE
				LOCAL:1=LOCAL
			NEXT
			IF !LOCAL:1
				PRINTL 未找到该角色，请检查输入是否有误
				PRINTW 如未发现错误，请手动翻页查找，可能存在译名不一致
			ELSE
				PRINTFORMW %RESULTS%的编号为{LOCAL:1}
			ENDIF
		CASE 1100
			CLEARLINE LINECOUNT - LINE_RECORD
			BREAK
		CASE 2000 TO 5000
			IF WORK_MODE == "SELECTION"
				C_ID = (CHOOSE_COMMAND - 2000) / 10
				CFLAG:C_ID:口上セレクタ = (CHOOSE_COMMAND - 2000) % 10
			ENDIF
	ENDSELECT
	CLEARLINE LINECOUNT - LINE_RECORD
	CONTINUE
WEND

REDRAW REDRAW_RECORD


@UPDATE_AND_SELECTION_OF_KOJO_MODDING_2(WORK_MODE, PAGE, OPERATE_MODE = 2)
#DIM DYNAMIC MAXPAGE_MODE,3
#DIM DYNAMIC PAGE
#DIM DYNAMIC MARK_KOJO_AVAILABLE, 人物数量上限
#DIM DYNAMIC NUM_OF_CHARA_WITH_KOJO,2
#DIM DYNAMIC LIST_OF_CHARA_WITH_KOJO, 人物数量上限
#DIM DYNAMIC LIST_OF_CHARA_WITH_KOJOS, 人物数量上限
#DIM DYNAMIC LIST_OF_CHARA_ALTERED_BY_TOUHOU, 人物数量上限
#DIM DYNAMIC C_ID
#DIM DYNAMIC LINE_RECORD
#DIM DYNAMIC CHOOSE_COMMAND
#DIMS DYNAMIC HTML
#DIMS DYNAMIC COLOR
#DIM DYNAMIC OPERATE_MODE
#DIMS DYNAMIC WORK_MODE
#DIM DYNAMIC NUM_OF_KOJO
#DIM DYNAMIC CHARACTER_PER_PAGE
#DIM DYNAMIC KOJO_RESULT
#DIM DYNAMIC P2_LOOP, 2
#DIMS DYNAMIC P2_LISTS, 2
#DIMS DYNAMIC P2_STR_BUILD
REDRAW 0
;确认角色口上存在情况
VARSET RESULT
VARSET KOJO_RESULT
FOR LOCAL, 0, CHARANUM
	CALL KOJO_COUNT_CHECK(LOCAL, 1)
	;有口上角色标记
	IF RESULT
		NUM_OF_CHARA_WITH_KOJO ++ 
		MARK_KOJO_AVAILABLE:LOCAL++
		SIF RESULT > 1
			NUM_OF_CHARA_WITH_KOJO:1++
	ENDIF
NEXT
IF WORK_MODE == "SELECTION"
	CHARACTER_PER_PAGE = 10
ELSE
	CHARACTER_PER_PAGE = 20
ENDIF
;不过滤无口上角色时的最多页数
MAXPAGE_MODE = (CHARANUM - 1) / CHARACTER_PER_PAGE + 1
;过滤无口上角色时的最多页数
MAXPAGE_MODE:1 = (NUM_OF_CHARA_WITH_KOJO - 1) / CHARACTER_PER_PAGE + 1
;过滤无/单口上角色时的最多页数
MAXPAGE_MODE:2 = (NUM_OF_CHARA_WITH_KOJO:1 - 1) / CHARACTER_PER_PAGE + 1
;制作以原作顺序排列的人物列表
VARSET RESULT
IF FLAG:並び替え
	CALL CHARASORT_EX
	LOCAL:1 = 0
	FOR LOCAL, 0, EX_CHARANUM
		SIF RESULT:LOCAL < 0 
			CONTINUE
		LIST_OF_CHARA_ALTERED_BY_TOUHOU:(LOCAL:1) = RESULT:LOCAL
		LOCAL:1 ++
	NEXT
ENDIF
;制作过滤无口上角色时的角色列表
LOCAL:2 = 0
FOR LOCAL, 0, CHARANUM
	SIF !MARK_KOJO_AVAILABLE:LOCAL
		CONTINUE
	IF FLAG:並び替え
		LIST_OF_CHARA_WITH_KOJO:(FINDELEMENT(LIST_OF_CHARA_ALTERED_BY_TOUHOU, LOCAL, 1, CHARANUM)) = LOCAL
	ELSE
		LIST_OF_CHARA_WITH_KOJO:(LOCAL:2) = LOCAL
	ENDIF
	LOCAL:2 ++
NEXT
;多个剧本列表
LOCAL:2 = 0
FOR LOCAL, 0, CHARANUM	
	SIF CFLAG:LOCAL:36<=1
		CONTINUE
	IF FLAG:並び替え
		LIST_OF_CHARA_WITH_KOJOS:(FINDELEMENT(LIST_OF_CHARA_ALTERED_BY_TOUHOU, LOCAL, 1, CHARANUM)) = LOCAL
	ELSE
		LIST_OF_CHARA_WITH_KOJOS:(LOCAL:2) = LOCAL
	ENDIF
	LOCAL:2 ++
NEXT

IF FLAG:並び替え
	FOR LOCAL, 0, NUM_OF_CHARA_WITH_KOJO
		IF !LIST_OF_CHARA_WITH_KOJO:LOCAL
			ARRAYSHIFT LIST_OF_CHARA_WITH_KOJO, -1, 0, LOCAL
			LOCAL --
		ENDIF		
	NEXT
	FOR LOCAL, 0, NUM_OF_CHARA_WITH_KOJO:1
		IF !LIST_OF_CHARA_WITH_KOJOS:LOCAL
			ARRAYSHIFT LIST_OF_CHARA_WITH_KOJOS, -1, 0, LOCAL
			LOCAL --
		ENDIF		
	NEXT
ENDIF

LINE_RECORD = LINECOUNT
DRAWLINE
UpdateMaxPages = MAXPAGE_MODE:OPERATE_MODE
IF PAGE >= UpdateMaxPages
	PAGE = UpdateMaxPages - 1
ELSEIF PAGE < 0 
	PAGE = 0
ENDIF
IF WORK_MODE == "SELECTION"
	RESETCOLOR
	PRINTFORML %UNICODE(0x3000) * 4%角色剧本选择界面%UNICODE(0x3000) * 4%
	PRINTFORML ＜　ＰＡＧＥ　．　%TOFULL(TOSTR(PAGE + 1))%　／　ＡＬＬ．ＰＡＧＥ．%TOFULL(TOSTR(UpdateMaxPages))%＞
ELSE
	HTML_PRINT @"<i><b><u>　　　　角色剧本ＵＰＤＡＴＥ选择界面　　　　</u></b></i>"
	HTML_PRINT @"<i><font color = '#0x00FF00'>＜　ＰＡＧＥ　．　%TOFULL(TOSTR(PAGE + 1))%　／　ＡＬＬ．ＰＡＧＥ．%TOFULL(TOSTR(UpdateMaxPages))%＞</font></i>"
ENDIF
IF OPERATE_MODE == 2
	HTML_PRINT @"<i><nonbutton title = '当前显示角色均为有多个剧本角色'><font color='#0x90EE90'>　　过滤无/单剧本角色功能开启　　</font></nonbutton></i>"
ELSEIF OPERATE_MODE == 1
	HTML_PRINT @"<i><nonbutton title = '当前显示角色均为有剧本角色'><font color='#0x90EE90'>　　过滤无剧本角色功能开启　　</font></nonbutton></i>"
ELSE
	HTML_PRINT @"<i><nonbutton title = '已显示全部角色，包括无剧本角色'><font color='#0x404040'>　　过滤无剧本角色功能关闭　　</font></nonbutton></i>"
ENDIF
LOCAL:1=0
FOR LOCAL, PAGE * CHARACTER_PER_PAGE, (PAGE + 1) * CHARACTER_PER_PAGE
	VARSET HTML
	VARSET RESULT
	VARSET RESULTS
	VARSET C_ID
	IF OPERATE_MODE
		IF OPERATE_MODE == 2
			SIF LIST_OF_CHARA_WITH_KOJOS:LOCAL && CFLAG:(LIST_OF_CHARA_WITH_KOJOS:LOCAL):36>1
				C_ID = LIST_OF_CHARA_WITH_KOJOS:LOCAL
			IF !C_ID
				CONTINUE
			ELSEIF LOCAL >= NUM_OF_CHARA_WITH_KOJO:1
				BREAK
			ENDIF
		ELSEIF OPERATE_MODE == 1
			SIF LIST_OF_CHARA_WITH_KOJO:LOCAL
				C_ID = LIST_OF_CHARA_WITH_KOJO:LOCAL
			SIF LOCAL >= NUM_OF_CHARA_WITH_KOJO
				BREAK
		ENDIF
	ELSE 
		SIF !LOCAL
			CONTINUE
		IF LOCAL >= CHARANUM-1
			BREAK
		ELSEIF FLAG:並び替え
			C_ID = LIST_OF_CHARA_ALTERED_BY_TOUHOU:LOCAL
		ELSE
			C_ID = LOCAL
		ENDIF
	ENDIF
	IF WORK_MODE == "SELECTION"
		RESETCOLOR
		PRINTPLAINFORM [{C_ID, 3, RIGHT}] - %CALLNAME:C_ID, 25, LEFT%
		IF FINDELEMENT(LIST_OF_CHARA_WITH_KOJO, C_ID) > -1
			VARSET RESULT
			VARSET RESULTS
			IF CFLAG:C_ID:口上セレクタ
				;check if the main dialogue still works with it
				TRYCCALLFORM M_KOJO_K{NO:C_ID}_{CFLAG:C_ID:口上セレクタ}
					KOJO_RESULT = RESULT
					IF !KOJO_RESULT
						剧本错误警告:C_ID = 1
						CALL COLORMESSAGE(@"%UNICODE(0x3000) * 4%剧本错误！请点击重置",C_RED,0,1)
					ENDIF
				CATCH
					剧本错误警告:C_ID = 1
					CALL COLORMESSAGE(@"%UNICODE(0x3000) * 4%剧本错误！请点击重置",C_RED,0,1)
				ENDCATCH
			ELSE
				TRYCCALLFORM M_KOJO_K{C_ID}
					KOJO_RESULT = RESULT
					IF !KOJO_RESULT
						剧本错误警告:C_ID = 1
						CALL COLORMESSAGE(@"%UNICODE(0x3000) * 4%剧本错误！请点击重置",C_RED,0,1)
					ENDIF
				CATCH
					剧本错误警告:C_ID = 1
					CALL COLORMESSAGE(@"%UNICODE(0x3000) * 4%剧本错误！请点击重置",C_RED,0,1)
				ENDCATCH
			ENDIF
			SIF !STRLENS(RESULTS:1) && KOJO_RESULT
				RESULTS:1 '= CFLAG:C_ID:口上セレクタ ? @"第%TOKANJI(CFLAG:C_ID:口上セレクタ)%" # "初始"
			SIF !剧本错误警告:C_ID
				PRINTFORM %UNICODE(0x3000) * 4%%RESULTS:1%剧本
		ELSE
			PRINTFORM %UNICODE(0x3000) * 4%无剧本
		ENDIF
		PRINTL
		VARSET NUM_OF_KOJO
		P2_STR_BUILD '= ""
		FOR LOCAL:3, 0, 10
			VARSET RESULT
			VARSET RESULTS
			IF LOCAL:3 > 0
				TRYCALLFORM M_KOJO_K{C_ID}_{LOCAL:3}
			ELSE
				TRYCALLFORM M_KOJO_K{C_ID}
			ENDIF
			SIF RESULT == 1
				NUM_OF_KOJO ++
			
			SIF !STRLENS(RESULTS:1) && RESULT == 1 ;&& CFLAG:C_ID:口上セレクタ == 0
				RESULTS:1 '= LOCAL:3 ? @"第%TOKANJI(LOCAL:3)%" # "初始"
			P2_LISTS:0 '= RESULTS:1
			P2_LISTS:1 '= @"\@RESULTS:2 != "" ? %RESULTS:2% # 无简介 \@"
			P2_STR_BUILD '= @"<button value='{(10000*(LOCAL:3+1))+C_ID}' title='%HTML_ESCAPE(P2_LISTS:1)%'>%UNICODE(0x3000) * 3%└ %P2_LISTS:0%剧本</button><br>"
			IF RESULT == 1 && CFLAG:C_ID:口上セレクタ == LOCAL:3
				P2_STR_BUILD '= @"<font color='lime'>%P2_STR_BUILD%</font>"
			ELSEIF GETBIT(RESULT:1, CFLAG:C_ID:口上セレクタ)
				P2_STR_BUILD '= @"<font color='orange'>%P2_STR_BUILD%</font>"
			ENDIF
			SIF RESULT
				HTML_PRINT P2_STR_BUILD
		NEXT
		;SIF NUM_OF_KOJO == 1
		;	CLEARLINE 1
	ELSE
		VARSET RESULT
		IF CFLAG:C_ID:口上セレクタ
			TRYCALLFORM M_KOJO_K{C_ID}_{CFLAG:C_ID:口上セレクタ}
		ELSE
			TRYCALLFORM M_KOJO_K{C_ID}
		ENDIF
		SIF !STRLENS(RESULTS:1) && RESULT == 1 && CFLAG:C_ID:口上セレクタ == 0
			RESULTS:1 = 初始
		RESETCOLOR
		VARSET COLOR
		TRYCALLFORM M_KOJO%RESULTS%_COLOR_K{C_ID}
		COLOR '= "0x" + hex_to_str(GETCOLOR())
		HTML += @"<button value='{C_ID}'><font color='#%COLOR%'>[{C_ID, 3, RIGHT}"
		HTML += @"]  -  %CALLNAME:C_ID, 16, LEFT%    %CSVCSTR(C_ID, 10), 100, LEFT%"
		IF FINDELEMENT(LIST_OF_CHARA_WITH_KOJO, C_ID) > -1
			HTML += @"----------------%CALLNAME:C_ID%%RESULTS:1%剧本</font></button>"
		ELSE
			HTML += @"----------------无剧本</font></button>"
		ENDIF
		HTML_PRINT HTML
		RESETCOLOR
	ENDIF
NEXT
IF WORK_MODE == "SELECTION"
	PRINTBUTTON @"%UNICODE(0x3000) * 2%［跳转首页］%UNICODE(0x3000) * 2%", 8101
	PRINTBUTTON	@"%UNICODE(0x3000) * 2%［上一页］%UNICODE(0x3000) * 2%", 8102
	PRINTBUTTON @"%UNICODE(0x3000) * 2%［下一页］%UNICODE(0x3000) * 2%", 8103
	PRINTBUTTON @"%UNICODE(0x3000) * 2%［跳转尾页］%UNICODE(0x3000) * 2%", 8104
ELSE
	{
	HTML_PRINT @"<button value = '8101' title = '跳转至首页'>　　［　＜＜　］　　</button>
	<button value = '8102' title = '上一页'>　　［　＜　］　　</button>
	<button value = '8103' title = '下一页'>　　［　＞　］　　</button>
	<button value = '8104' title = '跳转至尾页'>　　［　＞＞　］　　</button>"
	}
ENDIF

IF OPERATE_MODE == 2
	HTML_PRINT @"<button value = '1004' title = '当前显示角色均为有多个剧本角色'>　　［过滤功能：开启］　　</button>"
ELSEIF OPERATE_MODE == 1
	HTML_PRINT @"<button value = '1004' title = '当前显示角色均为有剧本角色'>　　［过滤功能：开启］　　</button>"
ELSE
	HTML_PRINT @"<button value = '1004' title = '已显示全部角色，包括无剧本角色'>　　［过滤功能：关闭］　　</button>"
ENDIF

@MultiKojoSet
#DIMS CID_LIST,人物数量上限
#DIMS KOJO_LIST,人物数量上限
#DIM CID_NUM
#DIM KOJO_NUM
#DIM CNT_CID_LIST,人物数量上限
#DIM CNT_KOJO_LIST,人物数量上限
;清行
CALL Reline()
PRINTL 请输入要变更的tw顺序角色编号，用/分割(例：14/15/16)
PRINTFORML 范围1～{CHARANUM-2}
PRINTL [0]退出
INPUTS
SIF RESULTS=="0"
	RETURN
CALLF CNT_SPLIT(RESULTS,CID_LIST,CNT_CID_LIST,CID_NUM)
FOR LOCAL,0,CID_NUM
	IF !ISNUMERIC(CID_LIST:LOCAL)
		PRINT 输入格式错误，请输入数字
	ELSEIF !INRANGE(TOINT(CID_LIST:LOCAL),1,CHARANUM-2)
		PRINT 输入值超过范围
	ELSEIF CNT_CID_LIST:LOCAL>1
		PRINT 角色编号重复
	ELSEIF CFLAG:TOINT(CID_LIST:LOCAL):36<=1
		PRINTFORM 列表中角色%CALLNAME:TOINT(CID_LIST:LOCAL),9,LEFT%未找到可变更口上
	ELSE
		PRINTFORML 第{LOCAL+1}个角色：%CALLNAME:TOINT(CID_LIST:LOCAL),9,LEFT%编号%CID_LIST:LOCAL,3,LEFT%
		CONTINUE
	ENDIF
	WAIT
	RESTART
NEXT
PRINTL 请依次输入角色所需变更口上编号，用/分割(例：1/3/2)
PRINTL 范围0～4，初期为0
PRINTL [-1]退出
INPUTS
SIF RESULTS=="-1"
	RETURN
SPLIT RESULTS, "/", KOJO_LIST
KOJO_NUM=RESULT
IF KOJO_NUM!=CID_NUM
	PRINTW 输入值错误，口上编号数量与角色编号数不一致
	RESTART
ENDIF
;检查格式
FOR LOCAL,0,CID_NUM
	IF !ISNUMERIC(KOJO_LIST:LOCAL)
		PRINT 输入格式错误，请输入数字
	ELSEIF !INRANGE(TOINT(KOJO_LIST:LOCAL),0,4)
		PRINT 输入值超过范围
	ELSE
		;检测口上是否存在
		RESULTS:1=
		TRYCCALLFORM M_KOJO_K{TOINT(CID_LIST:LOCAL)}\@KOJO_LIST:LOCAL!="0"?_{TOINT(KOJO_LIST:LOCAL)}#\@
			SIF !STRLENS(RESULTS:1) && RESULT == 1 && KOJO_LIST:LOCAL=="0"
				RESULTS:1 = 初期
			PRINTFORML 第{LOCAL+1}个角色：%CALLNAME:TOINT(CID_LIST:LOCAL),9,LEFT%切换%KOJO_LIST:LOCAL%号%RESULTS:1%口上
			CONTINUE
		CATCH
			PRINTFORM 未检测到%CALLNAME:TOINT(CID_LIST:LOCAL)%对应{TOINT(KOJO_LIST:LOCAL)}号口上，设置取消
		ENDCATCH
	ENDIF
	WAIT
	RESTART
NEXT
;全部无误后赋值，以免中断
FOR LOCAL,0,CID_NUM
	CFLAG:TOINT(CID_LIST:LOCAL):998=TOINT(KOJO_LIST:LOCAL)
NEXT
WAIT