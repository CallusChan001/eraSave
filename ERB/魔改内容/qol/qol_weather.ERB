;qol天气系统配套判定
;-------------------------------------------------
;概　要：一系列天气相关的区域分划，例如冥界、地底、月都等天气与幻想乡不同的区域
;备  注：为讨论进入招待室的情况，判定角色现在位置时需要采用 AT_AREA(CID)预处理
;		 如果是据点地图ID，则需要用VISITOR_LOCATION(PID)预处理
;参  数：是PID-AREA，参见ERB\DIM.ERH
;引  用：替换目标 CFLAG:MASTER:現在位置 != 天界等
;-------------------------------------------------
@IS_SANZUKAWA(PID)
#FUNCTION
#LOCALSIZE 1
#LOCALSSIZE 1
#DIM DYNAMIC PID
SIF GROUPMATCH(PID, 中有之道, 三途之川)
	RETURNF 1
RETURNF 0

@IS_HIGAN(PID)
#FUNCTION
#LOCALSIZE 1
#LOCALSSIZE 1
#DIM DYNAMIC PID
SIF GROUPMATCH(PID, 彼岸)
	RETURNF 1
RETURNF 0

@IS_MEIKAI(PID)
#FUNCTION
#LOCALSIZE 1
#LOCALSSIZE 1
#DIM DYNAMIC PID
SIF GROUPMATCH(PID, 迷途之家, 白玉楼庭, 白玉楼)
	RETURNF 1
RETURNF 0


@IS_JIGOKU(PID)
#FUNCTION
#LOCALSIZE 1
#LOCALSSIZE 1
#DIM DYNAMIC PID
SIF GROUPMATCH(PID, 地獄, 畜生界, 霊長園)
	RETURNF 1
RETURNF 0

@IS_KYUUJIGOKU(PID)
#FUNCTION
#LOCALSIZE 1
#LOCALSSIZE 1
#DIM DYNAMIC PID
SIF GROUPMATCH(GET_MAPID(PID), MAP地底)
	RETURNF 1
RETURNF 0

@IS_SENKAI(PID)
#FUNCTION
#LOCALSIZE 1
#LOCALSSIZE 1
#DIM DYNAMIC PID
SIF GROUPMATCH(PID, 天界, 神霊廟広場, 神霊廟道場, 仙人的宅邸)
	RETURNF 1
RETURNF 0

@IS_YUMESEKAI(PID)
#FUNCTION
#LOCALSIZE 1
#LOCALSSIZE 1
#DIM DYNAMIC PID
SIF GROUPMATCH(PID, 槐安通路)
	RETURNF 1
RETURNF 0

@IS_TSUKIMIYAKO(PID)
#FUNCTION
#LOCALSIZE 1
#LOCALSSIZE 1
#DIM DYNAMIC PID
SIF GROUPMATCH(PID, 槐安通路)
	RETURNF 0
SIF GROUPMATCH(GET_MAPID(PID), MAP月)
	RETURNF 1
RETURNF 0

@IS_MAKAI(PID)
#FUNCTION
#LOCALSIZE 1
#LOCALSSIZE 1
#DIM DYNAMIC PID
SIF GROUPMATCH(GET_MAPID(PID), MAP魔界)
	RETURNF 1
RETURNF 0

;-------------------------------------------------
;概　要：区域内天气无法查知（彼岸、梦境），也暂时包括还没有设计独立天气系统的区域（旧地狱、月都、魔界）
;-------------------------------------------------
@IS_NO_WEATHER_SEE(PID)
#FUNCTION
#LOCALSIZE 1
#LOCALSSIZE 1
#DIM DYNAMIC PID
RETURNF IS_HIGAN(PID) || IS_YUMESEKAI(PID) || IS_KYUUJIGOKU(PID) || IS_TSUKIMIYAKO(PID) || IS_MAKAI(PID)

;-------------------------------------------------
;概　要：区域内时间无法查知，没有时间观念（三途川、彼岸、地狱、旧地狱、梦境、魔界），月都虽然无昼夜但星空应该能辨别时间
;-------------------------------------------------
@IS_NO_TIME_SEE(PID)
#FUNCTION
#LOCALSIZE 1
#LOCALSSIZE 1
#DIM DYNAMIC PID
RETURNF IS_SANZUKAWA(PID) || IS_HIGAN(PID) || IS_JIGOKU(PID) || IS_KYUUJIGOKU(PID) || IS_YUMESEKAI(PID) || IS_MAKAI(PID)

;-------------------------------------------------
;概　要：区域内没有日照（三途川、彼岸、地狱、旧地狱、梦境、魔界）
;-------------------------------------------------
@IS_NO_SUN(PID)
#FUNCTION
#LOCALSIZE 1
#LOCALSSIZE 1
#DIM DYNAMIC PID
RETURNF IS_SANZUKAWA(PID) || IS_HIGAN(PID) || IS_JIGOKU(PID) || IS_KYUUJIGOKU(PID) || IS_YUMESEKAI(PID) || IS_MAKAI(PID)

;-------------------------------------------------
;概　要：区域内没有雨（彼岸、地狱、旧地狱、天界、梦境、月都、魔界）
;-------------------------------------------------
@IS_NO_RAIN(PID)
#FUNCTION
#LOCALSIZE 1
#LOCALSSIZE 1
#DIM DYNAMIC PID
RETURNF IS_HIGAN(PID) || IS_JIGOKU(PID) || IS_KYUUJIGOKU(PID) || IS_SENKAI(PID) || IS_YUMESEKAI(PID) || IS_TSUKIMIYAKO(PID) || IS_MAKAI(PID)

;-------------------------------------------------
;概　要：区域内没有强风（三途川、彼岸、地狱、旧地狱、天界、梦境、月都），魔界当亚空间风暴吧（）
;-------------------------------------------------
@IS_NO_WIND_VELOCITY(PID)
#FUNCTION
#LOCALSIZE 1
#LOCALSSIZE 1
#DIM DYNAMIC PID
RETURNF IS_SANZUKAWA(PID) || IS_HIGAN(PID) || IS_JIGOKU(PID) || IS_KYUUJIGOKU(PID) || IS_SENKAI(PID) || IS_YUMESEKAI(PID) || IS_TSUKIMIYAKO(PID)

;-------------------------------------------------
;概　要：区域内没有彩虹（三途川、彼岸、地狱、梦境、魔界），天界可以同步或人造彩虹，月都则是有流星雨
;-------------------------------------------------
@IS_NO_RAINBOW(PID)
#FUNCTION
#LOCALSIZE 1
#LOCALSSIZE 1
#DIM DYNAMIC PID
RETURNF IS_SANZUKAWA(PID) || IS_HIGAN(PID) || IS_JIGOKU(PID) || IS_YUMESEKAI(PID) || IS_MAKAI(PID)

;-------------------------------------------------
;概　要：区域内没有天气变动（彼岸、地狱、旧地狱、天界、梦境、月都、魔界）
;-------------------------------------------------
@IS_NO_WEATHER_CHANGE(PID)
#FUNCTION
#LOCALSIZE 1
#LOCALSSIZE 1
#DIM DYNAMIC PID
RETURNF IS_HIGAN(PID) || IS_JIGOKU(PID) || IS_KYUUJIGOKU(PID) || IS_SENKAI(PID) || IS_YUMESEKAI(PID) || IS_TSUKIMIYAKO(PID) || IS_MAKAI(PID)

;-------------------------------------------------
;概　要：区域内没有风力变动（三途川、彼岸、地狱、旧地狱、天界、梦境、月都、魔界），大概是最全的一个
;-------------------------------------------------
@IS_NO_WIND_CHANGE(PID)
#FUNCTION
#LOCALSIZE 1
#LOCALSSIZE 1
#DIM DYNAMIC PID
RETURNF IS_SANZUKAWA(PID) || IS_HIGAN(PID) || IS_JIGOKU(PID) || IS_KYUUJIGOKU(PID) || IS_SENKAI(PID) || IS_YUMESEKAI(PID) || IS_TSUKIMIYAKO(PID) || IS_MAKAI(PID)

;-------------------------------------------------
;概　要：区域内没有季节变动（三途川、彼岸、地狱、天界、梦境、月都、魔界）
;-------------------------------------------------
@IS_NO_SEASON_CHANGE(PID)
#FUNCTION
#LOCALSIZE 1
#LOCALSSIZE 1
#DIM DYNAMIC PID
RETURNF IS_SANZUKAWA(PID) || IS_HIGAN(PID) || IS_JIGOKU(PID) || IS_SENKAI(PID) || IS_YUMESEKAI(PID) || IS_TSUKIMIYAKO(PID) || IS_MAKAI(PID)

;-------------------------------------------------
;概　要：区域内天气，其中0为天气补丁的通用天气，其他地区在设计独立系统前也为固定天气
;-------------------------------------------------
@qol_REGION_CHANGE_WEATHER_SYSTEM
#LOCALSIZE 1
#LOCALSSIZE 1
#DIM LOCATION_ID
;通用天气部分由天气补丁的CHANGE_WEATHER_SYSTEM完成
;三途川说是会下雨，其他时候就是一片雾，此外三途河无风
GENSOKYO_WEATHER_SYSTEM:天系_三途 = GROUPMATCH(GENSOKYO_WEATHER_SYSTEM:天系_通用, 天气_雨, 天气_大雨, 天气_暴雨) ? 天气_雨 # 天气_雾
GENSOKYO_RAINBOW_SYSTEM:天系_三途 = 天气_无虹
GENSOKYO_WIND_VE_SYSTEM:天系_三途 = 天气_无风
;彼岸没有所有天气系统，天气标记为阴吧
GENSOKYO_WEATHER_SYSTEM:天系_彼岸 = 天气_阴
GENSOKYO_RAINBOW_SYSTEM:天系_彼岸 = 天气_无虹
GENSOKYO_WIND_VE_SYSTEM:天系_彼岸 = 天气_无风
;冥界目前似乎和幻想乡没什么差别，有季节有天气，直接复用吧
GENSOKYO_WEATHER_SYSTEM:天系_冥界 = GENSOKYO_WEATHER_SYSTEM:天系_通用
GENSOKYO_RAINBOW_SYSTEM:天系_冥界 = GENSOKYO_RAINBOW_SYSTEM:天系_通用
GENSOKYO_WIND_VE_SYSTEM:天系_冥界 = GENSOKYO_WIND_VE_SYSTEM:天系_通用
;地狱目前暂时按畜生界的天气衡量，主要是地狱区域太大了每块都不一样，畜生界似乎是恒定雾
GENSOKYO_WEATHER_SYSTEM:天系_地狱 = 天气_雾
GENSOKYO_RAINBOW_SYSTEM:天系_地狱 = 天气_无虹
GENSOKYO_WIND_VE_SYSTEM:天系_地狱 = 天气_无风
;旧地狱目前暂时也按阴天标记，实际上旧地狱应该是有季节差异却没有天气差异
GENSOKYO_WEATHER_SYSTEM:天系_旧狱 = 天气_阴
GENSOKYO_RAINBOW_SYSTEM:天系_旧狱 = 天气_无虹
GENSOKYO_WIND_VE_SYSTEM:天系_旧狱 = 天气_无风
;天界、仙界恒定晴朗，而且气候温和，彩虹可以跟着幻想乡走
GENSOKYO_WEATHER_SYSTEM:天系_天界 = 天气_晴
GENSOKYO_RAINBOW_SYSTEM:天系_天界 = GENSOKYO_RAINBOW_SYSTEM:天系_通用
GENSOKYO_WIND_VE_SYSTEM:天系_天界 = 天气_无风
;梦境算什么天气……不知道填个晴吧
GENSOKYO_WEATHER_SYSTEM:天系_梦境 = 天气_晴
GENSOKYO_RAINBOW_SYSTEM:天系_梦境 = 天气_无虹
GENSOKYO_WIND_VE_SYSTEM:天系_梦境 = 天气_无风
;月都似乎和天界类似，人造天气系统应该都会保持温和吧，彩虹只考虑流星而且月球流星管饱
GENSOKYO_WEATHER_SYSTEM:天系_月都 = 天气_晴
GENSOKYO_RAINBOW_SYSTEM:天系_月都 = GROUPMATCH(GENSOKYO_RAINBOW_SYSTEM:天系_通用, 天气_流星, 天气_流星群) ? 天气_流星群 # 天气_无虹
GENSOKYO_WIND_VE_SYSTEM:天系_月都 = 天气_无风
;魔界……暂时先写阴吧，被厚重空间能量遮蔽的感觉，风力按恒定强风算吧
GENSOKYO_WEATHER_SYSTEM:天系_魔界 = 天气_阴
GENSOKYO_RAINBOW_SYSTEM:天系_魔界 = 天气_无虹
GENSOKYO_WIND_VE_SYSTEM:天系_魔界 = 天气_强风

;-------------------------------------------------
;概　要：将天气、彩虹和风力按区域赋值
;-------------------------------------------------
@qol_SET_REGION_WEATHER
#LOCALSIZE 1
#LOCALSSIZE 1
#DIM LOCATION_ID
LOCATION_ID = AT_AREA(MASTER)
IF IS_SANZUKAWA(LOCATION_ID)
	TIME:5 = GENSOKYO_WEATHER_SYSTEM:天系_三途
	TIME:7 = GENSOKYO_RAINBOW_SYSTEM:天系_三途
	WIND_VELOCITY = GENSOKYO_WIND_VE_SYSTEM:天系_三途
ELSEIF IS_HIGAN(LOCATION_ID)
	TIME:5 = GENSOKYO_WEATHER_SYSTEM:天系_彼岸
	TIME:7 = GENSOKYO_RAINBOW_SYSTEM:天系_彼岸
	WIND_VELOCITY = GENSOKYO_WIND_VE_SYSTEM:天系_彼岸
ELSEIF IS_MEIKAI(LOCATION_ID)
	TIME:5 = GENSOKYO_WEATHER_SYSTEM:天系_冥界
	TIME:7 = GENSOKYO_RAINBOW_SYSTEM:天系_冥界
	WIND_VELOCITY = GENSOKYO_WIND_VE_SYSTEM:天系_冥界
ELSEIF IS_JIGOKU(LOCATION_ID)
	TIME:5 = GENSOKYO_WEATHER_SYSTEM:天系_地狱
	TIME:7 = GENSOKYO_RAINBOW_SYSTEM:天系_地狱
	WIND_VELOCITY = GENSOKYO_WIND_VE_SYSTEM:天系_地狱
ELSEIF IS_KYUUJIGOKU(LOCATION_ID)
	TIME:5 = GENSOKYO_WEATHER_SYSTEM:天系_旧狱
	TIME:7 = GENSOKYO_RAINBOW_SYSTEM:天系_旧狱
	WIND_VELOCITY = GENSOKYO_WIND_VE_SYSTEM:天系_旧狱
ELSEIF IS_SENKAI(LOCATION_ID)
	TIME:5 = GENSOKYO_WEATHER_SYSTEM:天系_天界
	TIME:7 = GENSOKYO_RAINBOW_SYSTEM:天系_天界
	WIND_VELOCITY = GENSOKYO_WIND_VE_SYSTEM:天系_天界
ELSEIF IS_YUMESEKAI(LOCATION_ID)
	TIME:5 = GENSOKYO_WEATHER_SYSTEM:天系_梦境
	TIME:7 = GENSOKYO_RAINBOW_SYSTEM:天系_梦境
	WIND_VELOCITY = GENSOKYO_WIND_VE_SYSTEM:天系_梦境
ELSEIF IS_TSUKIMIYAKO(LOCATION_ID)
	TIME:5 = GENSOKYO_WEATHER_SYSTEM:天系_月都
	TIME:7 = GENSOKYO_RAINBOW_SYSTEM:天系_月都
	WIND_VELOCITY = GENSOKYO_WIND_VE_SYSTEM:天系_月都
ELSEIF IS_MAKAI(LOCATION_ID)
	TIME:5 = GENSOKYO_WEATHER_SYSTEM:天系_魔界
	TIME:7 = GENSOKYO_RAINBOW_SYSTEM:天系_魔界
	WIND_VELOCITY = GENSOKYO_WIND_VE_SYSTEM:天系_魔界
ELSE
	TIME:5 = GENSOKYO_WEATHER_SYSTEM:天系_通用
	TIME:7 = GENSOKYO_RAINBOW_SYSTEM:天系_通用
	WIND_VELOCITY = GENSOKYO_WIND_VE_SYSTEM:天系_通用
ENDIF

;-------------------------------------------------
;概　要：天气系统强行设定，应该只会在幻想乡之主上用到
;-------------------------------------------------
@qol_FORCESET_REGION_WEATHER_SYSTEM
#LOCALSIZE 1
#LOCALSSIZE 1
#DIM LOCATION_ID
LOCATION_ID = AT_AREA(MASTER)
IF IS_SANZUKAWA(LOCATION_ID)
	GENSOKYO_WEATHER_SYSTEM:天系_三途 = TIME:5
ELSEIF IS_HIGAN(LOCATION_ID)
	GENSOKYO_WEATHER_SYSTEM:天系_彼岸 = TIME:5
ELSEIF IS_MEIKAI(LOCATION_ID)
	GENSOKYO_WEATHER_SYSTEM:天系_冥界 = TIME:5
ELSEIF IS_JIGOKU(LOCATION_ID)
	GENSOKYO_WEATHER_SYSTEM:天系_地狱 = TIME:5
ELSEIF IS_KYUUJIGOKU(LOCATION_ID)
	GENSOKYO_WEATHER_SYSTEM:天系_旧狱 = TIME:5
ELSEIF IS_SENKAI(LOCATION_ID)
	GENSOKYO_WEATHER_SYSTEM:天系_天界 = TIME:5
ELSEIF IS_YUMESEKAI(LOCATION_ID)
	GENSOKYO_WEATHER_SYSTEM:天系_梦境 = TIME:5
ELSEIF IS_TSUKIMIYAKO(LOCATION_ID)
	GENSOKYO_WEATHER_SYSTEM:天系_月都 = TIME:5
ELSEIF IS_MAKAI(LOCATION_ID)
	GENSOKYO_WEATHER_SYSTEM:天系_魔界 = TIME:5
ELSE
	GENSOKYO_WEATHER_SYSTEM:天系_通用 = TIME:5
ENDIF

@qol_FORCESET_REGION_RAINBOW_SYSTEM
#LOCALSIZE 1
#LOCALSSIZE 1
#DIM LOCATION_ID
LOCATION_ID = AT_AREA(MASTER)
IF IS_SANZUKAWA(LOCATION_ID)
	GENSOKYO_RAINBOW_SYSTEM:天系_三途 = TIME:7
ELSEIF IS_HIGAN(LOCATION_ID)
	GENSOKYO_RAINBOW_SYSTEM:天系_彼岸 = TIME:7
ELSEIF IS_MEIKAI(LOCATION_ID)
	GENSOKYO_RAINBOW_SYSTEM:天系_冥界 = TIME:7
ELSEIF IS_JIGOKU(LOCATION_ID)
	GENSOKYO_RAINBOW_SYSTEM:天系_地狱 = TIME:7
ELSEIF IS_KYUUJIGOKU(LOCATION_ID)
	GENSOKYO_RAINBOW_SYSTEM:天系_旧狱 = TIME:7
ELSEIF IS_SENKAI(LOCATION_ID)
	GENSOKYO_RAINBOW_SYSTEM:天系_天界 = TIME:7
ELSEIF IS_YUMESEKAI(LOCATION_ID)
	GENSOKYO_RAINBOW_SYSTEM:天系_梦境 = TIME:7
ELSEIF IS_TSUKIMIYAKO(LOCATION_ID)
	GENSOKYO_RAINBOW_SYSTEM:天系_月都 = TIME:7
ELSEIF IS_MAKAI(LOCATION_ID)
	GENSOKYO_RAINBOW_SYSTEM:天系_魔界 = TIME:7
ELSE
	GENSOKYO_RAINBOW_SYSTEM:天系_通用 = TIME:7
ENDIF

@qol_FORCESET_REGION_WIND_VE_SYSTEM
#LOCALSIZE 1
#LOCALSSIZE 1
#DIM LOCATION_ID
LOCATION_ID = AT_AREA(MASTER)
IF IS_SANZUKAWA(LOCATION_ID)
	GENSOKYO_WIND_VE_SYSTEM:天系_三途 = WIND_VELOCITY
ELSEIF IS_HIGAN(LOCATION_ID)
	GENSOKYO_WIND_VE_SYSTEM:天系_彼岸 = WIND_VELOCITY
ELSEIF IS_MEIKAI(LOCATION_ID)
	GENSOKYO_WIND_VE_SYSTEM:天系_冥界 = WIND_VELOCITY
ELSEIF IS_JIGOKU(LOCATION_ID)
	GENSOKYO_WIND_VE_SYSTEM:天系_地狱 = WIND_VELOCITY
ELSEIF IS_KYUUJIGOKU(LOCATION_ID)
	GENSOKYO_WIND_VE_SYSTEM:天系_旧狱 = WIND_VELOCITY
ELSEIF IS_SENKAI(LOCATION_ID)
	GENSOKYO_WIND_VE_SYSTEM:天系_天界 = WIND_VELOCITY
ELSEIF IS_YUMESEKAI(LOCATION_ID)
	GENSOKYO_WIND_VE_SYSTEM:天系_梦境 = WIND_VELOCITY
ELSEIF IS_TSUKIMIYAKO(LOCATION_ID)
	GENSOKYO_WIND_VE_SYSTEM:天系_月都 = WIND_VELOCITY
ELSEIF IS_MAKAI(LOCATION_ID)
	GENSOKYO_WIND_VE_SYSTEM:天系_魔界 = WIND_VELOCITY
ELSE
	GENSOKYO_WIND_VE_SYSTEM:天系_通用 = WIND_VELOCITY
ENDIF

;-------------------------------------------------
;INFO信息qol，类似于INFO_SHOW_WEATHER，天气赋值系统改了之后这个地方就可以不用根据地图修改参数了，但还是保持参数化吧
@qol_INFO_SHOW_WEATHER(ARG = -1, ARG:1 = -1, ARG:2 = -1, ARG:3 = -1)
#LOCALSIZE 1
#LOCALSSIZE 1
SIF ARG == -1
	ARG = MASTER
SIF ARG:1 == -1
	ARG:1 = TIME:2
SIF ARG:2 == -1
	ARG:2 = TIME:5
SIF ARG:3 == -1
	ARG:3 = TIME:7
IF FLAG:異常気象 >= 天气_Ｃ敏感 && FLAG:異常気象 <= 天气_桃色之风
	;异变级的天气，在哪都挡不住哦
	SETCOLOR C_PINK
	GOTO WEATHER_INFO
ELSEIF IS_NO_WEATHER_SEE(AT_AREA(ARG))
	;无法查知天气的区域，例如彼岸、梦境等，不显示天气信息
	;原功能相当于天気非表示
	RETURN
ELSE
	;原功能相当于元の天気的部分，被赋值系统取代
	;(天气补丁)暴雨特殊颜色
	SIF ARG:2 == 天气_暴雨
		SETCOLOR C_P_PURPLE
	$WEATHER_INFO
	PRINTFORM ― %GET_WEATHER(ARG:2)% ―
	RESETCOLOR
	IF WEATHERDLC
		CALL WEATHERSTAUSSHOW(ARG, ARG:1, ARG:2, ARG:3)
		GOTO SKIP
	ENDIF

	SELECTCASE ARG:2
	CASE 天气_少云, 天气_晴
		IF ARG:1 >= 时段_正午前_6_12 && ARG:1 <= 时段_昼下がり_15_18
			;06時から18時までは昼間
			SETCOLOR 0xFF9640
			PRINTFORM %UNICODE(0x2600)%
		ELSE
			;19時から06時までは夜間
			SETCOLOR 0xFFFF40
			PRINTFORM %UNICODE(0x262A)%
				;俗气のので保留
				;IF MOON_WATCH(DAY:3) > 0
				;PRINTFORM %UNICODE(0x262A)%
				;ELSE
				;UNICODE 0x2728
				;PRINT ★
				;ENDIF
		ENDIF
	CASE 天气_多云, 天气_阴
		SETCOLOR 0x9C9C9C
		PRINTFORM %UNICODE(0x2601)%
	CASE 天气_雨, 天气_大雨, 天气_雾雨, 天气_雾
		IF TEQUIP:TARGET:201 && OUTROOF(CFLAG:ARG:現在位置)
			SETCOLOR 0xFF99CC
			PRINTFORM %UNICODE(0x2602)%
			SETCOLOR 0xFFCCFF
			CALL HEART
			PRINTFORM 相合伞
			CALL HEART
		ELSE
			SETCOLOR 0x0000FF
			PRINTFORM %UNICODE(0x2602)%
		ENDIF
	CASE 天气_雪, 天气_大雪, 天气_细雪, 天气_雾雪, 天气_雨夹雪
		IF TEQUIP:TARGET:201 && OUTROOF(CFLAG:ARG:現在位置)
			SETCOLOR 0xFFCCFF
			PRINTFORM %UNICODE(0x2603)%
			SETCOLOR 0xFF99CC
			CALL HEART
			PRINTFORM 相合伞
			CALL HEART
		ELSE
			SETCOLOR 0xFFFFFF
			PRINTFORM %UNICODE(0x2603)%
		ENDIF
	CASE 天气_冰霰
		IF GET_HEAT_THUNDERSTORM() == 1 && TEQUIP:TARGET:201 && OUTROOF(CFLAG:ARG:現在位置)
			SETCOLOR 0xFF99CC
			PRINTFORM %UNICODE(0x2602)%
			SETCOLOR 0xFFCCFF
			CALL HEART
			PRINTFORM 相合伞
			CALL HEART
		ELSEIF TEQUIP:TARGET:201 && OUTROOF(CFLAG:ARG:現在位置)
			SETCOLOR 0xFFCCFF
			PRINTFORM %UNICODE(0x2603)%
			SETCOLOR 0xFF99CC
			CALL HEART
			PRINTFORM 相合伞
			CALL HEART
		ELSE
			SETCOLOR C_AQUA
			PRINTFORM ∵
		ENDIF
	CASE 天气_暴雨
		IF TEQUIP:TARGET:201 && OUTROOF(CFLAG:ARG:現在位置)
			SETCOLOR 0xFF99CC
			PRINTFORM %UNICODE(0x2602)%
			SETCOLOR 0xFFCCFF
			CALL HEART
			PRINTFORM 相合伞
			CALL HEART
		ELSE
			SETCOLOR C_気象_土砂災害警戒情報
			PRINTFORM %UNICODE(0x2602)%
		ENDIF
	CASE 天气_冰雹
		SETCOLOR C_AQUA
		PRINTFORM ◆
	ENDSELECT

	;(天候パッチ)雷
	;我搞不懂为什么两个雷和一个雷要和或讨论，这不是直接分开讨论就好了吗
	IF GET_FRONTAL_THUNDERSTORM() == 1 && GET_WEATHER_THUNDERSTORM() == 1
		SETCOLOR C_YELLOW
		PRINTFORM %UNICODE(0x26A1)%
	ENDIF
	IF GET_HEAT_THUNDERSTORM() == 1 && GET_WEATHER_THUNDERSTORM() == 1
		SETCOLOR C_YELLOW
		PRINTFORM %UNICODE(0x26A1)%
	ENDIF
	RESETCOLOR

	$SKIP
	IF ARG:3 == 天气_彩虹
		SETCOLOR 0xFF0000
		PRINT /
		SETCOLOR 0xFFCC00
		PRINT /
		SETCOLOR 0xFFFF00
		PRINT /
		SETCOLOR 0x66CC00
		PRINT /
		SETCOLOR 0x3399FF
		PRINT /
		SETCOLOR 0x0033CC
		PRINT /
		SETCOLOR 0x990099
		PRINT /
	ELSEIF ARG:3 == 天气_钻石尘
		SETCOLOR 0xFFFFFF
		PRINTFORM +。*:゜+:
	ENDIF
	RESETCOLOR
	CALL INFO_MOON

	;(天候パッチ)風速の表示
	SELECTCASE WIND_VELOCITY
		CASE 天气_强风
			PRINT 　
			SETCOLOR C_気象_注意報
			PRINT 强风
		CASE 天气_暴风
			PRINT 　
			SETCOLOR C_気象_注意報
			PRINTFORM %UNICODE(0x26A0)%
			SETCOLOR C_気象_警報
			PRINT 暴风
		CASE 天气_飓风
			PRINT 　
			SETCOLOR C_気象_注意報
			PRINTFORM %UNICODE(0x26A0)%
			SETCOLOR C_気象_土砂災害警戒情報
			PRINT 飓风
	ENDSELECT
	RESETCOLOR
ENDIF

@qol_WEATHER_FORECAST_1TERM_TO_CROP
#LOCALSIZE 1
#LOCALSSIZE 1
IF FLAG:地主 > 0 && !IS_NO_WEATHER_CHANGE(VISITOR_LOCATION(INT_NOUMIN(FLAG:地主, "場所ID")))
	;寒気・暖気による作物への影響
	IF (POWER_WARM_AIR == 3 || POWER_WARM_AIR == 4) && (DAY:2 == 季节_春 || DAY:2 == 季节_秋)
		FLAG:田地工作的成果 += 10
	ELSEIF POWER_COLD_AIR == 5 && DAY:2 == 季节_春
		FLAG:田地工作的成果 = MAX(0, FLAG:田地工作的成果 - 10)
		AMOUNT_COLD_CROP_DAMAGE += 10
	ENDIF
	;梅雨による作物への影響
	IF DAY:2 == 2 && (NOW_TERM == 1 || NOW_TERM == 2) && IS_RAINY_SEASON1:0 == 1
		FLAG:田地工作的成果 += 20
	ELSEIF DAY:2 == 2 && NOW_TERM == 4 && IS_RAINY_SEASON1:0 == 1
		FLAG:田地工作的成果 = MAX(0, FLAG:田地工作的成果 - 20)
	ELSEIF DAY:2 == 2 && NOW_TERM == 5 && IS_RAINY_SEASON1:0 == 1
		FLAG:田地工作的成果 = MAX(0, FLAG:田地工作的成果 - 40)
		IS_COLD_CROP_DAMAGE ++
	ELSEIF DAY:2 == 2 && NOW_TERM == 6 && IS_RAINY_SEASON1:0 == 1
		FLAG:田地工作的成果 = MAX(0, FLAG:田地工作的成果 - 80)
		IS_COLD_CROP_DAMAGE ++
	ENDIF
	;連続降水Counter20以上で水害発生の可能性、作物に壊滅的な被害
	IF CONTINUOUS_RAINFALL >= 20 && RAND:(MAX(1, (40 - CONTINUOUS_RAINFALL) / 5)) == 0
		IS_FLOOD_CROP_DAMAGE = 1
		FLAG:田地工作的成果 = 0
	ENDIF
ENDIF

@qol_WEATHER_CHANGE_TO_CROP(ARGS)
#LOCALSIZE 1
#LOCALSSIZE 1
IF FLAG:地主 > 0 && !IS_NO_WEATHER_CHANGE(VISITOR_LOCATION(INT_NOUMIN(FLAG:地主, "場所ID")))
	SELECTCASE ARGS
		CASE "STORM"
			FLAG:田地工作的成果 = MAX(0, FLAG:田地工作的成果 - 30)
			IS_STORM_CROP_DAMAGE ++
		CASEELSE
			THROW 未设置正确的获取内容。
	ENDSELECT
ENDIF

;连续降雨计数
@qol_CONTINUOUS_RAINFALL_COUNTER(ARG)
#LOCALSIZE 1
#LOCALSSIZE 1
SELECTCASE ARG
	CASE 天气_少云, 天气_晴, 天气_多云
		CONTINUOUS_RAINFALL = 0
	CASE 天气_大雨
		CONTINUOUS_RAINFALL += 1
	CASE 天气_暴雨
		CONTINUOUS_RAINFALL += 3
	CASE 天气_阴, 天气_雾雨, 天气_雾, 天气_细雪, 天气_雾雪
		CONTINUOUS_RAINFALL = MAX(0, CONTINUOUS_RAINFALL - 1)
ENDSELECT

;下击暴流生成，ARG默认设1
@qol_DOWNBURST_GENERATION(ARG = 1)
#LOCALSIZE 1
#LOCALSSIZE 1
IF ATMOSPHERIC_INSTABILITY <= -2 && ARG
	SELECTCASE WIND_VELOCITY
		CASE 天气_无风
			WIND_VELOCITY = 天气_暴风
			;ダウンバーストによる作物への影響
			CALL qol_WEATHER_CHANGE_TO_CROP("STORM")
		CASE 天气_强风
			WIND_VELOCITY = 天气_暴风
			;ダウンバーストによる作物への影響
			CALL qol_WEATHER_CHANGE_TO_CROP("STORM")
		CASEELSE
			WIND_VELOCITY = 天气_暴风
	ENDSELECT
ELSE
	WIND_VELOCITY = 天气_强风
	;鬼知道为什么分支一样，可能是方便拓展吧，先留着
	[SKIPSTART]
	SELECTCASE WIND_VELOCITY
		CASE 天气_无风
			WIND_VELOCITY = 天气_强风
		CASE 天气_强风
			WIND_VELOCITY = 天气_强风
		CASEELSE
			WIND_VELOCITY = 天气_强风
	ENDSELECT
	[SKIPEND]
ENDIF

;春连续降雪计数
@qol_CONTINUOUS_SNOWFALL_COUNTER_SPRING(ARG)
#LOCALSIZE 1
#LOCALSSIZE 1
SELECTCASE ARG
	CASE 天气_少云, 天气_晴 , 天气_多云, 天气_阴, 天气_雾
		IF CONTINUOUS_SNOWFALL >= 1
			SNOW_COVERAGE = CONTINUOUS_SNOWFALL
			CONTINUOUS_SNOWFALL = 0
			SNOW_COVERAGE = MAX(0, SNOW_COVERAGE - 3)
		ELSE
			SNOW_COVERAGE = MAX(0, SNOW_COVERAGE - 4)
		ENDIF
	CASE 天气_雨, 天气_大雨, 天气_雾雨 ,天气_暴雨
		IF CONTINUOUS_SNOWFALL >= 1
			SNOW_COVERAGE = CONTINUOUS_SNOWFALL
			CONTINUOUS_SNOWFALL = 0
			SNOW_COVERAGE = MAX(0, SNOW_COVERAGE - 8)
		ELSE
			SNOW_COVERAGE = MAX(0, SNOW_COVERAGE - 10)
		ENDIF
	CASE 天气_雪
		IF CONTINUOUS_SNOWFALL >= 1
			SNOW_COVERAGE = CONTINUOUS_SNOWFALL
			CONTINUOUS_SNOWFALL = 0
			SNOW_COVERAGE += 1
		ELSE
			SNOW_COVERAGE += 1
		ENDIF
	CASE 天气_大雪
		IF CONTINUOUS_SNOWFALL >= 1
			SNOW_COVERAGE = CONTINUOUS_SNOWFALL
			CONTINUOUS_SNOWFALL = 0
			SNOW_COVERAGE += 3
		ELSE
			SNOW_COVERAGE += 3
		ENDIF
	CASEELSE
		IF CONTINUOUS_SNOWFALL >= 1
			SNOW_COVERAGE = CONTINUOUS_SNOWFALL
			CONTINUOUS_SNOWFALL = 0
		ELSE
			SNOW_COVERAGE = MAX(0, SNOW_COVERAGE - 1)
		ENDIF
ENDSELECT

;春雪崩发生判定
@qol_POSSIBLITY_AVALANCHE_SPRING(ARG)
#LOCALSIZE 1
#LOCALSSIZE 1
IF SNOW_COVERAGE + CONTINUOUS_SNOWFALL >= 15 && (POWER_WARM_AIR >= 3 || ARG <= 天气_雾 || ARG == 天气_暴雨)
	POSSIBLITY_AVALANCHE = 2
ELSEIF SNOW_COVERAGE + CONTINUOUS_SNOWFALL >= 10 && (POWER_WARM_AIR == 4 || ARG == 天气_雨 || ARG == 天气_大雨 || ARG == 天气_暴雨)
	POSSIBLITY_AVALANCHE = 2
ELSEIF SNOW_COVERAGE + CONTINUOUS_SNOWFALL >= 7 && (POWER_WARM_AIR >= 5 || ARG == 天气_大雨 || ARG == 天气_暴雨)
	POSSIBLITY_AVALANCHE = 2
ELSE
	POSSIBLITY_AVALANCHE = 0
ENDIF

;冬连续降雪计数
@qol_CONTINUOUS_SNOWFALL_COUNTER_WINTER(ARG)
#LOCALSIZE 1
#LOCALSSIZE 1
SELECTCASE ARG
	CASE 天气_少云, 天气_晴, 天气_多云
		IF CONTINUOUS_SNOWFALL >= 1
			SNOW_COVERAGE = CONTINUOUS_SNOWFALL
			CONTINUOUS_SNOWFALL = 0
			SNOW_COVERAGE = MAX(0, SNOW_COVERAGE - 1)
		ELSE
			SNOW_COVERAGE = MAX(0, SNOW_COVERAGE - 3)
		ENDIF
	CASE 天气_阴
		IF CONTINUOUS_SNOWFALL >= 1
			SNOW_COVERAGE = CONTINUOUS_SNOWFALL
			CONTINUOUS_SNOWFALL = 0
		ELSE
			SNOW_COVERAGE = MAX(0, SNOW_COVERAGE - 1)
		ENDIF
	CASE 天气_雨, 天气_大雨, 天气_雾雨, 天气_暴雨
		IF CONTINUOUS_SNOWFALL >= 1
			SNOW_COVERAGE = CONTINUOUS_SNOWFALL
			CONTINUOUS_SNOWFALL = 0
			SNOW_COVERAGE = MAX(0, SNOW_COVERAGE - 4)
		ELSE
			SNOW_COVERAGE = MAX(0, SNOW_COVERAGE - 6)
		ENDIF
	CASE 天气_雪
		CONTINUOUS_SNOWFALL += 1
	CASE 天气_大雪
		CONTINUOUS_SNOWFALL += 3
ENDSELECT

;冬雪崩发生判定
@qol_POSSIBLITY_AVALANCHE_WINTER(ARG)
#LOCALSIZE 1
#LOCALSSIZE 1
IF SNOW_COVERAGE >= 15 && CONTINUOUS_SNOWFALL >= 10
	POSSIBLITY_AVALANCHE = 1
ELSEIF SNOW_COVERAGE >= 10 && CONTINUOUS_SNOWFALL >= 7 && WIND_VELOCITY >= 1
	POSSIBLITY_AVALANCHE = 1
ELSEIF SNOW_COVERAGE >= 5 && CONTINUOUS_SNOWFALL >= 5 && WIND_VELOCITY >= 2
	POSSIBLITY_AVALANCHE = 1
ELSE
	POSSIBLITY_AVALANCHE = 0
ENDIF

;冬低气压团雪崩发生判定
@qol_POSSIBLITY_AVALANCHE_WINTER_LOW(ARG)
#LOCALSIZE 1
#LOCALSSIZE 1
IF SNOW_COVERAGE + CONTINUOUS_SNOWFALL >= 15 && (POWER_WARM_AIR == 4 || ARG == 天气_雨)
	POSSIBLITY_AVALANCHE = 2
ELSEIF SNOW_COVERAGE + CONTINUOUS_SNOWFALL >= 10 && (POWER_WARM_AIR >= 5 || ARG == 天气_大雨)
	POSSIBLITY_AVALANCHE = 2
ELSEIF SNOW_COVERAGE >= 15 && CONTINUOUS_SNOWFALL >= 10
	POSSIBLITY_AVALANCHE = 1
ELSEIF SNOW_COVERAGE >= 10 && CONTINUOUS_SNOWFALL >= 7 && WIND_VELOCITY >= 1
	POSSIBLITY_AVALANCHE = 1
ELSEIF SNOW_COVERAGE >= 5 && CONTINUOUS_SNOWFALL >= 5 && WIND_VELOCITY >= 2
	POSSIBLITY_AVALANCHE = 1
ELSE
	POSSIBLITY_AVALANCHE = 0
ENDIF

;雪崩判定解除
@qol_NO_AVALANCHE
#LOCALSIZE 1
#LOCALSSIZE 1
CONTINUOUS_SNOWFALL = 0
SNOW_COVERAGE = 0
POSSIBLITY_AVALANCHE = 0

;相合伞解除
@qol_NO_SHARED_UMBRELLA
#LOCALSIZE 1
#LOCALSSIZE 1
FOR LOCAL,0,CHARANUM
	SIF TEQUIP:LOCAL:201
		TEQUIP:LOCAL:201 = 0
NEXT

;文本打印部
@qol_WEATHER_FORECAST_DIARY_PRINT(ARGS)
#LOCALSIZE 1
#LOCALSSIZE 1
SELECTCASE ARGS
	CASE "入梅"
		IF !IS_NO_RAIN(AT_AREA(MASTER))
			PRINTL 阴沉的天空…
		ELSE
			PRINTFORM 听说幻想乡
		ENDIF
		PRINTFORMW 好像进入梅雨季节了
		CALL ASK_DIARY("入梅了")
	CASE "长梅"
		IF !IS_NO_RAIN(AT_AREA(MASTER))
			PRINTL 不知不觉间夏天的闷热已经过去了
		ELSE
			PRINTFORM 听说幻想乡
		ENDIF
		PRINTFORMW 梅雨好像还没有结束
		CALL ASK_DIARY("漫长梅雨")
	CASE "出梅"
		IF !IS_NO_RAIN(AT_AREA(MASTER))
			PRINTL 清爽的夏日模样！
		ELSE
			PRINTFORM 听说幻想乡
		ENDIF
		PRINTFORMW 好像梅雨季节结束了
		CALL ASK_DIARY("出梅了")
	CASEELSE
		THROW 未设置正确的获取内容。
ENDSELECT

;(无天气, MASTER, 上回天气, 当前天气, 时段)
@qol_CHANGE_WEATHER_SYSTEM_INFO_PRINT(无天气, ARG, ARG:1, ARG:2, ARG:3)
#LOCALSIZE 1
#LOCALSSIZE 1
#DIM 无天气
SIF 无天气 || CFLAG:ARG:诶嘿嘿 || IS_NO_WEATHER_CHANGE(AT_AREA(ARG))
	RETURN
SELECTCASE ARG:2
	CASE 天气_少云
		IF GROUPMATCH(ARG:1, 天气_少云)
			RETURN
		ELSEIF GROUPMATCH(ARG:1, 天气_晴)
			PRINTW 少量的云出來了
		ELSEIF GROUPMATCH(ARG:1, 天气_多云, 天气_阴)
			IF ARG:3 >= 时段_夕_18_20 && ARG:3 <= 时段_深夜_23_4
				PRINTW 可以看见星星了
			ELSE
				PRINTW 太阳出来了
			ENDIF
		ELSEIF GROUPMATCH(ARG:1, 天气_雾雨, 天气_雾)
			IF ARG:3 >= 时段_夕_18_20 && ARG:3 <= 时段_深夜_23_4
				PRINTW 可以看见星星了
			ELSE
				PRINTW 太阳出来了
			ENDIF
			;念のため相合伞を解除
			CALL qol_NO_SHARED_UMBRELLA
		ELSEIF GROUPMATCH(ARG:1, 天气_雨, 天气_大雨, 天气_冰霰, 天气_暴雨, 天气_冰雹)
			IF ARG:3 >= 时段_夕_18_20 && ARG:3 <= 时段_深夜_23_4
				PRINTW 突然星空变的晴朗了！说不定能看到月虹呢
			ELSE
				PRINTW 突然太阳出来了！说不定能看到彩虹呢
			ENDIF
			;念のため相合伞を解除
			CALL qol_NO_SHARED_UMBRELLA
		ELSEIF GROUPMATCH(ARG:1, 天气_雪, 天气_大雪, 天气_细雪, 天气_雾雪, 天气_雨夹雪)
			IF ARG:3 >= 时段_夕_18_20 && ARG:3 <= 时段_深夜_23_4
				PRINTW 突然星空变的晴朗了！说不定能看到钻石尘呢
			ELSE
				PRINTW 突然太阳出来了！说不定能看到钻石尘呢
			ENDIF
			;念のため相合伞を解除
			CALL qol_NO_SHARED_UMBRELLA
		ELSE
			PRINTW 少量的云出來了
		ENDIF
	CASE 天气_晴
		;qol 与少云的文本做了区分，因此加了些描述
		IF GROUPMATCH(ARG:1, 天气_晴)
			RETURN
		ELSEIF GROUPMATCH(ARG:1, 天气_少云)
			PRINTW 天空放晴了！！
			;念のため相合伞を解除
			CALL qol_NO_SHARED_UMBRELLA
		ELSEIF GROUPMATCH(ARG:1, 天气_多云, 天气_阴)
			IF ARG:3 >= 时段_夕_18_20 && ARG:3 <= 时段_深夜_23_4
				PRINTW 天空放晴了！可以看见星星了
			ELSE
				PRINTW 天空放晴了！太阳出来了
			ENDIF
		ELSEIF GROUPMATCH(ARG:1, 天气_雾雨, 天气_雾)
			IF ARG:3 >= 时段_夕_18_20 && ARG:3 <= 时段_深夜_23_4
				PRINTW 天空放晴了！可以看见星星了
			ELSE
				PRINTW 天空放晴了！太阳出来了
			ENDIF
			;念のため相合伞を解除
			CALL qol_NO_SHARED_UMBRELLA
		ELSEIF GROUPMATCH(ARG:1, 天气_雨, 天气_大雨, 天气_冰霰, 天气_暴雨, 天气_冰雹)
			IF ARG:3 >= 时段_夕_18_20 && ARG:3 <= 时段_深夜_23_4
				PRINTW 突然星空变的晴朗了！这下应该能看到月虹了
			ELSE
				PRINTW 突然太阳出来了！这下应该能看到彩虹了
			ENDIF
			;念のため相合伞を解除
			CALL qol_NO_SHARED_UMBRELLA
		ELSEIF GROUPMATCH(ARG:1, 天气_雪, 天气_大雪, 天气_细雪, 天气_雾雪, 天气_雨夹雪)
			IF ARG:3 >= 时段_夕_18_20 && ARG:3 <= 时段_深夜_23_4
				PRINTW 突然星空变的晴朗了！这下应该能看到钻石尘了
			ELSE
				PRINTW 突然太阳出来了！这下应该能看到钻石尘了
			ENDIF
			;念のため相合伞を解除
			CALL qol_NO_SHARED_UMBRELLA
		ELSE
			PRINTW 天空放晴了！！
			;念のため相合伞を解除
			CALL qol_NO_SHARED_UMBRELLA
		ENDIF
	CASE 天气_多云
		IF GROUPMATCH(ARG:1, 天气_多云)
			RETURN
		ELSEIF GROUPMATCH(ARG:1, 天气_少云, 天气_晴)
			PRINTW 变成多云天气了
		ELSEIF GROUPMATCH(ARG:1, 天气_阴)
			PRINTW 云变薄了
		ELSE
			PRINTW 变成多云天气了
		ENDIF
	CASE 天气_阴
		IF GROUPMATCH(ARG:1, 天气_阴)
			RETURN
		ELSEIF GROUPMATCH(ARG:1, 天气_少云, 天气_晴)
			PRINTW 天空被云层完全覆盖了
		ELSEIF GROUPMATCH(ARG:1, 天气_多云)
			PRINTW 云变厚了
		ELSEIF GROUPMATCH(ARG:1, 天气_雨, 天气_大雨, 天气_冰霰, 天气_暴雨, 天气_冰雹)
			PRINTW 雨好像停了的样子
			;念のため相合伞を解除
			CALL qol_NO_SHARED_UMBRELLA
		ELSEIF GROUPMATCH(ARG:1, 天气_雪, 天气_大雪, 天气_细雪, 天气_雾雪, 天气_雨夹雪)
			PRINTW 雪好像停了的样子
			;念のため相合伞を解除
			CALL qol_NO_SHARED_UMBRELLA
		ELSE
			PRINTW 天空被云层完全覆盖了
		ENDIF
	CASE 天气_雨
		IF GROUPMATCH(ARG:1, 天气_雨)
			RETURN
		ELSEIF GROUPMATCH(ARG:1, 天气_少云, 天气_晴, 天气_多云, 天气_阴, 天气_雾雨, 天气_雾)
			PRINTW 下雨了！
		ELSEIF GROUPMATCH(ARG:1, 天气_大雨, 天气_冰霰, 天气_暴雨, 天气_冰雹)
			PRINTW 雨变小了
		ELSEIF GROUPMATCH(ARG:1, 天气_雪, 天气_大雪, 天气_细雪, 天气_雾雪, 天气_雨夹雪)
			PRINTW 雪天好像变成了雨天的样子
		ELSE
			PRINTW 下雨了！
		ENDIF
	CASE 天气_大雨
		IF GROUPMATCH(ARG:1, 天气_大雨)
			RETURN
		ELSEIF GROUPMATCH(ARG:1, 天气_雨, 天气_雪, 天气_冰雹)
			PRINTW 变成了瓢泼大雨！
		ELSEIF GROUPMATCH(ARG:1, 天气_暴雨)
			PRINTW 雨稍微小了一些
		ELSE
			PRINTW 变成了瓢泼大雨！
		ENDIF
	CASE 天气_雾雨
		IF GROUPMATCH(ARG:1, 天气_雾雨)
			RETURN
		ELSEIF GROUPMATCH(ARG:1, 天气_雨)
			PRINTW 雨点变小了
		ELSEIF GROUPMATCH(ARG:1, 天气_雾)
			PRINTW 变成雾雨了
		ELSE
			PRINTW 变成雾雨了
		ENDIF
	CASE 天气_雾
		IF GROUPMATCH(ARG:1, 天气_雾)
			RETURN
		ELSEIF GROUPMATCH(ARG:1, 天气_雾雨)
			PRINTW 雨停了
			;念のため相合伞を解除
			CALL qol_NO_SHARED_UMBRELLA
		ELSEIF GROUPMATCH(ARG:1, 天气_细雪)
			PRINTW 雪停了
			;念のため相合伞を解除
			CALL qol_NO_SHARED_UMBRELLA
		ELSE
			PRINTW 起雾了
			;念のため相合伞を解除
			CALL qol_NO_SHARED_UMBRELLA
		ENDIF
	CASE 天气_雪
		IF GROUPMATCH(ARG:1, 天气_雪)
			RETURN
		ELSEIF GROUPMATCH(ARG:1, 天气_多云, 天气_阴, 天气_雨, 天气_大雨, 天气_雾雨, 天气_雾, 天气_细雪, 天气_雾雪, 天气_雨夹雪, 天气_冰霰, 天气_暴雨, 天气_冰雹)
			PRINTW 下雪了！
		ELSEIF GROUPMATCH(ARG:1, 天气_大雪)
			PRINTW 雪好像变弱了
		ELSE
			PRINTW 下雪了！
		ENDIF
	CASE 天气_大雪
		IF GROUPMATCH(ARG:1, 天气_大雪)
			RETURN
		ELSEIF GROUPMATCH(ARG:1, 天气_阴)
			PRINTW 下大雪了！
		ELSEIF GROUPMATCH(ARG:1, 天气_雨)
			PRINTW 雨变成雪了！
		ELSEIF GROUPMATCH(ARG:1, 天气_雪, 天气_冰霰)
			PRINTW 雪越来越大了！
		ELSE
			PRINTW 下大雪了！
		ENDIF
	CASE 天气_细雪
		IF GROUPMATCH(ARG:1, 天气_细雪)
			RETURN
		ELSEIF GROUPMATCH(ARG:1, 天气_雾)
			PRINTW 变成细雪了
		ELSEIF GROUPMATCH(ARG:1, 天气_雪)
			PRINTW 雪花变小了
		ELSEIF GROUPMATCH(ARG:1, 天气_雾雪, 天气_大雪)
			PRINTW 雪好像变弱了
		ELSE
			PRINTW 下雪了！
		ENDIF
	CASE 天气_雾雪
		IF GROUPMATCH(ARG:1, 天气_雾雪)
			RETURN
		ELSEIF GROUPMATCH(ARG:1, 天气_雾)
			PRINTW 变成雾雪了
		ELSEIF GROUPMATCH(ARG:1, 天气_雪)
			PRINTW 雪花变小了
		ELSEIF GROUPMATCH(ARG:1, 天气_大雪)
			PRINTW 雪好像变弱了
		ELSE
			PRINTW 下雪了！
		ENDIF
	CASE 天气_雨夹雪
		IF GROUPMATCH(ARG:1, 天气_细雪)
			RETURN
		ELSEIF GROUPMATCH(ARG:1, 天气_雨)
			PRINTW 雨夹雪降下来了！
		ELSEIF GROUPMATCH(ARG:1, 天气_雪)
			PRINTW 雪中掺了一些雨点
		ELSE
			PRINTW 雨夹雪降下来了！
		ENDIF
	CASE 天气_冰霰
		;SIF GROUPMATCH(ARG:1, 天气_雨)
			PRINTW 落小冰雹了！
	CASE 天气_暴雨
		;SIF GROUPMATCH(ARG:1, 天气_雨)
			PRINTW 下起了暴雨！
	CASE 天气_冰雹
		;SIF GROUPMATCH(ARG:1, 天气_雨)
			PRINTW 下起了大块的冰雹！
	CASEELSE
		RETURN
ENDSELECT

;(无天气, MASTER, 上回风力, 当前风力)
@qol_CHANGE_WEATHER_SYSTEM_WIND_PRINT(无天气, ARG, ARG:1, ARG:2)
#LOCALSIZE 1
#LOCALSSIZE 1
#DIM 无天气
SIF 无天气 || CFLAG:ARG:诶嘿嘿 || IS_NO_WIND_CHANGE(AT_AREA(ARG))
	RETURN
SELECTCASE ARG:2
	CASE 天气_无风
		SIF ARG:1
			PRINTW 风停了
	CASE 天气_强风
		IF GROUPMATCH(ARG:1, 天气_无风)
			PRINTW 风变强了
		ELSEIF ARG:1 >= 天气_暴风
			PRINTW 风比刚才变弱了
		ENDIF
	CASE 天气_暴风
		IF GROUPMATCH(ARG:1, 天气_无风)
			PRINTW 风突然就变强了！
		ELSEIF GROUPMATCH(ARG:1, 天气_强风)
			PRINTW 风越来越强了！
		ELSEIF ARG:1 >= 天气_飓风
			PRINTW 风比刚才变弱了
		ENDIF
	CASE 天气_飓风
		IF GROUPMATCH(ARG:1, 天气_无风)
			PRINTW 骤然间刮起了猛烈的风！
		ELSEIF GROUPMATCH(ARG:1, 天气_强风, 天气_暴风)
			PRINTW 刮起了猛烈的风！
		ENDIF
	CASEELSE
		RETURN
ENDSELECT

@qol_WEATHER_CHANGE_DIARY_PRINT(ARGS)
#LOCALSIZE 1
#LOCALSSIZE 1
SELECTCASE ARGS
	CASE "春风"
		IF !IS_NO_WIND_CHANGE(AT_AREA(MASTER))
			PRINTW 风变强了 春天來了！
		ELSE
			PRINTFORMW 听说幻想乡好像进入春季了
		ENDIF
		CALL ASK_DIARY("第一缕春风")
	CASE "暴风雪"
		IF !IS_NO_WIND_CHANGE(AT_AREA(MASTER))
			PRINTW 突然下起了暴风雪！
		ELSE
			PRINTW 听说幻想乡好像突然遭遇了暴风雪
		ENDIF
		CALL ASK_DIARY("晴天急转暴风雪")
	CASE "台风"
		IF !IS_NO_WIND_CHANGE(AT_AREA(MASTER))
			PRINTW 台风好像袭来了
		ELSE
			PRINTW 听说幻想乡好像遭遇了台风
		ENDIF
		CALL ASK_DIARY("台风来袭了")
	CASEELSE
		THROW 未设置正确的获取内容。
ENDSELECT
