;-------------------------------------------------
;1 春・秋の天気(基本)
;-------------------------------------------------
@qol_CHANGE_WEATHER_SYSTEM_1(无天气)
#LOCALSIZE 1
#LOCALSSIZE 1
#DIM 无天气
#DIM 上回天气
#DIM 上回风力
GENSOKYO_RAINBOW_SYSTEM:天系_通用 = 天气_无虹
上回天气 = GENSOKYO_WEATHER_SYSTEM:天系_通用
上回风力 = GENSOKYO_WIND_VE_SYSTEM:天系_通用

SELECTCASE GENSOKYO_WEATHER_SYSTEM:天系_通用
	;晴天の時
	CASE 天气_少云
		;20%で晴天に移行
		IF RAND:5 == 0
			GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_晴
		;20%で多雲に移行
		ELSEIF RAND:5 == 0
			GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_多云
		;上記確率に引っかからなかったら晴天
		ELSE
			GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_少云
		ENDIF
	CASE 天气_晴
		;20%で晴天に移行
		IF RAND:5 == 0
			GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_少云
		;20%で多雲に移行
		ELSEIF RAND:5 == 0
			GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_多云
		ENDIF
	;陰天の時
	CASE 天气_多云, 天气_阴
		;50%で弱体化
		IF RAND:2 == 0
			GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_多云
		ELSE
			;晴天に移行
			GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_少云
		ENDIF
	;雨の時
	CASE 天气_雨, 天气_大雨, 天气_冰霰, 天气_暴雨, 天气_冰雹
		;50%で弱体化
		IF RAND:2 == 0 && GENSOKYO_WEATHER_SYSTEM:天系_通用 != 天气_雨
			GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_雨
		ELSE
			;25%で陰天に移行
			IF RAND:4 == 0
				GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_阴
			;雨なら10%で霧雨か雨雪交加に移行
			ELSEIF GENSOKYO_WEATHER_SYSTEM:天系_通用 == 天气_雨 && RAND:10 == 0
				GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_雾雨
			;晴天に移行
			ELSE
				GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_少云
				;更に低確率で晴朗
				SIF RAND:100 < 5
					GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_晴
				;雨→晴天を処理したら虹が出る
				GENSOKYO_RAINBOW_SYSTEM:天系_通用 = 天气_彩虹
				;市場が開かれる
				FLAG:市場の神 = 1
			ENDIF
		ENDIF
	;霧・霧雨の時
	CASE 天气_雾雨, 天气_雾
		GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_少云
	CASE IS >= 天气_雪
		;雪は冬以外降らない
		IF RAND:2
			;雨になる
			GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_雨
		ELSE
			;25%で陰天に移行
			IF RAND:4 == 0
				GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_阴
			;晴天に移行
			ELSE
				GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_少云
				;更に低確率で晴朗
				SIF RAND:100 < 5
					GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_晴
				;雪→晴天を処理したらダイヤモンドダストが出る
				GENSOKYO_RAINBOW_SYSTEM:天系_通用 = 天气_钻石尘
			ENDIF
		ENDIF
	CASEELSE
		GENSOKYO_WEATHER_SYSTEM:天系_通用 = RAND:4
ENDSELECT

;天气改变文本
CALL qol_CHANGE_WEATHER_SYSTEM_INFO_PRINT(无天气, MASTER, 上回天气, GENSOKYO_WEATHER_SYSTEM:天系_通用, TIME:2)

;連続降水Counter
CALL qol_CONTINUOUS_RAINFALL_COUNTER(GENSOKYO_WEATHER_SYSTEM:天系_通用)

;強風である場合風がおさまる
GENSOKYO_WIND_VE_SYSTEM:天系_通用 = 天气_无风
;风力改变文本
CALL qol_CHANGE_WEATHER_SYSTEM_WIND_PRINT(无天气, MASTER, 上回风力, GENSOKYO_WIND_VE_SYSTEM:天系_通用)

;春第2タームまでは降雪・雪崩判定
IF DAY:2 == 1 && NOW_TERM <= 2
	;連続降雪Counter
	CALL qol_CONTINUOUS_SNOWFALL_COUNTER_SPRING(GENSOKYO_WEATHER_SYSTEM:天系_通用)
	;雪崩発生フラグ判定
	CALL qol_POSSIBLITY_AVALANCHE_SPRING(GENSOKYO_WEATHER_SYSTEM:天系_通用)
ELSE
	CALL qol_NO_AVALANCHE
ENDIF

;-------------------------------------------------
;2 春・秋の天気(温暖前線接近時)
;-------------------------------------------------
@qol_CHANGE_WEATHER_SYSTEM_2(无天气)
#LOCALSIZE 1
#LOCALSSIZE 1
#DIM 无天气
#DIM 上回天气
#DIM 上回风力
GENSOKYO_RAINBOW_SYSTEM:天系_通用 = 天气_无虹
上回天气 = GENSOKYO_WEATHER_SYSTEM:天系_通用
上回风力 = GENSOKYO_WIND_VE_SYSTEM:天系_通用

SELECTCASE GENSOKYO_WEATHER_SYSTEM:天系_通用
	;晴朗の時
	CASE 天气_晴
		GENSOKYO_WEATHER_SYSTEM:天系_通用 = 0
	;晴天の時
	CASE 0
		;60%で多雲に移行
		IF RAND:2 == 0
			GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_多云
		;10%で陰天に移行
		ELSEIF RAND:10 == 0
			GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_阴
		ENDIF
	;多雲の時
	CASE 天气_多云
		;60%で陰天に移行
		IF RAND:2 == 0
			GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_阴
		;10%で雨に移行
		ELSEIF RAND:10 == 0
			GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_雨
		ENDIF
	;曇の時
	CASE 天气_阴
		;50%で雨に移行
		IF RAND:2 == 0
			GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_雨
		ENDIF
	;降雨時
	CASE 天气_雨, 天气_大雨, 天气_冰霰, 天气_暴雨, 天气_冰雹
		;熱雷発生フラグが立っているとき大雨に移行
		IF GET_HEAT_THUNDERSTORM() && GENSOKYO_WEATHER_SYSTEM:天系_通用 == 天气_雨
			GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_大雨
		ENDIF
	CASEELSE
		GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_多云
ENDSELECT

;天气改变文本
CALL qol_CHANGE_WEATHER_SYSTEM_INFO_PRINT(无天气, MASTER, 上回天气, GENSOKYO_WEATHER_SYSTEM:天系_通用, TIME:2)

;連続降水Counter
CALL qol_CONTINUOUS_RAINFALL_COUNTER(GENSOKYO_WEATHER_SYSTEM:天系_通用)

;春第2タームまでは降雪・雪崩判定
IF DAY:2 == 1 && NOW_TERM <= 2
	;連続降雪Counter
	CALL qol_CONTINUOUS_SNOWFALL_COUNTER_SPRING(GENSOKYO_WEATHER_SYSTEM:天系_通用)
	;雪崩発生フラグ判定
	CALL qol_POSSIBLITY_AVALANCHE_SPRING(GENSOKYO_WEATHER_SYSTEM:天系_通用)
ELSE
	CALL qol_NO_AVALANCHE
ENDIF

;-------------------------------------------------
;3 春・秋の天気(暖域時)
;-------------------------------------------------
@qol_CHANGE_WEATHER_SYSTEM_3(无天气)
#LOCALSIZE 1
#LOCALSSIZE 1
#DIM 无天气
#DIM 上回天气
#DIM 上回风力
GENSOKYO_RAINBOW_SYSTEM:天系_通用 = 天气_无虹
上回天气 = GENSOKYO_WEATHER_SYSTEM:天系_通用
上回风力 = GENSOKYO_WIND_VE_SYSTEM:天系_通用

IF (GENSOKYO_WEATHER_SYSTEM:天系_通用 != 天气_少云 || GENSOKYO_WEATHER_SYSTEM:天系_通用 != 天气_多云)
	IF RAND:2 == 1
		GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_少云
		IF GENSOKYO_WEATHER_SYSTEM:天系_通用 >= 天气_雨
			;雨→晴天を処理したら虹が出る
			GENSOKYO_RAINBOW_SYSTEM:天系_通用 = 1
			;市場が開かれる
			FLAG:市場の神 = 1
		ENDIF
	ELSE
		GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_多云
	ENDIF
ENDIF

;天气改变文本
CALL qol_CHANGE_WEATHER_SYSTEM_INFO_PRINT(无天气, MASTER, 上回天气, GENSOKYO_WEATHER_SYSTEM:天系_通用, TIME:2)

;連続降水Counter
CALL qol_CONTINUOUS_RAINFALL_COUNTER(GENSOKYO_WEATHER_SYSTEM:天系_通用)

;風速変化の確立
LOCAL = RAND:10

;50%の確率で風速弱体化。暖気が強い場合20%の確率で風速強化
;但し春第1タームは確実に強風
IF DAY:2 == 1 && NOW_TERM == 1 && GENSOKYO_WIND_VE_SYSTEM:天系_通用 != 天气_强风
	GENSOKYO_WIND_VE_SYSTEM:天系_通用 = 天气_强风
	CALL qol_WEATHER_CHANGE_DIARY_PRINT("春风")
ELSEIF POWER_WARM_AIR >= 4 && LOCAL >= 8 && GENSOKYO_WIND_VE_SYSTEM:天系_通用 == 天气_无风
	GENSOKYO_WIND_VE_SYSTEM:天系_通用 = 天气_强风
ELSEIF LOCAL >= 5 && GENSOKYO_WIND_VE_SYSTEM:天系_通用 > 天气_无风
	GENSOKYO_WIND_VE_SYSTEM:天系_通用 = 天气_无风
ENDIF
;风力改变文本
CALL qol_CHANGE_WEATHER_SYSTEM_WIND_PRINT(无天气, MASTER, 上回风力, GENSOKYO_WIND_VE_SYSTEM:天系_通用)

;春第2タームまでは降雪・雪崩判定
IF DAY:2 == 1 && NOW_TERM <= 2
	;連続降雪Counter
	CALL qol_CONTINUOUS_SNOWFALL_COUNTER_SPRING(GENSOKYO_WEATHER_SYSTEM:天系_通用)
	;雪崩発生フラグ判定
	CALL qol_POSSIBLITY_AVALANCHE_SPRING(GENSOKYO_WEATHER_SYSTEM:天系_通用)
ELSE
	CALL qol_NO_AVALANCHE
ENDIF

;-------------------------------------------------
;4 春・秋の天気(寒冷前線通過時)
;-------------------------------------------------
@qol_CHANGE_WEATHER_SYSTEM_4(无天气)
#LOCALSIZE 1
#LOCALSSIZE 1
#DIM 无天气
#DIM 上回天气
#DIM 上回风力
GENSOKYO_RAINBOW_SYSTEM:天系_通用 = 天气_无虹
上回天气 = GENSOKYO_WEATHER_SYSTEM:天系_通用
上回风力 = GENSOKYO_WIND_VE_SYSTEM:天系_通用

IF GENSOKYO_WEATHER_SYSTEM:天系_通用 == 天气_冰雹 && GET_FRONTAL_THUNDERSTORM() == 1 && GET_HEAT_THUNDERSTORM() == 1
	GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_暴雨
ELSEIF (GENSOKYO_WEATHER_SYSTEM:天系_通用 == 天气_冰霰 || GENSOKYO_WEATHER_SYSTEM:天系_通用 == 天气_冰雹)
	GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_大雨
ELSEIF GENSOKYO_WEATHER_SYSTEM:天系_通用 != 天气_雨 && GENSOKYO_WEATHER_SYSTEM:天系_通用 != 天气_大雨 && GENSOKYO_WEATHER_SYSTEM:天系_通用 != 天气_暴雨
	IF RAND:3 == 0
		GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_雨
	ELSEIF RAND:3 == 1 && GET_FRONTAL_THUNDERSTORM() == 1
		SELECTCASE POWER_WARM_AIR
			CASE 5
				IF RAND:3 == 0
					GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_冰雹
				ELSE
					GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_冰霰
				ENDIF
			CASEELSE
				GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_冰霰
		ENDSELECT
	ELSE
		GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_大雨
	ENDIF
ELSE
	IF GET_FRONTAL_THUNDERSTORM() == 1 && GET_HEAT_THUNDERSTORM() == 1
		IF GENSOKYO_WEATHER_SYSTEM:天系_通用 != 天气_暴雨
			GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_暴雨
		ENDIF
	ELSE
		SELECTCASE GENSOKYO_WEATHER_SYSTEM:天系_通用
			CASE 天气_雨
				IF RAND:3 == 0
					GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_大雨
				ENDIF
			CASE 天气_大雨
				IF RAND:3 == 0
					GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_雨
				ENDIF
			CASE 天气_暴雨
				GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_大雨
			CASEELSE
				GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_大雨
		ENDSELECT
	ENDIF
ENDIF

;天气改变文本
CALL qol_CHANGE_WEATHER_SYSTEM_INFO_PRINT(无天气, MASTER, 上回天气, GENSOKYO_WEATHER_SYSTEM:天系_通用, TIME:2)

;連続降水Counter
CALL qol_CONTINUOUS_RAINFALL_COUNTER(GENSOKYO_WEATHER_SYSTEM:天系_通用)

;風速変化の確立
LOCAL = RAND:4

;(天候パッチ)熱界雷時の処理　ダウンバースト発生
IF GET_FRONTAL_THUNDERSTORM() == 1 && GET_HEAT_THUNDERSTORM() == 1 && ATMOSPHERIC_INSTABILITY <= -1
	CALL qol_DOWNBURST_GENERATION()
;強風である場合25%で風がおさまる
ELSEIF LOCAL == 0 && GENSOKYO_WIND_VE_SYSTEM:天系_通用 != 天气_无风
	GENSOKYO_WIND_VE_SYSTEM:天系_通用 = 天气_无风
ENDIF
;风力改变文本
CALL qol_CHANGE_WEATHER_SYSTEM_WIND_PRINT(无天气, MASTER, 上回风力, GENSOKYO_WIND_VE_SYSTEM:天系_通用)

;春第2タームまでは降雪・雪崩判定
IF DAY:2 == 1 && NOW_TERM <= 2
	;連続降雪Counter
	CALL qol_CONTINUOUS_SNOWFALL_COUNTER_SPRING(GENSOKYO_WEATHER_SYSTEM:天系_通用)
	;雪崩発生フラグ判定
	CALL qol_POSSIBLITY_AVALANCHE_SPRING(GENSOKYO_WEATHER_SYSTEM:天系_通用)
ELSE
	CALL qol_NO_AVALANCHE
ENDIF


;-------------------------------------------------
;5 春・秋の天気(寒冷渦接近時)
;-------------------------------------------------
@qol_CHANGE_WEATHER_SYSTEM_5(无天气)
#LOCALSIZE 1
#LOCALSSIZE 1
#DIM 无天气
#DIM 上回天气
#DIM 上回风力
GENSOKYO_RAINBOW_SYSTEM:天系_通用 = 天气_无虹
上回天气 = GENSOKYO_WEATHER_SYSTEM:天系_通用
上回风力 = GENSOKYO_WIND_VE_SYSTEM:天系_通用

;暖気が1の場合降雪の可能性
IF POWER_WARM_AIR == 1
	SELECTCASE GENSOKYO_WEATHER_SYSTEM:天系_通用
		;陰天
		CASE 天气_阴
			SELECTCASE RAND:5
				CASE 0
					GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_冰霰
				CASE 1, 2
					GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_雪
				CASEELSE
					GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_大雪
			ENDSELECT
		;雪
		CASE 天气_雪
			SELECTCASE RAND:5
				CASE 0
					GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_冰雹
				CASE 1
					GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_阴
				CASEELSE
					GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_大雪
			ENDSELECT
		;大雪
		CASE 天气_大雪
			SELECTCASE RAND:5
				CASE 0
					GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_冰霰
				CASE 1
					GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_阴
				CASEELSE
					GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_雪
			ENDSELECT
		;冰雹
		CASE 天气_冰霰
			SELECTCASE RAND:4
				CASE 0
					GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_雪
				CASEELSE
					GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_大雪
			ENDSELECT
		CASEELSE
			GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_大雪
	ENDSELECT
ELSE
	SELECTCASE GENSOKYO_WEATHER_SYSTEM:天系_通用
		;陰天
		CASE 天气_阴
			IF GET_FRONTAL_THUNDERSTORM() == 1 && GET_HEAT_THUNDERSTORM() == 1 && RAND:6 == 0
				SELECTCASE RAND:6
					CASE 0
						GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_冰雹
					CASE IS >= 2
						GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_大雨
					CASEELSE
						GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_暴雨
				ENDSELECT
			ELSE
				SELECTCASE RAND:6
					CASE 0
						GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_冰霰
					CASE 1
						GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_雨夹雪
					CASE 2
						GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_雨
					CASEELSE
						GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_大雨
				ENDSELECT
			ENDIF
		;雨
		CASE 天气_雨
			IF GET_FRONTAL_THUNDERSTORM() == 1 && GET_HEAT_THUNDERSTORM() == 1 && RAND:4 == 0
				SELECTCASE RAND:6
					CASE 0
						GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_冰雹
					CASE IS >= 2
						GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_大雨
					CASEELSE
						GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_暴雨
				ENDSELECT
			ELSE
				SELECTCASE RAND:6
					CASE 0
						GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_冰霰
					CASE 1
						GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_雨夹雪
					CASE 2
						GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_阴
					CASEELSE
						GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_大雨
				ENDSELECT
			ENDIF
		;大雨
		CASE 天气_大雨, 天气_暴雨
			SELECTCASE RAND:5
				CASE 0
					GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_冰霰
				CASE 1
					GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_阴
				CASEELSE
					IF GENSOKYO_WEATHER_SYSTEM:天系_通用 == 天气_暴雨
						GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_大雨
					ELSEIF GET_FRONTAL_THUNDERSTORM() == 1 && GET_HEAT_THUNDERSTORM() == 1 && RAND:2 == 0
						GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_暴雨
					ELSE
						GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_雨
					ENDIF
			ENDSELECT
		;雨雪交加
		CASE 天气_雨夹雪
			SELECTCASE RAND:5
				CASE 0
					GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_冰霰
				CASE 1, 2
					GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_阴
				CASEELSE
					GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_雨
			ENDSELECT
		;冰雹
		CASE 天气_冰霰
			SELECTCASE RAND:4
				CASE 0
					GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_雨
				CASEELSE
					GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_大雨
			ENDSELECT
		;ひょう
		CASE 天气_冰雹
			SELECTCASE RAND:4
				CASE 0
					GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_暴雨
				CASEELSE
					GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_大雨
			ENDSELECT
		CASEELSE
			GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_大雨
	ENDSELECT
ENDIF

;天气改变文本
CALL qol_CHANGE_WEATHER_SYSTEM_INFO_PRINT(无天气, MASTER, 上回天气, GENSOKYO_WEATHER_SYSTEM:天系_通用, TIME:2)

;連続降水Counter
CALL qol_CONTINUOUS_RAINFALL_COUNTER(GENSOKYO_WEATHER_SYSTEM:天系_通用)

;風速変化の確立
LOCAL = RAND:20

;(天候パッチ)熱界雷時の処理　ダウンバースト発生
IF GET_FRONTAL_THUNDERSTORM() == 1 && GET_HEAT_THUNDERSTORM() == 1 && ATMOSPHERIC_INSTABILITY <= -1
	CALL qol_DOWNBURST_GENERATION()
;10%の確率で暴風
ELSEIF LOCAL == 0 && GENSOKYO_WIND_VE_SYSTEM:天系_通用 == 天气_强风
	GENSOKYO_WIND_VE_SYSTEM:天系_通用 = 天气_暴风
;50%の確率で暴風から強風に弱体化
ELSEIF LOCAL >= 10 && GENSOKYO_WIND_VE_SYSTEM:天系_通用 == 天气_暴风
	GENSOKYO_WIND_VE_SYSTEM:天系_通用 = 天气_强风
ELSEIF GENSOKYO_WIND_VE_SYSTEM:天系_通用 == 天气_无风
	GENSOKYO_WIND_VE_SYSTEM:天系_通用 = 天气_强风
ENDIF
;风力改变文本
CALL qol_CHANGE_WEATHER_SYSTEM_WIND_PRINT(无天气, MASTER, 上回风力, GENSOKYO_WIND_VE_SYSTEM:天系_通用)

;春第2タームまでは降雪・雪崩判定
IF DAY:2 == 1 && NOW_TERM <= 2
	;連続降雪Counter
	CALL qol_CONTINUOUS_SNOWFALL_COUNTER_SPRING(GENSOKYO_WEATHER_SYSTEM:天系_通用)
	;雪崩発生フラグ判定
	CALL qol_POSSIBLITY_AVALANCHE_SPRING(GENSOKYO_WEATHER_SYSTEM:天系_通用)
ELSE
	CALL qol_NO_AVALANCHE
ENDIF

;-------------------------------------------------
;6 夏の天気(基本)
;-------------------------------------------------
@qol_CHANGE_WEATHER_SYSTEM_6(无天气)
#LOCALSIZE 1
#LOCALSSIZE 1
#DIM 无天气
#DIM 上回天气
#DIM 上回风力
GENSOKYO_RAINBOW_SYSTEM:天系_通用 = 天气_无虹
上回天气 = GENSOKYO_WEATHER_SYSTEM:天系_通用
上回风力 = GENSOKYO_WIND_VE_SYSTEM:天系_通用

;暖気が1のとき
IF POWER_WARM_AIR == 1
	SELECTCASE GENSOKYO_WEATHER_SYSTEM:天系_通用
		;晴天の時
		CASE 天气_少云
			;20%で晴天に移行
			IF RAND:5 == 0
				GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_晴
			;20%で多雲に移行
			ELSEIF RAND:5 == 0
				GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_多云
			;上記確率に引っかからなかったら晴天
			ELSE
				GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_少云
			ENDIF
		CASE 天气_晴
			;20%で晴天に移行
			IF RAND:5 == 0
				GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_晴
			;20%で多雲に移行
			ELSEIF RAND:5 == 0
				GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_多云
			ENDIF
		;陰天の時
		CASE 天气_多云, 天气_阴
			;25%で弱体化
			IF RAND:4 == 0
				GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_多云
			ELSE
				;20%で晴天に移行
				IF RAND:5 == 0
					GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_少云
				ENDIF
			ENDIF
		;雨の時
		CASE 天气_雨, 天气_大雨, 天气_暴雨, 天气_冰雹
			;25%で弱体化
			IF RAND:4 == 0 && GENSOKYO_WEATHER_SYSTEM:天系_通用 != 天气_雨
				GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_雨
			ELSE
				;25%で陰天に移行
				IF RAND:4 == 0
					GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_阴
				;雨なら10%で霧雨に移行
				ELSEIF GENSOKYO_WEATHER_SYSTEM:天系_通用 == 天气_雨 && RAND:10 == 0
					GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_雾雨
				;10%で晴天に移行
				ELSEIF RAND:10 == 0
					GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_少云
					;更に低確率で晴朗
					SIF RAND:100 < 5
						GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_晴
					;雨→晴天を処理したら虹が出る
					GENSOKYO_RAINBOW_SYSTEM:天系_通用 = 天气_彩虹
					;市場が開かれる
					FLAG:市場の神 = 1
				ENDIF
			ENDIF
		;霧・霧雨の時
		CASE 天气_雾雨, 天气_雾
			GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_少云
		;雪の時
		CASE IS >= 天气_雪
			;雪は冬以外降らない
			IF GENSOKYO_WEATHER_SYSTEM:天系_通用 == 天气_雪
				;雨になる
				CLEARBIT GENSOKYO_WEATHER_SYSTEM:天系_通用,3
				SETBIT GENSOKYO_WEATHER_SYSTEM:天系_通用,2
			;50%の確率で弱体化
			ELSEIF GETBIT(GENSOKYO_WEATHER_SYSTEM:天系_通用,0) && RAND:2
				CLEARBIT GENSOKYO_WEATHER_SYSTEM:天系_通用,0
			ELSE
				;25%で陰天に移行
				IF RAND:4 == 0
					GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_阴
				;雪なら10%で細雪か雨雪交加に移行
				ELSEIF GENSOKYO_WEATHER_SYSTEM:天系_通用 == 天气_雪 && RAND:10 == 0
					IF RAND:2
						GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_细雪
					ELSE
						GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_雨夹雪
					ENDIF
					;更に10%の確率で霧雪,冰雹に移行
					SIF RAND:10 == 0
						SETBIT GENSOKYO_WEATHER_SYSTEM:天系_通用,0
				;10%で晴天に移行
				ELSEIF RAND:10 == 0
					GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_少云
					;更に低確率で晴朗
					SIF RAND:100 < 5
						GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_晴
					;雪→晴天を処理したらダイヤモンドダストが出る
					GENSOKYO_RAINBOW_SYSTEM:天系_通用 = 天气_钻石尘
				ENDIF
			ENDIF
		CASEELSE
			GENSOKYO_WEATHER_SYSTEM:天系_通用 = RAND:4
	ENDSELECT
;暖気が2以上
ELSE
	SELECTCASE GENSOKYO_WEATHER_SYSTEM:天系_通用
		;晴天の時
		CASE 天气_少云
			GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_晴
		;陰天の時
		CASE 2,3
			GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_晴
		;雨の時
		CASE 天气_雨, 天气_大雨, 天气_雾雨, 天气_雾, 天气_冰霰
			;25%で陰天に移行
			IF RAND:4 == 0
				GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_阴
			;75%で晴天に移行
			ELSE
				GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_少云
				;更に確率で晴朗
				SIF RAND:10 < 5
					GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_晴
				;雨→晴天を処理したら虹が出る
				GENSOKYO_RAINBOW_SYSTEM:天系_通用 = 天气_彩虹
				;市場が開かれる
				FLAG:市場の神 = 1
			ENDIF
		CASE 天气_暴雨, 天气_冰雹
			GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_大雨
		;雪の時
		CASE IS >= 天气_雪
			;雪は冬以外降らない
			IF GENSOKYO_WEATHER_SYSTEM:天系_通用 == 天气_雪
				;雨になる
				CLEARBIT GENSOKYO_WEATHER_SYSTEM:天系_通用,3
				SETBIT GENSOKYO_WEATHER_SYSTEM:天系_通用,2
			;50%の確率で弱体化
			ELSEIF GETBIT(GENSOKYO_WEATHER_SYSTEM:天系_通用,0) && RAND:2
				CLEARBIT GENSOKYO_WEATHER_SYSTEM:天系_通用,0
			ELSE
				;25%で陰天に移行
				IF RAND:4 == 0
					GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_阴
				;雪なら10%で細雪か雨雪交加に移行
				ELSEIF GENSOKYO_WEATHER_SYSTEM:天系_通用 == 8 && RAND:10 == 0
					IF RAND:2
						GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_细雪
					ELSE
						GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_雨夹雪
					ENDIF
					;更に10%の確率で霧雪,冰雹に移行
					SIF RAND:10 == 0
						SETBIT GENSOKYO_WEATHER_SYSTEM:天系_通用,0
				;10%で晴天に移行
				ELSEIF RAND:10 == 0
					GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_少云
					;更に低確率で晴朗
					SIF RAND:100 < 5
						GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_晴
					;雪→晴天を処理したらダイヤモンドダストが出る
					GENSOKYO_RAINBOW_SYSTEM:天系_通用 = 天气_钻石尘
				ENDIF
			ENDIF
		CASEELSE
			GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_晴
	ENDSELECT
ENDIF

;天气改变文本
CALL qol_CHANGE_WEATHER_SYSTEM_INFO_PRINT(无天气, MASTER, 上回天气, GENSOKYO_WEATHER_SYSTEM:天系_通用, TIME:2)

;連続降水Counter
CALL qol_CONTINUOUS_RAINFALL_COUNTER(GENSOKYO_WEATHER_SYSTEM:天系_通用)

;強風である場合風がおさまる
IF GENSOKYO_WIND_VE_SYSTEM:天系_通用 != 天气_无风
	GENSOKYO_WIND_VE_SYSTEM:天系_通用 = 天气_无风
ENDIF
;风力改变文本
CALL qol_CHANGE_WEATHER_SYSTEM_WIND_PRINT(无天气, MASTER, 上回风力, GENSOKYO_WIND_VE_SYSTEM:天系_通用)

;降雪・雪崩判定を解除
CALL qol_NO_AVALANCHE

;-------------------------------------------------
;7 夏の天気(夕立接近時)
;-------------------------------------------------
@qol_CHANGE_WEATHER_SYSTEM_7(无天气)
#LOCALSIZE 1
#LOCALSSIZE 1
#DIM 无天气
#DIM 上回天气
#DIM 上回风力
GENSOKYO_RAINBOW_SYSTEM:天系_通用 = 天气_无虹
上回天气 = GENSOKYO_WEATHER_SYSTEM:天系_通用
上回风力 = GENSOKYO_WIND_VE_SYSTEM:天系_通用

SELECTCASE CAN_CUMULUS:0
	CASE 1
		GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_少云
		SIF !CFLAG:MASTER:诶嘿嘿 && !无天气
			PRINTW 绵云出来了
	CASE 2, 3
		IF RAND:2 == 0
			GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_少云
			SIF !CFLAG:MASTER:诶嘿嘿 && !无天气
				PRINTW 绵云出来了
		ELSE
			;GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_阴
			GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_多云
			SIF !CFLAG:MASTER:诶嘿嘿 && !无天气
				PRINTW 变成多云天气了
		ENDIF
	CASE 4
		IF RAND:2 == 0
			GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_多云
			SIF !CFLAG:MASTER:诶嘿嘿 && !无天气
				PRINTW 卷云正在蔓延
		ELSE
			GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_阴
			SIF !CFLAG:MASTER:诶嘿嘿 && !无天气
				PRINTW 昙多起来了
		ENDIF
		SIF !CFLAG:MASTER:诶嘿嘿 && !无天气
			PRINTW 冷风吹起来了！
	CASE 5
		GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_阴
		IF !CFLAG:MASTER:诶嘿嘿 && !无天气
			PRINTW 天暗下来了……
			PRINTW 冷风吹起来了！
		ENDIF
ENDSELECT

;連続降水Counter
CALL qol_CONTINUOUS_RAINFALL_COUNTER(GENSOKYO_WEATHER_SYSTEM:天系_通用)

;降雪・雪崩判定を解除
CALL qol_NO_AVALANCHE

;-------------------------------------------------
;8 夏の天気(夕立通過後)
;-------------------------------------------------
@qol_CHANGE_WEATHER_SYSTEM_8(无天气)
#LOCALSIZE 1
#LOCALSSIZE 1
#DIM 无天气
#DIM 上回天气
#DIM 上回风力
GENSOKYO_RAINBOW_SYSTEM:天系_通用 = 天气_无虹
上回天气 = GENSOKYO_WEATHER_SYSTEM:天系_通用
上回风力 = GENSOKYO_WIND_VE_SYSTEM:天系_通用

SELECTCASE CAN_CUMULUS:0
	CASE 1
		IF RAND:2 == 0 && GENSOKYO_WEATHER_SYSTEM:天系_通用 <= 天气_晴
			GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_少云
		ELSEIF GENSOKYO_WEATHER_SYSTEM:天系_通用 < 天气_阴
			GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_阴
			SIF !CFLAG:MASTER:诶嘿嘿 && !无天气
				PRINTW 昙多起来了
		ENDIF
	CASE 2
		IF RAND:3 == 0 && GENSOKYO_WEATHER_SYSTEM:天系_通用 < 天气_雨
			GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_雨
			SIF !CFLAG:MASTER:诶嘿嘿 && !无天气
				PRINTW 下雨了！
		ELSEIF GENSOKYO_WEATHER_SYSTEM:天系_通用 < 天气_阴
			GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_阴
			SIF !CFLAG:MASTER:诶嘿嘿 && !无天气
				PRINTW 昙多起来了
		ENDIF
	CASE 3
		IF RAND:3 == 0 && GENSOKYO_WEATHER_SYSTEM:天系_通用 < 天气_雨
			GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_大雨
			SIF !CFLAG:MASTER:诶嘿嘿 && !无天气
				PRINTW 变成了瓢泼大雨！
		ELSEIF GENSOKYO_WEATHER_SYSTEM:天系_通用 < 天气_雨
			GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_雨
			SIF !CFLAG:MASTER:诶嘿嘿 && !无天气
				PRINTW 下雨了！
		ENDIF
	CASE 4
		IF RAND:3 == 0 && GENSOKYO_WEATHER_SYSTEM:天系_通用 == 天气_大雨
			GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_冰霰
			SIF !CFLAG:MASTER:诶嘿嘿 && !无天气
				PRINTW 落冰雹了！
		ELSEIF GENSOKYO_WEATHER_SYSTEM:天系_通用 != 天气_大雨
			GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_大雨
			SIF !CFLAG:MASTER:诶嘿嘿 && !无天气
				PRINTW 变成了瓢泼大雨！
		ENDIF
	CASE 5
		IF RAND:3 == 0 && GENSOKYO_WEATHER_SYSTEM:天系_通用 != 天气_冰雹
			GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_冰雹
			SIF !CFLAG:MASTER:诶嘿嘿 && !无天气
				PRINTW 下起了大块的冰雹！
		ELSEIF GENSOKYO_WEATHER_SYSTEM:天系_通用 != 天气_暴雨
			GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_暴雨
			SIF !CFLAG:MASTER:诶嘿嘿 && !无天气
				PRINTW 雨下得很大！
		ENDIF
ENDSELECT

;連続降水Counter
CALL qol_CONTINUOUS_RAINFALL_COUNTER(GENSOKYO_WEATHER_SYSTEM:天系_通用)

;ダウンバースト発生
IF CAN_CUMULUS:2 == 2 && GENSOKYO_WIND_VE_SYSTEM:天系_通用 != 天气_暴风
	GENSOKYO_WIND_VE_SYSTEM:天系_通用 = 天气_暴风
	;ダウンバーストによる作物への影響
	CALL qol_WEATHER_CHANGE_TO_CROP("STORM")
ELSEIF CAN_CUMULUS:2 == 1 && GENSOKYO_WIND_VE_SYSTEM:天系_通用 != 天气_强风
	GENSOKYO_WIND_VE_SYSTEM:天系_通用 = 天气_强风
ENDIF
;风力改变文本
CALL qol_CHANGE_WEATHER_SYSTEM_WIND_PRINT(无天气, MASTER, 上回风力, GENSOKYO_WIND_VE_SYSTEM:天系_通用)

;降雪・雪崩判定を解除
CALL qol_NO_AVALANCHE

;-------------------------------------------------
;9 冬の天気(基本)
;-------------------------------------------------
@qol_CHANGE_WEATHER_SYSTEM_9(无天气)
#LOCALSIZE 1
#LOCALSSIZE 1
#DIM 无天气
#DIM 上回天气
#DIM 上回风力
GENSOKYO_RAINBOW_SYSTEM:天系_通用 = 天气_无虹
上回天气 = GENSOKYO_WEATHER_SYSTEM:天系_通用
上回风力 = GENSOKYO_WIND_VE_SYSTEM:天系_通用

SELECTCASE GENSOKYO_WEATHER_SYSTEM:天系_通用
	;晴天の時
	CASE 天气_少云
		;20%で多雲に移行
		IF RAND:5 == 0
			GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_多云
		;5%で陰天に移行
		ELSEIF RAND:20 == 0
			GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_阴
		;上記確率に引っかからなかったら晴天
		ELSE
			GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_少云
		ENDIF
	CASE 天气_晴
		;20%で多雲に移行
		IF RAND:5 == 0
			GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_多云
		ELSE
			GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_少云
		ENDIF
	;陰天の時
	CASE 天气_多云, 天气_阴
		;25%で強化or弱体化
		IF RAND:4 == 0
			INVERTBIT GENSOKYO_WEATHER_SYSTEM:天系_通用,0
		ELSE
			;20%で晴天に移行
			IF RAND:5 == 0
				GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_少云
			;多雲なら10%で雨か雪に移行
			ELSEIF GENSOKYO_WEATHER_SYSTEM:天系_通用 == 天气_多云 && RAND:10 == 0
				GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_雪
				;更に10%の確率で霧雨,細雪に移行
				SIF RAND:10 == 0
					SETBIT GENSOKYO_WEATHER_SYSTEM:天系_通用,1
			;陰天なら30%で雪に移行
			ELSEIF GENSOKYO_WEATHER_SYSTEM:天系_通用 == 天气_阴 && RAND:10 < 3
				GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_雪
			ENDIF
		ENDIF
	;雨の時
	CASE 天气_雨, 天气_大雨, 天气_暴雨, 天气_冰雹
		GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_雪
	;霧・霧雨の時
	CASE 天气_雾雨, 天气_雾
		GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_少云
	;雪の時
	CASE IS >= 天气_雪
		;大雪は弱体化
		IF GENSOKYO_WEATHER_SYSTEM:天系_通用 == 天气_大雪
			GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_雪

		ELSE
			;50%で陰天に移行
			IF RAND:2 == 0
				GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_阴
			;雪なら10%で細雪か雨雪交加に移行
			ELSEIF GENSOKYO_WEATHER_SYSTEM:天系_通用 == 天气_雪 && RAND:10 == 0
				GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_细雪
				;更に10%の確率で霧雪,冰雹に移行
				SIF RAND:10 == 0
					SETBIT GENSOKYO_WEATHER_SYSTEM:天系_通用,0
			;25%で晴天に移行
			ELSEIF RAND:4 == 0
				GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_少云
				;更に低確率で晴朗
				SIF RAND:100 < 5
					GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_晴
				;雪→晴天を処理したらダイヤモンドダストが出る
				GENSOKYO_RAINBOW_SYSTEM:天系_通用 = 天气_钻石尘
			ENDIF
		ENDIF
	CASEELSE
		GENSOKYO_WEATHER_SYSTEM:天系_通用 = RAND:4
ENDSELECT

;天气改变文本
CALL qol_CHANGE_WEATHER_SYSTEM_INFO_PRINT(无天气, MASTER, 上回天气, GENSOKYO_WEATHER_SYSTEM:天系_通用, TIME:2)

;連続降水Counter
CALL qol_CONTINUOUS_RAINFALL_COUNTER(GENSOKYO_WEATHER_SYSTEM:天系_通用)

;連続降雪Counter
CALL qol_CONTINUOUS_SNOWFALL_COUNTER_WINTER(GENSOKYO_WEATHER_SYSTEM:天系_通用)

;強風である場合風がおさまる
SELECTCASE GENSOKYO_WIND_VE_SYSTEM:天系_通用
	CASE 天气_强风
		GENSOKYO_WIND_VE_SYSTEM:天系_通用 = 天气_无风
	CASE 天气_暴风
		GENSOKYO_WIND_VE_SYSTEM:天系_通用 = 天气_强风
	CASE IS >= 天气_飓风
		GENSOKYO_WIND_VE_SYSTEM:天系_通用 = 天气_暴风
ENDSELECT
;风力改变文本
CALL qol_CHANGE_WEATHER_SYSTEM_WIND_PRINT(无天气, MASTER, 上回风力, GENSOKYO_WIND_VE_SYSTEM:天系_通用)

;雪崩発生フラグ判定
CALL qol_POSSIBLITY_AVALANCHE_WINTER(GENSOKYO_WEATHER_SYSTEM:天系_通用)

;-------------------------------------------------
;10 冬の天気(日本海低気圧接近時)
;-------------------------------------------------
@qol_CHANGE_WEATHER_SYSTEM_10(无天气)
#LOCALSIZE 1
#LOCALSSIZE 1
#DIM 无天气
#DIM 上回天气
#DIM 上回风力
GENSOKYO_RAINBOW_SYSTEM:天系_通用 = 天气_无虹
上回天气 = GENSOKYO_WEATHER_SYSTEM:天系_通用
上回风力 = GENSOKYO_WIND_VE_SYSTEM:天系_通用

SELECTCASE GENSOKYO_WEATHER_SYSTEM:天系_通用
	;晴朗の時
	CASE 天气_晴
		GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_少云
	;晴天の时
	CASE 天气_少云
		;60%で多雲に移行
		IF RAND:2 == 0
			GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_多云
		;10%で陰天に移行
		ELSEIF RAND:10 == 0
			GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_阴
		ENDIF
	;多雲の時
	CASE 天气_多云
		;60%で陰天に移行
		IF RAND:2 == 0
			GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_阴
		;10%で雨・雪に移行
		ELSEIF RAND:10 == 0
			;暖気の強さによって雨か雪を区別
			IF POWER_WARM_AIR >= 3
				GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_雨
			ELSE
				GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_雪
			ENDIF
		ENDIF
	;曇の時
	CASE 天气_阴
		;50%で雨・雪に移行
		IF RAND:2 == 0
			;暖気の強さによって雨か雪を区別
			IF POWER_WARM_AIR >= 3
				GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_雨
			ELSE
				GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_雪
			ENDIF
		ENDIF
	;雨の時
	CASE 天气_雨
		;50%で大雨・大雪に移行
		IF RAND:2 == 0
			;暖気の強さによって雨か雪を区別
			IF POWER_WARM_AIR >= 3
				GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_大雨
			ELSE
				GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_大雪
			ENDIF
		ENDIF
	;雪の時
	CASE 天气_雪
		;50%で大雨・大雪に移行
		IF RAND:2 == 0
			;暖気の強さによって雨か雪を区別
			IF POWER_WARM_AIR >= 3
				GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_大雨
			ELSE
				GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_大雪
			ENDIF
		ENDIF
	CASE 天气_雾雨, 天气_雾, IS >= 天气_细雪
		;暖気の強さによって雨か雪を区別
		IF POWER_WARM_AIR >= 3
			GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_雨
		ELSE
			GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_雪
		ENDIF
ENDSELECT

;天气改变文本
CALL qol_CHANGE_WEATHER_SYSTEM_INFO_PRINT(无天气, MASTER, 上回天气, GENSOKYO_WEATHER_SYSTEM:天系_通用, TIME:2)

;連続降水Counter
CALL qol_CONTINUOUS_RAINFALL_COUNTER(GENSOKYO_WEATHER_SYSTEM:天系_通用)

;連続降雪Counter
CALL qol_CONTINUOUS_SNOWFALL_COUNTER_WINTER(GENSOKYO_WEATHER_SYSTEM:天系_通用)

IF POWER_WARM_AIR >= 4 || IS_SoJ_LOW ==2
	IF GENSOKYO_WIND_VE_SYSTEM:天系_通用 == 天气_无风
		GENSOKYO_WIND_VE_SYSTEM:天系_通用 = 天气_强风
	ENDIF
ENDIF
;风力改变文本
CALL qol_CHANGE_WEATHER_SYSTEM_WIND_PRINT(无天气, MASTER, 上回风力, GENSOKYO_WIND_VE_SYSTEM:天系_通用)

;雪崩発生フラグ判定
CALL qol_POSSIBLITY_AVALANCHE_WINTER_LOW(GENSOKYO_WEATHER_SYSTEM:天系_通用)

;-------------------------------------------------
;11 冬の天気(日本海低気圧通過時)
;-------------------------------------------------
@qol_CHANGE_WEATHER_SYSTEM_11(无天气)
#LOCALSIZE 1
#LOCALSSIZE 1
#DIM 无天气
#DIM 上回天气
#DIM 上回风力
GENSOKYO_RAINBOW_SYSTEM:天系_通用 = 天气_无虹
上回天气 = GENSOKYO_WEATHER_SYSTEM:天系_通用
上回风力 = GENSOKYO_WIND_VE_SYSTEM:天系_通用

IF IS_SoJ_LOW == 2
	IF GENSOKYO_WEATHER_SYSTEM:天系_通用 != 天气_大雪 || GENSOKYO_WIND_VE_SYSTEM:天系_通用 < 天气_暴风
		GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_大雪
		IF POWER_COLD_AIR >= 4 && POWER_WARM_AIR >= 5
			GENSOKYO_WIND_VE_SYSTEM:天系_通用 = 天气_飓风
			CALL qol_WEATHER_CHANGE_DIARY_PRINT("暴风雪")
		ELSE
			GENSOKYO_WIND_VE_SYSTEM:天系_通用 = 天气_暴风
			CALL qol_WEATHER_CHANGE_DIARY_PRINT("暴风雪")
		ENDIF
	ENDIF
	RETURN
ENDIF

SELECTCASE GENSOKYO_WEATHER_SYSTEM:天系_通用
	CASE IS < 天气_雪
		IF POWER_COLD_AIR < 3
			GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_雪
		ELSE
			GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_大雪
		ENDIF
	CASE 天气_雪
		IF POWER_COLD_AIR >= 3
			IF RAND:3 > 1
				GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_大雪
			ELSEIF RAND:3 == 1
				GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_冰霰
			ENDIF
		ENDIF
	CASE 天气_大雪
		IF RAND:3 == 0
			GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_雪
		ELSEIF RAND:3 == 1
			GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_冰霰
		ENDIF
	CASE 天气_冰霰
		IF POWER_COLD_AIR >= 3
			GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_大雪
		ELSE
			GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_雪
		ENDIF
	CASEELSE
		GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_雪
ENDSELECT

;天气改变文本
CALL qol_CHANGE_WEATHER_SYSTEM_INFO_PRINT(无天气, MASTER, 上回天气, GENSOKYO_WEATHER_SYSTEM:天系_通用, TIME:2)

;連続降水Counter
CALL qol_CONTINUOUS_RAINFALL_COUNTER(GENSOKYO_WEATHER_SYSTEM:天系_通用)

;連続降雪Counter
CALL qol_CONTINUOUS_SNOWFALL_COUNTER_WINTER(GENSOKYO_WEATHER_SYSTEM:天系_通用)

;風速変化の確立
LOCAL = RAND:20

;5%の確率で暴風
IF LOCAL == 0 && GENSOKYO_WIND_VE_SYSTEM:天系_通用 == 天气_强风 && POWER_COLD_AIR >= 4
	GENSOKYO_WIND_VE_SYSTEM:天系_通用 = 天气_暴风
;50%の確率で暴風から強風に弱体化
ELSEIF LOCAL >= 10 && GENSOKYO_WIND_VE_SYSTEM:天系_通用 == 天气_暴风
	GENSOKYO_WIND_VE_SYSTEM:天系_通用 = 天气_强风
ELSEIF LOCAL >= 10 && GENSOKYO_WIND_VE_SYSTEM:天系_通用 == 天气_无风
	GENSOKYO_WIND_VE_SYSTEM:天系_通用 = 天气_强风
ENDIF
;风力改变文本
CALL qol_CHANGE_WEATHER_SYSTEM_WIND_PRINT(无天气, MASTER, 上回风力, GENSOKYO_WIND_VE_SYSTEM:天系_通用)

;雪崩発生フラグ判定
CALL qol_POSSIBLITY_AVALANCHE_WINTER(GENSOKYO_WEATHER_SYSTEM:天系_通用)

;-------------------------------------------------
;12 冬の天気(南岸低気圧接近時)
;-------------------------------------------------
@qol_CHANGE_WEATHER_SYSTEM_12(无天气)
#LOCALSIZE 1
#LOCALSSIZE 1
#DIM 无天气
#DIM 上回天气
#DIM 上回风力
GENSOKYO_RAINBOW_SYSTEM:天系_通用 = 天气_无虹
上回天气 = GENSOKYO_WEATHER_SYSTEM:天系_通用
上回风力 = GENSOKYO_WIND_VE_SYSTEM:天系_通用

SELECTCASE GENSOKYO_WEATHER_SYSTEM:天系_通用
	;晴天の時
	CASE 天气_少云, 天气_晴
		GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_阴
	;陰天の時
	CASE 天气_多云, 天气_阴
		;25%で強化or弱体化
		IF RAND:4 == 0
			INVERTBIT GENSOKYO_WEATHER_SYSTEM:天系_通用,0
		ELSE
			;多雲なら25%で雨か雪に移行
			IF GENSOKYO_WEATHER_SYSTEM:天系_通用 == 天气_多云 && RAND:4 == 0
				GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_雪
			;陰天なら50%で雨に移行
			ELSEIF GENSOKYO_WEATHER_SYSTEM:天系_通用 == 天气_阴 && RAND:2 == 0
				GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_雪
			ENDIF
		ENDIF
	;雨の時
	CASE 天气_雨, 天气_大雨, 天气_雾雨, 天气_雾, 天气_暴雨, 天气_冰雹
		GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_雪
	;雪の時
	CASE IS >= 天气_雪
		;10%で強化
		IF !GETBIT(GENSOKYO_WEATHER_SYSTEM:天系_通用,0) && RAND:10 == 0
			SETBIT GENSOKYO_WEATHER_SYSTEM:天系_通用,0
		;50%の確率で弱体化
		ELSEIF GETBIT(GENSOKYO_WEATHER_SYSTEM:天系_通用,0) && RAND:2
			CLEARBIT GENSOKYO_WEATHER_SYSTEM:天系_通用,0

		ELSE
			;25%で陰天に移行
			IF RAND:4 == 0
				GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_阴
			;雪なら10%で細雪か雨雪交加に移行
			ELSEIF GENSOKYO_WEATHER_SYSTEM:天系_通用 == 天气_雪 && RAND:10 == 0
				GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_细雪
				;更に10%の確率で霧雪,冰雹に移行
				SIF RAND:10 == 0
					SETBIT GENSOKYO_WEATHER_SYSTEM:天系_通用,0
			ENDIF
		ENDIF
	CASEELSE
		GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_雪
ENDSELECT

;天气改变文本
CALL qol_CHANGE_WEATHER_SYSTEM_INFO_PRINT(无天气, MASTER, 上回天气, GENSOKYO_WEATHER_SYSTEM:天系_通用, TIME:2)

;連続降水Counter
CALL qol_CONTINUOUS_RAINFALL_COUNTER(GENSOKYO_WEATHER_SYSTEM:天系_通用)

;連続降雪Counter
CALL qol_CONTINUOUS_SNOWFALL_COUNTER_WINTER(GENSOKYO_WEATHER_SYSTEM:天系_通用)

;雪崩発生フラグ判定
CALL qol_POSSIBLITY_AVALANCHE_WINTER_LOW(GENSOKYO_WEATHER_SYSTEM:天系_通用)

;-------------------------------------------------
;13 冬の天気(寒冷渦接近時)
;-------------------------------------------------
@qol_CHANGE_WEATHER_SYSTEM_13(无天气)
#LOCALSIZE 1
#LOCALSSIZE 1
#DIM 无天气
#DIM 上回天气
#DIM 上回风力
GENSOKYO_RAINBOW_SYSTEM:天系_通用 = 天气_无虹
上回天气 = GENSOKYO_WEATHER_SYSTEM:天系_通用
上回风力 = GENSOKYO_WIND_VE_SYSTEM:天系_通用

SELECTCASE GENSOKYO_WEATHER_SYSTEM:天系_通用
	;曇り
	CASE 天气_阴
		SELECTCASE RAND:5
			CASE 0
				GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_冰霰
			CASE 1, 2
				GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_雪
			CASEELSE
				GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_大雪
		ENDSELECT
	;雪
	CASE 天气_雪
		SELECTCASE RAND:5
			CASE 0
				GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_冰霰
			CASE 1
				GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_阴
			CASEELSE
				GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_大雪
		ENDSELECT
	;大雪
	CASE 天气_大雪
		SELECTCASE RAND:5
			CASE 0
				GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_冰霰
			CASE 1
				GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_阴
			CASEELSE
				GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_雪
		ENDSELECT
	;冰雹
	CASE 天气_冰霰
		SELECTCASE RAND:4
			CASE 0
				GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_雪
			CASEELSE
				GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_大雪
		ENDSELECT
	CASEELSE
		GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_大雪
ENDSELECT

;天气改变文本
CALL qol_CHANGE_WEATHER_SYSTEM_INFO_PRINT(无天气, MASTER, 上回天气, GENSOKYO_WEATHER_SYSTEM:天系_通用, TIME:2)

;連続降水Counter
CALL qol_CONTINUOUS_RAINFALL_COUNTER(GENSOKYO_WEATHER_SYSTEM:天系_通用)

;連続降雪Counter
CALL qol_CONTINUOUS_SNOWFALL_COUNTER_WINTER(GENSOKYO_WEATHER_SYSTEM:天系_通用)

;風速変化の確立
LOCAL = RAND:20

;10%の確率で暴風
IF LOCAL == 0 && GENSOKYO_WIND_VE_SYSTEM:天系_通用 == 天气_强风
	GENSOKYO_WIND_VE_SYSTEM:天系_通用 = 天气_暴风
;50%の確率で暴風から強風に弱体化
ELSEIF LOCAL >= 10 && GENSOKYO_WIND_VE_SYSTEM:天系_通用 == 天气_暴风
	GENSOKYO_WIND_VE_SYSTEM:天系_通用 = 天气_强风
ELSEIF GENSOKYO_WIND_VE_SYSTEM:天系_通用 == 天气_无风
	GENSOKYO_WIND_VE_SYSTEM:天系_通用 = 天气_强风
ENDIF
;风力改变文本
CALL qol_CHANGE_WEATHER_SYSTEM_WIND_PRINT(无天气, MASTER, 上回风力, GENSOKYO_WIND_VE_SYSTEM:天系_通用)

;雪崩発生フラグ判定
CALL qol_POSSIBLITY_AVALANCHE_WINTER(GENSOKYO_WEATHER_SYSTEM:天系_通用)

;-------------------------------------------------
;14 梅雨・秋雨の天気(基本)
;-------------------------------------------------
@qol_CHANGE_WEATHER_SYSTEM_14(无天气)
#LOCALSIZE 1
#LOCALSSIZE 1
#DIM 无天气
#DIM 上回天气
#DIM 上回风力
GENSOKYO_RAINBOW_SYSTEM:天系_通用 = 天气_无虹
上回天气 = GENSOKYO_WEATHER_SYSTEM:天系_通用
上回风力 = GENSOKYO_WIND_VE_SYSTEM:天系_通用

SELECTCASE GENSOKYO_WEATHER_SYSTEM:天系_通用
	CASE 天气_晴
		GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_多云
	CASE 天气_多云
		IF RAND:4 == 0
			GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_少云
		ELSE
			GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_阴
		ENDIF
	CASE 天气_阴
		IF RAND:3 == 0
			GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_多云
		ELSE
			GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_雨
		ENDIF
	CASE 天气_雨, 天气_大雨, 天气_雾雨, 天气_雾, 天气_暴雨, 天气_冰雹
		IF GET_HEAT_THUNDERSTORM() == 1 && GET_FRONTAL_THUNDERSTORM() == 1 && (GENSOKYO_WEATHER_SYSTEM:天系_通用 == 天气_雨 || GENSOKYO_WEATHER_SYSTEM:天系_通用 == 天气_大雨) && RAND:2 == 0 && GENSOKYO_WEATHER_SYSTEM:天系_通用 != 天气_暴雨
			GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_暴雨
		ELSEIF (GET_HEAT_THUNDERSTORM() == 1 || GET_FRONTAL_THUNDERSTORM() == 1) && GENSOKYO_WEATHER_SYSTEM:天系_通用 == 天气_雨
			IF RAND:4 == 0
				GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_冰霰
			ELSE
				GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_大雨
			ENDIF
		ELSEIF RAND:3 == 0
			GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_少云
			;雨→晴天を処理したら虹が出る
			GENSOKYO_RAINBOW_SYSTEM:天系_通用 = 天气_彩虹
			;市場が開かれる
			FLAG:市場の神 = 1
		ELSE
			GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_阴
		ENDIF
	CASEELSE
		GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_阴
ENDSELECT

;天气改变文本
CALL qol_CHANGE_WEATHER_SYSTEM_INFO_PRINT(无天气, MASTER, 上回天气, GENSOKYO_WEATHER_SYSTEM:天系_通用, TIME:2)

;連続降水Counter
CALL qol_CONTINUOUS_RAINFALL_COUNTER(GENSOKYO_WEATHER_SYSTEM:天系_通用)

;強風である場合風がおさまる
IF GENSOKYO_WIND_VE_SYSTEM:天系_通用 != 天气_无风
	GENSOKYO_WIND_VE_SYSTEM:天系_通用 = 天气_无风
ENDIF
;风力改变文本
CALL qol_CHANGE_WEATHER_SYSTEM_WIND_PRINT(无天气, MASTER, 上回风力, GENSOKYO_WIND_VE_SYSTEM:天系_通用)

;降雪・雪崩判定を解除
CALL qol_NO_AVALANCHE

;-------------------------------------------------
;15 梅雨・秋雨(梅雨末期・秋雨+台風接近)・先駆降雨帯発生時の天気
;-------------------------------------------------
@qol_CHANGE_WEATHER_SYSTEM_15(无天气)
#LOCALSIZE 1
#LOCALSSIZE 1
#DIM 无天气
#DIM 上回天气
#DIM 上回风力
#DIM 活発化フラグ
GENSOKYO_RAINBOW_SYSTEM:天系_通用 = 天气_无虹
上回天气 = GENSOKYO_WEATHER_SYSTEM:天系_通用
上回风力 = GENSOKYO_WIND_VE_SYSTEM:天系_通用
活発化フラグ = 0

SIF POWER_WARM_AIR == 5 || GET_LOCATION_LOW() == "台風勢力圏" || GET_HEAT_THUNDERSTORM() == 1 || GET_FRONTAL_THUNDERSTORM() == 1
	活発化フラグ = 1

SELECTCASE GENSOKYO_WEATHER_SYSTEM:天系_通用
	CASE 天气_阴
		IF 活発化フラグ == 1
			IF RAND:3 == 0
				GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_大雨
			ELSEIF RAND:2 == 0 && (GET_HEAT_THUNDERSTORM() == 1 || GET_FRONTAL_THUNDERSTORM() == 1)
				IF GET_HEAT_THUNDERSTORM() == 1 && GET_FRONTAL_THUNDERSTORM() == 1
					IF RAND:4 == 0
						GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_冰雹
					ELSE
						GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_暴雨
					ENDIF
				ELSE
					GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_冰霰
				ENDIF
			ELSE
				GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_雨
			ENDIF
		ELSE
			IF RAND:4 == 0
				GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_大雨
			ELSE
				GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_雨
			ENDIF
		ENDIF
	CASE 天气_雨, 天气_雾雨, 天气_雾
		IF GET_HEAT_THUNDERSTORM() == 1 && GET_FRONTAL_THUNDERSTORM() == 1 && RAND:2
			IF RAND:4 == 0
				GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_冰雹
			ELSE
				GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_暴雨
			ENDIF
		ELSEIF 活発化フラグ == 1
			IF RAND:3 != 0
				GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_大雨
			ELSEIF RAND:2 == 0
				GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_冰霰
			ELSE
				GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_阴
			ENDIF
		ELSE
			IF RAND:2 != 0
				GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_大雨
			ELSE
				GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_阴
			ENDIF
		ENDIF
	CASE 天气_大雨, 天气_暴雨, 天气_冰雹
		IF GET_HEAT_THUNDERSTORM() == 1 && GET_FRONTAL_THUNDERSTORM() == 1
			IF RAND:4 == 0 && GENSOKYO_WEATHER_SYSTEM:天系_通用 != 天气_冰雹
				GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_冰雹
			ELSEIF RAND:2 == 0 && GENSOKYO_WEATHER_SYSTEM:天系_通用 != 天气_暴雨
				GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_暴雨
			ELSEIF GENSOKYO_WEATHER_SYSTEM:天系_通用 == 天气_暴雨
				IF RAND:3
					GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_雨
				ELSE
					GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_大雨
				ENDIF
			ELSE
				GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_阴
			ENDIF
		ELSEIF 活発化フラグ == 1
			IF RAND:2 != 0
				GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_冰霰
			ELSE
				GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_雨
			ENDIF
		ELSE
			IF RAND:2 != 0
				GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_雨
			ELSE
				GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_阴
			ENDIF
		ENDIF
	CASE 天气_冰霰
		IF GET_HEAT_THUNDERSTORM() == 1 && GET_FRONTAL_THUNDERSTORM() == 1
			GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_暴雨
		ELSE
			GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_大雨
		ENDIF
	CASEELSE
		GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_雨
ENDSELECT

;天气改变文本
CALL qol_CHANGE_WEATHER_SYSTEM_INFO_PRINT(无天气, MASTER, 上回天气, GENSOKYO_WEATHER_SYSTEM:天系_通用, TIME:2)

;連続降水Counter
CALL qol_CONTINUOUS_RAINFALL_COUNTER(GENSOKYO_WEATHER_SYSTEM:天系_通用)

;(天候パッチ)熱界雷時の処理　ダウンバースト発生
IF GET_FRONTAL_THUNDERSTORM() == 1 && GET_HEAT_THUNDERSTORM() == 1 && ATMOSPHERIC_INSTABILITY <= -1
	CALL qol_DOWNBURST_GENERATION(RAND:2 == 0)
;強風である場合風がおさまる
ELSEIF GENSOKYO_WIND_VE_SYSTEM:天系_通用 != 天气_无风
	GENSOKYO_WIND_VE_SYSTEM:天系_通用 = 天气_无风
ENDIF
;风力改变文本
CALL qol_CHANGE_WEATHER_SYSTEM_WIND_PRINT(无天气, MASTER, 上回风力, GENSOKYO_WIND_VE_SYSTEM:天系_通用)

;降雪・雪崩判定を解除
CALL qol_NO_AVALANCHE

;-------------------------------------------------
;16 台風接近時の天気(強風域)
;-------------------------------------------------
@qol_CHANGE_WEATHER_SYSTEM_16(无天气)
#LOCALSIZE 1
#LOCALSSIZE 1
#DIM 无天气
#DIM 上回天气
#DIM 上回风力
#DIMS 低気圧位置
#DIM 豪雨フラグ
GENSOKYO_RAINBOW_SYSTEM:天系_通用 = 天气_无虹
上回天气 = GENSOKYO_WEATHER_SYSTEM:天系_通用
上回风力 = GENSOKYO_WIND_VE_SYSTEM:天系_通用
低気圧位置 = %GET_LOCATION_LOW()%
豪雨フラグ = 0

;台風勢力5以上、台風の位置が西かつ暖気5のとき豪雨フラグオン
SIF IS_TYPHOON:1 >= 5 && IS_TYPHOON:2 == 3 && POWER_WARM_AIR >= 5
	豪雨フラグ = 1

SELECTCASE GENSOKYO_WEATHER_SYSTEM:天系_通用
	CASE 天气_阴
		IF RAND:2 == 0
			GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_大雨
		ELSE
			GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_雨
		ENDIF
	CASE 天气_雨, 天气_雾雨, 天气_雾
		IF RAND:2 != 0
			GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_大雨
		ELSEIF RAND:2 == 0 && 低気圧位置 == "台風通過強風域"
			GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_阴
		ENDIF
	CASE 天气_大雨, 天气_暴雨
		IF RAND:2 != 0 && 豪雨フラグ == 1 && GENSOKYO_WEATHER_SYSTEM:天系_通用 != 天气_暴雨 && 低気圧位置 != "台風通過強風域"
			GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_暴雨
		ELSEIF RAND:2 != 0
			GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_雨
		ENDIF
	CASE 天气_冰霰, 天气_冰雹
		GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_大雨
	CASEELSE
		GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_雨
ENDSELECT

;天气改变文本
CALL qol_CHANGE_WEATHER_SYSTEM_INFO_PRINT(无天气, MASTER, 上回天气, GENSOKYO_WEATHER_SYSTEM:天系_通用, TIME:2)

;連続降水Counter
CALL qol_CONTINUOUS_RAINFALL_COUNTER(GENSOKYO_WEATHER_SYSTEM:天系_通用)

SELECTCASE GENSOKYO_WIND_VE_SYSTEM:天系_通用
	CASE 天气_无风
		GENSOKYO_WIND_VE_SYSTEM:天系_通用 = 天气_强风
		低気圧位置 = %GET_LOCATION_LOW()%
		IF 低気圧位置 == "台風接近強風域"
			CALL qol_WEATHER_CHANGE_DIARY_PRINT("台风")
		ENDIF
	CASE IS >= 天气_暴风
		GENSOKYO_WIND_VE_SYSTEM:天系_通用 = 天气_强风
ENDSELECT
;风力改变文本
CALL qol_CHANGE_WEATHER_SYSTEM_WIND_PRINT(无天气, MASTER, 上回风力, GENSOKYO_WIND_VE_SYSTEM:天系_通用)

;降雪・雪崩判定を解除
CALL qol_NO_AVALANCHE

;-------------------------------------------------
;21 台風接近時の天気(暴風域・最接近)
;-------------------------------------------------
@qol_CHANGE_WEATHER_SYSTEM_21(无天气)
#LOCALSIZE 1
#LOCALSSIZE 1
#DIM 无天气
#DIM 上回天气
#DIM 上回风力
#DIM 現在台風位置
#DIMS 低気圧位置
#DIM 颶風フラグ
#DIM 豪雨フラグ
GENSOKYO_RAINBOW_SYSTEM:天系_通用 = 天气_无虹
上回天气 = GENSOKYO_WEATHER_SYSTEM:天系_通用
上回风力 = GENSOKYO_WIND_VE_SYSTEM:天系_通用
低気圧位置 = %GET_LOCATION_LOW()%
颶風フラグ = 0
豪雨フラグ = 0

;台風勢力5以上、台風の位置が西かつ暖気5のとき豪雨フラグオン
SIF IS_TYPHOON:1 >= 5 && IS_TYPHOON:2 == 3 && POWER_WARM_AIR >= 5
	豪雨フラグ = 1

SELECTCASE 低気圧位置
	CASE "台風接近暴風域", "台風最接近"
		現在台風位置 = 1
	CASE "台風通過暴風域"
		現在台風位置 = 2
ENDSELECT

SELECTCASE GENSOKYO_WEATHER_SYSTEM:天系_通用
	CASE 天气_阴
		IF RAND:2 == 0
			GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_大雨
		ELSE
			GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_雨
		ENDIF
	CASE 天气_雨, 天气_雾雨, 天气_雾
		IF RAND:2 != 0
			GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_大雨
		ELSEIF RAND:2 == 0
			IF 豪雨フラグ == 1
				GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_暴雨
			ELSEIF 現在台風位置 == 2
				GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_阴
			ENDIF
		ELSE
			GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_阴
		ENDIF
	CASE 天气_大雨, 天气_暴雨
		IF GENSOKYO_WEATHER_SYSTEM:天系_通用 != 天气_暴雨 && 豪雨フラグ == 1
			GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_暴雨
		ELSEIF RAND:2 != 0 && 現在台風位置 == 2
			GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_阴
		ELSEIF RAND:2 != 0
			IF GENSOKYO_WEATHER_SYSTEM:天系_通用 == 天气_暴雨
				GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_大雨
			ELSE
				GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_雨
			ENDIF
		ENDIF
	CASE 天气_冰霰, 天气_冰雹
		GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_大雨
	CASEELSE
		GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_雨
ENDSELECT

;天气改变文本
CALL qol_CHANGE_WEATHER_SYSTEM_INFO_PRINT(无天气, MASTER, 上回天气, GENSOKYO_WEATHER_SYSTEM:天系_通用, TIME:2)

;連続降水Counter
CALL qol_CONTINUOUS_RAINFALL_COUNTER(GENSOKYO_WEATHER_SYSTEM:天系_通用)

;台風勢力5以上、寒気1かつ暖気5のとき颶風フラグオン
SIF IS_TYPHOON:1 >= 5 && POWER_COLD_AIR == 1 && POWER_WARM_AIR >= 5
	颶風フラグ = 1

SELECTCASE GENSOKYO_WIND_VE_SYSTEM:天系_通用
	CASE 天气_无风
		IF 颶風フラグ == 1
			GENSOKYO_WIND_VE_SYSTEM:天系_通用 = 天气_飓风
		ELSE
			GENSOKYO_WIND_VE_SYSTEM:天系_通用 = 天气_暴风
		ENDIF
	CASE 天气_强风
		GENSOKYO_WIND_VE_SYSTEM:天系_通用 = 天气_暴风
		;台風直撃による作物への影響
		SIF IS_TYPHOON:2 == 2
			CALL qol_WEATHER_CHANGE_TO_CROP("STORM")
	CASE 天气_暴风
		IF 颶風フラグ == 1 && 現在台風位置 == 1
			GENSOKYO_WIND_VE_SYSTEM:天系_通用 = 天气_飓风
			;猛烈な風による作物への影響
			CALL qol_WEATHER_CHANGE_TO_CROP("STORM")
		ENDIF
	CASE IS >= 天气_飓风
		IF 現在台風位置 == 2
			GENSOKYO_WIND_VE_SYSTEM:天系_通用 = 天气_暴风
		ENDIF
ENDSELECT
;风力改变文本
CALL qol_CHANGE_WEATHER_SYSTEM_WIND_PRINT(无天气, MASTER, 上回风力, GENSOKYO_WIND_VE_SYSTEM:天系_通用)

;降雪・雪崩判定を解除
CALL qol_NO_AVALANCHE

;-------------------------------------------------
;17 台風接近時の天気(台風の目)
;-------------------------------------------------
@qol_CHANGE_WEATHER_SYSTEM_17(无天气)
#LOCALSIZE 1
#LOCALSSIZE 1
#DIM 无天气
#DIM 上回天气
#DIM 上回风力
GENSOKYO_RAINBOW_SYSTEM:天系_通用 = 天气_无虹
上回天气 = GENSOKYO_WEATHER_SYSTEM:天系_通用
上回风力 = GENSOKYO_WIND_VE_SYSTEM:天系_通用

IF IS_TYPHOON:1 < 3 && (GENSOKYO_WEATHER_SYSTEM:天系_通用 != 天气_多云 || GENSOKYO_WIND_VE_SYSTEM:天系_通用 != 天气_无风)
	GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_多云
	GENSOKYO_WIND_VE_SYSTEM:天系_通用 = 天气_无风
	CALL qol_NO_SHARED_UMBRELLA
ELSEIF GENSOKYO_WEATHER_SYSTEM:天系_通用 != 天气_少云 || GENSOKYO_WIND_VE_SYSTEM:天系_通用 != 天气_无风
	GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_少云
	GENSOKYO_WIND_VE_SYSTEM:天系_通用 = 天气_无风
	CALL qol_NO_SHARED_UMBRELLA
ENDIF

;风力改变文本，这里不走天气改变文本，因为台风一般下雨，走天气改变会显示进风眼放晴有彩虹，但台风眼一过又下雨就很难绷
CALL qol_CHANGE_WEATHER_SYSTEM_WIND_PRINT(无天气, MASTER, 上回风力, GENSOKYO_WIND_VE_SYSTEM:天系_通用)

;降雪・雪崩判定を解除
CALL qol_NO_AVALANCHE

;-------------------------------------------------
;18 霧の天気
;-------------------------------------------------
@qol_CHANGE_WEATHER_SYSTEM_18(无天气)
#LOCALSIZE 1
#LOCALSSIZE 1
#DIM 无天气
#DIM 上回天气
#DIM 上回风力
GENSOKYO_RAINBOW_SYSTEM:天系_通用 = 天气_无虹
上回天气 = GENSOKYO_WEATHER_SYSTEM:天系_通用
上回风力 = GENSOKYO_WIND_VE_SYSTEM:天系_通用

SELECTCASE GENSOKYO_WEATHER_SYSTEM:天系_通用
	CASE 天气_雾雨
		GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_雾
	CASE 天气_细雪
		GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_雾
	CASE 天气_雾
		IF DAY:2 == 4
			GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_细雪
		ELSE
			GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_雾雨
		ENDIF
	CASEELSE
		IF RAND:2 == 0
			GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_雾
		ELSEIF DAY:2 == 4
			GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_细雪
		ELSE
			GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_雾雨
		ENDIF
ENDSELECT

;天气改变文本
CALL qol_CHANGE_WEATHER_SYSTEM_INFO_PRINT(无天气, MASTER, 上回天气, GENSOKYO_WEATHER_SYSTEM:天系_通用, TIME:2)

;連続降水Counter
CALL qol_CONTINUOUS_RAINFALL_COUNTER(GENSOKYO_WEATHER_SYSTEM:天系_通用)

;-------------------------------------------------
;19 疑似好天
;-------------------------------------------------
@qol_CHANGE_WEATHER_SYSTEM_19(无天气)
#LOCALSIZE 1
#LOCALSSIZE 1
#DIM 无天气
#DIM 上回天气
#DIM 上回风力
GENSOKYO_RAINBOW_SYSTEM:天系_通用 = 天气_无虹
上回天气 = GENSOKYO_WEATHER_SYSTEM:天系_通用
上回风力 = GENSOKYO_WIND_VE_SYSTEM:天系_通用

IF GENSOKYO_WEATHER_SYSTEM:天系_通用 != 天气_晴 || GENSOKYO_WIND_VE_SYSTEM:天系_通用 != 天气_无风
	GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_晴
	GENSOKYO_WIND_VE_SYSTEM:天系_通用 = 天气_无风
ENDIF

;天气改变文本
CALL qol_CHANGE_WEATHER_SYSTEM_INFO_PRINT(无天气, MASTER, 上回天气, GENSOKYO_WEATHER_SYSTEM:天系_通用, TIME:2)

;雪崩発生フラグ判定
CALL qol_POSSIBLITY_AVALANCHE_WINTER(GENSOKYO_WEATHER_SYSTEM:天系_通用)

;-------------------------------------------------
;20 集中豪雨時の天気
;-------------------------------------------------
@qol_CHANGE_WEATHER_SYSTEM_20(无天气)
#LOCALSIZE 1
#LOCALSSIZE 1
#DIM 无天气
#DIM 上回天气
#DIM 上回风力
GENSOKYO_RAINBOW_SYSTEM:天系_通用 = 天气_无虹
上回天气 = GENSOKYO_WEATHER_SYSTEM:天系_通用
上回风力 = GENSOKYO_WIND_VE_SYSTEM:天系_通用

SELECTCASE GENSOKYO_WEATHER_SYSTEM:天系_通用
	CASE 天气_大雨
		SELECTCASE RAND:3
			CASE 0,1
				GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_暴雨
			CASE 2
				IF ATMOSPHERIC_INSTABILITY <= 0
					GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_冰雹
				ELSE
					GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_冰霰
				ENDIF
		ENDSELECT
	CASE 天气_暴雨
		SELECTCASE RAND:4
			CASE 0
				GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_大雨
			CASE 1
				IF ATMOSPHERIC_INSTABILITY <= 0
					GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_冰雹
				ELSE
					GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_冰霰
				ENDIF
		ENDSELECT
	CASE 天气_冰霰, 天气_冰雹
				GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_暴雨
	CASEELSE
		GENSOKYO_WEATHER_SYSTEM:天系_通用 = 天气_暴雨
ENDSELECT

;天气改变文本
CALL qol_CHANGE_WEATHER_SYSTEM_INFO_PRINT(无天气, MASTER, 上回天气, GENSOKYO_WEATHER_SYSTEM:天系_通用, TIME:2)

;連続降水Counter
CALL qol_CONTINUOUS_RAINFALL_COUNTER(GENSOKYO_WEATHER_SYSTEM:天系_通用)

;(天候パッチ)熱界雷時の処理　ダウンバースト発生
IF ATMOSPHERIC_INSTABILITY <= 0 && RAND:4 == 0
	CALL qol_DOWNBURST_GENERATION(RAND:2 == 0)
;強風である場合風がおさまる
ELSEIF GENSOKYO_WIND_VE_SYSTEM:天系_通用 != 天气_无风
	GENSOKYO_WIND_VE_SYSTEM:天系_通用 = 天气_无风
ENDIF
;风力改变文本
CALL qol_CHANGE_WEATHER_SYSTEM_WIND_PRINT(无天气, MASTER, 上回风力, GENSOKYO_WIND_VE_SYSTEM:天系_通用)

;降雪・雪崩判定を解除
CALL qol_NO_AVALANCHE
