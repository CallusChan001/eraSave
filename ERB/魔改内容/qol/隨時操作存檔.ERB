;随时存档，顾名思义即使在任何时候都能调用的存档功能，不过事实上是给游戏过程中用的，睡觉时候本来就有了也不需要这个，所以睡觉时不能调用（那还叫什么随时）
;不确定tw能不能支持，如果能就好了，不能随时存档还是挺麻烦的，能用把这句话删了
;我稍微优化了下逻辑,删掉了我感觉冗余的东西,同时加上了默认存档信息
;见你漏写了很多PRINTFORM,不想查了,全给你换成FORM了
@Real_Time_Save(操作模式 = 1001, qSave)
#DIM 最大存档数量
#DIM 存档编号
#DIM 操作模式               ;1001为存档，1002为读档，1003为删档，默认模式为存档
#DIM 最大页数               ;每页显示20个存档位加一个99号自动存档位
#DIM 页码 = 1
#DIM 行数
#DIM 选择的存档位
#DIM PREV_REDRAW
#DIM qSave

;魔改添加非就寝时段存档执行开关
PREV_REDRAW = CURRENTREDRAW()
REDRAW 0
;SAVENOS 最大存档数量
最大存档数量 = 1000
最大页数 = 最大存档数量 / 40
SIF 最大存档数量 % 40
	最大页数 ++
行数 = LINECOUNT
$开始位置
CUSTOMDRAWLINE ━
PRINT 请选择要
SELECTCASE 操作模式
	CASE 1001
		PRINT 存档
	CASE 1002
		PRINT 读取
	CASE 1003
		PRINT 删除
	CASE 1004
		PRINT 存档并注释
	CASEELSE
		THROW 模式错误，无法进行对存档的操作
ENDSELECT
PRINTFORML 的存档位置 - 第{页码}/{最大页数}页
DRAWLINE
IF 操作模式 == 1001
	SETCOLOR C_YELLOW
	PRINT [存档]
	RESETCOLOR
ELSE
	PRINTBUTTON "[存档]", 1001
ENDIF
IF 操作模式 == 1002
	SETCOLOR C_YELLOW
	PRINT [读档]
	RESETCOLOR
ELSE
	PRINTBUTTON "[读档]", 1002
ENDIF
IF 操作模式 == 1003
	SETCOLOR C_YELLOW
	PRINT [删档]
	RESETCOLOR
ELSE
	PRINTBUTTON "[删档]", 1003
ENDIF
IF 操作模式 == 1004
	SETCOLOR C_YELLOW
	PRINT [注释存档]
	RESETCOLOR
ELSE
	PRINTBUTTON "[存档并注释]", 1004
ENDIF
PRINTFORML
IF Real_Time_Save == 1
	PRINTFORML
	SETCOLOR C_RED
	PRINTFORML 温馨提示：非就寢时段存档目前处于测试阶段，且非就寢时段存档的读取会有较长的读取时间，若有任何问题请反馈
	RESETCOLOR
ENDIF
CUSTOMDRAWLINE ━
FOR 存档编号, 40 * (页码 - 1), MIN(40 * 页码, 最大存档数量)
	VARSET RESULT:0
	RESULTS:0 = 
	CALL saveColor(存档编号)
	SIF STRFIND(RESULTS:0, "*自动存档*") != -1
		SETCOLOR C_GREEN
	SIF NEW_SAVE_MARK:存档编号 == 1
		FONTSTYLE 1
	PRINTFORML \@ NEW_SAVE_MARK:存档编号 == 1 ? %"[NEW!]", 6%# %" " * 6, 6%\@[{存档编号, 4}] - %RESULTS:0%
	FONTSTYLE 0
	RESETCOLOR
NEXT
;自动存档编号为99
VARSET RESULT:0
RESULTS:0 = 
CALL saveColor(1000)
SIF STRFIND(RESULTS:0, "*自动存档*") != -1
	SETCOLOR C_GREEN
SIF NEW_SAVE_MARK:1000 == 1
	FONTSTYLE 1
PRINTFORML \@ NEW_SAVE_MARK:1000 == 1 ? %"[NEW!]", 6%# %" " * 6, 6%\@[{1000, 4}] - %RESULTS:0%
FONTSTYLE 0
RESETCOLOR
IF 页码 == 1
	{
	HTML_PRINT @"<nonbutton title = '跳转至首页'>　　［　＜＜＜　］　　</nonbutton>
	<nonbutton title = '上十页'>　　［　＜＜　］　　</nonbutton>
	<nonbutton title = '上一页'>　　［　＜　］　　</nonbutton>
	<button title = '下一页' value = '9997'>　　［　＞　］　　</button>
	<button title = '下十页' value = '9998'>　　［　＞＞　］　　</button>
	<button title = '跳转至尾页' value = '9999'>　　［　＞＞＞　］　　</button>"
	}
ELSEIF 页码 == 最大页数
	{
	HTML_PRINT @"<button title = '跳转至首页' value = '9994'>　　［　＜＜＜　］　　</button>
	<button title = '上十页' value = '9995'>　　［　＜＜　］　　</button>
	<button title = '上一页' value = '9996'>　　［　＜　］　　</button>
	<nonbutton title = '下一页'>　　［　＞　］　　</nonbutton>
	<nonbutton title = '下十页'>　　［　＞＞　］　　</nonbutton>
	<nonbutton title = '跳转至尾页'>　　［　＞＞＞　］　　</nonbutton>"
	}
ELSEIF INRANGE(页码, 11, 最大页数 - 11)
	{
	HTML_PRINT @"<button title = '跳转至首页' value = '9994'>　　［　＜＜＜　］　　</button>
	<button title = '上十页' value = '9995'>　　［　＜＜　］　　</button>
	<button title = '上一页' value = '9996'>　　［　＜　］　　</button>
	<button title = '下一页' value = '9997'>　　［　＞　］　　</button>
	<button title = '下十页' value = '9998'>　　［　＞＞　］　　</button>
	<button title = '跳转至尾页' value = '9999'>　　［　＞＞＞　］　　</button>"
	}
ELSEIF INRANGE(页码, 1, 10)
	{
	HTML_PRINT @"<button title = '跳转至首页' value = '9994'>　　［　＜＜＜　］　　</button>
	<nonbutton title = '上十页'>　　［　＜＜　］　　</nonbutton>
	<button title = '上一页' value = '9996'>　　［　＜　］　　</button>
	<button title = '下一页' value = '9997'>　　［　＞　］　　</button>
	<button title = '下十页' value = '9998'>　　［　＞＞　］　　</button>
	<button title = '跳转至尾页' value = '9999'>　　［　＞＞＞　］　　</button>"
	}
ELSEIF INRANGE(页码, 最大页数 - 11, 最大页数)
	{
	HTML_PRINT @"<button title = '跳转至首页' value = '9994'>　　［　＜＜＜　］　　</button>
	<button title = '上十页' value = '9995'>　　［　＜＜　］　　</button>
	<button title = '上一页' value = '9996'>　　［　＜　］　　</button>
	<button title = '下一页' value = '9997'>　　［　＞　］　　</button>
	<nonbutton title = '下十页'>　　［　＞＞　］　　</nonbutton>
	<button title = '跳转至尾页' value = '9999'>　　［　＞＞＞　］　　</button>"
	}
ENDIF
HTML_PRINT @"<button value = '10000' title = '退出'>　　［ＥＸＩＴ］　　</button>"
CUSTOMDRAWLINE ━
INPUT
SELECTCASE RESULT
	CASE 9994
		页码 = 1
	CASE 9995
		页码 -= 10
	CASE 9996
		页码 --
	CASE 9997
		页码 ++
	CASE 9998
		页码 += 10
	CASE 9999
		页码 = 最大页数
	CASE 10000
		CLEARLINE LINECOUNT - 行数
		REDRAW PREV_REDRAW
		RETURN 1
	CASE 1001
		IF qSave != -1
			SIF 操作模式 != 1001
				操作模式 = 1001
		ELSE
			CALL 区
		ENDIF
	CASE 1002
		SIF 操作模式 != 1002
			操作模式 = 1002
	CASE 1003
		SIF 操作模式 != 1003
			操作模式 = 1003
	CASE 1004
		IF qSave != -1
			SIF 操作模式 != 1004
				操作模式 = 1004
		ELSE
			CALL 区
		ENDIF
	CASEELSE
		IF INRANGE(RESULT, 0, 最大存档数量)
			选择的存档位 = RESULT
			VARSET RESULT:0
			RESULTS:0 = 
			CHKDATA 选择的存档位
			IF RESULT != 1
				PRINTFORM 已存在存档{选择的存档位}，备注信息：
				PRINTFORML %RESULTS:0%
				PRINT 是否要
				SELECTCASE 操作模式
					CASE 1001, 1004
						SETCOLOR 0xff5050
						PRINTFORML 替换该存档？
						RESETCOLOR
						CALL ASK_YN
						IF !RESULT
							;两个代码都一样,应该考虑做成函数,减少篇幅,方便维护
							;存档
							VARSET RESULTS
							IF 操作模式 == 1004
								PRINTFORML 请填写您的备注（若未填入任何字符则按正常存档自动生成备注信息）
								INPUTS
							ENDIF
							CALL saveManageSaveGame(选择的存档位, qSave, RESULTS)
						ELSE
							PRINTFORMW 未替换存档
						ENDIF
					CASE 1002
						PRINTFORML 读取该存档？
						CALL ASK_YN
						IF !RESULT
							REDRAW PREV_REDRAW
							;debloat custom code, purge image resource on save file loading so eldritch horrors stop spawning
							CALL RM_全部リリース
							LOADDATA 选择的存档位
						ELSE
							PRINTFORMW 未读取存档
						ENDIF
					CASE 1003
						PRINTFORML 删除该存档？
						CALL ASK_YN
						IF !RESULT
							SETCOLOR C_RED
							PRINTFORML 注意：你正在执行删除存档的操作，此操作不可逆
							PRINTFORML 确定要删除存档{选择的存档位}吗？
							CALL ASK_YN
							RESETCOLOR
							IF !RESULT
								PRINTFORML 删除存档{选择的存档位}
								DELDATA 选择的存档位
								NEW_SAVE_MARK:选择的存档位 = 0
								SAVEGLOBAL
								PRINTFORMW 存档{选择的存档位}删除完成
							ELSE
								PRINTFORMW 未删除存档
							ENDIF
						ELSE
							PRINTFORMW 未删除存档
						ENDIF
					CASEELSE
						THROW 模式错误，无法进行存档操作
				ENDSELECT
			ELSE
				SELECTCASE 操作模式
					CASE 1001, 1004
						PRINTFORML 要将存档存储在{选择的存档位}吗？
						CALL ASK_YN
						IF !RESULT
							;存档
							VARSET RESULTS
							IF 操作模式 == 1004
								PRINTFORML 请填写您的备注（若未填入任何字符则按正常存档自动生成备注信息）
								INPUTS
							ENDIF
							CALL saveManageSaveGame(选择的存档位, qSave, RESULTS)
						ELSE
							PRINTFORMW 未存储存档
						ENDIF
					CASE 1002
						PRINTFORMW 此处没有存档文件
					CASE 1003
						PRINTFORMW 此处没有存档文件
				ENDSELECT
			ENDIF
		ELSE
			PRINTFORMW 超出最大存档范围
		ENDIF
ENDSELECT
;放到最后面处理,不仅可以减少代码数量,还可以防止漏写
CLEARLINE LINECOUNT - 行数
GOTO 开始位置

;根据存档状况上色
@saveColor(saveId)
	#DIM saveId
	SELECTCASE CHKDATA(saveId)
		CASE 1
			SETCOLOR C_L_GRAY
		CASE 2
			SETCOLOR C_RED
		CASE 3
			SETCOLOR C_ORANGE
		CASE 4
			SETCOLOR C_P_PURPLE
	ENDSELECT

;存档
@saveManageSaveGame(选择的存档位, qSave, 存档备注信息)
#DIM DYNAMIC 选择的存档位
#DIMS DYNAMIC 存档备注信息
#DIM qSave
#DIM DYNAMIC MODE
GETTIME
SIF !STRLENS(存档备注信息)
	存档备注信息 = %createSaveInfo(qSave)%
SIF qSave == 1 && STRFIND(存档备注信息, "⭐即时存档⭐") == -1
	存档备注信息 += " ⭐即时存档⭐"
SIF qSave == 2 && STRFIND(存档备注信息, "*自动存档*") == -1
	存档备注信息 += " *自动存档*"
;PRINTFORML 需要添加备注信息吗？
;CALL ASK_YN
;IF !RESULT
;	存档备注信息 += @"{DAY:0, 3, RIGHT}日"  
;	INPUTS
;	存档备注信息 += RESULTS
;	SIF MODE == 1
;		存档备注信息 += " ⭐即时存档⭐ "
;ELSE
;	存档备注信息 += createSaveInfo(MODE)
;ENDIF
SIF qSave == 1
	SaveDataInTrain = 1
SAVEDATA 选择的存档位, 存档备注信息
PRINTFORMW 已存档{选择的存档位}，存档备注信息：%存档备注信息%
IF STRFIND(存档备注信息, "⭐即时存档⭐") == -1
	NEW_SAVE_MARK:选择的存档位 = 1
	SAVEGLOBAL
ENDIF

@区
SELECTCASE RAND:4
	CASE 0
		PLAYSOUND @"区/vamos!.MP3"
	CASE 1
		PLAYSOUND @"区/卡了.MP3"
	CASE 2
		PLAYSOUND @"区/操操操.MP3"
	CASE 3
		PLAYSOUND @"区/卡键.MP3"
ENDSELECT
