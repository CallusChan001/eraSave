;-------------------------------------------------
;函数名:MSG_CODE
;概　要:使用微软哥特字体的符号
;主要是为了控制符号显示
;-------------------------------------------------
@MSG_CODE(ARGS)
#LOCALSIZE 1
#LOCALSSIZE 1
LOCALS = %GETFONT()%
SETFONT "MS Gothic"
PRINTS ARGS
SETFONT LOCALS

;-------------------------------------------------
;函数名:IMG_TALK
;概　要:简单图文混排
;引　用：SPTALK,CID,表情="",默认=0,MESSAGE,整形=0
;K63_PRINT_FACE与SPTALK的功能融合版，为了简单图片与对话混排，默认整形
;-------------------------------------------------
@IMG_TALK(基本画像, MESSAGE, 整形=1, CID, PATTERN,尺寸)
#DIMS DYNAMIC RESOURCENAME
#DIMS DYNAMIC 基本画像
#DIMS DYNAMIC MESSAGE
#DIMS DYNAMIC MESSAGE_PRINT,50
#DIMS DYNAMIC FONTCOLOR
#DIM DYNAMIC TEMP_COLOR
#DIM DYNAMIC CID
#DIM DYNAMIC 整形
#DIM DYNAMIC 尺寸
#DIM DYNAMIC PATTERN
#LOCALSIZE 1
#LOCALSSIZE 1
SIF !尺寸
	尺寸 = 角色画像表示尺寸比
TEMP_COLOR = GETCOLOR()

;口上色取得判定:通常角色
IF CID
	CALL SET_KOJO_COLOR(CID)
	IF GETCOLOR() == 12632256 || !FLAG:口上色
		FONTCOLOR = 0xD5DEDF
	ELSE
		FONTCOLOR = 0%TOSTR(GETCOLOR(), "X")% 
	ENDIF
ELSE
	FONTCOLOR = 0%TOSTR(GETCOLOR(), "X")% 
ENDIF
VARSET MESSAGE_PRINT
IF 顔絵付きメッセージ == 1
	;向后兼容@K63_PRINT_IMG
	RESOURCENAME = K{CID}_%基本画像%_{PATTERN}
	;SIF !SPRITECREATED(RESOURCENAME)
	SIF !RM_リソースチェック(RESOURCENAME)
		RESOURCENAME =  K{CID}_%基本画像%
	;SIF !SPRITECREATED(RESOURCENAME)
	SIF !RM_リソースチェック(RESOURCENAME)
		RESOURCENAME =  %基本画像%
	;IF !SPRITECREATED(RESOURCENAME) 
	IF !RM_リソースチェック(RESOURCENAME) 
		PRINTFORMDL 资源「%RESOURCENAME%」未设置
		GOTO 图形错误
	ELSE
		CALL 画像セット(RESOURCENAME, 0, 0, 尺寸, 0)
	ENDIF

	;オプション指定で行送りを揃える
	SIF 整形
		MESSAGE = %SPTALK整形(MESSAGE)%

	SPLIT MESSAGE,"/",MESSAGE_PRINT
	FOR LOCAL,0,RESULT
		SIF LOCAL != 0
			CALL ダミーセット(0, 0, 尺寸 * RM_リソースの幅(RESOURCENAME)/RM_リソースの高さ(RESOURCENAME), LOCAL)
		TEMP_HTML:LOCAL += @"<font color='#%FONTCOLOR%'>%MESSAGE_PRINT:LOCAL%</font>"
	NEXT
	CALL 画像一斉表示(0, 0, 1, -1)
	PRINTL
ELSE
	$图形错误
	;顔絵メッセージOFF時は通常街道に表示させる
	SIF CID
		PRINTFORML －%CALLNAME:CID%－
	SPLIT MESSAGE,"/",MESSAGE_PRINT
	FOR LOCAL,0,RESULT
		SIF MESSAGE_PRINT:LOCAL != ""
			PRINTFORML %MESSAGE_PRINT:LOCAL%
	NEXT
ENDIF
SETCOLOR TEMP_COLOR

;-------------------------------------------------
;函数名:PRINT_ANIME
;概　要:插入动图，展示后将其删除
;主要是确保动图被删掉，不然会爆炸
;-------------------------------------------------
@PRINT_ANIME(ARGS)
#LOCALSIZE 1
#LOCALSSIZE 1
LOCAL = CURRENTREDRAW()
REDRAW 0
SETANIMETIMER 40
CALL PRINT_IMAGE,ARGS,1000
WAIT
CLEARLINE 10
REDRAW LOCAL
SETANIMETIMER 32627

;-------------------------------------------------
;函数名:有色二择提示
;概　要:选项的附属信息表单，
;高亮例举变动项目、展示差异
;-------------------------------------------------
@有色二择提示(项目,未激活提示 = "未开启",已激活提示 = "已开启",条件,未激活颜色 = C_L_GRAY,已激活颜色 = C_YELLOW)
#LOCALSIZE 1
#LOCALSSIZE 2
#DIMS DYNAMIC 项目
#DIMS DYNAMIC 未激活提示
#DIMS DYNAMIC 已激活提示
#DIM DYNAMIC 条件
#DIM DYNAMIC 未激活颜色
#DIM DYNAMIC 已激活颜色
IF 条件
	LOCALS '= HTMLCOLOR(未激活颜色)
	LOCALS:1 '= HTMLCOLOR(已激活颜色)
ELSE
	LOCALS:1 '= HTMLCOLOR(未激活颜色)
	LOCALS '= HTMLCOLOR(已激活颜色)
ENDIF
HTML_PRINT @"%项目%  <font color='%LOCALS%'>%未激活提示%</font>  <font color='%LOCALS:1%'>%已激活提示%</font>"

;=====================================================================
;打印多行带引号指定角色口上色文本，用空格做换行符
;引  用：PRINT_DIALOGUE, ARGS, ARGS:1
;例1：灵梦
;CALL SET_KOJO_COLOR(1)
;PRINTFORML 「乐园的可爱巫女%CALLNAME:1%每天都为你抽签」
;PRINTW 「一个月50籌碼、够便宜吧？」
;RESETCOLOR
;常规单行
;CALL PrintMultiDia(@"乐园的可爱巫女%CALLNAME:1%每天都为你抽签 一个月50籌碼、够便宜吧？",,1,,"W")
;或代码块，适合文本行数或每行字数较多时使用
;{
;CALL PrintMultiDia(@"乐园的可爱巫女%CALLNAME:1%每天都为你抽签
;一个月50籌碼、够便宜吧？",,1,,"W")
;}
;例2：
;SETCOLOR 0xFF0000
;FONTSTYLE 11;粗体1斜体2下划线8
;PRINTPLAIN 『[你]刚才，对我做了些什么吧？』
;FONTREGULAR
;RESETCOLOR
;PRINTW
;
;CALL PrintMultiDia("[你]刚才，对我做了些什么吧？",1,,0xFF0000,"P",11)
;PRINTW
;=====================================================================
@PrintMultiDia(Dia,Quotes,CID,Color=-1,Form="L",IFontStyle)
#DIMS Dia;Dialog，对话
#DIM CID;可填角色编号识别口上色，优先级高于Color，省略以指定颜色
#DIM Color;直接填写颜色
#DIM Quotes;0「」，1『』，-1（）心声
;Fontstyle   0=通常,bit0=粗体,bit1=斜体,bit2=删除线,bit3=下划线
#DIM IFontStyle;IF Fontstyle on
;PRINTFORM 0=PRINT,1=PRINTL,2=PRINTW,3=PRINTPLAIN +FORM
#DIMS Form;默认L
;\n空格换行，等效大括号多行转行；对话规范使用标点符号通常无需空格
IF INRANGE(CID,1,CHARANUM)
	IF FLAG:口上色
		CALL SET_KOJO_COLOR(CID)
		Color=GETCOLOR();获取口上色
		RESETCOLOR
	ENDIF
ELSEIF !INRANGE(Color,0,0xFFFFFF)
	Color=GETCOLOR();当前色
ENDIF
Dia'=REPLACE(Dia,"hph","❤");Heart
SELECTCASE Form
	CASE "L"
		LOCAL=1;换行
	CASE "W"
		LOCAL=2;换行，停顿
	CASE "P","PL","PLAIN"
		LOCAL=3;直接打印，无视[按钮]
	CASEELSE
		LOCAL=0;不换行
ENDSELECT
SELECTCASE Quotes
	CASE 0
		LOCALS'="」","「";常规
	CASE 1
		LOCALS'="』","『";对方，如自机或多人对话
	CASE -1
		LOCALS'="）","（";心声，例（刚才这么说是不是不太好）
ENDSELECT
Dia'=REPLACE(Dia,"\n ",@"%LOCALS%\n\n%LOCALS:1%");空行，避免空引号
Dia'=REPLACE(Dia," ",@"%LOCALS%\n%LOCALS:1%");换行，代码块换行符被视为空格
CALL COLORMESSAGE(@"%LOCALS:1%%Dia%%LOCALS%",Color,LOCAL,IFontStyle)

@TheStoryOfMultiDia
SETCOLOR C_RED
PRINTL （PRINT直接打印，D默认颜色，L换行，W换行并暂停，FORM按格式解析……）
PRINTW （角色说话用直角单引号「」，别人或者自机MASTER用直角双引号『』和旁白区分，心里话用括号（）……）
PRINTW 「啊————！好烦呐！」
RESETCOLOR
PRINTW 似乎叫太大声，有谁过来了
SETCOLOR C_AQUA
PRINTL 『怎么啦？又卡文写不出来吗？』
RESETCOLOR
PRINTW 前辈投来关切的目光
SETCOLOR C_RED
PRINTL 「每次敲好的文本还要费事批量加上引号和PRINT之类的好麻烦！而且引号还没法直接打出来！」
SETCOLOR C_AQUA
PRINTL 『哎呀，这种事情不是直接用输入法快捷短语就能搞定吗』
RESETCOLOR
PRINTW 哭笑不得地让自己去查怎么设置
SETCOLOR C_RED
PRINTL 「难道就没有什么方法能省掉这一步吗？」
PRINTL 「还有还有，要出现其它角色，用上别的颜色」
PRINTL 「每次还要设置颜色SETCOLOR，转回来重置RESETCOLOR，真的好麻烦！」
RESETCOLOR
CALL COLORMESSAGE("『不是有COLORMESSAGE()吗，角色对话什么的也还有CHARA_TEXT()可以用呢』",C_AQUA,1)
CALL COLORMESSAGE,@"「有是有，但是写色色部分的爱心HPH_PRINT()的%UNICODE(0x2764)%又不兼容了！」",C_RED,1
CALL COLORMESSAGE("『虽然很想说直接往输入法塞进去这个符号❤就是了…』",C_AQUA,2)
CALL COLORMESSAGE,"「就没有什么方法能解决吗？」\n「再怎么说最开头那个批量加PRINT和引号的，真的好费事！」",C_RED,2
{
CALL PrintMultiDia,"那，既然这样
只需要对COLORMESSAGE()上游稍微改造一下
应该就算是能完美解决问题了吧",1,,C_AQUA,"W"
}
CALL PrintMultiDia("诶？诶？真的诶！\n 这么好用的东西为什么还藏着掖着不早点拿出来？！",,,C_RED,"W")
SETCOLOR C_AQUA
CALL PrintMultiDia,"啊哈哈，就知道你这杂鱼不会自己动手hph",1
CALL PrintMultiDia,"虽然从头到尾呢 一直都没谁有这种改进的想法…///",-1
PRINTDW 暗自心虚的模样还好没被识破
CALL PrintMultiDia,"你看你看，比如魔理沙和自机的对话",1
TARGET=11
LOCAL=CFLAG:11:998
CFLAG:11:998=1;雾雨迷境
PRINTL
CALL PrintMultiDia,@"+++！你个大笨蛋，坏心眼！ 居然背着我偷偷摸摸做那种事！ …也不叫上我一起//\/",,TARGET,,"W"
CALL PrintMultiDia,@"啊哈哈，这不是根本找不到$$$在哪吗 再说了，谁知道堂堂雾雨魔理沙也好这口啊……",1,,GETDEFCOLOR(),"W"
PRINTW
;era通用：+++，%CALLNAME:MASTER%；$$$，%CALLNAME:TARGET%；***，%NAME:TARGET%，带空格会换行，手动输入（本来也不常用）
;===，%CALLNAME:PLAYER%，调教者，tw就是MASTER
;///，%NAME:ASSI%；tw不存在，会报错，欲用请//\/转义或PRINT不加FORM解析格式（传入字符串则不用(@"")前加@）两者择一
TARGET=0
CFLAG:11:998=LOCAL
SETCOLOR C_AQUA
{
CALL PrintMultiDia,"只是随便举的例子啊？不要当真哦
所以呐，你就安心照这么用着吧
方法和例子已经就这么白白送给你了
要好好使用哦",1,,,"W"
}
CALL PrintMultiDia("好耶！就知道前辈有办法解决！ …不过，魔理沙是发现自机做了什么呀",,,C_RED)
CALL PrintMultiDia,"这种事情自己去写！",1
FORCEWAIT
RESETCOLOR

;-------------------------------------------------
;函数名:FADEIN_GROUP
;概　要:FADEIN的统合输出
;主要是为了提前控制行数，不要在演出时换行
;-------------------------------------------------
@FADEIN_GROUP(COMMENT, NUM_COMMENT, TIME_REDRAW = 1000, TIME_FADE = 8)
#LOCALSIZE 2
#LOCALSSIZE 1
#DIMS REF COMMENT         ;表示する文字列の配列
#DIM  NUM_COMMENT         ;配列の要素数
#DIM  PREV_REDRAW         ;一時退避用
#DIM  TIME_REDRAW         ;行追加の間隔、標準で1秒
#DIM  TIME_FADE         
#DIM  DYNAMIC NOW_COMMENT ;表示隠蔽制御用
PREV_REDRAW = CURRENTREDRAW()
REDRAW 0
FOR LOCAL:0, 0, NUM_COMMENT
	FOR LOCAL:1, 0, NUM_COMMENT
		IF LOCAL:1 < NOW_COMMENT
			PRINTFORML %COMMENT:(LOCAL:1)%
		ELSEIF LOCAL:1 == NOW_COMMENT
			CALL FADEINL(TIME_FADE,NUM_COMMENT - NOW_COMMENT - 1,COMMENT:(LOCAL:1))
		ENDIF
	NEXT
	TWAIT TIME_REDRAW, 0
	NOW_COMMENT ++
	SIF LOCAL:0 < NUM_COMMENT -1
		CLEARLINE NUM_COMMENT
NEXT
REDRAW PREV_REDRAW
WAIT

@FADEINL(ARG = 8, ARG:1 = 1, ARGS = "VOID", ARGS:1 = "VOID", ARGS:2 = "VOID", ARGS:3 = "VOID", ARGS:4 = "VOID", ARGS:5 = "VOID", ARGS:6 = "VOID", ARGS:7 = "VOID", ARGS:8 = "VOID", ARGS:9 = "VOID", ARGS:10 = "VOID", ARGS:11 = "VOID", ARGS:12 = "VOID", ARGS:13 = "VOID", ARGS:14 = "VOID", ARGS:15 = "VOID", ARGS:16 = "VOID", ARGS:17 = "VOID", ARGS:18 = "VOID", ARGS:19 = "VOID", ARGS:20 = "VOID", ARGS:21 = "VOID", ARGS:22 = "VOID", ARGS:23 = "VOID", ARGS:24 = "VOID", ARGS:25 = "VOID", ARGS:26 = "VOID", ARGS:27 = "VOID", ARGS:28 = "VOID", ARGS:29 = "VOID")
#LOCALSIZE 1
#LOCALSSIZE 1
CALL FADE(ARG, GETBGCOLOR(), GETCOLOR(), ARGS, ARGS:1, ARGS:2, ARGS:3, ARGS:4, ARGS:5, ARGS:6, ARGS:7, ARGS:8, ARGS:9, ARGS:10, ARGS:11, ARGS:12, ARGS:13, ARGS:14, ARGS:15, ARGS:16, ARGS:17, ARGS:18, ARGS:19, ARGS:20, ARGS:21, ARGS:22, ARGS:23, ARGS:24, ARGS:25, ARGS:26, ARGS:27, ARGS:28, ARGS:29,,ARG:1)
RETURN RESULT


