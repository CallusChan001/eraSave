;-------------------------------------------------
;函数:简单的拼接图合成
;概　要:使用了雨水为猫包绘准备的系统
;主要是为了移植从画师的PSD自动生成的拼接图
;-------------------------------------------------
@SPIRIT_GET_BASE_RESOURCE(CHARA, 表示形式 = "", 服装 = "", 表情:0 = "", 表情:1 = "", 差分 = "", 乳量 = "")
#DIM DYNAMIC CHARA
#DIMS DYNAMIC 表示形式
#DIMS DYNAMIC 妊肚
#DIMS DYNAMIC 乳量
#DIMS DYNAMIC 私处
#DIMS DYNAMIC 服装
#DIMS DYNAMIC 差分
#DIMS DYNAMIC 表情, 2
#DIMS DYNAMIC 全图索引, 2
#DIMS DYNAMIC Version
#LOCALSIZE 1
#LOCALSSIZE 1
;生成特效
;PRINTFORML %LOCALS%%TOSTR(CHARA)%判定为{STRCOUNT(LOCALS,TOSTR(CHARA))}
IF !STRCOUNT(LOCALS,TOSTR(CHARA))
	CALL SPIRIT_BUILD_特效差分(,CHARA,CFLAG:CHARA:差し替え適用)
	LOCALS = %LOCALS%%TOSTR(CHARA)%/
ENDIF

;SPIRIT_{妊肚}_{乳量}_{私处}_{服装}_{表情}_{CHARA}

;初步判定
IF 服装 == ""
    IF CHECK_NUDE(CHARA)
        服装 = 裸
    ELSE
        服装 = 服
    ENDIF
	CALL IMAGE_CHARA_OVERRIDE("特殊服装", CHARA)
	IF RESULT:1
		服装 += RESULTS
	ENDIF
ENDIF

RESULT = 0
TRYCALLFORM SPIRIT_Version{CHARA}
SIF RESULT
	Version = %RESULTS%

;妊肚 = %SPIRIT_妊肚判定(CHARA)%
;乳量 = %SPIRIT_乳量判定(CHARA)%
TRYCCALLFORM SPIRIT_特殊私处_{CHARA}(服装)
	SIF RESULT
		私处 '= RESULTS
CATCH
ENDCATCH

;PRINTFORML 传入的表情「%表情:0%/%表情:1%」

;表情(RESULTSに格納)
IF 表情 == ""
	CALL SPIRIT_GET_FACETYPE(Version, CHARA, 表示形式, 服装, 差分)
	;PRINTFORML 表情算出「%RESULTS:0%/%RESULTS:1%」
	表情 '= RESULTS
ENDIF
SIF 表情:1 == "" && RESULTS:1 != ""
	表情:1 '= RESULTS:1
全图索引:0 = %SPIRIT_全图索引(Version, CHARA, 妊肚, 乳量, 私处, 服装, 表情:0)%
SIF !RM_リソースチェック(全图索引:0)
	CALL SPIRIT_BUILD_BASE_IMAGE(Version, CHARA, 妊肚, 乳量, 私处, 服装, 表情:0)

IF 表情:1 == ""
	全图索引:1 = 
ELSE
	全图索引:1 = %SPIRIT_全图索引(Version, CHARA, 妊肚, 乳量, 私处, 服装, 表情:1)%
	SIF !RM_リソースチェック(全图索引:1)
		CALL SPIRIT_BUILD_BASE_IMAGE(Version, CHARA, 妊肚, 乳量, 私处, 服装, 表情:1)
ENDIF


IF FLAG:立絵種類 || 表示形式 == "顔絵"
	; 颜绘
	CALL SPIRIT_颜像生成(Version, CHARA, 全图索引:0, 全图索引:1)
ELSE
	; 立绘
	RESULTS:0 = %全图索引:0%
	RESULTS:1 = %全图索引:1%
ENDIF
;PRINTFORML 立绘「%RESULTS:0%/%RESULTS:1%」
RETURN
;}


; 立像生成{
@SPIRIT_BUILD_BASE_IMAGE(Version, CHARA, 妊肚, 乳量, 私处, 服装, 表情)
#DIMS DYNAMIC Version
#DIM DYNAMIC G_ID
#DIM DYNAMIC CHARA
#DIMS DYNAMIC 妊肚
#DIMS DYNAMIC 乳量
#DIMS DYNAMIC 私处
#DIMS DYNAMIC 服装
#DIMS DYNAMIC 表情
#DIMS DYNAMIC 体图索引
#DIMS DYNAMIC 表情索引
#DIMS DYNAMIC 全图索引

体图索引 = %SPIRIT_体图索引(Version, CHARA, 妊肚, 乳量, 私处, 服装)%
表情索引 = %SPIRIT_表情索引(Version, CHARA, 服装, 表情)%
全图索引 = %SPIRIT_全图索引(Version, CHARA, 妊肚, 乳量, 私处, 服装, 表情)%

IF !RM_リソースチェック(体图索引)
	CALL SPIRIT_BUILD_体差分(Version, CHARA, 妊肚, 乳量, 私处, 服装)
ENDIF
IF STRCOUNT(表情,"覆盖")
	G_ID = 0
	CALL 画像合成(G_ID, "ダミー", 0, 0, 500, 500)
	CALL 画像合成(G_ID, 表情索引)
ELSE
	G_ID = 0
	CALL 画像合成(G_ID, "ダミー", 0, 0, 500, 500)
	CALL 画像合成(G_ID, 体图索引)
	CALL 画像合成(G_ID, 表情索引)
ENDIF

SPRITECREATE 全图索引, G_ID
RETURN
;}

; 颜像生成{

@SPIRIT_颜像生成(Version, CHARA, 全图索引:0, 全图索引:1)
#DIMS DYNAMIC Version
#DIM DYNAMIC CHARA
#DIMS DYNAMIC 全图索引, 2
#DIMS DYNAMIC 颜像索引, 2

颜像索引:0 = %SPIRIT_颜像索引(全图索引:0)%
SIF !RM_リソースチェック(颜像索引:0)
	CALL SPIRIT_颜像切分(Version, CHARA, 全图索引:0)

IF 全图索引:1 == ""
	颜像索引:1 = 
ELSE
	颜像索引:1 = %SPIRIT_颜像索引(全图索引:1)%
	SIF !RM_リソースチェック(颜像索引:1)
		CALL SPIRIT_颜像切分(Version, CHARA, 全图索引:1)
ENDIF

RESULTS:0 = %颜像索引:0%
RESULTS:1 = %颜像索引:1%
RETURN


@SPIRIT_颜像切分(Version, CHARA, 全图索引)
#DIMS DYNAMIC Version
#DIM DYNAMIC G_ID
#DIM DYNAMIC CHARA
#DIMS DYNAMIC 全图索引
#DIMS DYNAMIC 颜像索引
#DIM DYNAMIC 颜RECT,4

IF RM_リソースチェック(@"{CHARA}%Version%_颜像范围")
	颜RECT:0 = SPRITEPOSX(@"{CHARA}%Version%_颜像范围")
	颜RECT:1 = SPRITEPOSY(@"{CHARA}%Version%_颜像范围")
	颜RECT:2 = SPRITEWIDTH(@"{CHARA}%Version%_颜像范围")
	颜RECT:3 = SPRITEHEIGHT(@"{CHARA}%Version%_颜像范围")
ELSE
	颜RECT:0 = -100
	颜RECT:1 = 0
	颜RECT:2 = 300
	颜RECT:3 = 300
ENDIF

颜像索引 = %SPIRIT_颜像索引(全图索引)%

G_ID = 0
CALL 画像合成(G_ID, "ダミー", 0, 0, 颜RECT:2, 颜RECT:3)
CALL 画像合成(G_ID, 全图索引, 颜RECT:0, 颜RECT:1, 500, 500)

SPRITECREATE 颜像索引, G_ID

RETURN

; 索引{
@SPIRIT_颜像索引(ARGS)
#FUNCTIONS
RETURNF @"颜%ARGS%"

@SPIRIT_全图索引(Version, CHARA, 妊肚, 乳量, 私处, 服装, 表情)
#FUNCTIONS
#DIMS DYNAMIC Version
#DIM DYNAMIC CHARA
#DIMS DYNAMIC 妊肚
#DIMS DYNAMIC 乳量
#DIMS DYNAMIC 私处
#DIMS DYNAMIC 服装
#DIMS DYNAMIC 表情
RETURNF @"%Version%_%妊肚%_%乳量%_%私处%_%服装%_%表情%_{CHARA}"

@SPIRIT_体图索引(Version, CHARA, 妊肚, 乳量, 私处, 服装)
#FUNCTIONS
#DIMS DYNAMIC Version
#DIM DYNAMIC CHARA
#DIMS DYNAMIC 妊肚
#DIMS DYNAMIC 乳量
#DIMS DYNAMIC 私处
#DIMS DYNAMIC 服装
RETURNF @"%Version%体图_%妊肚%_%乳量%_%私处%_%服装%_{CHARA}"

@SPIRIT_表情索引(Version, CHARA, 服装, 表情)
#FUNCTIONS
#DIMS DYNAMIC Version
#DIM DYNAMIC CHARA
#DIMS DYNAMIC 表情
#DIMS DYNAMIC 服装
RETURNF @"{CHARA}%COND_STR(@"%Version%_%服装%",Version != "")%_表情_%表情%"
;}

@SPIRIT_BUILD_体差分(Version, CHARA, 妊肚, 乳量, 私处, 服装)
#DIMS DYNAMIC Version
#DIM DYNAMIC G_ID
#DIM DYNAMIC CHARA
#DIMS DYNAMIC 妊肚
#DIMS DYNAMIC 乳量
#DIMS DYNAMIC 私处
#DIMS DYNAMIC 服装
#DIMS DYNAMIC 体图索引

体图索引 = %SPIRIT_体图索引(Version, CHARA, 妊肚, 乳量, 私处, 服装)%
SIF RM_リソースチェック(体图索引)
	RETURN

G_ID = 0
CALL 画像合成(G_ID, "ダミー", 0, 0, 500, 500)

; 躯干差分
CALL SPIRIT_GET_裸躯差分(Version, CHARA, 妊肚)
SIF RESULT
	CALL 画像合成(G_ID, RESULTS)

CALL SPIRIT_GET_服差分(Version, CHARA, 妊肚, 乳量, 服装)
SIF RESULT
	CALL 画像合成(G_ID, RESULTS)

;; 胸部差分
;CALL SPIRIT_GET_裸胸差分(Version, CHARA, 乳量)
;SIF RESULT
;	CALL 画像合成(G_ID, RESULTS)
;
;; 腹部差分
;CALL SPIRIT_GET_裸腹差分(Version, CHARA, 妊肚, 乳量)
;SIF RESULT
;	CALL 画像合成(G_ID, RESULTS)

; 私处差分
CALL SPIRIT_GET_私处差分(Version, CHARA, 私处)
SIF RESULT
	CALL 画像合成(G_ID, RESULTS)

; 合成体图
SPRITECREATE 体图索引, G_ID
RETURN

@SPIRIT_GET_裸躯差分(Version, CHARA, 妊肚)
#DIMS DYNAMIC Version
#DIM DYNAMIC CHARA
#DIMS DYNAMIC 妊肚
IF 妊肚 != "" && RM_リソースチェック(@"{CHARA}%Version%_裸躯_膨腹_躯干")
	RESULTS = %@"{CHARA}%Version%_裸躯_膨腹_躯干"%
	RETURN 1
ENDIF
RESULTS = %@"{CHARA}%Version%_裸躯_躯干"%

RETURN 1

; SPIRIT{CHARA}_裸胸{_乳量}
; 无 <- 裸胸{_乳量}
@SPIRIT_GET_裸胸差分(Version, CHARA, 乳量)
#DIMS DYNAMIC Version
#DIM DYNAMIC CHARA
#DIMS DYNAMIC 乳量
IF RM_リソースチェック(@"{CHARA}%Version%_裸胸_%乳量%")
	RESULTS = %@"{CHARA}%Version%_裸胸_%乳量%"%
	RETURN 1
ENDIF
IF RM_リソースチェック(@"{CHARA}%Version%_裸胸")
	RESULTS = %@"{CHARA}%Version%_裸胸"%
	RETURN 1
ENDIF
RETURN 0

; SPIRIT{CHARA}_裸腹{_乳量}_妊肚
; 无 <- 裸腹_妊肚 <- 裸腹{_乳量}_妊肚
@SPIRIT_GET_裸腹差分(Version, CHARA, 妊肚, 乳量)
#DIMS DYNAMIC Version
#DIM DYNAMIC CHARA
#DIMS DYNAMIC 妊肚
#DIMS DYNAMIC 乳量
IF 妊肚 != ""
	IF RM_リソースチェック(@"{CHARA}%Version%_裸腹_%乳量%_妊肚")
		RESULTS = %@"{CHARA}%Version%_裸腹_%乳量%_妊肚"%
		RETURN 1
	ENDIF
	IF RM_リソースチェック(@"{CHARA}%Version%_裸腹_妊肚")
		RESULTS = %@"{CHARA}%Version%_裸腹_妊肚"%
		RETURN 1
	ENDIF
ENDIF
RETURN 0

; SPIRIT{CHARA}_私处_私处
; 无 <- 私处_私处 <- 私处_膨腹_私处
@SPIRIT_GET_私处差分(Version, CHARA, 私处)
#DIMS DYNAMIC Version
#DIM DYNAMIC CHARA
#DIMS DYNAMIC 私处

SIF 私处 == ""
	RETURN 0

IF SPIRIT_妊肚判定(CHARA) != "" && RM_リソースチェック(@"{CHARA}%Version%_膨腹_%私处%")
	RESULTS = %@"{CHARA}%Version%_膨腹_%私处%"%
	RETURN 1
ENDIF

IF RM_リソースチェック(@"{CHARA}%Version%_私处_%私处%")
	RESULTS = %@"{CHARA}%Version%_私处_%私处%"%
	RETURN 1
ENDIF
RETURN 0

; 底层服差分{
@SPIRIT_GET_服差分(Version, CHARA, 妊肚, 乳量, 服装)
#DIMS DYNAMIC Version
#DIM DYNAMIC CHARA
#DIMS DYNAMIC REF 妊肚,0
#DIMS DYNAMIC 乳量
#DIMS DYNAMIC 服装

RESULT = 0
IF 服装 != "" && 妊肚 == "妊肚"
	CALL SPIRIT_GET_妊娠服装差分(Version, CHARA, 乳量, 服装)
ELSEIF 服装 != ""
	CALL SPIRIT_GET_服装差分(Version, CHARA, 妊肚, 乳量, 服装)
ENDIF
SIF !RESULT
	CALL SPIRIT_GET_常服差分(Version, CHARA, 妊肚, 乳量)

RETURN 1

; SPIRIT{CHARA}_服躯{_特殊服装}{_妊肚}
; 无 <- 服躯{_特殊服装} <- 服躯{_特殊服装}_妊肚
; SPIRIT{CHARA}_服胸{_特殊服装}{_膨腹}{_乳量}
; 无 <- 服胸{_特殊服装}{_乳量} <- 服胸{_特殊服装}{_膨腹}{_乳量}
; SPIRIT{CHARA}_服腹{_特殊服装}{_乳量}{_妊肚}
; 无 <- 服腹{_特殊服装}_妊肚 <- 服腹{_特殊服装}{_乳量}_妊肚
@SPIRIT_GET_妊娠服装差分(Version, CHARA, 乳量, 服装)
#DIMS DYNAMIC Version
#DIM DYNAMIC CHARA
#DIMS DYNAMIC 乳量
#DIMS DYNAMIC 服装
IF RM_リソースチェック(@"{CHARA}%Version%_服躯_%服装%_妊肚")
	RESULTS:0 = %@"{CHARA}%Version%_服躯_%服装%_妊肚"%
ELSEIF RM_リソースチェック(@"{CHARA}%Version%_服躯_%服装%")
	RESULTS:0 = %@"{CHARA}%Version%_服躯_%服装%"%
ELSE
	RETURN 0
ENDIF
RETURN 1

; SPIRIT{CHARA}_服躯{_特殊服装}{_妊肚}
; 无 <- 服躯{_特殊服装} <- 服躯{_特殊服装}_妊肚 <- 服躯{_特殊服装}_膨腹
; SPIRIT{CHARA}_服胸{_特殊服装}{_膨腹}{_乳量}
; 无 <- 服胸{_特殊服装}{_乳量} <- 服胸{_特殊服装}{_膨腹}{_乳量}
; SPIRIT{CHARA}_服腹{_特殊服装}{_乳量}{_妊肚}
; 无 <- 服腹{_特殊服装}_妊肚 <- 服腹{_特殊服装}{_乳量}_妊肚 <- 服腹{_特殊服装}_膨腹 <- 服腹{_特殊服装}{_乳量}_膨腹
@SPIRIT_GET_服装差分(Version, CHARA, 妊肚, 乳量, 服装)
#DIMS DYNAMIC Version
#DIM DYNAMIC CHARA
#DIMS DYNAMIC REF 妊肚,0
#DIMS DYNAMIC 乳量
#DIMS DYNAMIC 服装

LOCALS = %妊肚%

; 躯干
IF LOCALS == "膨腹" && RM_リソースチェック(@"{CHARA}%Version%_服躯_%服装%_膨腹")
	RESULTS:0 = %@"{CHARA}%Version%_服躯_%服装%_膨腹"%
ELSEIF LOCALS != "" && RM_リソースチェック(@"{CHARA}%Version%_服躯_%服装%_妊肚")
	RESULTS:0 = %@"{CHARA}%Version%_服躯_%服装%_妊肚"%
ELSEIF RM_リソースチェック(@"{CHARA}%Version%_服躯_%服装%")
	RESULTS:0 = %@"{CHARA}%Version%_服躯_%服装%"%
	妊肚 = 
ELSE
	RETURN 0
ENDIF
RETURN 1

; SPIRIT{CHARA}_服躯_常服{_妊肚}
; 服躯_常服 <- 服躯_常服_妊肚 <- 服躯_常服_膨腹
; SPIRIT{CHARA}_服胸_常服{_膨腹}{_乳量}
; 无 <- 服胸_常服{_乳量} <- 服胸_常服{_膨腹}{_乳量}
; SPIRIT{CHARA}_服腹_常服{_乳量}{_妊肚}
; 无 <- 服腹_常服_妊肚 <- 服腹_常服{_乳量}_妊肚 <- 服腹_常服_膨腹 <- 服腹_常服{_乳量}_膨腹
@SPIRIT_GET_常服差分(Version, CHARA, 妊肚, 乳量)
#DIMS DYNAMIC Version
#DIM DYNAMIC CHARA
#DIMS DYNAMIC 妊肚
#DIMS DYNAMIC 乳量

RESULTS:0 = 

; 躯干
IF 妊肚 == "膨腹" && RM_リソースチェック(@"{CHARA}%Version%_服躯_服_膨腹")
	RESULTS:0 = %@"{CHARA}%Version%_服躯_常服_膨腹"%
ELSEIF 妊肚 != "" && RM_リソースチェック(@"{CHARA}%Version%_服躯_服_妊肚")
	RESULTS:0 = %@"{CHARA}%Version%_服躯_服_妊肚"%
ELSEIF RM_リソースチェック(@"{CHARA}%Version%_服躯_常服")
	RESULTS:0 = %@"{CHARA}%Version%_服躯_服"%
ELSE
	RETURN 0
ENDIF
RETURN 1

@SPIRIT_妊肚判定(ARG)
#FUNCTIONS
#LOCALSIZE 1
#LOCALSSIZE 1
SIF CFLAG:ARG:妊娠経過日数 >= 40
	RETURNF "妊肚"
SIF 充填率(ARG,3) >= 50
	RETURNF "膨腹"
RETURNF ""

@SPIRIT_BUILD_特效差分(Version, CHARA, ARG)
#DIMS DYNAMIC 特效 = "照れ", "吐息", "汗"
#DIM DYNAMIC CHARA
#DIMS DYNAMIC Version
#LOCALSIZE 1
#LOCALSSIZE 1
FOR LOCAL, 0, VARSIZE("特效")
	IF ARG == 1
		;PRINTFORML 在处理 別立ち_%特效:LOCAL%_{CHARA}
		SIF RM_リソースチェック(@"別立ち_%特效:LOCAL%_{CHARA}")
			CALL SPIRIT_特效切分(Version, CHARA, @"別立ち_%特效:LOCAL%_{CHARA}")
	ELSE
		;PRINTFORML 在处理 別立ち{ARG}_%特效:LOCAL%_{CHARA}
		SIF RM_リソースチェック(@"別立ち{ARG}_%特效:LOCAL%_{CHARA}")
			CALL SPIRIT_特效切分(Version, CHARA, @"別立ち{ARG}_%特效:LOCAL%_{CHARA}")
	ENDIF
NEXT

@SPIRIT_特效切分(Version, CHARA, 全图索引)
#DIMS DYNAMIC Version
#DIM DYNAMIC G_ID
#DIM DYNAMIC CHARA
#DIMS DYNAMIC 全图索引
#DIMS DYNAMIC 颜像索引
#DIM DYNAMIC 颜RECT,4
#LOCALSIZE 1
#LOCALSSIZE 1
IF RM_リソースチェック(@"{CHARA}%Version%_颜像范围")
	颜RECT:0 = SPRITEPOSX(@"{CHARA}%Version%_颜像范围")
	颜RECT:1 = SPRITEPOSY(@"{CHARA}%Version%_颜像范围")
	颜RECT:2 = SPRITEWIDTH(@"{CHARA}%Version%_颜像范围")
	颜RECT:3 = SPRITEHEIGHT(@"{CHARA}%Version%_颜像范围")
ELSE
	颜RECT:0 = -100
	颜RECT:1 = 0
	颜RECT:2 = 300
	颜RECT:3 = 300
ENDIF
颜像索引 = %REPLACE(全图索引, "別立ち", "別顔")%
;PRINTFORML 在为%全图索引%生成颜像索引：%颜像索引%
G_ID = 0
CALL 画像合成(G_ID, "ダミー", 0, 0, 颜RECT:2, 颜RECT:3)
CALL 画像合成(G_ID, 全图索引, 颜RECT:0, 颜RECT:1, 500, 500)
SPRITECREATE 颜像索引, G_ID
RETURN

@SPIRIT_GET_FACETYPE(Version, CHARA, 表示形式, 服装, 差分)
#DIM DYNAMIC CHARA
#DIMS DYNAMIC 表示形式
#DIMS DYNAMIC 服装
#DIMS DYNAMIC 差分
#DIMS DYNAMIC Version
#DIMS DYNAMIC TEMP, 2

IF TALENT:CHARA:心情 == -1 || CFLAG:CHARA:ブチギレ
	RESULTS:0 = 憤怒
	LOCAL = -1
ELSEIF TALENT:CHARA:心情 == 1 || BASE:CHARA:情緒 == MAXBASE:CHARA:情緒 || TCVAR:CHARA:烂醉
	RESULTS:0 = 笑顔
	LOCAL = 1
ELSE
	RESULTS:0 = 通常
	LOCAL = 0
ENDIF
SIF CHECK_CHARA(CHARA, "陥落")
	LOCAL ++
SIF BATHROOM(CFLAG:CHARA:現在位置) && 風呂逐出条件(CHARA)
	LOCAL --
SIF MARK:CHARA:反発刻印
	LOCAL --
SIF BASE:CHARA:憤怒 >= 500
	LOCAL --
SIF BASE:CHARA:情緒 >= 650
	LOCAL ++
;addition custom code: display jealous characters' angry portraits if you're on dates with them and they aren't the current target (thanks to Harris for the idea) 
IF TALENT:CHARA:嫉妬 && FLAG:デート相手 == CHARA && TARGET != CHARA 
	RESULTS:0 = 憤怒 
	LOCAL = -1 
ENDIF 
SELECTCASE RESULTS:0
CASE "笑顔"
;	笑顔時は表情が固まらないように
;	IF LOCAL < 0
		RESULTS:1 = 通常
;	ELSE
;		RESULTS:1 = 笑顔
;	ENDIF
CASE "憤怒"
	IF LOCAL > 0
		RESULTS:1 = 通常
	ELSE
		RESULTS:1 = 憤怒
	ENDIF
CASEELSE
	IF LOCAL > 0
		RESULTS:1 = 笑顔
	ELSEIF LOCAL < 0
		RESULTS:1 = 憤怒
	ELSE
		RESULTS:1 = 通常
	ENDIF
ENDSELECT
[IF DEBUG]
PRINTFORML 初步判定表情「%RESULTS:0%/%RESULTS:1%」
PRINTFORML {NO:CHARA}%COND_STR(@"%Version%_%服装%",Version != "")%_表情_%RESULTS:0% 检测为：{RM_リソースチェック(@"{NO:CHARA}%COND_STR(@"%Version%_%服装%",Version != "")%_表情_%RESULTS:0%")}
PRINTFORML {NO:CHARA}%COND_STR(@"%Version%_%服装%",Version != "")%_表情_%RESULTS:1% 检测为：{RM_リソースチェック(@"{NO:CHARA}%COND_STR(@"%Version%_%服装%",Version != "")%_表情_%RESULTS:1%")}
[ENDIF]
;表情がなければ通常
;SIF !RM_リソースチェック(@"%表示形式%_%服装%_%RESULTS:0%_{CHARA}%差分%") ;debloat custom code
;addition custom code, duplicated characters
SIF !RM_リソースチェック(@"{NO:CHARA}%COND_STR(@"%Version%_%服装%",Version != "")%_表情_%RESULTS:0%")
	RESULTS:0 = 通常
;SIF !RM_リソースチェック(@"%表示形式%_%服装%_%RESULTS:1%_{CHARA}%差分%") ;debloat custom code
;addition custom code, duplicated characters
SIF !RM_リソースチェック(@"{NO:CHARA}%COND_STR(@"%Version%_%服装%",Version != "")%_表情_%RESULTS:1%")
	RESULTS:1 = 通常

;発情用
;IF TCVAR:CHARA:発情 && RM_リソースチェック(@"%表示形式%_%服装%_発情_{CHARA}%差分%") ;debloat custom code
;addition custom code, duplicated characters
IF TCVAR:CHARA:発情 && RM_リソースチェック(@"{NO:CHARA}%COND_STR(@"%Version%_%服装%",Version != "")%_表情_発情")
	RESULTS:0 = 発情
	RESULTS:1 = 発情
ENDIF

;睡眠用
;IF CFLAG:CHARA:睡眠 && RM_リソースチェック(@"%表示形式%_%服装%_睡眠_{CHARA}%差分%") ;debloat custom code
;addition custom code, duplicated characters
IF CFLAG:CHARA:睡眠 && RM_リソースチェック(@"{NO:CHARA}%COND_STR(@"%Version%_%服装%",Version != "")%_表情_睡眠")
	RESULTS:0 = 睡眠
	RESULTS:1 = 睡眠
ENDIF
IF CFLAG:CHARA:うふふ && GETPALAMLV(PALAM:CHARA:欲情, 10) >= 7 && RM_リソースチェック(@"{NO:CHARA}%COND_STR(@"%Version%_%服装%",Version != "")%_表情_トロ顔")
	RESULTS:0 = トロ顔
	RESULTS:1 = トロ顔
	IF NOWEX:CHARA:TotalEX >= 5
		RESULTS:0 = イキ顔
		RESULTS:1 = イキ顔
	ENDIF
ENDIF
;専用の特殊表情変化
CALL IMAGE_CHARA_OVERRIDE("特殊表情", CHARA)
SIF RESULTS:0 == ""
	RESULTS:0 = 通常
SIF RESULTS:1 == ""
	RESULTS:1 = 通常
;以下指令から追加される表情
SIF FLAG:時間停止 || CHARA != TARGET || CFLAG:CHARA:睡眠
	RETURN 0

;現在的表情を一時保存
TEMP:0 = %RESULTS:0%
TEMP:1 = %RESULTS:1%
;挿入中なら性交表情
;SIF IS_SEXING(CHARA) && RM_リソースチェック(@"%表示形式%_%服装%_性交_{CHARA}%差分%") ;debloat custom code
;addition custom code, duplicated characters
SIF IS_SEXING(CHARA) && RM_リソースチェック(@"{NO:CHARA}%COND_STR(@"%Version%_%服装%",Version != "")%_表情_性交")
	RESULTS:0 = 性交

SELECTCASE SELECTCOM
	;愛撫系
	CASE 0 TO 15
		IF SELECTCOM == 9
			RESULTS:0 = 自慰
		ELSE
			RESULTS:0 = 愛撫
		ENDIF
	;性交系
	CASE 60 TO 79, 317 TO 323
		RESULTS:0 = 性交
	;Counterまたは日常系愛撫
	CASE 313 TO 316
		IF CFLAG:CHARA:诶嘿嘿
			RESULTS:0 = 愛撫
		ELSEIF MARK:CHARA:不埒刻印 == 3
			RESULTS:1 = 愛撫
		ENDIF
ENDSELECT
;PRINTFORML 表情チェック　%表示形式%_%服装%_%RESULTS:0%_{CHARA}%差分%
;表情がなければ戻す
;SIF !RM_リソースチェック(@"%表示形式%_%服装%_%RESULTS:0%_{CHARA}%差分%") ;debloat custom code
;addition custom code, duplicated characters
SIF !RM_リソースチェック(@"{NO:CHARA}%COND_STR(@"%Version%_%服装%",Version != "")%_表情_%RESULTS:0%")
	RESULTS:0 = %TEMP:0%
;SIF !RM_リソースチェック(@"%表示形式%_%服装%_%RESULTS:1%_{CHARA}%差分%") ;debloat custom code
;addition custom code, duplicated characters
SIF !RM_リソースチェック(@"{NO:CHARA}%COND_STR(@"%Version%_%服装%",Version != "")%_表情_%RESULTS:1%")
	RESULTS:1 = %TEMP:1%
;発情時2/3でマウスオーバーと表情交代
;SIF RAND:3 && TCVAR:CHARA:発情 && RM_リソースチェック(@"%表示形式%_%服装%_発情_{CHARA}%差分%") ;debloat custom code
;addition custom code, duplicated characters
SIF RAND:3 && TCVAR:CHARA:発情 && RM_リソースチェック(@"{NO:CHARA}%COND_STR(@"%Version%_%服装%",Version != "")%_表情_発情")
	SWAP RESULTS:0, RESULTS:1
;専用の特殊表情変化
CALL IMAGE_CHARA_OVERRIDE("特殊表情COM", CHARA, SELECTCOM)
