@DOKU_GET_HOW_MOOD(ARG)
#FUNCTION
#LOCALSIZE 1
#LOCALSSIZE 1
SIF CFLAG:ARG:322
RETURNF -2
;惹火了
RETURNF TALENT:ARG:15 
;自然心情，-1不高兴，0正常，1好心情

@DOKU_GET_HOW_RELATION(ARG)
#FUNCTION
#LOCALSIZE 1
#LOCALSSIZE 1
#DIM DYNAMIC CHARA_RELATION
CHARA_RELATION = 0
;无陷落无反发
SIF TALENT:ARG:8
CHARA_RELATION = 1
;思慕
SIF MARK:ARG:3
CHARA_RELATION = -1 * MARK:ARG:3
;反发
SIF TALENT:ARG:3
CHARA_RELATION = 2
;恋慕
SIF TALENT:ARG:7
CHARA_RELATION = 3
;恋人
RETURNF CHARA_RELATION

@DOKU_CHECK_KOJO(ARG,ARGS)
;ARGS为 "初期" 则检测是否为初期口上
VARSET RESULTS
RESULTS:1 = 初期
IF CFLAG:ARG:998
TRYCALLFORM M_KOJO_K{ARG}_{CFLAG:ARG:998}
SIF RESULTS:1 == "初期"
RETURN 0
ELSE
TRYCCALLFORM M_KOJO_K{ARG}
CATCH
RETURN 0
ENDCATCH
ENDIF
LOCALS '= RESULTS:1
VARSET RESULTS
RETURN LOCALS == ARGS

@DOKU_GET_KOJO_NAME(ARG)
VARSET RESULTS
IF CFLAG:ARG:998
TRYCCALLFORM M_KOJO_K{ARG}_{CFLAG:ARG:998}
CATCH
VARSET RESULTS
RETURN 0
ENDCATCH
ELSE
TRYCCALLFORM M_KOJO_K{ARG}
CATCH
VARSET RESULTS
RETURN 0
ENDCATCH
ENDIF
LOCALS '= RESULTS
VARSET RESULTS
RESULTS '= LOCALS
RETURN 0

@DOKU_CHECK_HAVE_LOVER()
#FUNCTION
#LOCALSIZE 1
#LOCALSSIZE 1
;防备魔改，不能偷懒
FOR LOCAL, 1, CHARANUM
SIF TALENT:LOCAL:恋人
RETURNF 1
NEXT
RETURNF 0

@DOKU_GET_DATE_CHARA(ARG=0)
;ARG==0时带出去也算约会
;ARG==1时只检测约会
;ARG==2时看关系
;返回值是约会人数
;多人约会（flag:73非空/带出去也算约会）时由于target的顺序一定是同行者在前，所以人数相当于最后一个同行者在TARGET数组中的编号
;ARG为2时由于个数和编号未必对应，所以返回值只代表个数
#FUNCTION
#LOCALSIZE 3
#LOCALSSIZE 1
SIF ARG==1&&FLAG:73<=0
RETURNF 0
SIF CFLAG:0:320 !=60
RETURNF 0
LOCAL:1 = 0
LOCAL:2 = FLAG:73>0||!ARG
FOR LOCAL,0,GET_TARGETNUM()
IF CFLAG:(TARGET:(LOCAL+1)):320>0
SIF LOCAL:2||(ARG==2&&DOKU_GET_HOW_RELATION(TARGET:(LOCAL+1))>0)
LOCAL:1 ++
ENDIF
NEXT
RETURNF LOCAL:1

@DOKU_GET_PLACE(ARG)
#FUNCTION
#LOCALSIZE 1
#LOCALSSIZE 1
RETURNF CFLAG:ARG:300

@DOKU_GET_HERO_PLACE()
#FUNCTION
#LOCALSIZE 1
#LOCALSSIZE 1
RETURNF CFLAG:0:300

@DOKU_GET_HERO_NAME()
;这个是为了可替换
#FUNCTIONS
#LOCALSIZE 1
#LOCALSSIZE 1
RETURNF CALLNAME:MASTER

@DOKU_GET_LIVE_PLACE(ARG=0)
;完全的为了可读性以及自己坚持的规范
#FUNCTION
#LOCALSIZE 1
#LOCALSSIZE 1
RETURNF CFLAG:ARG:311

@DOKU_GET_HERO_LIVE_PLACE()
#FUNCTION
#LOCALSIZE 1
#LOCALSSIZE 1
RETURNF CFLAG:0:311

@DOKU_GET_LIVE_MAP(ARG=0)
#FUNCTION
#LOCALSIZE 1
#LOCALSSIZE 1
RETURNF CFLAG:ARG:311/100

@DOKU_CHECK_WORKING(ARG)
#FUNCTION
#LOCALSIZE 1
#LOCALSSIZE 1
RETURNF CFLAG:ARG:325 == 5

@DOKU_GET_TRIBENAME(ARG,ARG:1,ARG:2)
;利用原版方法、更方便地获取种族名（叫tribename是因为原版也叫这个）
;ARG:1-是否从CSV获取非玩家角色的种族（不好用在检测，只能用在PRINT）
;ARG:2-0不去括号，1去括号并保留括号外的，2只保留括号内，如果没有则保留括号外的
#FUNCTIONS
#LOCALSIZE 10
#LOCALSSIZE 10
#DIM DYNAMIC VAR_SEARCH_USED
#DIM DYNAMIC MULTI_TRIB_CHECK = 0
#DIMS DYNAMIC STR_LIST, 10
#DIMS DYNAMIC TEMP_STRING
IF ARG >0 && ARG:1
LOCALS = %CSVCSTR(ARG,10)%
IF STRFIND(LOCALS, "●") > -1
SPLIT LOCALS, "●", STR_LIST
FOR LOCAL, 0, RESULT
IF STRFIND(STR_LIST:LOCAL, "種族：") > -1
LOCALS:1 = %REPLACE(STR_LIST:LOCAL, "種族：", "")%
LOCALS:1 = %REPLACE(LOCALS:1, "　", "")%
IF LOCALS:1 != ""
TEMP_STRING = %LOCALS:1%
VARSET LOCALS
IF !ARG:2
RETURNF TEMP_STRING
ELSEIF ARG:2 == 1
SPLIT TEMP_STRING,"（",LOCALS
TEMP_STRING = %LOCALS:0%
VARSET LOCALS
RETURNF TEMP_STRING
ELSE
SPLIT TEMP_STRING,"（",LOCALS
TEMP_STRING = %LOCALS:1%
IF TEMP_STRING == ""
TEMP_STRING = %LOCALS:0%
ELSE
VARSET LOCALS
SPLIT TEMP_STRING,"）",LOCALS
TEMP_STRING = %LOCALS:0%
ENDIF
VARSET LOCALS
RETURNF TEMP_STRING
ENDIF
ENDIF
ENDIF
NEXT
ENDIF
ENDIF
FOR VAR_SEARCH_USED,190,198
SIF TALENT:ARG:VAR_SEARCH_USED
MULTI_TRIB_CHECK++
NEXT
IF MULTI_TRIB_CHECK <=0
SIF !ARG:2
RETURNF "名称不明（不明）"
RETURNF "名称不明"
ENDIF
IF MULTI_TRIB_CHECK >1
SIF !ARG:2
RETURNF "名称不明（复合）"
RETURNF "名称不明"
ENDIF
IF !ARG:2
RETURNF GET_TRIBENAME(ARG,MIN(VAR_SEARCH_USED+189,196))
ELSEIF ARG:2 == 1
TEMP_STRING = %GET_TRIBENAME(ARG,MIN(VAR_SEARCH_USED+189,196))%
SPLIT TEMP_STRING,"（",LOCALS
TEMP_STRING = %LOCALS:0%
VARSET LOCALS
RETURNF TEMP_STRING
ELSE
TEMP_STRING = %GET_TRIBENAME(ARG,MIN(VAR_SEARCH_USED+189,196))%
SPLIT TEMP_STRING,"（",LOCALS
TEMP_STRING = %LOCALS:1%
IF TEMP_STRING == ""
TEMP_STRING = %LOCALS:0%
ELSE
VARSET LOCALS
SPLIT TEMP_STRING,"）",LOCALS
TEMP_STRING = %LOCALS:0%
ENDIF
VARSET LOCALS
RETURNF TEMP_STRING
ENDIF

@DOKU_GET_RACENUM(ARG=0)
;真正检测的时候还是用它吧，ARG是目标
#FUNCTION
#LOCALSIZE 10
#LOCALSSIZE 10
LOCAL:1 = 0
LOCAL:2 = 0
FOR LOCAL:0,190,198
SIF TALENT:ARG:(LOCAL:0) <= 0
CONTINUE
LOCAL:1 ++
LOCAL:2 = LOCAL:0
NEXT
SIF LOCAL:1>1
RETURNF -1
SIF LOCAL:1 <=0
RETURNF -2
RETURNF 100*LOCAL:2 + TALENT:ARG:(LOCAL:2)

@DOKU_GET_RACENAME(ARG=0)
;与GET_TRIBENAME不同，是用来检测种族的查表函数，ARG同上
#FUNCTIONS
#LOCALSIZE 1
#LOCALSSIZE 1
LOCALS = 
SELECTCASE DOKU_GET_RACENUM(ARG)
CASE -1
LOCALS = 奇美拉
CASE -2
LOCALS = 
CASE 19001
LOCALS = 人类
CASE 19002
LOCALS = 仙人
CASE 19003
LOCALS = 天人
CASE 19004
LOCALS = 月人
CASE 19005
LOCALS = 魔界人
CASE 19006
LOCALS = 外界人
CASE 19101
LOCALS = 妖怪
CASE 19102
LOCALS = 鬼
CASE 19103
LOCALS = 吸血鬼
CASE 19104
LOCALS = 河童
CASE 19105
LOCALS = 天狗
CASE 19106
LOCALS = 妖兽
CASE 19107
LOCALS = 妖鸟
CASE 19108
LOCALS = 妖虫
CASE 19109
LOCALS = 恶魔
CASE 19110
LOCALS = 小人
CASE 19111
LOCALS = 座敷童子
CASE 19112
LOCALS = 山童
CASE 19201
LOCALS = 妖精
CASE 19301
LOCALS = 神灵
CASE 19302
LOCALS = 死神
CASE 19303
LOCALS = 阎魔
CASE 19401
LOCALS = 幽灵
CASe 19402
LOCALS = 骚灵
CASE 19403
LOCALS = 亡灵
CASE 19404
LOCALS = 恶灵
CASE 19501
LOCALS = 付丧神
CASE 19601
LOCALS = 人形
CASE 19602
LOCALS = 机械
CASE 19603
LOCALS = 魔像
CASE 19604
LOCALS = 埴轮
CASE 19605
LOCALS = 僵尸
CASE 19606
LOCALS = 人形乙女
CASE 19701
LOCALS = 巫女
CASE 19702
LOCALS = 魔法使
CASE 19703
LOCALS = 女仆
CASE 19704
LOCALS = 蓬莱人
CASEELSE
LOCALS = 未知种族
ENDSELECT
RETURNF LOCALS

@DOKU_CHECK_PLACE_MATCH(ARG,ARG:1)
;ARG 角色编号
;ARG:1 地点
#FUNCTION
#LOCALSIZE 1
#LOCALSSIZE 1
RETURNF CFLAG:ARG:300 == ARG:1

@DOKU_CHECK_HERO_PLACE_MATCH(ARG)
;ARG地点
#FUNCTION
#LOCALSIZE 1
#LOCALSSIZE 1
RETURNF CFLAG:0:300 == ARG

@DOKU_CHECK_PLACE_MATCH_PLUS(ARG,ARGS)
;ARG 角色编号
;ARGS 形如"1/2/3~4/5/7~8"格式的，用"/"分割"数字"或"数字~数字"的字符串
#FUNCTION
#LOCALSIZE 1
#LOCALSSIZE 1
RETURNF DOKU_NUM_MATCH_SLASH_SPLIT_F(CFLAG:ARG:300,ARGS)

@DOKU_CHECK_HERO_PLACE_MATCH_PLUS(ARGS)
#FUNCTION
#LOCALSIZE 1
#LOCALSSIZE 1
RETURNF DOKU_CHECK_PLACE_MATCH_PLUS(0,ARGS)

@DOKU_GET_GENDER(ARG=0,ARG:1=0)
#FUNCTION
#LOCALSIZE 1
#LOCALSSIZE 1
;默认futa为女，ARG:1非零时futa为男
;返回值0女1男
RETURNF (GETBIT(TALENT:ARG:2,1) - GETBIT(TALENT:ARG:2,0)*(!ARG:1))>0

@DOKU_GET_GENDER_STR(ARG=0,ARG:1=0)
#FUNCTIONS
#LOCALSIZE 1
#LOCALSSIZE 1
LOCALS = 小姐
SIF DOKU_GET_GENDER(ARG,ARG:1)
LOCALS = 先生
RETURNF LOCALS

@DOKU_CHECK_HANGING(ARG=0)
#FUNCTION
LOCAL:1 = -4
FOR LOCAL,1,CHARANUM
SIF TARGET:LOCAL <= 0
BREAK
SIF TARGET:LOCAL == ARG
CONTINUE
IF CFLAG:(TARGET:LOCAL):320
LOCAL:2 = DOKU_GET_HOW_RELATION(TARGET:LOCAL)
SIF LOCAL:1 < LOCAL:2
LOCAL:1 = LOCAL:2
ENDIF
NEXT
;-3~3->1~7
RETURNF 4+LOCAL:1

@DOKU_CHECK_IS_ROAD(ARG)
#FUNCTION
;约会道中（这样读起来更舒服，不容易把TO和ARG连起来理解）
RETURNF ARG%100 == 97

@DOKU_CHECK_IS_ROAD_TO(ARG)
#FUNCTION
;约会道中（原版写法）
RETURNF ARG%100 == 97

@DOKU_CHECK_IS_INVITATION_ROOM(ARG)
#FUNCTION
;招待室（xx的房间）
RETURNF ARG%100 == 98

@DOKU_CHECK_IS_OMANEKIBEYA(ARG)
#FUNCTION
;招待室（xx的房间），日文（原版）写法
RETURNF ARG%100 == 98

@DOKU_CHECK_IS_SUKIMA(ARG)
#FUNCTION
;隙间（不参与计算的技术性地点）检定
RETURNF ARG%100 == 99

@DOKU_GET_MAINMAP_ENTRANCE()
#FUNCTION
RETURNF GET_ENTRANCE(MAIN_MAP)

@DOKU_GET_CHARA_CURRMAP_ENTRANCE(ARG=0)
#FUNCTION
RETURNF GET_ENTRANCE(CFLAG:ARG:300)

@DOKU_GET_MAINMAP_ENTERANCE()
#FUNCTION
RETURNF GET_ENTRANCE(MAIN_MAP)

@DOKU_GET_CHARA_CURRMAP_ENTERANCE(ARG=0)
#FUNCTION
RETURNF GET_ENTRANCE(CFLAG:ARG:300)

@DOKU_CHECK_HAVE_PENIS(ARG)
#FUNCTION
RETURNF GETBIT(TALENT:ARG:2,1)

@DOKU_CHECK_HAS_PENIS(ARG)
#FUNCTION
RETURNF GETBIT(TALENT:ARG:2,1)

@DOKU_GET_ORGASM(ARG)
#FUNCTION
VARSET LOCAL

FOR LOCAL,0,5
SIF NOWEX:ARG:LOCAL
SETBIT LOCAL:1,LOCAL
NEXT

SIF NOWEX:ARG:11
SETBIT LOCAL:1,6

RETURNF LOCAL:1

@DOKU_GET_ORGASM_S(ARG)
#FUNCTIONS
LOCALS '= ""

SIF NOWEX:ARG:0
LOCALS += "C/"

SIF NOWEX:ARG:1
LOCALS += "V/"

SIF NOWEX:ARG:2
LOCALS += "A/"

SIF NOWEX:ARG:3
LOCALS += "B/"

SIF NOWEX:ARG:4
LOCALS += "M/"

SIF NOWEX:ARG:11
LOCALS += "CUM/"

RETURNF LOCALS

@DOKU_CHECK_ORGASM(ARG=0,ARGS)
#FUNCTION
#DIMS TEMP_STR,10
VARSET LOCALS
VARSET LOCAL
VARSET TEMP_STR
SPLIT DOKU_GET_ORGASM_S(ARG),"/",LOCALS
LOCAL = RESULT - 1
SIF LOCAL <= 0
RETURNF 0

SPLIT ARGS,"/",TEMP_STR
LOCAL:1 = MAX(0,RESULT)
SIF LOCAL:1 == 0 || TEMP_STR:(LOCAL:1) != ""
ARGS += "/"
VARSET TEMP_STR
SPLIT ARGS,"/",TEMP_STR
SIF LOCAL:1 > LOCAL
RETURNF 0
SIF TEMP_STR:(LOCAL:1) == ""
LOCAL:1 --

FOR LOCAL:2,0,LOCAL:1
	FOR LOCAL:3,0,LOCAL
	SIF LOCALS:(LOCAL:3) == TEMP_STR:(LOCAL:2)
	BREAK
	NEXT
	SIF LOCAL:3 == LOCAL
	RETURNF 0
NEXT
RETURNF 1

@DOKU_CHECK_CHARA_PLAYERNOWMAP(ARG)
#FUNCTION
;检测角色是否和玩家处在同一地图
IF CFLAG:ARG:300 == SUKIMA()
SIF CFLAG:ARG:360 ;出禁
RETURNF -1
SIF CFLAG:ARG:358 / 100 == CFLAG:0:300 / 100 ;自宅在当前地图（且没出门）。对，就是可以写得这么粗暴
RETURNF 2
RETURNF 0 ;其余肯定不在同一张地图
ELSEIF CFLAG:ARG:300 % 100 == 97
;道中，不算
RETURNF -2
ELSE
RETURNF CFLAG:ARG:300 / 100 == CFLAG:0:300 / 100 ;除了特殊情况外，可以直接看CFLAG:300
ENDIF
RETURNF 0

