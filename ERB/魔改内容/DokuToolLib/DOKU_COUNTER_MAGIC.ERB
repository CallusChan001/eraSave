;客制化的COUNTER（角色对玩家的行动）功能。依赖DCCS（指令管理系统）的流程来做接口
;@DOKU_COUNTER_MAGIC_MESS_AFTER_DCCS应当在@USE_DCCS_P2之后调用。也是在@M_KOJO_XXX_MESSAGE_COM_KX_666666中调用
;@DOKU_COUNTER_MAGIC_FLAGSET应当在FLAGSETTING里调用
;@DOKU_COUNTER_MAGIC_IN_REAL_COUNTER在自己的@M_KOJO_XXX_MESSAGE_COUNTER_KX_666666里调用
;XXX指口上名，KX是K角色编号
;三个函数应当全部在按上述注释在合适的位置被调用
;

@DOKU_COUNTER_MAGIC_FLAGSET(ARG)
TRYCCALLFORM DCM_EXIST_SUMMON_COUNTER%RESULTS%_K{ARG} ;检测能不能生成，不能的话走原版得了
SIF !RESULT
RETURN 0
CATCH
RETURN 0
ENDCATCH

TCVAR:ARG:30 = 66 ;原版逆推生成block
MASTER_COUNTER_RECORD:ARG:0 = 0
MASTER_COUNTER_RECORD:ARG:1 = 0

TCVAR:ARG:20 = 666666 ;==0的时候防止SOURCE前段生成（==1的时候没有这个所以不用防），==2的时候直接传球，到REAL里生成

;这个东西会禁用在指令MESSAGE里调用TRAIN_MESSAGE的行为。
;解决方法是启用DCCS的同时，在M_KOJO_XXX_MESSAGE_COM_KX_666666中调用DOKU_COUNTER_MAGIC_WITH_TRAIN_MESS、紧接着调用TRAIN_MESSAGE
RETURN 0

@DOKU_COUNTER_MAGIC_IN_EVENT_10(ARG)
TRYCCALLFORM DCM_EXIST_SUMMON_COUNTER%RESULTS%_K{ARG} ;检测能不能生成，不能的话走原版得了
SIF !RESULT
RETURN 0
CATCH
RETURN 0
ENDCATCH
TCVAR:ARG:20 = 666666 ;准备接入自己的COUNTER

TCVAR:ARG:30 = 66 ;原版逆推生成block
MASTER_COUNTER_RECORD:ARG:0 = 0
MASTER_COUNTER_RECORD:ARG:1 = 0

RETURN 0


@DOKU_COUNTER_MAGIC_MESS_AFTER_DCCS(ARG)
SIF CFLAG:ARG:317 == 1
RETURN 0
;正常推倒不应当有COUNTER。去指令的MESSAGE里做。
;尤其是DCCS下，有些MESS的时点都是SUCCESS里出，那种情况下根本不应该出COUNTER

TRYCCALLFORM DCM_EXIST_SUMMON_COUNTER%RESULTS%_K{ARG} ;检测能不能生成，不能的话走原版得了
SIF !RESULT
RETURN 0
CATCH
RETURN 0
ENDCATCH

CALL DOKU_COUNTER_MAGIC_READY(ARG)

TCVAR:ARG:20 = 666666

CFLAG:ARG:325 = 5;暂时行动设置为0，防止原版生成。时点很紧凑所以不会有影响

RETURN 0


@DOKU_COUNTER_MAGIC_IN_REAL_COUNTER(ARG)
;都到这了，指定是能生成的，不走原版
CALL DOKU_COUNTER_MAGIC_RECOVER(ARG)

CALL DOKU_GET_KOJO_NAME(ARG)

TRYCCALLFORM DCM_SUMMON_COUNTER%RESULTS%_K{ARG} ;试图生成
LOCAL = RESULT
TRYCALLFORM DCM_COUNTER_MESS%RESULTS%_K{ARG}_{LOCAL}
TRYCALLFORM DCM_COUNTER_CALC%RESULTS%_K{ARG}_{LOCAL}
CATCH
TCVAR:20 = 0
RETURN -1
ENDCATCH

SIF TCVAR:20 == 666666 ;正常应当在CALC里设置正确的TCVAR。317==2的情况下TCVAR:20>=150000会报错。!=2的时候其实没关系，但是少写个条件
TCVAR:20 = 0

VARSET RESULTS
RETURN RESULT










@DOKU_COUNTER_MAGIC_WITH_TRAIN_MESS(ARG)
;DOKU_COUNTER_MAGIC_MESS_AFTER_DCCS的变种。不同的地方在于哪怕是正常推倒状态下也会准备生成。
;并不会接入自己的流程所以没有TCVAR:20=666666
;不需要阻止原版生成所以没有CFLAG:325=5
CALL DOKU_COUNTER_MAGIC_READY(ARG)

;这里本该CALL TRAIN_MESSAGE
;但我认为TRAIN_MESSAGE这个东西的实现过程是非常sb的，所以我不会在这里调用它
;想要调用就自己去外面的M_KOJO_XXX_MESSAGE_COM_KX_666666中调用
RETURN 0

@DOKU_COUNTER_MAGIC_READY(ARG)
TCVAR:ARG:30 = 0

MASTER_COUNTER_RECORD:ARG:0 = 0
MASTER_COUNTER_RECORD:ARG:1 = 0
FLAG:7 = 1 ;FLAG:口上文本設定
CALLF DOKUDIM_COUNTER_MAGIC_STORE(ARG,1,1+2*(TEQUIP:ARG:20&&1)+4*(CFLAG:ARG:373&&1)+8*(CFLAG:ARG:325==5))
TEQUIP:ARG:20 = 0;口球
CFLAG:ARG:373 = 1;扮演口上有

RETURN 0

@DOKU_COUNTER_MAGIC_RECOVER(ARG)
LOCAL = DOKUDIM_COUNTER_MAGIC_STORE(ARG,1,1+2*(TEQUIP:ARG:20&&1)+4*(CFLAG:ARG:373&&1))
IF !GETBIT(LOCAL,0)
CALLF DOKUDIM_COUNTER_MAGIC_STORE(ARG,1,0)
RETURN 0
ENDIF
TEQUIP:ARG:20 = GETBIT(LOCAL,1)
CFLAG:ARG:373 = GETBIT(LOCAL,2)
CFLAG:ARG:325 = 5*GETBIT(LOCAL,3)

CALLF DOKUDIM_COUNTER_MAGIC_STORE(ARG,1,0)
RETURN 0

