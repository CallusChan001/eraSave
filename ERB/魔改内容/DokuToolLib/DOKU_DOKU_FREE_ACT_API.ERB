;提供的API，不注明参数个数则是无最小参数个数
;@M_KOJO_XXX_FAEXIST_KX 存在判定 返回值随意
;@DOKU_FA_XXX_KX_STOP_EXIST 高级STOP使用判定 返回值随意
;@DOKU_FA_XXX_KX_SHOULD_STOP 高级STOP的真实STOP条件，返回1是应当中断

;@M_KOJO_XXX_FA_TIME_KX 指令耗时 返回值为指令耗时，DOK U库会输入两个参数（TYPE、DURATION）
;@M_KOJO_XXX_FA_CHAIN_KX CHAIN 返回值为指令CHAIN，两个参数（同上）
;@M_KOJO_XXX_FA_NAME_KX 指令显示名称 返回值为指令显示名称，两个参数（同上）

;@DOKU_FA_XXX_KX_HAPPEN_CALC 发生时计算
;@DOKU_FA_XXX_KX_HAPPEN_MESS 发生文本（对应CTRL 4->1）
;@DOKU_FA_XXX_KX_HAPPEN_CALC 切替发生时计算
;@DOKU_FA_XXX_KX_CHAPPEN_MESS 切替发生文本（对应CTRL 3->1）
;↑此时切替前的是TYPE、切替后的是MESS
;@DOKU_FA_XXX_KX_PROGRESS_CALC 进行中计算
;@DOKU_FA_XXX_KX_PROGRESS_MESS 进行时文本（晚于计算）
;@DOKU_FA_XXX_KX_END_CALC 结束时计算
;@DOKU_FA_XXX_KX_END_MESS 结束文本（被结束的行动种类是TYPE）
;@DOKU_FA_XXX_KX_STOP 中断（具体条件见case "中断"）
;@DOKU_FA_XXX_KX_INTERACT 交互
;@DOKU_FA_XXX_KX_SOURCE SOURCE计算
;@DOKU_FA_XXX_COMF_ABLE_KX 陪同行动指令是否显示（返回1为显示）
;【重要事项】：这些文本都是【只要执行自由行动、就会触发】的，而通常来讲“角色和玩家不在同一地点的话，不应该出文本”，这个判断需要自己写

;@DOKU_FA_TRY_CREATE_XXX_KX 尝试生成
;注意：需要返回非0的同时TYPE和DURATION都合法，才会真正生成自由行动
;可以靠RESULT为0来在有TYPE和DURATION的情况下阻止生成
;@DOKU_FA_TRY_CHANGE_XXX_KX 尝试切替
;注意：有NEXT、有DURATION_NEXT的情况下，才会成功切替
;（有TYPE且）无DURATION的情况下自动切替
;否则RESULT非0的情况下进行（强行）切替
;@DOKU_FA_TRY_DEL_XXX_KX 尝试删除，RESULT为0则不进行删除
;注意：有TYPE、未切替、无DURATION的情况下，自动删除（结束）

;@DOKU_FA_IN_DIARY_EXIST(ARG) 在@DIARY_XXX_KX_EXIST 里调用【重点】
;@DOKU_FA_IN_FLAGSETTING(ARG) 在@M_KOJO_XXX_FLAGSETTING_KX 里调用【重点】

;提供的变量（其实是式中函数），格式都是DOKUDIM_FA_YYY(ARG,ARG:1,ARG:2)
;ARG为角色编号，必填
;ARG:1为控制用变量，不填或为0则为获取当前值，否则将ARG:2写入变量的值
;下为一览（只写YYY）：
;CTRL 控制变量
;0 不在行动
;1 在行动
;2 即将结束行动
;3 即将结束行动，且有之后的预定行动（只预定种类）
;4 即将开始行动
;TYPE 行动种类，0为没有行动，其他情况下必须为正数
;DURATION 行动剩余时间
;NEXT 切替用，下一个行动的种类，值的要求同TYPE
;DURATION_NEXT 切替用，下一个行动的时间
;OVER_DURATION 如果TIME_PASSING比DURATION大，那么一些情况下就需要用到TIME_PASSING超出DURATION的部分，所以做了这么个东西

;TIME 陪同行动指令耗时（M_KOJO_XXX_FA_TIME_K60实际是在DOKU_FA_IN_FLAGSETTING里的）
;CHAIN 陪同行动CHAIN，同上
;NAME 第三个参数应当是字符串(ARGS)而不是数值(ARG:2)，其余同上
;读取例： LOCAL = DOKUDIM_FA_CTRL(60)
;写入例： CALLF DOKUDIM_FA_DURATION(60,1,666)


@EXIST_FreeAct66
#LOCALSIZE 1
#LOCALSSIZE 1

@FreeAct66(ARG, O_DATA, V_NAME)
#FUNCTION
#LOCALSIZE 1
#LOCALSSIZE 1
#DIMS O_DATA
#DIMS V_NAME
;LOCALS-指令名
;LOCAL-指令耗时
SELECTCASE O_DATA
CASE "名称"
IF DOKUDIM_REAL_TARGET() == 0
CALLF MAKE_STR(V_NAME, DOKUDIM_FA_NAME(TARGET) )
ELSE
CALLF MAKE_STR(V_NAME, DOKUDIM_FA_NAME(DOKUDIM_REAL_TARGET()) )
ENDIF
CASE "CHAIN"
CALLF MAKE_INT(V_NAME, DOKUDIM_FA_CHAIN(DOKUDIM_REAL_TARGET()) )
CASE "経過時間"
CALLF MAKE_INT(V_NAME, DOKUDIM_FA_TIME(DOKUDIM_REAL_TARGET()) )
ENDSELECT
RETURNF 0

@自由行動内容_66(ARG, ARGS, 時間係数, CHAIN)
#DIM CHARA
#DIM CHAIN
#DIM 時間係数
AWAIT 1
VARSET RESULTS
VARSET RESULT
VARSET LOCALS
IF CFLAG:ARG:998
TRYCALLFORM M_KOJO_K{ARG}_{CFLAG:ARG:998}
LOCALS '= RESULTS
ELSE
TRYCALLFORM M_KOJO_K{TARGET}
LOCALS '= RESULTS
ENDIF

VARSET RESULTS
VARSET RESULT

SELECTCASE ARGS
CASE "行動中"
SELECTCASE DOKUDIM_FA_CTRL(ARG)
CASE 4
TRYCALLFORM DOKU_FA%LOCALS%_K{ARG}_HAPPEN_CALC
TRYCALLFORM DOKU_FA%LOCALS%_K{ARG}_HAPPEN_MESS
CALLF DOKUDIM_FA_CTRL(ARG,1,1)
;CTRL4->1
CASE 3
TRYCALLFORM DOKU_FA%LOCALS%_K{ARG}_CHAPPEN_CALC
TRYCALLFORM DOKU_FA%LOCALS%_K{ARG}_CHAPPEN_MESS
CALLF DOKUDIM_FA_TYPE(ARG,1,DOKUDIM_FA_NEXT(ARG))
CALLF DOKUDIM_FA_DURATION(ARG,1,DOKUDIM_FA_DURATION_NEXT(ARG))
CALLF DOKUDIM_FA_NEXT(ARG,1,0)
CALLF DOKUDIM_FA_DURATION_NEXT(ARG,1,0)
CALLF DOKUDIM_FA_CTRL(ARG,1,1)
;CTRL3->1
CASE 1
TRYCALLFORM DOKU_FA%LOCALS%_K{ARG}_PROGRESS_CALC
TRYCALLFORM DOKU_FA%LOCALS%_K{ARG}_PROGRESS_MESS
ENDSELECT
CASE "完了"
TRYCALLFORM DOKU_FA%LOCALS%_K{ARG}_END_CALC
TRYCALLFORM DOKU_FA%LOCALS%_K{ARG}_END_MESS
CALLF DOKUDIM_FA_CTRL(ARG,1,0)
CALLF DOKUDIM_FA_TYPE(ARG,1,0)
CALLF DOKUDIM_FA_DURATION(ARG,1,0)
CALLF DOKUDIM_FA_NEXT(ARG,1,0)
CALLF DOKUDIM_FA_DURATION_NEXT(ARG,1,0)
;CTRL2->0
CASE "中断"
TRYCCALLFORM DOKU_FA%LOCALS%_K{ARG}_STOP_EXIST
;先这么凑合着，自己手写条件
TRYCCALLFORM DOKU_FA_%LOCALS%_K{ARG}_SHOULD_STOP
IF RESULT
IF DOKUDIM_FA_CTRL(ARG)
TRYCALLFORM DOKU_FA%LOCALS%_K{ARG}_STOP
CALLF DOKUDIM_FA_CTRL(ARG,1,0)
CALLF DOKUDIM_FA_TYPE(ARG,1,0)
CALLF DOKUDIM_FA_DURATION(ARG,1,0)
CALLF DOKUDIM_FA_NEXT(ARG,1,0)
CALLF DOKUDIM_FA_DURATION_NEXT(ARG,1,0)
ENDIF
ELSE
SELECTCASE DOKUDIM_FA_CTRL(ARG)
CASE 1,3,4
TRYCALLFORM 自由行動内容_66(ARG, "行動中")
CASE 2
TRYCALLFORM 自由行動内容_66(ARG, "完了")
CASEELSE
ENDSELECT
ENDIF
CATCH
SELECTCASE DOKUDIM_FA_CTRL(ARG)
CASE 1,3,4
TRYCALLFORM 自由行動内容_66(ARG, "行動中")
CASE 2
TRYCALLFORM 自由行動内容_66(ARG, "完了")
CASEELSE
ENDSELECT
ENDCATCH
CATCH
SELECTCASE DOKUDIM_FA_CTRL(ARG)
CASE 1,3,4,2
;这么写的意思是，ctrl为2的情况是“正好结束了”，和其他情况还不同
TRYCALLFORM DOKU_FA%LOCALS%_K{ARG}_STOP
CALLF DOKUDIM_FA_CTRL(ARG,1,0)
CALLF DOKUDIM_FA_TYPE(ARG,1,0)
CALLF DOKUDIM_FA_DURATION(ARG,1,0)
CALLF DOKUDIM_FA_NEXT(ARG,1,0)
CALLF DOKUDIM_FA_DURATION_NEXT(ARG,1,0)
CASE 0
;这里不该有文本
ENDSELECT
RETURN 0
ENDCATCH
CASE "付き合う"
TRYCALLFORM DOKU_FA%LOCALS%_K{ARG}_INTERACT
CASE "ソース"
TRYCALLFORM DOKU_FA%LOCALS%_K{ARG}_SOURCE
ENDSELECT
RETURN RESULT


@EVENTTRAIN
SIF DOKUDIM_HAS_EVENTTRAIN()
RETURN 0

CALLF DOKUDIM_HAS_EVENTTRAIN(1,1)

CALLF DOKUDIM_FA_CHAIN(0,2)
CALLF DOKUDIM_FA_CTRL(0,2)
CALLF DOKUDIM_FA_DURATION(0,2)
CALLF DOKUDIM_FA_DURATION_NEXT(0,2)
CALLF DOKUDIM_FA_NEXT(0,2)
CALLF DOKUDIM_FA_TIME(0,2)
CALLF DOKUDIM_FA_TYPE(0,2)
RETURN 0

@DOKUDIM_HAS_EVENTTRAIN(ARG=0,ARG:1=0)
#FUNCTION
#LOCALSIZE 1
#LOCALSSIZE 1
IF ARG
LOCAL = ARG:1
ENDIF
RETURNF LOCAL

;↑API↓TOOL————————————————————————————————————————————————————————

@DOKU_FA_IN_DIARY_EXIST(ARG)
CALL DOKU_FA_IN_DIARY_EXIST_P1(ARG)
;对原版自由行动属性进行设置
CALLF DOKUDIM_FA_OVER_DURATION(ARG,1,0)
;清空overduration，因为这东西只是生成/切替/删除 的时候有应用场景
CALL DOKU_FA_IN_DIARY_EXIST_P2(ARG)
;维护自制的自由行动
RETURN 0

@DOKU_FA_IN_DIARY_EXIST_P2(ARG)
Activity_Type:ARG = 66
;对对对锁66就行，原版FA的主要判定是依赖Duration，Type可以不管
SELECTCASE DOKUDIM_FA_CTRL(ARG)
CASE 2
Activity_Duration:ARG = 1
CASEELSE
;非完了的情况下，把Duration拉满，来防止原版FA的发生
Activity_Duration:ARG = 999999
ENDSELECT
RETURN 0

@DOKU_FA_IN_DIARY_EXIST_P1(ARG)
VARSET LOCALS
VARSET RESULTS
IF CFLAG:ARG:998
TRYCALLFORM M_KOJO_K{ARG}_{CFLAG:ARG:998}
LOCALS '= RESULTS
ELSE
TRYCALLFORM M_KOJO_K{ARG}
LOCALS '= RESULTS
ENDIF

TRYCCALLFORM DOKU_FA%LOCALS%_K{ARG}_STOP_EXIST
CFLAG:ARG:延迟 = 0
CATCH
ENDCATCH

CALLF DOKUDIM_FA_OVER_DURATION(ARG,1,0)

SELECTCASE DOKUDIM_FA_CTRL(ARG)
CASE 0
CALLF DOKUDIM_FA_DURATION(ARG,1,0)
TRYCALLFORM DOKU_FA_TRY_CREATE%LOCALS%_K{ARG}
IF RESULT
IF DOKUDIM_FA_TYPE(ARG) && DOKUDIM_FA_DURATION(ARG)>0
CALLF DOKUDIM_FA_CTRL(ARG,1,4)
RETURN 1
ELSEIF DOKUDIM_FA_NEXT(ARG) && DOKUDIM_FA_DURATION_NEXT(ARG)
CALLF DOKUDIM_FA_TIME(ARG,1,DOKUDIM_FA_NEXT(ARG))
CALLF DOKUDIM_FA_DURATION(ARG,1,DOKUDIM_FA_DURATION_NEXT(ARG))
CALLF DOKUDIM_FA_NEXT(ARG,1,0)
CALLF DOKUDIM_FA_DURATION_NEXT(ARG,1,0)
RETURN 1
ENDIF
ENDIF
CASE 1
LOCAL = DOKUDIM_FA_DURATION(ARG) - DOKU_TIMEPASSING()
SIF LOCAL < 0
CALLF DOKUDIM_FA_OVER_DURATION(ARG,1,LOCAL*-1)
LOCAL = MAX(0,LOCAL)
CALLF DOKUDIM_FA_DURATION(ARG,1,LOCAL)
TRYCALLFORM DOKU_FA_TRY_CHANGE%LOCALS%_K{ARG}
IF (RESULT || (DOKUDIM_FA_DURATION(ARG)<=0) && (DOKUDIM_FA_NEXT(ARG) && DOKUDIM_FA_DURATION_NEXT(ARG) > 0))
CALLF DOKUDIM_FA_CTRL(ARG,1,3)
RETURN 1
ENDIF
TRYCALLFORM DOKU_FA_TRY_DEL%LOCALS%_K{ARG}
IF RESULT || DOKUDIM_FA_DURATION(ARG) <= 0
CALLF DOKUDIM_FA_DURATION(ARG,1,0)
CALLF DOKUDIM_FA_CTRL(ARG,1,2)
RETURN 1
ENDIF
CASEELSE
;不可能的情况
RETURN 0
ENDSELECT
RETURN 1

@DOKU_FA_IN_FLAGSETTING(ARG)
CALL DOKU_FA_IN_FLAGSETTING_P1(ARG)
CALL DOKU_FA_IN_FLAGSETTING_P2(ARG)
;NAME TIME CHAIN
RETURN 1

@DOKU_FA_IN_FLAGSETTING_P1(ARG)
VARSET RESULT
VARSET LOCALS
VARSET RESULTS
IF CFLAG:ARG:998
TRYCALLFORM M_KOJO_K{ARG}_{CFLAG:ARG:998}
LOCALS '= RESULTS
ELSE
TRYCALLFORM M_KOJO_K{ARG}
LOCALS '= RESULTS
ENDIF
LOCAL = DOKUDIM_FA_TYPE(ARG)
LOCAL:1 = DOKUDIM_FA_DURATION(ARG)
TRYCCALLFORM M_KOJO%LOCALS%_FA_TIME_K{ARG}(LOCAL,LOCAL:1)
CALLF DOKUDIM_FA_TIME(ARG,1,RESULT)
CATCH
CALLF DOKUDIM_FA_TIME(ARG,1,30)
ENDCATCH
VARSET RESULTS
TRYCCALLFORM M_KOJO%LOCALS%_FA_NAME_K{ARG}(LOCAL,LOCAL:1)
CALLF DOKUDIM_FA_NAME(ARG,1,RESULTS)
CATCH
CALLF DOKUDIM_FA_NAME(ARG,1,"陪同行动")
ENDCATCH
VARSET RESULT
TRYCCALLFORM M_KOJO%LOCALS%_FA_CHAIN_K{ARG}(LOCAL,LOCAL:1)
CALLF DOKUDIM_FA_CHAIN(ARG,1,RESULT)
CATCH
CALLF DOKUDIM_FA_CHAIN(ARG,1,0)
ENDCATCH
VARSET RESULT
VARSET RESULTS
RETURN 0

@DOKU_FA_IN_FLAGSETTING_P2(ARG)
Activity_Type:ARG = 66

SELECTCASE DOKUDIM_FA_CTRL(ARG)
CASE 1
VARSET RESULT
VARSET RESULTS
IF CFLAG:ARG:998
TRYCALLFORM M_KOJO_K{ARG}_{CFLAG:ARG:998}
VARSET RESULT
TRYCALLFORM DOKU_FA%RESULTS%_COMF_ABLE_K{ARG}
ELSE
TRYCALLFORM M_KOJO_K{ARG}
VARSET RESULT
TRYCALLFORM DOKU_FA%RESULTS%_COMF_ABLE_K{ARG}
ENDIF
IF RESULT
Activity_Duration:ARG = 999999
ELSE
Activity_Duration:ARG = 0
ENDIF
CASEELSE
Activity_Duration:ARG = 0
ENDSELECT
RETURN 0

;↑TOOL↓VAR————————————————————————————————————————————————————————


@DOKUDIM_FA_CTRL(ARG=0,ARG:1=0,ARG:2=0)
#FUNCTION
#LOCALSSIZE 1
IF ARG:1 == 1
LOCAL:ARG = ARG:2
ELSEIF ARG:1 == 2
VARSET LOCAL
ENDIF
RETURNF LOCAL:ARG

@DOKUDIM_FA_DURATION(ARG=0,ARG:1=0,ARG:2=0)
#FUNCTION
#LOCALSSIZE 1
IF ARG:1 == 1
LOCAL:ARG = ARG:2
ELSEIF ARG:1 == 2
VARSET LOCAL
ENDIF
RETURNF LOCAL:ARG

@DOKUDIM_FA_TYPE(ARG=0,ARG:1=0,ARG:2=0)
#FUNCTION
#LOCALSSIZE 1
IF ARG:1 == 1
LOCAL:ARG = ARG:2
ELSEIF ARG:1 == 2
VARSET LOCAL
ENDIF
RETURNF LOCAL:ARG

@DOKUDIM_FA_DURATION_NEXT(ARG=0,ARG:1=0,ARG:2=0)
#FUNCTION
#LOCALSSIZE 1
IF ARG:1 == 1
LOCAL:ARG = ARG:2
ELSEIF ARG:1 == 2
VARSET LOCAL
ENDIF
RETURNF LOCAL:ARG

@DOKUDIM_FA_NEXT(ARG=0,ARG:1=0,ARG:2=0)
#FUNCTION
#LOCALSSIZE 1
IF ARG:1 == 1
LOCAL:ARG = ARG:2
ELSEIF ARG:1 == 2
VARSET LOCAL
ENDIF
RETURNF LOCAL:ARG

@DOKUDIM_FA_OVER_DURATION(ARG=0,ARG:1=0,ARG:2=0)
#FUNCTION
#LOCALSSIZE 1
IF ARG:1 == 1
LOCAL:ARG = ARG:2
ELSEIF ARG:1 == 2
VARSET LOCAL
ENDIF
RETURNF LOCAL:ARG

;↑VAR1↓VAR2

@DOKUDIM_FA_TIME(ARG=0,ARG:1=0,ARG:2=0)
#FUNCTION
#LOCALSSIZE 1
IF ARG:1 == 1
LOCAL:ARG = ARG:2
ELSEIF ARG:1 == 2
VARSET LOCAL
ENDIF
RETURNF LOCAL:ARG

@DOKUDIM_FA_CHAIN(ARG=0,ARG:1=0,ARG:2=0)
#FUNCTION
#LOCALSSIZE 1
IF ARG:1 == 1
LOCAL:ARG = ARG:2
ELSEIF ARG:1 == 2
VARSET LOCAL
ENDIF
RETURNF LOCAL:ARG

@DOKUDIM_FA_NAME(ARG=0,ARG:1=0,ARGS)
#FUNCTIONS
#LOCALSIZE 1
#LOCALSSIZE 1
#DIMS DOKUDIM_FA_NAME,200
SIF ARG:1
DOKUDIM_FA_NAME:ARG '= ARGS
RETURNF DOKUDIM_FA_NAME:ARG