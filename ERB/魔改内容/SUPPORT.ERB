;剧本侧支援函数
;COMMON系的函数变体
;-------------------------------------------------
;函数名:对话中逆推处理
;概　要:略
;参　数:角色编号，缺省为TARGET
;剧本用函数，处理逆推发生机制，要与攻守交替、诶嘿嘿中事件同步
;-------------------------------------------------
@对话中逆推处理(ARG)
SIF !TARGET
	RETURN
SIF !ARG
	ARG = TARGET
CFLAG:MASTER:诶嘿嘿 = 2
CFLAG:诶嘿嘿 = 2
TFLAG:COMABLE管理 = 3
FOR LOCAL,1,GET_TARGETNUM() + 1
	SIF CFLAG:(TARGET:LOCAL):诶嘿嘿 == 0
		CONTINUE
	CFLAG:(TARGET:LOCAL):诶嘿嘿 = 2
	SETBIT TCVAR:(TARGET:LOCAL):随便,1
NEXT

;-------------------------------------------------
;函数名:AddEXP_D
;概　要:附带信息的经验值增加
;与@AddEXP区别是参数是编号而非字符串
;-------------------------------------------------
@AddEXP_D(ARG,C_ID,变动数)
#LOCALSIZE 1
#LOCALSSIZE 1
#DIM C_ID
#DIM 变动数

SIF 变动数 == 0
	RETURN
IF EXPNAME:ARG == ""
	PRINTFORMW 没有找到编号：{ARG}的EXP，请保存日志报错
	RETURN
ENDIF
EXP:C_ID:ARG += 变动数
CALL COLORMESSAGE(@"%EXP_TR(ARG)%＋{变动数}（%CALLNAME:C_ID%）",C_YELLOW,2,1)

;-------------------------------------------------
;函数名:RANDNR
;概　要:管理随机但不重复的差分发生
;灵感来自M_KOJO_K103_RANDNR，实现了其想要的(1.可变长度、2.链表式)的功能
;原本只用于K103剧本中以无意义编号管理的所有随机差分，现改成字符串关键字，对任意事件、指令、差分均可启用
;例如RANDNR(4, "60恋人")可以指定【正常位】指令-【恋人】分枝的随机数，不与上一次重复，替代原本的RAND:4
;没有用额外的字符串，而是利用了LOCALS可以在单个存档里暂存的特征
;-------------------------------------------------
@RANDNR(ARG, ARGS)
#LOCALSIZE 1
#LOCALSSIZE 1
#FUNCTION
#DIM DYNAMIC CURRENT_RAND_VALUE
#DIM DYNAMIC LAST_RAND_VALUE
IF ARG > 1
	IF DIC_CONTAINSKEY(LOCALS, ARGS)
		LAST_RAND_VALUE = TOINT(DIC_GET(LOCALS, ARGS))
	ELSE
		LAST_RAND_VALUE = -999
	ENDIF
	CURRENT_RAND_VALUE = RAND:ARG
	WHILE CURRENT_RAND_VALUE == LAST_RAND_VALUE
		CURRENT_RAND_VALUE = RAND:ARG
	WEND
	LOCALS '= DIC_SET(LOCALS, ARGS, TOSTR(CURRENT_RAND_VALUE))
	RETURNF CURRENT_RAND_VALUE
ELSE
	RETURNF 0
ENDIF
[SKIPSTART]
;Overview: Manages the generation of random but non-repeating numbers.
;Inspired by M_KOJO_K103_RANDNR, it implements the desired features (1. variable length, 2. linked-list like functionality).
;Originally, it was only used in K103 scripts to manage all random variations with meaningless numbers. Now it uses string keywords, allowing it to be enabled for any event, command, or branch.
;For example, RANDNR(4, "60Lover") can specify the random number for the [Missionary Position] command - [Lover] branch, ensuring it doesn't repeat the previous one, replacing the original RAND:4.
;Instead of using additional global strings, , this function leverages LOCALS variable behavior: unlike DYNAMIC variables, LOCALS are not released after a function call but persist throughout the entire game's save/load cycle. This makes LOCALS a form of persistent, function-specific storage

过去的测试例
FOR I, 0, 1000
	CALLF M_KOJO_K103_RANDNR_DIC,RAND(2,5),RAND:1000
NEXT
$LOOP
INPUT
IF !RESULT
	
ELSE
	PRINTFORML {RESULT}号的随机值为 {M_KOJO_K103_RANDNR_DIC(3,RESULT)}
	GOTO LOOP
ENDIF
[SKIPEND]

; @沉浸式播放器