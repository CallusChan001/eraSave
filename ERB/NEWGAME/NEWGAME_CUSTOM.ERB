;-----------------------------------------------------------
;キャラカスタマイズ
;-----------------------------------------------------------
@CUSTOM_TERMINAL(ARG, HIKITSUGI)
#DIM HIKITSUGI
#DIM nLineCnt_Head

CALL CUSTOM_INIT(ARG)

nLineCnt_Head = LINECOUNT
REDRAW 0
DO
SIF LINECOUNT - nLineCnt_Head > 0
	CLEARLINE LINECOUNT - nLineCnt_Head
		
;名前ボタン表示
DRAWLINE
CALL CUSTOM_NAME_BUTTON(ARG)
;基礎ボタン付き表示
DRAWLINE
CALL CUSTOM_BASE_BUTTON(ARG)
;能力・性技表示
CALL PRINT_STATE_ABL(ARG)
PRINTL
CALL COLORMESSAGE(@"　[2000] 能力编辑",0x70C070,1)
;素質表示
CALL PRINT_STATE_TALENT(ARG)
PRINTL
;CALL COLORMESSAGE(@"　移到素质名上查看提示哦",C_YELLOW,1) ;qol custom code
CALL COLORMESSAGE(@"　[3000] 素质编辑",0x70C070,1)
;経験表示
CALL PRINT_STATE_EXP(ARG)
PRINTL
CALL COLORMESSAGE(@"　[4000] 经验编辑",0x70C070,1)
IF HIKITSUGI
	CALL COLORMESSAGE(@"　[5000] 继承设置的保存",0x70C070,1)
ENDIF
DRAWLINE
PRINTL 　[9999] 设定完毕
PRINTL
INPUT
SELECTCASE RESULT
	CASE 9999
		PRINTL 这样真的可以吗？
		CALL ASK_YN()
		SIF !RESULT
			RETURN 1
	;NAME
	CASE 0 TO 999
		CALL CUSTOM_NAME(ARG, RESULT)
	;BASE
	CASE 1000 TO 1999
		CALL CUSTOM_BASE(ARG, RESULT - 1000)
	;ABL
	CASE 2000
		CALL CUSTOM_MENU("ABL", ARG)
	;TALENT
	CASE 3000
		CALL CUSTOM_MENU("TALENT", ARG)
		CALL CUSTOM_INIT(ARG)
	;EXP
	CASE 4000
		CALL CUSTOM_MENU("EXP", ARG)
		CALL CUSTOM_INIT(ARG)
	CASE 5000
		IF HIKITSUGI
			CALL SAVE_CUSTOM_TALENT(ARG, "恋人")
		ENDIF
ENDSELECT
LOOP 1
;-----------------------------------------------------------
;排他処理
;-----------------------------------------------------------
@CUSTOM_INIT(ARG)
#DIM nTalentCheck = 90,91,164,165,166,189,191,192,193,194,195,196,197
;ARGが女、扶她時の排他
IF TALENT:ARG:性別 & 1
	ABL:ARG:断袖属性 = 0
	ABL:ARG:断袖中毒 = 0
	EXP:ARG:断袖経験 = 0
	IF !(TALENT:ARG:2 & 2)
		EXP:ARG:射精経験 = 0
		EXP:ARG:挿入経験 = 0
	ENDIF
	SIF TALENT:ARG:性別 != 1
		TALENT:ARG:具現 = 0
;ARGが男時の排他
ELSE
	ABL:ARG:Ｖ感覚 = 0
	ABL:ARG:百合属性 = 0
	ABL:ARG:百合中毒 = 0
	EXP:ARG:百合経験 = 0
	EXP:ARG:噴乳経験 = 0
	EXP:ARG:Ｖ経験 = 0
	EXP:ARG:乳交経験 = 0
	EXP:ARG:Ｖ性交経験 = 0
	EXP:ARG:Ｖ拡張経験 = 0
	EXP:ARG:子宮口開発経験 = 0
	TALENT:ARG:Ｖ感度 = 0
	TALENT:ARG:処女 = 0
	TALENT:ARG:母乳体質 = 0
	TALENT:ARG:胸圍 = -2
	TALENT:ARG:風騷 = 0
	TALENT:ARG:初潮 = 0
	TALENT:ARG:具現 = 0
ENDIF
;チ○コ
IF TALENT:ARG:2 & 2
	MAXBASE:ARG:精力 = 1000
	BASE:ARG:精力 = 1000
	MAXBASE:ARG:勃起 = 1500
	SIF TALENT:ARG:絶倫
		MAXBASE:ARG:勃起 *= 2
	;実績BONUS分
	SIF ARG == MASTER && GETBIT(FLAG:702,8)
		MAXBASE:MASTER:精力 += 100
ELSE
	TALENT:ARG:非童貞 = 0
	MAXBASE:ARG:精力 = 0
	BASE:ARG:精力 = 0
	MAXBASE:ARG:勃起 = 0
	TALENT:ARG:形状 = 0
ENDIF
;処女ならV系経験無し
IF TALENT:ARG:処女 == 1
	ABL:ARG:Ｖ感覚 = 0
	EXP:ARG:Ｖ経験 = 0
	EXP:ARG:Ｖ性交経験 = 0
	EXP:ARG:Ｖ拡張経験 = 0
	EXP:ARG:子宮口開発経験 = 0
	CSTR:ARG:処女喪失履歴 = 
	CFLAG:ARG:処女喪失記念日 = 0
	CFLAG:ARG:処女喪失対象 = 0
	EXP:ARG:膣射経験 = 0
	CFLAG:ARG:累計膣内精液 = 0
;非処女はV経験１
ELSEIF TALENT:ARG:2 & 1 && EXP:ARG:Ｖ経験 == 0
	EXP:ARG:Ｖ経験 = 1
ENDIF

;初吻
IF !EXP:ARG:接吻経験 || TALENT:ARG:無接吻経験
	TALENT:ARG:無接吻経験 = 1
	EXP:ARG:キス経験 = 0
	CSTR:ARG:ファーストキス喪失履歴 =
	CFLAG:ARG:ファーストキス喪失記念日 = 0
	CFLAG:ARG:ファーストキス喪失相手 = 0
	CFLAG:ARG:ファーストキス喪失対象 = 0
ELSE
	TALENT:ARG:無接吻経験 = 0
ENDIF

IF IS_DOUTEI(ARG)
	EXP:ARG:挿入経験 = 0
	CSTR:ARG:童貞喪失履歴 =
	CFLAG:ARG:童貞喪失記念日 = 0
	CFLAG:ARG:童貞喪失相手 = 0
ELSEIF HAS_PENIS(ARG) && EXP:ARG:挿入経験 == 0
	EXP:ARG:挿入経験 = 1
ENDIF

IF !EXP:ARG:Ａ性交経験
	CSTR:ARG:アナル処女喪失履歴 =
	CFLAG:ARG:アナル処女喪失記念日 = 0
	CFLAG:ARG:アナル処女喪失対象 = 0
	CFLAG:ARG:アナル処女喪失相手 = 0
ENDIF

;set PC to human unless other racial traits were chosen
IF ARG == 0
	;female only races
	SIF TALENT:ARG:氷精 == 2 && IS_MALE(ARG)
		TALENT:ARG:氷精 = 0
	SIF TALENT:ARG:追加種族 == 1 && IS_MALE(ARG)
		TALENT:ARG:追加種族 = 0
	SIF TALENT:ARG:追加種族 == 3 && IS_MALE(ARG)
		TALENT:ARG:追加種族 = 0
	SIF TALENT:ARG:人妻 && IS_MALE(ARG)
		TALENT:ARG:人妻 = 0
	;-----------------

	LOCAL:9 = 0
	FOR LOCAL, 0, VARSIZE("nTalentCheck")
		SIF TALENT:ARG:(nTalentCheck:LOCAL)
			LOCAL:9 = 1
	NEXT
	SIF !LOCAL:9 && !TALENT:ARG:人間 ;automatically attach human if no other races present
		TALENT:ARG:人間 = 1
ENDIF

;;qol custom code
;CALL Qol_Info_Reset(ARG)

;-----------------------------------------------------------
;名前変更ボタン
;-----------------------------------------------------------
@CUSTOM_NAME_BUTTON(ARG)
#DIMS CONST colortext = "<font color='#70C070'>[变更]</font>"
#DIMS html
;名前
html '= @"<button value='{0}'>" + HTML_SPACE(2) + @"名字：%NAME:ARG%"+ HTML_SPACE(2) + colortext + "</button>"
html += HTML_SPACE(4)
;呼び名
html += @"<button value='{1}'>" + @"称呼：%CALLNAME:ARG%"+ HTML_SPACE(2) + colortext + "</button>"
HTML_PRINT html + "<br><br>"

@CUSTOM_NAME(ARG, ARG:1)
SELECTCASE ARG:1
	CASE 0
		PRINTL
		PRINTFORML 现在的名字是%NAME:ARG%。
		PRINTFORML 想要变更的话请输入新的名字。(留空为取消)
		PRINTL
		INPUTS
		IF RESULTS != ""
			NAME:ARG '= RESULTS
			PRINTFORMW 已变更。现在的名字是%NAME:ARG%。
		ENDIF
	CASE 1
		PRINTL
		PRINTFORML 现在的称呼是%CALLNAME:ARG%。
		PRINTFORML 想要变更的话请输入新的称呼。(留空为取消)
		PRINTL
		INPUTS
		IF RESULTS != ""
			CALLNAME:ARG '= RESULTS
			PRINTFORMW 已变更。现在的称呼是%CALLNAME:ARG%。
		ENDIF
ENDSELECT

;-----------------------------------------------------------
;基礎変更ボタン
;-----------------------------------------------------------
@CUSTOM_BASE_BUTTON(ARG)
#DIM baseButtonVal = 1000
PRINT  
CALL BASE_BAR(ARG, "体力", 28, "緑")

IF BASE:ARG:体力 > 1000
	SETCOLOR 0x70C070
	PRINTBUTTON "[－]", 0 + baseButtonVal
ELSE
	SETCOLOR 0x969696
	PRINT [－]
ENDIF
IF BASE:ARG:体力 < 2500
	SETCOLOR 0x70C070
	PRINTBUTTON "[＋]", 1 + baseButtonVal
ELSE
	SETCOLOR 0x969696
	PRINT [＋]
ENDIF
RESETCOLOR
PRINTL
PRINT  
CALL BASE_BAR(ARG, "気力", 28, "青")
IF BASE:ARG:気力 > 1000
	SETCOLOR 0x70C070
	PRINTBUTTON "[－]", 2 + baseButtonVal
ELSE
	SETCOLOR 0x969696
	PRINT [－]
ENDIF
IF BASE:ARG:気力 < 2500
	SETCOLOR 0x70C070
	PRINTBUTTON "[＋]", 3 + baseButtonVal
ELSE
	SETCOLOR 0x969696
	PRINT [＋]
ENDIF
RESETCOLOR
PRINTL
PRINTL

;-----------------------------------------------------------
;基礎変更
;ARG:0 角色登録番号
;ARG:1 0=体力減少, 1=体力増加, 2=気力減少, 3=気力増加
;-----------------------------------------------------------
@CUSTOM_BASE(ARG, ARG:1)
SELECTCASE ARG:1
	CASE 0
		MAXBASE:ARG:体力 -= 100
	CASE 1
		MAXBASE:ARG:体力 += 100
	CASE 2
		MAXBASE:ARG:気力 -= 100
	CASE 3
		MAXBASE:ARG:気力 += 100
	CASE 4
ENDSELECT
MAXBASE:ARG:体力 = LIMIT(MAXBASE:ARG:体力, 1000, 2500)
BASE:ARG:体力 = MAXBASE:ARG:体力
MAXBASE:ARG:気力 = LIMIT(MAXBASE:ARG:気力, 1000, 2500)
BASE:ARG:気力 = MAXBASE:ARG:気力

;-----------------------------------------------------------
;メニュー呼び出し
;-----------------------------------------------------------
@CUSTOM_MENU(ARGS, ARG)
#DIM CURRENTLINE
CURRENTLINE = LINECOUNT
PRINTL
PRINTL
CALLFORM CUSTOM_%ARGS%_MENU(ARG)
RESETCOLOR
PRINTL
PRINTL
PRINTL   [9999] 返回
PRINTL
INPUT
SIF RESULT == 9999
	RETURN
CALLFORM CUSTOM_%ARGS%(RESULT, ARG)
CLEARLINE LINECOUNT - CURRENTLINE
RESTART

;-----------------------------------------------------------
;能力変更
;-----------------------------------------------------------
@CUSTOM_ABL_MENU(ARG)
LOCAL:1 = 0
FOR LOCAL,0,50
	SIF CUSTOM_選べない能力(LOCAL, ARG)
		CONTINUE
	;custom code
	PRINTFORM %ABL_TR(LOCAL),12%:{ABL:ARG:LOCAL,5,LEFT} 
	IF ABL:ARG:LOCAL != 0
		PRINTBUTTON "[－]", LOCAL * 2
	ELSE
		SETCOLOR 0x969696
		PRINT [－]
		RESETCOLOR
	ENDIF
	IF ABL:ARG:LOCAL != 2
		PRINTBUTTON "[＋]", LOCAL * 2 + 1
	ELSE
		SETCOLOR 0x969696
		PRINT [＋]
		RESETCOLOR
	ENDIF
	LOCAL:1 ++
	SIF LOCAL:1 % 3 == 0
		PRINTL 
NEXT

;-----------------------------------------------------------
;-----------------------------------------------------------
@CUSTOM_ABL(ARG, ARG:1)
	LOCAL = ARG / 2
;例外処理
SIF CUSTOM_選べない能力(LOCAL, ARG:1)
	RETURN 0
;増減と正常化
;奇数(+)
IF (ARG) % 2
	ABL:(ARG:1):LOCAL ++
;偶数(-)
ELSE
	ABL:(ARG:1):LOCAL --
ENDIF
ABL:(ARG:1):LOCAL = LIMIT(ABL:(ARG:1):LOCAL, 0, 2)
RETURN 1

;-----------------------------------------------------------
;-----------------------------------------------------------
@CUSTOM_選べない能力(argAbl, ARG)
#DIM argAbl
#FUNCTION
;个別処理
SELECTCASE ABLNAME:argAbl
	;未使用
	CASE ""
		RETURNF 1
	;時間停止
	CASE "TSLv"
		RETURNF 1
	;同性愛関係
	CASE "百合属性", "百合中毒"
		SIF !(TALENT:ARG:2 & 1)
			RETURNF 1
	CASE "断袖属性", "断袖中毒"
		SIF TALENT:ARG:2 & 1
			RETURNF 1
ENDSELECT
;汎用処理
SELECTCASE argAbl
	;中毒関係
	CASE 30 TO 39
		RETURNF 1
	;生活系
	CASE 40 TO 49
		RETURNF 1
	;性技
	CASE 50 TO 59
		RETURNF 1
ENDSELECT
RETURNF 0

;-----------------------------------------------------------
;素質変更
;-----------------------------------------------------------
@CUSTOM_TALENT_MENU(ARG)
VARSET LOCAL
FOR LOCAL, 0, 200
	IF CUSTOM_不能选的素質(LOCAL, ARG) == 1
		CONTINUE
	ELSEIF CUSTOM_不能选的素質(LOCAL, ARG) == 2
		;表示はさせる
		SETCOLOR 50, 50, 50
		LOCALS = [{LOCAL,3}]%TALENT_TR(LOCAL)%
		GOTO SKIP
	ENDIF
	SETCOLOR 150, 150, 150
	SELECTCASE NAME_TR(TALENTNAME:LOCAL)
	;TALENT.CSVに登録されていない
	CASE ""
		CONTINUE
	;exceptions for listing
	CASE NAME_TR("氷精"), NAME_TR("人类"), NAME_TR("妖怪"), NAME_TR("神霊"), NAME_TR("幽霊"), NAME_TR("人形"), NAME_TR("人妻"), NAME_TR("音感"), NAME_TR("動物耳")
		GOTO LIST
	;オンオフ型の素質(TALENTNAMEと素質表示が一致)
	CASE GET_TALENTNAME(LOCAL,1)
		SIF TALENT:ARG:LOCAL
			SETCOLOR C_AQUA
		LOCALS = [{LOCAL,3}]%TALENT_TR(LOCAL)%
	CASE NAME_TR("非童貞")
		SIF !TALENT:ARG:LOCAL
			SETCOLOR C_AQUA
		LOCALS = [{LOCAL,3}]童贞
	CASE NAME_TR("無接吻経験")
		SIF !TALENT:ARG:LOCAL
			SETCOLOR C_AQUA
		LOCALS = [{LOCAL,3}]%TALENT_TR(LOCAL)%
	;量的素質
	CASEELSE
		$LIST
		LOCALS =  [{LOCAL,3}]%TALENT_TR(LOCAL)%
		IF STRLENS(GET_TALENTNAME(LOCAL, TALENT:ARG:LOCAL))
			LOCALS:1 = 【%GET_TALENTNAME(LOCAL, TALENT:ARG:LOCAL)%】
			SETCOLOR C_AQUA
		ELSEIF TALENTNAME:LOCAL == "性別"
			LOCALS:1 = 【女性】
			SETCOLOR C_AQUA
		ELSE
			LOCALS:1 = 【－】
		ENDIF
		;LOCALS is used for string comparison earlier
		LOCALS =  [{LOCAL,3}]%TALENT_TR(LOCAL)%
		LOCALS += LOCALS:1
	ENDSELECT
	$SKIP
	IF LOCAL:1 % 3 == 0
		PRINTL 
		PRINT   
	ENDIF
	IF CUSTOM_不能选的素質(LOCAL, ARG) == 2
		PRINTPLAINFORM %LOCALS, 30, LEFT%
	ELSE
		PRINTFORM %LOCALS, 30, LEFT%
	ENDIF
	LOCAL:1 ++
NEXT

;-----------------------------------------------------------
@CUSTOM_TALENT(inputed, chara)
#DIM inputed
#DIM chara
SIF inputed < 0|| inputed > 500
	RETURN 0
SIF TALENTNAME:inputed == ""
	RETURN 0
SIF CUSTOM_不能选的素質(inputed, chara)
	RETURN 0
SELECTCASE TALENTNAME:inputed
	;addition custom code, but this looks like it should be on JP as well
	CASE "氷精" ;female only for yuki onna
		IF !GETBIT(TALENT:CHARA:性別, 0)
			TALENT:chara:inputed = !TALENT:chara:inputed
		ELSE
			IF TALENT:chara:inputed == 2
				TALENT:chara:inputed = 0
			ELSE
				TALENT:chara:inputed ++
			ENDIF
		ENDIF
	CASE "人类" ;button handling
		IF TALENT:chara:inputed == 6
			TALENT:chara:inputed = 0
		ELSE
			TALENT:chara:inputed ++
		ENDIF
	CASE "人妻" ;button handling
		IF TALENT:chara:inputed == 2
			TALENT:chara:inputed = 0
		ELSE
			TALENT:chara:inputed ++
		ENDIF
	CASE "幽霊" ;button handling
		IF TALENT:chara:inputed == 4
			TALENT:chara:inputed = 0
		ELSE
			TALENT:chara:inputed ++
		ENDIF
	CASE "妖怪" ;skip vampire, skip angel
		IF TALENT:chara:inputed == 2
			TALENT:chara:inputed = 4
		ELSEIF TALENT:chara:inputed == 11
			TALENT:chara:inputed = 0
			;TALENT:chara:inputed = 90
		ELSE
			TALENT:chara:inputed ++
		ENDIF
	CASE "神霊" ;skip enma
		IF TALENT:chara:inputed == 2
			TALENT:chara:inputed = 0
		ELSE
			TALENT:chara:inputed ++
		ENDIF
	CASE "人形" ;skip puppet maiden
		IF TALENT:chara:inputed == 5
			TALENT:chara:inputed = 0
		ELSE
			TALENT:chara:inputed ++
		ENDIF
	CASE "追加種族" ;female only for shrine maiden/maid, skip hourai
		IF TALENT:chara:inputed == 0 && !GETBIT(TALENT:CHARA:性別, 0)
			TALENT:chara:inputed = 2
		ELSEIF TALENT:chara:inputed == 2 && !GETBIT(TALENT:CHARA:性別, 0)
			TALENT:chara:inputed = 0
		ELSEIF TALENT:chara:inputed == 3
			TALENT:chara:inputed = 0
		ELSE
			TALENT:chara:inputed ++
		ENDIF
	CASE "人形" ;skip puppet maiden
		IF TALENT:chara:inputed == 5
			TALENT:chara:inputed = 0
		ELSE
			TALENT:chara:inputed ++
		ENDIF
	CASE "追加種族" ;female only for shrine maiden/maiden, skip hourai
		IF TALENT:chara:inputed == 0 && !GETBIT(TALENT:CHARA:性別, 0)
			TALENT:chara:inputed = 2
		ELSEIF TALENT:chara:inputed == 2 && !GETBIT(TALENT:CHARA:性別, 0)
			TALENT:chara:inputed = 0
		ELSEIF TALENT:chara:inputed == 3
			TALENT:chara:inputed = 0
		ELSE
			TALENT:chara:inputed ++
		ENDIF
	;----------------------------

	;Bugfix, special handling of virginity
	CASE "処女"
		IF TALENT:chara:処女 == 1
			TALENT:chara:処女 = 0
		ELSE
			TALENT:chara:処女++
		ENDIF
	;Bugfix, special handling of m sensitivity
	CASE "Ｍ感度"
		IF TALENT:chara:Ｍ感度 == 1
			TALENT:chara:Ｍ感度 = -1
		ELSE
			TALENT:chara:Ｍ感度++
		ENDIF
	;Bugfix, special handling of animal ears
	CASE "動物耳"
		IF TALENT:chara:動物耳 == 4
			TALENT:chara:動物耳 = 0
		ELSE
			TALENT:chara:動物耳++
		ENDIF
	
	;先に例外処理
	CASE "性別"
		IF TALENT:chara:性別 == 3
			TALENT:chara:性別 = 1
		ELSE
			TALENT:chara:性別 ++
		ENDIF
		CALL CUSTOM_INIT(chara)
	CASE "性別嗜好"
		IF TALENT:chara:inputed == 3
			TALENT:chara:inputed = -1
		ELSE
			TALENT:chara:inputed ++
		ENDIF
	CASE "体型"
		;体型が「小人(-5)」と「巨人(3)」の場合を独立処理
		IF TALENT:chara:inputed == -5
			TALENT:chara:inputed = -2
		ELSEIF TALENT:chara:inputed == 5
			TALENT:chara:inputed = -5
		ELSEIF TALENT:chara:inputed == 2
			TALENT:chara:inputed = 5
		ELSE
			TALENT:chara:inputed ++
		ENDIF
	CASE "非童貞", "無接吻経験"
		TALENT:chara:inputed = !TALENT:chara:inputed
	CASE "母乳体質"
		TALENT:chara:母乳体質 = !TALENT:chara:母乳体質
		IF TALENT:chara:母乳体質
			MAXBASE:chara:母乳 = 2000
		ELSE
			MAXBASE:chara:母乳 = 0
		ENDIF
	;汎用処理
	CASEELSE
		;真偽型の素質は反転
		IF NAME_TR(TALENTNAME:inputed) == GET_TALENTNAME(inputed,1)
			TALENT:chara:inputed = !TALENT:chara:inputed
		ELSE
		;他は端まで行くと-1を掛ける
			IF STRLENS(GET_TALENTNAME(inputed,TALENT:chara:inputed + 1)) || TALENT:chara:inputed == -1
				TALENT:chara:inputed ++
			ELSE
				TALENT:chara:inputed *= -1
				SIF !STRLENS(GET_TALENTNAME(inputed, TALENT:chara:inputed))
					TALENT:chara:inputed ++
			ENDIF
		ENDIF
ENDSELECT

;-----------------------------------------------------------
@CUSTOM_不能选的素質(ARG, CHARA)
#FUNCTION
#DIM CHARA
;RETURNF 2は表示はさせるもの
SELECTCASE ARG
	CASE 164,166,190,191,192,193,194,195,196,197 ;allow racial talents (some)
		SIF !FLAG:周回数 ;ng+ only
			RETURNF 2
		RETURNF 0
	CASE 198 ;female only
		SIF !FLAG:周回数 ;ng+ only
			RETURNF 2
		SIF !GETBIT(TALENT:CHARA:性別, 0)
			RETURNF 2
		RETURNF 0

	;skip logging, 46
	CASE 46
		RETURNF 1
		
	;劣情察知～,ボーナス素質および特殊陥落,種族,孕ませ系
	CASE IS >=157
		RETURNF 1
	;你は選べない１
	CASE GETNUM(TALENT, "習得速度") TO GETNUM(TALENT, "擅長用針"), GETNUM(TALENT, "薬毒耐性"), GETNUM(TALENT, "容易高潮"), GETNUM(TALENT, "盲信"), GETNUM(TALENT, "小悪魔")
		SIF CHARA == NO:MASTER
			RETURNF 1
	;你は選べない２
	CASE GETNUM(TALENT, "魅力") TO GETNUM(TALENT, "謎之魅力"), GETNUM(TALENT, "回復速度") TO GETNUM(TALENT, "不可思議的根"), GETNUM(TALENT, "狂気"), GETNUM(TALENT, "人気")
		SIF CHARA == NO:MASTER
			RETURNF 1
	;你は選べず、女性器がないと選べない
	CASE GETNUM(TALENT, "母乳体質"), GETNUM(TALENT, "妊娠願望") 
		SIF CHARA == NO:MASTER
			RETURNF 1
		SIF !GETBIT(TALENT:CHARA:性別, 0)
			RETURNF 2
	;女性器がないと選べない
	CASE GETNUM(TALENT, "処女"), GETNUM(TALENT, "風騷"), GETNUM(TALENT, "Ｖ感度"), GETNUM(TALENT, "胸圍")
		SIF !GETBIT(TALENT:CHARA:性別, 0)
			RETURNF 2
	;男性器がないと選べない
	CASE GETNUM(TALENT, "非童貞")
		SIF !GETBIT(TALENT:CHARA:性別, 1)
			RETURNF 2
	;男性器があると選べない
	CASE GETNUM(TALENT, "具現")
		SIF GETBIT(TALENT:CHARA:性別, 1)
			RETURNF 2
	;你以外は選べない
	CASE GETNUM(TALENT, "年齢")
		SIF CHARA != NO:MASTER
			RETURNF 1
	;全員選べないが表示はする
	CASE GETNUM(TALENT, "無接吻経験")
		RETURNF 2
	;全員選べない１
	CASE GETNUM(TALENT, "恋慕") TO GETNUM(TALENT, "思慕"), GETNUM(TALENT, "心情"), GETNUM(TALENT, "容姿"), GETNUM(TALENT, "把柄"), GETNUM(TALENT, "調合知識"), GETNUM(TALENT, "調合Lv"), GETNUM(TALENT, "採集Lv"), GETNUM(TALENT, "釣魚Lv"), GETNUM(TALENT, "伐採Lv")
		RETURNF 1
	;全員選べない２
	CASE GETNUM(TALENT, "自慰狂") TO GETNUM(TALENT, "子宮口性感"), GETNUM(TALENT, "迷信"), GETNUM(TALENT, "嫉妬"), GETNUM(TALENT, "濃厚精液"), GETNUM(TALENT, "精愛味覚"), GETNUM(TALENT, "禁断的知識"), GETNUM(TALENT, "精力超群") TO GETNUM(TALENT, "母性")
		RETURNF 1
	;女性器があって幼児か幼稚がないと選べない
	CASE GETNUM(TALENT, "初潮")
		SIF !GETBIT(TALENT:CHARA:性別, 0)
			RETURNF 2
		SIF !TALENT:CHARA:幼児／幼児退行 && !TALENT:CHARA:幼稚
			RETURNF 2
ENDSELECT
RETURNF 0

;-----------------------------------------------------------
;経験変更
;-----------------------------------------------------------
@CUSTOM_EXP_MENU(ARG)
VARSET LOCAL
FOR LOCAL,0,100
	SIF CUSTOM_選べない経験(LOCAL, ARG)
		CONTINUE
	PRINTFORM   [{LOCAL,2}] - %EXP_TR(LOCAL),16%:{EXP:ARG:LOCAL,5,LEFT} 
	LOCAL:1 ++
	SIF LOCAL:1 % 3 == 0
		PRINTL 
NEXT

;-----------------------------------------------------------
@CUSTOM_EXP(argExp, argChara)
#DIM argExp
#DIM argChara
SIF CUSTOM_選べない経験(argExp, argChara)
	RETURN 0
PRINTFORM 请输入%EXP_TR(argExp)%    (0-30)
	PRINTBUTTON "[中止]",-1
	PRINTL   [ 0]  [ 1]  [ 5]  [10]  [20]  [30]
	PRINTL
INPUT
SIF RESULT < 0
	RETURN 0
EXP:argChara:argExp = RESULT
EXP:argChara:argExp = LIMIT(EXP:argChara:argExp, 0, 30)
RETURN 1

;-----------------------------------------------------------
@CUSTOM_選べない経験(argExp, argChara)
#FUNCTION
#DIM argExp
#DIM argChara
SELECTCASE EXPNAME:argExp
	CASE "", "肛射経験", "膣射経験"
		RETURNF 1
	;女性器無は不可
	CASE "百合経験", "Ｖ経験", "Ｖ絶頂経験", "Ｖ性交経験", "Ｖ拡張経験", "子宮口開発経験", "噴乳経験", "乳交経験"
		SIF !(TALENT:argChara:性別 & 1)
			RETURNF 1
	;女性器ありは不可
	CASE "断袖経験"
		SIF TALENT:argChara:性別 & 1
			RETURNF 1
	;男性器無は不可
	CASE "挿入経験", "射精経験"
		SIF !HAS_PENIS(argChara)
			RETURNF 1
	;時間停止がらみは不可
	CASE "時姦経験", "無自覚Ｃ経験", "無自覚Ｖ経験", "無自覚Ａ経験", "無自覚Ｂ経験", "無自覚Ｍ経験", "無自覚絶頂経験"
		RETURNF 1
ENDSELECT
SELECTCASE argExp
	;生活系
	CASE 70 TO 89
		RETURNF 1
	;行為系はすり合わせが面倒
	CASE 100 TO 109
		RETURNF 1
ENDSELECT
RETURNF 0