;-----------------------------------------------------------
;初次游玩
;-----------------------------------------------------------
@NEWGAME
ゲーム開始処理中 = 1
;本体バージョンを記録
SAVESTR:本体バージョン = %eraTW_Version%
;クラス関数初期化
CALL MAKE_ALL_CLASS_LIST
;默认オプション
CALL DEFAULT_OPTION
;筹码初期化
CALL SET_CHARISMA("NEW")
PBAND = 12

;日期１日目
DAY = 1
DAY:2 = 1
DAY:3 = 1
;FLAG:酒虫 = 1
VAR_FERMENT = 普通的水
;(天候パッチ)冷害発生回数・風害発生回数・水害発生回数・連続降水Counterをリセット
IS_COLD_CROP_DAMAGE = 0
IS_STORM_CROP_DAMAGE = 0
IS_FLOOD_CROP_DAMAGE = 0
CONTINUOUS_RAINFALL = 0
;(天候パッチ)降雪・雪崩判定を解除
CONTINUOUS_SNOWFALL = 0
SNOW_COVERAGE = 0
POSSIBLITY_AVALANCHE = 0
;(天候パッチ)時間帯をリセット
WEATHER_TIMEZOME = 0
;(天候パッチ)長期天候傾向をリセット
YEAR_WEATHER_CONTROL = 0
;(天候パッチ)短期予報を行う
CALL WEATHER_FORECAST_1TERM
;(天候パッチ)1日予報を行う
CALL WEATHER_FORECAST_1DAY
FLAG:CHILD_SUPPORT_RELIEF = 1
FLAG:MOB_PREGNANCY_TYPES = 1
FLAG:TWINS_CHANCE = 10
FLAG:TRIPLETS_CHANCE = 2
;キャラ生成
CALL ADD_ALL_CHARACTERS
;MASTER生成
CALL NEWGAME_MAKE
;custom code
CALL SET_ALTERNATIVE_DEFAULTS
FOR LOCAL, 1, CHARANUM
	CALL KOJO_MULTIPLE(LOCAL,1)
NEXT
gDialogueUpdateInit = 1
CALL UPDATE_AND_SELECTION_OF_KOJO_MODDING("SELECTION")
gDialogueUpdateInit = 0
;recolored map or default
CALL DEFAULT_MAP_COLOR
;住居設定
CALL SET_MAINHOME
;オプション
CALL NEWGAME_OPTION

;角色設定
FOR LOCAL,0,CHARANUM
	;開始位置
	CFLAG:LOCAL:現在位置 = CFLAG:LOCAL:初期位置
	;服装
	CALL SELECT_CLOTHES(LOCAL, "今日の衣装")
	CALL CTRL_CLOTHES_SET(LOCAL, "現在衣装の変更_便装")
	CALL CLOTHES_SETTING_TRAIN(LOCAL)
	CALL 内衣確認リセット(LOCAL)
	CVARSET BASE, GETNUM(BASE, "酒気"), 0
NEXT
;MASTER起床時間セット（7時）
TIME:3 = 420
;MASTER睡眠中フラグON
CALL SET_SLEEP(1,MASTER,0)
ゲーム開始処理中 = 0
CALL INIT_STARTINGLV

RETURN 0

;FileName_SHOP_CUSTOM.ERB -------------------------- Rev1.00
;CALL		USER
;ARG		NONE
;RETURN		VOID
;COMMENT	
;角色を追加したときにいちいちここをいじる必要の無いように
;人物数量上限はERHファイルで定義
;-----------------------------------------------------------
@ADD_ALL_CHARACTERS(ARG) ;custom arg to skip muiltiple dialogue choice
FOR LOCAL,CHARANUM,人物数量上限
	SIF !EXISTCSV(LOCAL)
		CONTINUE
	CALL ADDCHARA_INIT(LOCAL)
	CALL UPDATE_MAIN_UPDATE_ABL(LOCAL)
	CALL UPDATE_MAIN_UPDATE_SEX_EXP(LOCAL)
	IF !EXP:LOCAL:接吻経験
		TALENT:LOCAL:無接吻経験 = 1
	ENDIF
NEXT
;CALL PREGNANCY_TYPES_NEWGAME_SET()
CALL 旧生育系统预设

;-----------------------------------------------------------
;設定変更
;-----------------------------------------------------------
@NEWGAME_OPTION
#DIMS choices, 100
;初期化
;甲斐性無、農民
SETBIT FLAG:1000, 1
SETBIT FLAG:1000, 2
;TINKO
SIF !TALENT:MASTER:形状
	TALENT:MASTER:形状 = 3
;服
SIF !CFLAG:MASTER:基本服装セット
	CALL MASTER_CLOTHES_INIT
;
SIF !ゲーム開始処理中
	新游戏特典 '= "无"
;選択肢
VARSET choices
PRINTL
PRINTL
PRINTL 在游戏开始前你可以变更游戏各种的设定。
DRAWLINE
;你設定
SIF !FLAG:扮演
	choices:0 '= @"%CALLNAME:MASTER%进行自定义角色"
SIF HAS_PENIS(MASTER)
	choices:1 '= @"%CALLNAME:MASTER%的男性器为：%TINKO_SIMPLE(MASTER)%"
choices:2 '= @"%CALLNAME:MASTER%的服装为：%MASTER_CLOTHES_NAME(CFLAG:MASTER:基本服装セット)%"
IF FLAG:甲斐性無
	choices:3 '= @"%CALLNAME:MASTER%没有告白的胆量"
ELSE
	choices:3 '= @"%CALLNAME:MASTER%有告白的胆量"
ENDIF
IF FLAG:地主
	choices:4 '= @"%CALLNAME:MASTER%是为%CALLNAME:(FLAG:地主)%劳动的农民"
ELSE
	choices:4 '= @"%CALLNAME:MASTER%是无债一身轻的自由人"
ENDIF
choices:5 '= @"%CALLNAME:MASTER%居住在%GET_MAPNAME(MAIN_MAP)%的%STR_TR(默认初期位置 + (MAIN_MAP ? 8000 # 0) )%"
;幻想乡
choices:10 '= @"可以进行各角色的处女・非处女变更（＋futa化与否＆形状）"
choices:11 '= @"可以进行各角色的体型变更"
;特典
IF ゲーム開始処理中
	choices:20 '= NEWGAMEBONUS_TEXT()
ELSEIF ゲーム继承処理中
	choices:20 '= 周回特典_表示()
ENDIF

CALL ASK_CHOICES(choices, 100, "完成自定义阶段并开始游戏")
SELECTCASE RESULT
	CASE 0
		IF !FLAG:扮演
			CALL CUSTOM_TERMINAL(MASTER)
			CALL SET_MAINHOME
		ENDIF
	CASE 100
		CALL VIRGIN_SET_APPLY
		CALL SET_BODY_APPLY
		CALL NEWGAME_BONUS_APPLY
		CALL SET_APPLY_NOUMIN
		;CALL 周回特典_適用
		ITEM:平价食材 = 10
		RETURN
	CASE 1
		SIF HAS_PENIS(MASTER)
			CALL TINKO_DECIDE(MASTER)
	CASE 2
		CALL MASTER_CLOTHES
	CASE 3
		CALL KAISYONASI
	CASE 4
		CALL RESET_NOUMIN
		CALL SET_NOUMIN
	CASE 5
		CALL SET_MAINHOME
	CASE 10
		CALL VIRGIN_SET
	CASE 11
		CALL BODY_SET
	CASE 20
		IF ゲーム開始処理中
			CALL NEWGAME_BONUS_SELECT
		ELSEIF ゲーム继承処理中
			CALL 周回特典
		ENDIF
ENDSELECT
RESTART

;-----------------------------------------------------------
;設定変更
;-----------------------------------------------------------
@NEWGAME_VERUP
;恋人修正済フラグを立てる
SETBIT FLAG:1000, 3
SETBIT FLAG:1000, 4
modding_version = NOW_VERSION
CALL VERSION_UP
PRINTW 游戏开始处理完成
CALL saveManageSaveGame(1000,, "初次游玩")
PRINTL 已自动保存
gDialogueUpdateInit = 1;custom code
CALL UPDATE_MAIN("init")
gDialogueUpdateInit = 0;custom code
PRINTL
CALL COLORMESSAGE(@"许多角色都有剧本自定义的设置，可在其能力界面中访问。请务必查看！",C_YELLOW,1)
PRINTL 要进入蛇版魔改补丁设置界面吗？
CALL ASK_YN()
SIF !RESULT
	CALL OPTION_DLC
CALL COLORMESSAGE(@"补丁设置在主页面的[400] - OPTION 游戏设置内",C_YELLOW,1)

;custom code, set all abilities up to date upon the start
@INIT_STARTINGLV
PRINTFORML 正在匹配经验与能力等级…
SKIPDISP 1
FOR LOCAL,0,CHARANUM
	SIF CFLAG:LOCAL:出禁
		CONTINUE
	SIF !(GETCHARA(LOCAL) != -1 && EXISTCSV(LOCAL)) ;failsafe before csv checking in following functions
		CONTINUE
	
	CALL SET_MINIMUM_EXP_ALL(LOCAL, 1)
	CALL SOURCE_ABLUP(LOCAL)
	
	SIF EXP:LOCAL:キス経験 == 0 && TALENT:LOCAL:キス未経験 == 0 && CFLAG:LOCAL:ファーストキス喪失対象 != 1 ;set never kissed
		 TALENT:LOCAL:キス未経験 = 1
NEXT
CALL SHOW_ABLUP_SELECT
SKIPDISP 0