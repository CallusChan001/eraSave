;-------------------------------------------------
;乐谱の入手条件を取得
;SCORE_NO 乐谱番号
;
;RETURNFS 入手条件
;-------------------------------------------------
@GET_SCORE_INFO_MESSAGE(SCORE_NO)
#FUNCTIONS
#DIM SCORE_NO		; 乐谱番号
#DIM SCORE_INFO_NO	; 入手条件番号
#DIMS MESSAGE		; メッセージ
#DIMS NAME_STR   	; bugfix custom code, allow printing of multiple names

SCORE_INFO_NO = GET_SCORE_INFO_NO(SCORE_NO)

SELECTCASE SCORE_INFO_NO
	CASE IS < 乐谱入手方法：随机
		; 関連角色の演奏大成功時に入手
		;bugfix custom code, print all valid characters
		NAME_STR =
		FOR LOCAL, 0, 3
			SIF nTempScoreInfo:LOCAL > 0
				NAME_STR += @"%CALLNAME:(nTempScoreInfo:LOCAL)%、"
		NEXT
		MESSAGE = 让%LIST_IN_STRING(NAME_STR, "、", " or")%听到大成功的演奏时入手
	CASE 乐谱入手方法：随机
		MESSAGE = 演奏时随机入手
	CASE 乐谱入手方法：シリーズ初取得
		MESSAGE = 在第一次入手本系列的乐谱时入手
	CASE 乐谱入手方法：シリーズ随机コンプ
		MESSAGE = 在获得了本系列的所有随机乐谱时入手
	CASE 乐谱入手方法：シリーズ関連角色弾幕勝負
		MESSAGE = 在和本系列的关联角色进行弹幕胜负时入手
	CASE 乐谱入手方法：シリーズ関連角色陥落
		MESSAGE = 在攻略了本系列的关联角色（恋慕・爱欲）时入手
	CASE 乐谱入手方法：シリーズ完全コンプ
		MESSAGE = 在入手了本系列的其他所有乐谱时入手
	CASE 乐谱入手方法：随机（角色無し）
		; 角色テーマだが、関連角色がいないので随机入手
		MESSAGE = 演奏时随机入手
	; --- 特殊条件 ---
	CASE 乐谱入手方法：妖精依頼成功
		; 妖精大戦争
		MESSAGE = 桑尼・露娜・斯塔・大妖精・琪露诺的委托成功时入手
	CASE 乐谱入手方法：文依頼成功
		;東方文花帖
		MESSAGE = 文的委托成功时入手
	CASE 乐谱入手方法：極依頼成功
		;ダブルスポイラー
		MESSAGE = 果的委托成功时入手
	CASE 乐谱入手方法：依賴失敗時
		;弾幕アマノジャク
		MESSAGE = 委托失败时入手
	CASEELSE
		; それ以外
		MESSAGE = 错误
ENDSELECT

RETURNF MESSAGE

;-------------------------------------------------
;乐谱入手時のメッセージを取得
;SCORE_NO 乐谱番号
;
;RETURNFS 乐谱入手時のメッセージ
;-------------------------------------------------
@GET_SCORE_OBTAIN_MESSAGE(SCORE_NO)
#FUNCTIONS
#DIM SCORE_NO	; 乐谱番号

#DIM SERIES_NO		; シリーズ番号
#DIM SCORE_INFO_NO	; 入手条件番号
#DIMS MESSAGE		; メッセージ

SERIES_NO = GET_SERIES_NO(SCORE_NO)
SCORE_INFO_NO = GET_SCORE_INFO_NO(SCORE_NO)

SELECTCASE SCORE_INFO_NO
	CASE IS < 乐谱入手方法：随机
		;不合理，应该总是由TARGET交予
		MESSAGE = 演奏后%CALLNAME:TARGET%给了你%GET_SCORE_FULLNAME(SCORE_NO)%的乐谱！
	CASE 乐谱入手方法：随机, 乐谱入手方法：随机（角色無し）
		MESSAGE = 收到了%GET_SCORE_FULLNAME(SCORE_NO)%的乐谱！
	CASE 乐谱入手方法：シリーズ初取得
		MESSAGE = %GET_SERIES_SHORT_NAME(SERIES_NO)%的攻略開始！ 入手了%GET_SCORE_FULLNAME(SCORE_NO)%的乐谱！
	CASE 乐谱入手方法：シリーズ随机コンプ
		MESSAGE = %GET_SERIES_SHORT_NAME(SERIES_NO)%的全阶段完成！ 获得了%GET_SCORE_FULLNAME(SCORE_NO)%的乐谱！
	CASE 乐谱入手方法：シリーズ関連角色弾幕勝負
		MESSAGE = 哔秋！ 尽管残机减少了但是入手了%GET_SCORE_FULLNAME(SCORE_NO)%的乐谱！
	CASE 乐谱入手方法：シリーズ関連角色陥落
		MESSAGE = 枕边放着%GET_SCORE_FULLNAME(SCORE_NO)%的乐谱。
	CASE 乐谱入手方法：シリーズ完全コンプ
		MESSAGE = %GET_SERIES_SHORT_NAME(SERIES_NO)%完全Clear了！ 入手了%GET_SCORE_FULLNAME(SCORE_NO)%的乐谱！
	; --- 特殊条件 ---
	CASE 乐谱入手方法：妖精依頼成功
		MESSAGE = ⑨！ 拿到了%GET_SCORE_FULLNAME(SCORE_NO)%的乐谱！
	CASE 乐谱入手方法：文依頼成功
		MESSAGE = 取材成功！ 入手了%GET_SCORE_FULLNAME(SCORE_NO)%的乐谱！
	CASE 乐谱入手方法：極依頼成功
		MESSAGE = Shoot Chance！ 入手了%GET_SCORE_FULLNAME(SCORE_NO)%的乐谱！
	CASE 乐谱入手方法：依賴失敗時
		MESSAGE = 入手了%GET_SCORE_FULLNAME(SCORE_NO)%的乐谱！
	CASEELSE
		MESSAGE = 错误
ENDSELECT

RETURNF MESSAGE
