@约会終了タイムUP処理
;bugfix custom code
#DIM DYNAMIC AFFECTED_CHARAS, キャラクタ数上限
#LOCALSIZE 10
#LOCALSSIZE 5
VARSET LOCAL
;気力ゼロ,衰弱時约会中断
FOR LOCAL,0,CHARANUM
	;bugfix custom code
	IF CHK_DATENOW(CFLAG:LOCAL:约会中) && (CFLAG:LOCAL:衰弱 || (BASE:LOCAL:気力 == 0 && CFLAG:LOCAL:現在位置 != 情人用宿屋 && CFLAG:LOCAL:現在位置 != 情人旅館 && CFLAG:LOCAL:現在位置 != OMANEKIBEYA()))
		LOCAL:1 ++
		AFFECTED_CHARAS:LOCAL = 1
	ENDIF
	;bug fix, separate master
	IF LOCAL:1 && LOCAL == MASTER
		LOCAL:1 --
		LOCAL:3 ++
	ENDIF
	SIF CHK_DATENOW(CFLAG:LOCAL:约会中) && CFLAG:LOCAL:衰弱
		CALL ENDUFUFU(LOCAL)
NEXT
SIF NEMUKE() >= 720 || BASE:MASTER:酒気 >= MAXBASE:MASTER:酒気
	LOCAL:2 ++

;bugfix custom code
SIF LOCAL:2 || LOCAL:3
	AFFECTED_CHARAS:MASTER = 1
;お招きしていて連泊でなければ泊めて貰える
IF CFLAG:MASTER:招待 && (LOCAL:3 || LOCAL:2) && (!FLAG:70 || CFLAG:TARGET:344 == 1)
	IF !TALENT:TARGET:恋慕
		PRINTFORML %CALLNAME:(CFLAG:MASTER:招待)%也差不多到了清醒过来的时候了吧、是时候离开了…
		IF CFLAG:TARGET:诶嘿嘿
			PRINTFORMW 挣脱开紧紧抱住的%CALLNAME:(CFLAG:MASTER:招待)%、回到了%GET_MAPNAME(MAIN_MAP)%…
		ELSE
			PRINTFORMW 仿佛要避开%CALLNAME:(CFLAG:MASTER:招待)%火热的视线一般、回到了%GET_MAPNAME(MAIN_MAP)%…
		ENDIF
		GOTO ESCAPE
	ELSEIF TFLAG:泡風呂
		CALL 泡風呂終了判定("你限界")
		GOTO ESCAPE
	ENDIF
	;bug fix
	IF LOCAL:3
		PRINT 快筋疲力尽了
	ELSEIF LOCAL:2
		PRINT 差不多到了回去的时间了
	ENDIF
	SIF !TCVAR:(CFLAG:MASTER:招待):连住禁止
		PRINTFORMW 今天%CALLNAME:(CFLAG:MASTER:招待)%提供了借宿的样子
	SIF TCVAR:(CFLAG:MASTER:招待):连住禁止
		PRINTFORMW 今天也住在%CALLNAME:(CFLAG:MASTER:招待)%这吗？
	PRINTL [0]住下来 [1]回去
	WHILE 1
	INPUT
	SIF RESULT == 0 || RESULT == 1
		BREAK
	WEND
	IF !RESULT && !TCVAR:(CFLAG:MASTER:招待):连住禁止
		PRINTFORMW 恭敬不如从命那就住下来吧
		;约会は終了
		CFLAG:MASTER:约会中 = MAIN_MAP
		CFLAG:TARGET:约会中 = MAIN_MAP
		FLAG:约会的对象 = 0
		TFLAG:约会前好感度 = 0
		TFLAG:约会道中 = 0
		CALL 守矢神簽约会的对象のリセット()
		;以後の処理をカット
		GOTO OTOMARI
	ELSE
		SIF TCVAR:(CFLAG:MASTER:招待):连住禁止
			PRINTFORML 连着在女孩子家里打扰不太好…
		PRINTFORMW 果然还是委婉谢绝吧…
	ENDIF
ENDIF
$ESCAPE
;bug fix
IF LOCAL:3 && CFLAG:MASTER:現在位置
	PRINTL 
	PRINTW ***** 因为气力归０了、踏上了归途 *****
	CALL 帰宅の時間経過
	FLAG:時間停止 = 0
	;(天候パッチ)時刻・天候に応じて背景色を変更
	CALL SET_MAP_WEATHER_BGCOLOR
ELSEIF LOCAL:2
	PRINTL
	IF BASE:MASTER:酒気 >= MAXBASE:MASTER:酒気
		PRINTW ***** 酩酊大醉、着踏上了归途 *****
		PRINTFORMW 回到了%GET_MAPNAME(MAIN_MAP)%的%CALLNAME:MASTER%直接当场倒下了
	ELSE
		PRINTW ***** 因为倦意袭来了、踏上了归途 *****
	ENDIF
	CALL 帰宅の時間経過
ENDIF
IF LOCAL:1 || LOCAL:2 || LOCAL:3 ;bug fix
	FOR LOCAL, 0, CHARANUM
		;bugfix custom code
		SIF !AFFECTED_CHARAS:LOCAL
			CONTINUE

		IF LOCAL && CFLAG:LOCAL:衰弱
			IF !CFLAG:LOCAL:神社在住
				CFLAG:LOCAL:初期位置 = SUKIMA()
				CFLAG:LOCAL:現在位置 = SUKIMA()
				CFLAG:LOCAL:拠点来訪 = 0
				IF IS_HERE(LOCAL) ;bugfix custom code
					CALL KOJO_MESSAGE_SEND("EVENT",20,LOCAL,2,0)
					PRINTFORMW 筋疲力尽的%CALLNAME:LOCAL%回去了
				ENDIF ;bugfix custom code
			ELSE
				;custom code
				IF IS_HERE(LOCAL) ;bugfix custom code
					PRINTFORMW %CALLNAME:LOCAL%回去%NAME_FROM_PLACE(CFLAG:LOCAL:初期位置)%了
				ENDIF ;bugfix custom code
				SIF CFLAG:MASTER:初期位置 != CFLAG:LOCAL:初期位置
					LOCK:(CFLAG:LOCAL:初期位置) = 1
				CFLAG:LOCAL:現在位置 = CFLAG:LOCAL:初期位置
				SIF IS_HERE(LOCAL) ;bugfix custom code
					CALL KOJO_MESSAGE_SEND("EVENT",3,LOCAL,1,0) ;bugfix custom code
			ENDIF
		ENDIF
		CALL SET_DATE(99, LOCAL)
		IF CFLAG:LOCAL:招待
			CFLAG:LOCAL:招待 = 0
			IF LOCAL == 0
			ELSEIF CFLAG:LOCAL:神社在住
				CFLAG:LOCAL:初期位置 = CFLAG:LOCAL:神社在住
			ELSE
				CFLAG:LOCAL:初期位置 = SUKIMA()
			ENDIF
		ENDIF
		SIF CFLAG:LOCAL:陪睡中 > 0
			CFLAG:LOCAL:陪睡中 = 0
		SIF CFLAG:LOCAL:诶嘿嘿
			CALL ENDUFUFU(LOCAL)
		;恶作剧OFF
		CALL SET_PRANK(0,LOCAL,1)
	NEXT
	CALL 守矢神簽约会的对象のリセット()
ENDIF
IF TFLAG:约会道中 && !FLAG:约会的对象
	TIME += TFLAG:约会道中
	TFLAG:约会道中 = 0
	CFLAG:MASTER:現在位置 = GET_ENTRANCE(CFLAG:MASTER:约会中)
ENDIF
$OTOMARI
