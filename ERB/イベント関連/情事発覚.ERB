;FileName_情事发觉.ERB --------------------------------- Rev1.00
;情事の发觉
;CALL		USER
;ARG		0:入室角色NO, 1:发觉状况(0:同室, 1:覗き)
;RETURN		0:同室, 1:立ち去る
;COMMENT	
;-----------------------------------------------------------
@AFFAIR_DISCLOSURE(ARG, NOZOKI)
#DIM PREV_TARGET
#DIM IRAI_ABLE
#DIM NOZOKI
#DIM FORCE_STOP
#DIM FORCE_GETOUT
#DIM WITNESSED
#DIM HAPPENING_TYPE
#DIM NUM_TARGET
NUM_TARGET = GET_TARGETNUM()
VARSET LOCAL
CALL TARGETSET_CHECK
FORCE_STOP = 0
FORCE_GETOUT = 0
HAPPENING_TYPE = 0
WITNESSED = TFLAG:現在的TARGET
SIF CFLAG:MASTER:恶作剧
	WITNESSED = TARGET
IF WITNESSED && (CFLAG:(WITNESSED):诶嘿嘿 || CFLAG:(WITNESSED):恶作剧) && !(CFLAG:ARG:诶嘿嘿 || CFLAG:ARG:恶作剧) && !GETBIT(FLAG:潜伏,0)
	DRAWLINE
	IF CFLAG:(WITNESSED):恶作剧
		PRINTFORMW 对睡着的%CALLNAME:WITNESSED%恶作剧的时候被%CALLNAME:ARG%看到了！
		IF FLAG:催眠強度 > 0
			PRINTFORML %CALLNAME:MASTER%慌张的一边表示这是很正常的按摩行为，一边发动催眠。
			IF FLAG:催眠強度 >= 75
				PRINTFORMW %CALLNAME:ARG%呆愣了一会，有些犹豫的接受了%CALLNAME:MASTER%的解释。
				PRINTL
				PRINTFORMW 真刺激
				PRINTL
			ELSEIF FLAG:催眠強度 <= 74
				PRINTFORMW %CALLNAME:ARG%不信！
				PRINTL
				PRINTFORMW 可恶，催眠深度不够！
				PRINTL
				SIF !CFLAG:ARG:掌握把柄 && CFLAG:(WITNESSED):恶作剧 == 2 
					PRINTFORMW 被%CALLNAME:ARG%掌握住了把柄
			ENDIF
		ELSE
			SIF !CFLAG:ARG:掌握把柄 && CFLAG:(WITNESSED):恶作剧 == 2 
				PRINTFORMW 被%CALLNAME:ARG%掌握住了把柄
			PRINTFORMW %CALLNAME:ARG%露出了傻眼的表情
		ENDIF
		CALL TOUCH_SET(0,0,0,1)
		DRAWLINE
		FORCE_STOP = 1
		SIF FLAG:催眠強度 <= 74
			CFLAG:ARG:掌握把柄 += 10
	ELSEIF TFLAG:102 == 3 && TFLAG:抱住模式 
		PRINTFORMW 和%CALLNAME:(TFLAG:現在的TARGET)%调情的时候被%CALLNAME:ARG%看到了！
		PRINTFORMW %CALLNAME:(TFLAG:現在的TARGET)%遗憾的从%CALLNAME:MASTER%身上离开…
		CALL UWAKI_RECORD(ARG,0,TFLAG:現在的TARGET)
		TFLAG:抱住模式 = 0
		TFLAG:102 = 1
		CALL 被逆推終了
	ELSEIF FLAG:催眠強度 >= 5
		PRINTFORMW %CALLNAME:ARG%看到了和%CALLNAME:(TFLAG:現在的TARGET)%的情事现场！
		PRINTFORML 由于%CALLNAME:ARG%中了催眠术，所以%CALLNAME:MASTER%并不十分慌张…
		PRINTFORMW 要怎么解决目前的局面？
		CALL AFFAIR_DISCLOSURE_RESULT_PRINT(ARG,NOZOKI)
		$INPUT_EMBRACE
		INPUT
		CLEARLINE 1
		IF !NOZOKI
			IF RESULT == 0
				IF FLAG:催眠強度 >= 95
					PRINTFORMW %CALLNAME:MASTER%告诉%CALLNAME:ARG%这是最新的放松按摩方法。
					PRINTFORMW %CALLNAME:ARG%露出了恍然大悟的神情，好奇的靠了过来
					CFLAG:ARG:诶嘿嘿 = CFLAG:(WITNESSED):诶嘿嘿
					CFLAG:ARG:同室 = 1
					CALL KOJO_MESSAGE_SEND("EVENT",6,ARG,WITNESSED,4)
					CALL KOJO_MESSAGE_SEND("EVENT",7,WITNESSED,ARG,4)
					CFLAG:ARG:ヤラせちゃった = 1
				ELSEIF FLAG:催眠強度 >= 75
					PRINTFORMW %CALLNAME:MASTER%邀请%CALLNAME:ARG%体验能让她特别舒服的按摩手法
					PRINTFORMW %CALLNAME:ARG%红着脸看了一会就离开了现场
					CALL KOJO_MESSAGE_SEND("EVENT",6,ARG,WITNESSED,3)
					CALL KOJO_MESSAGE_SEND("EVENT",7,WITNESSED,ARG,3)
					IF CFLAG:ARG:現在位置 == CFLAG:ARG:前一次的位置
						CALL GETOUT(ARG)
					ELSE
						CFLAG:ARG:現在位置 = CFLAG:ARG:前一次的位置
					ENDIF
					;角色を动かしたのでTARGET再设定
					CALL TARGETSET_CHECK
					CFLAG:ARG:同室 = 0
					RETURN 1
				ELSE
					PRINTFORMW %CALLNAME:MASTER%邀请%CALLNAME:ARG%参加能让她特别快乐的游戏
					PRINTFORML 犹豫的%CALLNAME:ARG%看到%CALLNAME:MASTER%下流的样子察觉出不对劲
					PRINTFORML %CALLNAME:ARG%愤怒地把%CALLNAME:MASTER%他们赶走了
					PRINTFORMW 被%CALLNAME:ARG%抓到了把柄
					CALL GET_ANGRY(ARG)
					FORCE_STOP = 1
					FORCE_GETOUT = 1
					CFLAG:ARG:掌握把柄 += 20
					CALL UWAKI_RECORD(ARG)
					;目击者
					CALL KOJO_MESSAGE_SEND("EVENT",6,ARG,WITNESSED,1)
					;被目击者（地位最高的TARGET为代表）
					CALL KOJO_MESSAGE_SEND("EVENT",7,WITNESSED,ARG,1)
					DRAWLINE
				ENDIF
			ELSEIF RESULT == 1
				IF FLAG:催眠強度 >= 75
					PRINTFORMW %CALLNAME:MASTER%解释这是新版的符卡战斗规则。
					PRINTFORMW %CALLNAME:ARG%露出了恍然大悟的神情，红着脸看了一会就离开了现场
					CALL KOJO_MESSAGE_SEND("EVENT",6,ARG,WITNESSED,3)
					CALL KOJO_MESSAGE_SEND("EVENT",7,WITNESSED,ARG,3)
					IF CFLAG:ARG:現在位置 == CFLAG:ARG:前一次的位置
						CALL GETOUT(ARG)
					ELSE
						CFLAG:ARG:現在位置 = CFLAG:ARG:前一次的位置
					ENDIF
					;角色を动かしたのでTARGET再设定
					CALL TARGETSET_CHECK
					CFLAG:ARG:同室 = 0
					RETURN 1
				ELSEIF FLAG:催眠強度 >= 50
					PRINTFORMW %CALLNAME:MASTER%对%CALLNAME:ARG%说这是高级的人体炼成术
					PRINTFORMW %CALLNAME:ARG%说着“那就小点声”然后离开了
					CALL KOJO_MESSAGE_SEND("EVENT",6,ARG,WITNESSED,3)
					CALL KOJO_MESSAGE_SEND("EVENT",7,WITNESSED,ARG,3)
					CALL GET_ANGRY(ARG)
					IF CFLAG:ARG:現在位置 == CFLAG:ARG:前一次的位置
						CALL GETOUT(ARG)
					ELSE
						CFLAG:ARG:現在位置 = CFLAG:ARG:前一次的位置
					ENDIF
					;角色を动かしたのでTARGET再设定
					CALL TARGETSET_CHECK
					CFLAG:ARG:同室 = 0
					RETURN 1
				ELSE
					PRINTFORML %CALLNAME:MASTER%对%CALLNAME:ARG%解释这是正常的造人课程
					PRINTFORMW ……没能成功
					PRINTFORMW %CALLNAME:ARG%愤怒的把%CALLNAME:MASTER%他们赶走了
					PRINTFORMW 被%CALLNAME:ARG%抓到了把柄
					CALL GET_ANGRY(ARG)
					FORCE_STOP = 1
					FORCE_GETOUT = 1
					CFLAG:ARG:掌握把柄 += 20
					CALL UWAKI_RECORD(ARG)
					;目击者
					CALL KOJO_MESSAGE_SEND("EVENT",6,ARG,WITNESSED,1)
					;被目击者（地位最高的TARGET为代表）
					CALL KOJO_MESSAGE_SEND("EVENT",7,WITNESSED,ARG,1)
					DRAWLINE
				ENDIF			
			ELSEIF RESULT == 2
				GOTO 放弃催眠
			ELSE
				CLEARLINE 1
				GOTO INPUT_EMBRACE
			ENDIF
		ELSE
			IF RESULT == 1
				IF FLAG:催眠強度 >= 75
					PRINTFORMW %CALLNAME:MASTER%说着这是最新的符卡战斗修行方式
					PRINTFORMW %CALLNAME:ARG%从%NAME_FROM_PLACE(CFLAG:ARG:現在位置)%\@ TALENT:ARG:羞恥心 > 0 ? 满脸通红、目不转睛的# 饶有兴趣的\@眺望着%CALLNAME:MASTER%那边的行为……
				ELSEIF FLAG:催眠強度 >= 50
					PRINTFORML %CALLNAME:MASTER%发动催眠术邀请%CALLNAME:ARG%参加能让她特别舒服的游戏
					PRINTFORML %CALLNAME:ARG%有些犹豫、\@ TALENT:ARG:羞恥心 > 0 ? 满脸通红的 # 露出很感兴趣的样子\@打听着%CALLNAME:MASTER%那边到底在做什么
					PRINTFORMW 邀请失败的%CALLNAME:MASTER%慌忙把%CALLNAME:ARG%赶走了
					CALL GETOUT(ARG)
					CALL TARGETSET_CHECK
					CFLAG:ARG:同室 = 0
					RETURN 1
				ELSE
					PRINTFORMW %CALLNAME:MASTER%对%CALLNAME:ARG%解释这是学造人的课程					
					PRINTFORML %CALLNAME:ARG%害羞的看了一会后突然发现自己被愚弄了
					PRINTFORMW %CALLNAME:ARG%愤怒了
					CALL GET_ANGRY(ARG)
					SIF !CFLAG:ARG:掌握把柄
						PRINTFORMW 被%CALLNAME:ARG%掌握住了把柄
					CFLAG:ARG:情事目撃 = TARGET
					CALL UWAKI_RECORD(ARG)
				ENDIF
			ELSEIF RESULT == 2
				GOTO 放弃催眠
			ELSE
				CLEARLINE 1
				GOTO INPUT_EMBRACE
			ENDIF
		ENDIF
	ELSE
	$放弃催眠
		IF NOZOKI
			PRINTFORMW 在【%NAME_FROM_PLACE(CFLAG:ARG:現在位置)%】被%CALLNAME:ARG%撞见了与%CALLNAME:(TFLAG:現在的TARGET)%的情事！
		ELSE
			PRINTFORMW %CALLNAME:ARG%闯入了和%CALLNAME:(TFLAG:現在的TARGET)%的情事现场！
		ENDIF
		CALL UWAKI_RECORD(ARG)
		;恋人之间（或者恋人在场）的情况
		IF TALENT:MASTER:恋人 && (TALENT:(TFLAG:現在的TARGET):恋人 || WITH_恋人())
			;闯入
			IF !NOZOKI
				[SKIPSTART]
				;有恋慕或关系较好的邀请方案，弃用
				IF (CFLAG:ARG:6 > 1 || TALENT:ARG:恋慕)
					PRINTFORMW %CALLNAME:ARG%面色不豫的样子…
					PRINTL [0] 一并推倒
					PRINTL [1] 还是算了
					$INPUT_EMBRACE
					INPUT
					IF RESULT == 0
						IF !NOZOKI
							PRINTFORMW %CALLNAME:MASTER%一招手示意%CALLNAME:ARG%就好像很开心的依偎了过来
							CFLAG:ARG:诶嘿嘿 = CFLAG:(WITNESSED):诶嘿嘿
							CFLAG:ARG:同室 = 1
							;目击者
							CALL KOJO_MESSAGE_SEND("EVENT",6,ARG,WITNESSED,4)
							;被目击者（地位最高的TARGET为代表）
							CALL KOJO_MESSAGE_SEND("EVENT",7,WITNESSED,ARG,4)
						ENDIF
					ELSEIF RESULT == 1
						PRINTFORMW %CALLNAME:ARG%和%CALLNAME:MASTER%一对上视线就慌慌张张地离开了现场…
						IF !NOZOKI
							;目击者
							CALL KOJO_MESSAGE_SEND("EVENT",6,ARG,WITNESSED,3)
							;被目击者（地位最高的TARGET为代表）
							CALL KOJO_MESSAGE_SEND("EVENT",7,WITNESSED,ARG,3)
						ENDIF
						IF CFLAG:ARG:現在位置 == CFLAG:ARG:前一次的位置
							CALL GETOUT(ARG)
						ELSE
							CFLAG:ARG:現在位置 = CFLAG:ARG:前一次的位置
						ENDIF
						;角色を动かしたのでTARGET再设定
						CALL TARGETSET_CHECK
						CFLAG:ARG:同室 = 0
						RETURN 1
					ELSE
						CLEARLINE 1
						GOTO INPUT_EMBRACE
					ENDIF
				[SKIPEND]
				;如果察觉方是恋人，则会加入进来
				IF TALENT:ARG:恋人
					;虽然新增了恋人离开的情况，但这是仅限口上侧有特殊的处子保护标志介入保护角色：口上内抱き寄せ判定_初回，不影响既往的口上事件写法和判定
					CALL 情事中合意取得(ARG)
					IF TFLAG:不让推倒
						PRINTFORMW %CALLNAME:ARG%神情复杂地移开视线、踉踉跄跄地逃离了现场…
						;目击者
						CALL KOJO_MESSAGE_SEND("EVENT",6,ARG,WITNESSED,3)
						;被目击者（地位最高的TARGET为代表）
						CALL KOJO_MESSAGE_SEND("EVENT",7,WITNESSED,ARG,3)
						IF CFLAG:ARG:現在位置 == CFLAG:ARG:前一次的位置
							CALL GETOUT(ARG)
						ELSE
							CFLAG:ARG:現在位置 = CFLAG:ARG:前一次的位置
						ENDIF
						;角色移动了，所以重新设定TARGET
						CALL TARGETSET_CHECK
						CFLAG:ARG:同室 = 0
						IF !CFLAG:ARG:情事目撃
							TALENT:ARG:心情 = MIN(TALENT:ARG:心情, 0)
							BASE:ARG:憤怒 += MAXBASE:ARG:憤怒 / 2
						ENDIF
						CFLAG:ARG:情事目撃 = TARGET
						RETURN 1
					ELSE
						CFLAG:ARG:诶嘿嘿 = CFLAG:(WITNESSED):诶嘿嘿
						;目击者
						CALL KOJO_MESSAGE_SEND("EVENT",6,ARG,WITNESSED,2)
						CALL UWAKI_RECORD(ARG,1)
						;被目击者（地位最高的TARGET为代表）
						CALL KOJO_MESSAGE_SEND("EVENT",7,WITNESSED,ARG,2)
						CALL UWAKI_RECORD(WITNESSED, 1)
						DRAWLINE
						RETURN 0
					ENDIF
				;因为是恋人间认同的性爱，无关者只能回避
				ELSE
					PRINTFORMW %CALLNAME:ARG%和%CALLNAME:MASTER%一对上视线就慌慌张张地离开了现场…
					;目击者
					CALL KOJO_MESSAGE_SEND("EVENT",6,ARG,WITNESSED,3)
					;被目击者（地位最高的TARGET为代表）
					CALL KOJO_MESSAGE_SEND("EVENT",7,WITNESSED,ARG,3)
					IF CFLAG:ARG:現在位置 == CFLAG:ARG:前一次的位置
						CALL GETOUT(ARG)
					ELSE
						CFLAG:ARG:現在位置 = CFLAG:ARG:前一次的位置
					ENDIF
					;角色移动了，所以重新设定TARGET
					CALL TARGETSET_CHECK
					CFLAG:ARG:同室 = 0
					RETURN 1
				ENDIF
			;偷窥
			ELSE
				PRINTFORMW %CALLNAME:ARG%面色不豫地背过脸去慌慌张张地离开了现场…
				IF !NOZOKI
					;目击者
					CALL KOJO_MESSAGE_SEND("EVENT",6,ARG,WITNESSED,3)
					;被目击者（地位最高的TARGET为代表）
					CALL KOJO_MESSAGE_SEND("EVENT",7,WITNESSED,ARG,3)
				ENDIF
				IF CFLAG:ARG:現在位置 == CFLAG:ARG:前一次的位置
					CALL GETOUT(ARG)
				ELSE
					CFLAG:ARG:現在位置 = CFLAG:ARG:前一次的位置
				ENDIF
				;角色移动了，所以重新设定TARGET… 但是因为偷窥者并不在场，大概不会有问题。
				CALL TARGETSET_CHECK
				CFLAG:ARG:同室 = 0
				RETURN 1
			ENDIF
		;从此处往下都是性爱中不包含恋人的情况
		ELSEIF (TALENT:ARG:淫乱 || TALENT:ARG:風騷)
			PRINTFORMW %CALLNAME:MASTER%一招手示意%CALLNAME:ARG%就好像很开心的依偎了过来
			CFLAG:ARG:诶嘿嘿 = CFLAG:(WITNESSED):诶嘿嘿
			CFLAG:ARG:同室 = 1
			;目击者
			CALL KOJO_MESSAGE_SEND("EVENT",6,ARG,WITNESSED,4)
			;被目击者（地位最高的TARGET为代表）
			CALL KOJO_MESSAGE_SEND("EVENT",7,WITNESSED,ARG,4)
		ELSEIF TALENT:ARG:無知 && !(CFLAG:ARG:既成事實 & 合意_诶嘿嘿)
		;无知，不懂在干什么
			IF NOZOKI
				PRINTFORMW %CALLNAME:ARG%从%NAME_FROM_PLACE(CFLAG:ARG:現在位置)%\@ TALENT:ARG:羞恥心 > 0 ? 满脸通红、目不转睛的# 饶有兴趣的\@打量着%CALLNAME:MASTER%那边的行为……
			ELSE
				PRINTFORML %CALLNAME:ARG%\@ TALENT:ARG:羞恥心 > 0 ? 满脸通红的 # 露出很感兴趣的样子\@凑上来打听%CALLNAME:MASTER%你们到底在做什么
				PRINTFORMW %CALLNAME:MASTER%他们哭笑不得地把%CALLNAME:ARG%哄走了
				CALL GETOUT(ARG)
				CALL TARGETSET_CHECK
				CFLAG:ARG:同室 = 0
				RETURN 1
			ENDIF
		;在这个角色的寝室办事
		ELSEIF CFLAG:ARG:初期位置 == CFLAG:MASTER:現在位置
			;允许了室友和玩家在共用寝室办事进入加入或回避分支
			SIF PRIVATEROOM_CHECK()
				GOTO 目击者与被目击者共用私室
			PRINTFORML 发现自己的房间被当做偷情地点的%CALLNAME:ARG%、愤怒的把%CALLNAME:MASTER%他们赶出去了
			PRINTFORMW 被%CALLNAME:ARG%掌握住了把柄
			CALL GET_ANGRY(ARG)
			FORCE_STOP = 1
			FORCE_GETOUT = 1
			CFLAG:ARG:掌握把柄 += 20
			IF !NOZOKI
				;目击者
				CALL KOJO_MESSAGE_SEND("EVENT",6,ARG,WITNESSED,1)
				;被目击者（地位最高的TARGET为代表）
				CALL KOJO_MESSAGE_SEND("EVENT",7,WITNESSED,ARG,1)
				DRAWLINE
			ENDIF
		ELSEIF CFLAG:ARG:行動 == 5 && CFLAG:MASTER:現在位置 == CFLAG:ARG:職場
			PRINTFORML 发现工作场所被当做偷情地点的%CALLNAME:ARG%、愤怒的把%CALLNAME:MASTER%他们赶出去了
			PRINTFORMW 被%CALLNAME:ARG%掌握住了把柄
			CALL GET_ANGRY(ARG)
			FORCE_STOP = 1
			FORCE_GETOUT = 1
			CFLAG:ARG:掌握把柄 += 20
			IF !NOZOKI
				;目击者
				CALL KOJO_MESSAGE_SEND("EVENT",6,ARG,WITNESSED,1)
				;被目击者（地位最高的TARGET为代表）
				CALL KOJO_MESSAGE_SEND("EVENT",7,WITNESSED,ARG,1)
				DRAWLINE
			ENDIF
		;在第三方的私人房间乱搞，被来访者撞见并掌握把柄，来访者就此进入打断情事
		;采用PRIVATEROOM的原判断有问题，见@ROOMSETTING;1.共用寝室的target可能不是私室家主；2.如恋人在场一样，家主在场但target不一定是家主
		ELSEIF CFLAG:ARG:態度 < 3 && !PRIVATEROOM_CHECK()
			SIF !CFLAG:ARG:掌握把柄
				PRINTFORMW 被%CALLNAME:ARG%掌握住了把柄
			IF !NOZOKI
				;目击者
				CALL KOJO_MESSAGE_SEND("EVENT",6,ARG,WITNESSED,1)
				;被目击者（地位最高的TARGET为代表）
				CALL KOJO_MESSAGE_SEND("EVENT",7,WITNESSED,ARG,1)
			ENDIF
			CALL TOUCH_SET(0,0,0,1)
			DRAWLINE
			FORCE_STOP = 1
			CFLAG:ARG:掌握把柄 += 10
		ELSE
		$目击者与被目击者共用私室
			IF !NOZOKI
				;如果只看态度，会造成取得合意的情况，所以只有在有合意的情况下才会屈从
				IF TCVAR:MASTER:亲热 >= 3 && (GETBIT(CFLAG:ARG:既成事實 , 1) || TALENT:ARG:合意誤認)
					PRINTFORMW %CALLNAME:ARG%屈从于情爱的气氛、依偎到了%CALLNAME:MASTER%的身边
					CFLAG:ARG:诶嘿嘿 = CFLAG:(WITNESSED):诶嘿嘿
					;姑且，情绪达到可以推倒的程度
					;理性虽然与推倒无关，但如果理性的话就不会掺和进来吧
					BASE:ARG:情緒 = MAX(BASE:ARG:情緒, 300)
					BASE:ARG:理性 = MIN(BASE:ARG:理性, 300)
					;目击者
					CALL KOJO_MESSAGE_SEND("EVENT",6,ARG,WITNESSED,2)
					CALL UWAKI_RECORD(ARG,1)
					;被目击者（地位最高的TARGET为代表）
					CALL KOJO_MESSAGE_SEND("EVENT",7,WITNESSED,ARG,2)
					CALL UWAKI_RECORD(WITNESSED,1)
					DRAWLINE
					RETURN 0
				ELSE
					PRINTFORMW %CALLNAME:ARG%\@TALENT:ARG:恋慕 ?满脸失望的 #不高兴的转过了脸、匆忙 \@离开了…
					;目击者
					CALL KOJO_MESSAGE_SEND("EVENT",6,ARG,WITNESSED,3)
					;被目击者（地位最高的TARGET为代表）
					CALL KOJO_MESSAGE_SEND("EVENT",7,WITNESSED,ARG,3)
					IF CFLAG:ARG:現在位置 == CFLAG:ARG:前一次的位置
						CALL GETOUT(ARG)
					ELSE
						CFLAG:ARG:現在位置 = CFLAG:ARG:前一次的位置
					ENDIF
					CALL TARGETSET_CHECK
					CFLAG:ARG:同室 = 0
					IF TALENT:ARG:恋慕 && !CFLAG:ARG:情事目撃
						TALENT:ARG:心情 = MIN(TALENT:ARG:心情, 0)
						BASE:ARG:憤怒 += MAXBASE:ARG:憤怒 / 2
					ENDIF
					CFLAG:ARG:情事目撃 = TARGET
					RETURN 1
				ENDIF
			;覗かれた场合
			ELSE
				PRINTFORMW %CALLNAME:ARG%\@TALENT:ARG:恋慕 ?满脸失望 #不高兴的转过了脸 \@
				IF TALENT:ARG:恋慕 && !CFLAG:ARG:情事目撃
					IF TCVAR:ARG:亲热 < 3
						TALENT:ARG:心情 = MIN(TALENT:ARG:心情, 0)
						BASE:ARG:憤怒 += MAXBASE:ARG:憤怒 / 4
					ENDIF
					CFLAG:ARG:情事目撃 = TARGET
				ENDIF
			ENDIF
		ENDIF
	ENDIF
ENDIF
IF FORCE_STOP
	SIF CFLAG:MASTER:恶作剧
		CALL SET_PRANK(0,TARGET,2)
	CALL ENDUFUFU_ALL
	IF FORCE_GETOUT
		CALL GETOUT(MASTER)
		FOR LOCAL,1,NUM_TARGET + 1
			SIF (TARGET:LOCAL) == ARG
				CONTINUE
			SIF CFLAG:(TARGET:LOCAL):睡眠
				CONTINUE
			CALL GETOUT(TARGET:LOCAL)
		NEXT
	ENDIF
	CALL TARGETSET_CHECK
	CFLAG:MASTER:现在忙得抽不出手 = 0
	TFLAG:106 = 0
	;2025年7月22日 如果逐出了，则玩家与目击者不在同一地点，但也不要更新同室状态
	RETURN FORCE_GETOUT
ENDIF

;通常遭遇
IF !CFLAG:ARG:睡眠 && (!NOZOKI || CFLAG:ARG:現在位置 == CFLAG:MASTER:現在位置) && !FLAG:時間停止 && !CHK_DATENOW(CFLAG:ARG:约会中) && (TIME != TIME:3 || CFLAG:MASTER:現在位置 != CFLAG:MASTER:初期位置 || CFLAG:MASTER:あなた前ターン位置 != CFLAG:MASTER:初期位置) && !GETBIT(FLAG:潜伏,0)
	;外出时偶然遭遇
	IF CHK_DATENOW(CFLAG:MASTER:约会中)
		CALL PRINT_FACE, ARG, "通常"
		IF !CFLAG:(NO:ARG):面識 && TALENT:ARG:路人
			PRINTFORMW %CALLNAME:MASTER%向%CALLNAME:ARG%打了声招呼
		ELSE
			IF !TCVAR:ARG:今天有否见面 && !cMorningGreeting:ARG:0
				PRINTFORMW 在目的地遇到了%CALLNAME:ARG%
			ELSE
				PRINTFORMW 出门在外又遇到了%CALLNAME:ARG%
			ENDIF
		ENDIF
		IF CFLAG:(NO:ARG):面識 == 0
			CALL SYOTAIMEN(ARG)
		ELSE
			FOR LOCAL:4,1,CHARANUM
				IF CHK_DATENOW(CFLAG:(LOCAL:4):约会中)
					IF !TCVAR:ARG:今天有否见面
						CALL KOJO_MESSAGE_SEND("EVENT",1,ARG,9,LOCAL:4)
					ENDIF
					CALL KOJO_MESSAGE_SEND("EVENT",1,ARG,7,LOCAL:4)
					BREAK
				ELSEIF LOCAL:4 == CHARANUM - 1
					IF !TCVAR:ARG:今天有否见面
						CALL KOJO_MESSAGE_SEND("EVENT",1,ARG,9,0)
					ENDIF
					CALL KOJO_MESSAGE_SEND("EVENT",1,ARG,6,0)
				ENDIF
			NEXT
		ENDIF
	;是否从刚才就知道她在这里
	ELSEIF CFLAG:ARG:前一次的位置 == CFLAG:ARG:現在位置
		CALL PRINT_FACE, ARG, "通常"
		IF CFLAG:ARG:同室 ;custom code, if you've already seen them while time is stopped or they're asleep
			PRINTFORMW %CALLNAME:ARG%正待在这里
			SIF !TCVAR:ARG:今天有否见面 && !cMorningGreeting:ARG:0
				PRINTFORML 看到%CALLNAME:ARG%的%CALLNAME:MASTER%、意识到今天还没打招呼呢
		ELSE
			PRINTFORM %CALLNAME:ARG%\@CAN_MOVE(CFLAG:MASTER:前一次的位置, CFLAG:ARG:現在位置) == 2 ?  # 还\@在
			PRINTFORMW %NAME_FROM_PLACE(CFLAG:ARG:現在位置)%呢
			SIF !TCVAR:ARG:今天有否见面 && !cMorningGreeting:ARG:0
				PRINTFORML 看到%CALLNAME:ARG%的%CALLNAME:MASTER%、意识到今天还没打招呼呢
		ENDIF
		HAPPENING_TYPE = 1
	;澡堂に入ってきた场合
	ELSEIF CAN_MOVE(CFLAG:MASTER:前一次的位置, CFLAG:ARG:前一次的位置) < 2 && CFLAG:ARG:前一次的位置 != CFLAG:MASTER:前一次的位置 && BATHCHECK(CFLAG:MASTER:現在位置)
		CALL PRINT_FACE, ARG, "通常", "裸", "", ""
		PRINTFORMW %CALLNAME:ARG%进入了浴场…
		IF 風呂逐出条件(ARG)
			IF CFLAG:ARG:掌握把柄 && HETEROSEX(MASTER,ARG) && FLAG:催眠強度 < 50
				PRINTFORMW %CALLNAME:MASTER%没法违抗%CALLNAME:ARG%被从浴场里赶了出来
				CFLAG:MASTER:現在位置 = KICKOUT()
				HAPPENING_TYPE = 5
				CALL TARGETSET_CHECK
				CFLAG:ARG:同室 = 0
			ELSE
				PRINTFORMW %CALLNAME:ARG%和%CALLNAME:MASTER%一对上视线就慌慌张张地离开了现场…
				CFLAG:ARG:現在位置 = CFLAG:ARG:前一次的位置
				CALL TARGETSET_CHECK
				CFLAG:ARG:同室 = 0
				HAPPENING_TYPE = 4
			ENDIF
		ELSE
			PRINTFORMW %CALLNAME:ARG%好像想和%CALLNAME:MASTER%一起进来的样子
			SIF !TCVAR:ARG:今天有否见面 && !cMorningGreeting:ARG:0
				PRINTFORML %CALLNAME:MASTER%注意到他今天还没有向%CALLNAME:ARG%打招呼呢
			HAPPENING_TYPE = 3
		ENDIF
	;厕所に入ってきた场合
	ELSEIF CAN_MOVE(CFLAG:MASTER:前一次的位置, CFLAG:ARG:前一次的位置) < 2 && CFLAG:ARG:前一次的位置 != CFLAG:MASTER:前一次的位置 && IN_TOILET(CFLAG:MASTER:現在位置)
		PRINTW 有人在敲门
		$TOILET_INPUT_LOOP
		PRINTL [0]里面有人ー　[1]请进
		INPUT
		IF RESULT == 1
			PRINTFORMW %CALLNAME:ARG%进到了%NAME_FROM_PLACE(CFLAG:ARG:現在位置)%里来
			HAPPENING_TYPE = 2
		ELSEIF RESULT == 0
			PRINTW 脚步声远去了
			CFLAG:ARG:現在位置 = CFLAG:ARG:前一次的位置
			CALL TARGETSET_CHECK
			CFLAG:ARG:同室 = 0
			HAPPENING_TYPE = -1
		ELSE
			GOTO TOILET_INPUT_LOOP
		ENDIF
	;ELSEIF CAN_MOVE(CFLAG:MASTER:前一次的位置, CFLAG:ARG:前一次的位置) < 2 && CFLAG:ARG:前一次的位置 != CFLAG:MASTER:前一次的位置
	;↑の条件では来访时に初对面口上が出ない场合があるので变更
	ELSEIF CFLAG:ARG:前一次的位置 != CFLAG:MASTER:前一次的位置
		CALL CHANGE_CLOTHES(ARG, "衣装リセット")
		CALL CLOTHES_SETTING_TRAIN(ARG)
		CALL PRINT_FACE, ARG, "通常"
		IF CFLAG:ARG:前一次的位置 != MAXROOM && CFLAG:ARG:前一次的位置 != OMANEKIBEYA()
			PRINTFORMW %CALLNAME:ARG%来到了%NAME_FROM_PLACE(CFLAG:ARG:現在位置)%
		ENDIF
		SIF !TCVAR:ARG:今天有否见面 && !cMorningGreeting:ARG:0
			PRINTFORML %CALLNAME:MASTER%看到%CALLNAME:ARG%了、回想起来今天还没有打招呼呢
		HAPPENING_TYPE = 2
	ENDIF
	;2025年7月9日 在外出与据点见面的统一状态处理中，需要HAPPENING_TYPE > 0来排除在外见面的情况
	IF !FLAG:70 && CFLAG:ARG:現在位置 == CFLAG:MASTER:現在位置
		IF !NOZOKI && HAPPENING_TYPE > 0
			;初对面
			IF CFLAG:(NO:ARG):面識 == 0
				CALL SYOTAIMEN(ARG)
			ELSEIF CFLAG:ARG:情事目撃 && CFLAG:(CFLAG:ARG:情事目撃):現在位置 != CFLAG:ARG:現在位置
				PRINTFORMW 被%CALLNAME:ARG%追问了刚才和%CALLNAME:(CFLAG:ARG:情事目撃)%在一起的事情…
				CALL KOJO_MESSAGE_SEND("EVENT",8,ARG,CFLAG:ARG:情事目撃,1)
				CFLAG:ARG:情事目撃 = 0
			ELSEIF CFLAG:ARG:情事目撃 && CFLAG:(CFLAG:ARG:情事目撃):現在位置 == CFLAG:ARG:現在位置
				PRINTFORMW %CALLNAME:ARG%就刚才的事情对%CALLNAME:MASTER%和%CALLNAME:(CFLAG:ARG:情事目撃)%进行了说教…
				CALL KOJO_MESSAGE_SEND("EVENT",8,ARG,CFLAG:ARG:情事目撃,2)
				CFLAG:ARG:情事目撃 = 0
			ELSE
				IF !TCVAR:ARG:今天有否见面
					CALL KOJO_MESSAGE_SEND("EVENT",1,ARG,8,0)
				ENDIF
				;通常遭遇口上
				CALL KOJO_MESSAGE_SEND("EVENT",1,ARG,HAPPENING_TYPE,0)
			ENDIF
		ENDIF
		CALL FirstMeetToday(ARG)
	ENDIF
	;2025年7月22日 RETURN 0用来结算同室状态，必须让同室==0的情况返回1
	SIF GROUPMATCH(HAPPENING_TYPE,-1,4,5)
		RETURN 1
ENDIF

@OSASOI(ARG)
#DIM 目的地
#DIM 自宅地域
#DIM 现在地域

自宅地域 = CFLAG:ARG:自宅位置 / 100
现在地域 = GET_MAPID(CFLAG:MASTER:現在位置)
;仕事がある日は诱わない
SIF !CHARA_HOLIDAY(ARG)
	RETURN
SIF GROUPMATCH(TIME:5, 5, 9)
	RETURN
SIF !AT_HOME(MASTER)
	RETURN
IF (TALENT:ARG:恋慕 || TALENT:ARG:思慕) && CFLAG:ARG:宴会参加 && TFLAG:宴会場 / 100 != MAIN_MAP && TFLAG:宴会場 / 100 != 现在地域 && (FLAG:開始日 - DAY) == 0
	目的地 = TFLAG:宴会場 / 100
	PRINTFORML %CALLNAME:ARG%邀请你一起去今天有宴会的%GET_MAPNAME(目的地)%
	GOTO INVITE_ENKAI
ENDIF
IF TALENT:ARG:恋慕 && !RAND:5 && !CFLAG:ARG:宴会参加
	IF CFLAG:ARG:积攒度 >= (700 + TALENT:ARG:羞恥心 * 150) && !CFLAG:ARG:神社在住 && !CFLAG:ARG:自宅位置
		PRINTFORML 被%CALLNAME:ARG%邀请去房间
		CALL KOJO_MESSAGE_SEND("EVENT",27,ARG,3,自宅地域)
		CALL ASK_YN("去","不去")
		IF !RESULT
			PRINTFORML 接受了%CALLNAME:ARG%的邀请、%CALLNAME:MASTER%走向了她的房间……
			CALL KOJO_MESSAGE_SEND("EVENT",27,ARG,4,自宅地域)
			PRINTFORMW +{TIME_REQUIRED(现在地域, 自宅地域)}分
			TIME += TIME_REQUIRED(现在地域, 自宅地域)
			CFLAG:ARG:约会中 = 自宅地域
			CFLAG:MASTER:约会中 = 自宅地域
			FLAG:约会的对象 = ARG
			CFLAG:MASTER:同行 = 60
			CFLAG:ARG:同行 = 60
			TFLAG:约会前好感度 = CFLAG:ARG:2
			TARGET = ARG
			CALL OMANEKI_ENTER(ARG)
			CALL TARGETSET_CHECK
			;お诱いに乘ったか断ったかをRETURNで返すようにしました
			RETURN 1
		ELSE
			PRINTFORMW %CALLNAME:ARG%内心非常遗憾
			CALL KOJO_MESSAGE_SEND("EVENT",27,ARG,5,自宅地域)
			RETURN 0
		ENDIF
	ELSEIF !RAND:4 && CFLAG:ARG:經常去的地方 >= 0 && CFLAG:ARG:經常去的地方 != MAIN_MAP
		目的地 = CFLAG:ARG:經常去的地方
	ELSE
		目的地 = RAND:10
		SIF 目的地 == MAIN_MAP
			RETURN
	ENDIF
	;要求移动时间が60以上だと不发
	IF TIME_REQUIRED(现在地域, 目的地) >= 60
		RETURN 0
	ENDIF
	PRINTFORML %CALLNAME:ARG%邀请去%GET_MAPNAME(目的地)%
	$INVITE_ENKAI
	CALL KOJO_MESSAGE_SEND("EVENT",27,ARG,0,目的地)
	CALL ASK_YN("去","不去")
	IF !RESULT
		CALL KOJO_MESSAGE_SEND("EVENT",27,ARG,1,目的地)
		CFLAG:MASTER:同行 = 60
		CFLAG:ARG:同行 = 60
		TFLAG:约会前好感度 = CFLAG:ARG:2
		CALL SET_DATE(目的地, ARG, TIME_REQUIRED(CFLAG:MASTER:约会中, 目的地) - 1000)
		CALL TARGETSET_CHECK
		RETURN 1
	ELSE
		PRINTFORMW %CALLNAME:ARG%表示遗憾
		CALL KOJO_MESSAGE_SEND("EVENT",27,ARG,2,目的地)
		RETURN 0
	ENDIF
ENDIF

RETURN 0



@UWAKI_RECORD(ARG,公认,浮气相手)
#DIM 浮气相手
#DIM 公认

SIF UWAKI_KNOWN:ARG:0 == ""
	UWAKI_KNOWN:ARG:0 = /
SIF UWAKI_ACCEPTED:ARG:0 == ""
	UWAKI_ACCEPTED:ARG:0 = /


IF 浮气相手
	LOCALS = %CALLNAME:浮气相手%
	SIF !STRCOUNT(UWAKI_KNOWN:ARG:0,LOCALS)
		UWAKI_KNOWN:ARG:0 = %UWAKI_KNOWN:ARG:0%%LOCALS%/
	SIF !STRCOUNT(UWAKI_ACCEPTED:ARG:0,LOCALS) && 公认
		UWAKI_ACCEPTED:ARG:0 = %UWAKI_ACCEPTED:ARG:0%%LOCALS%/
ELSE
	;第三参数が0の场合、你と同じ场所にいる角色全员记录
	FOR LOCAL,1,CHARANUM
		SIF LOCAL == ARG
			CONTINUE
		SIF CFLAG:LOCAL:現在位置 != CFLAG:MASTER:現在位置
			CONTINUE
		;迫る予定の无い人を弹く应急处置。
		SIF !(TCVAR:MASTER:亲热 >= 3 && GETBIT(CFLAG:LOCAL:既成事實, 1))
			CONTINUE
		LOCALS = %CALLNAME:LOCAL%
		SIF !STRCOUNT(UWAKI_KNOWN:ARG:0,LOCALS)
			UWAKI_KNOWN:ARG:0 = %UWAKI_KNOWN:ARG:0%%LOCALS%/
		SIF !STRCOUNT(UWAKI_ACCEPTED:ARG:0,LOCALS) && 公认
			UWAKI_ACCEPTED:ARG:0 = %UWAKI_ACCEPTED:ARG:0%%LOCALS%/
	NEXT
ENDIF

@GET_UWAKI_HISTORY(ARG,浮气相手)
;公认だと2を返す
#FUNCTION
#DIM 浮气相手
LOCALS = /%CALLNAME:浮气相手%/

IF STRCOUNT(UWAKI_ACCEPTED:ARG:0,LOCALS)
	RETURNF 2
ELSEIF STRCOUNT(UWAKI_KNOWN:ARG:0,LOCALS)
	RETURNF 1
ELSE
	RETURNF 0
ENDIF

@SYOTAIMEN(ARG)

;----------魔改内容，无基础相性设定则进行相性初始设定----------
;TODO-------------相性机制优化，预留工作----------------------
;SIF !RELATION:ARG:MASTER
;	RELATION:ARG:MASTER = 100
;SIF !RELATION:MASTER:ARG
;	RELATION:MASTER:ARG = 100
;--------------------魔改内容结束----------------------------

SIF CFLAG:ARG:面識
	RETURN
IF TALENT:ARG:路人 == 1
	IF TALENT:MASTER:謎之魅力
		PRINTFORMW %CALLNAME:ARG%感到%CALLNAME:MASTER%的【谜之魅力】、胸口剧烈的跳动
		CALL COLORMESSAGE(@"好感和亲密提升了",C_YELLOW,2,1)
		ABL:ARG:親密 += 2
		CFLAG:ARG:好感度 += 300
	ENDIF
	IF TALENT:MASTER:魅惑
		PRINTFORMW %CALLNAME:ARG%被%CALLNAME:MASTER%【魅惑】了
		CALL COLORMESSAGE(@"好感和亲密提升了",C_YELLOW,2,1)
		ABL:ARG:親密 += 2
		CFLAG:ARG:好感度 += 300
	ENDIF
	IF TALENT:MASTER:形状 > 0
		IF TALENT:MASTER:禁断的知識 == 1
			IF TALENT:MASTER:形状 < 3
				PRINTFORMW %CALLNAME:ARG%注意到%CALLNAME:MASTER%下体微小的鼓包
				PRINTFORMW 露出了很失望的样子
				PRINTFORMW %CALLNAME:MASTER%让小兄弟旋转了起来
				PRINTFORMW %CALLNAME:ARG%惊奇不已！
				CALL COLORMESSAGE(@"好感和亲密上升了",C_YELLOW,2,1)
				ABL:ARG:親密 += 2
				CFLAG:ARG:好感度 += 300
			ELSEIF TALENT:MASTER:形状 == 4
				PRINTFORMW %CALLNAME:ARG%看到%CALLNAME:MASTER%的下体在发光
				PRINTFORMW 认为%CALLNAME:MASTER%是个奇怪的人
				PRINTFORMW %CALLNAME:MASTER%让小兄弟不断闪烁五颜六色的光
				PRINTFORMW %CALLNAME:ARG%被迷惑了！
				CALL COLORMESSAGE(@"好感和亲密上升了",C_YELLOW,2,1)
				ABL:ARG:親密 += 2
				CFLAG:ARG:好感度 += 300
			ELSEIF TALENT:MASTER:形状 == 5
				PRINTFORMW %CALLNAME:ARG%注意到%CALLNAME:MASTER%下体的鼓包奇形怪状
				PRINTFORMW 对%CALLNAME:MASTER%感到很害怕
				PRINTFORMW %CALLNAME:MASTER%让小兄弟像震动棒一样震动
				PRINTFORMW %CALLNAME:ARG%被吸引了！
				CALL COLORMESSAGE(@"好感和亲密上升了",C_YELLOW,2,1)
				ABL:ARG:親密 += 2
				CFLAG:ARG:好感度 += 300
			ELSEIF TALENT:MASTER:形状 == 6
				PRINTFORMW %CALLNAME:ARG%注意到%CALLNAME:MASTER%下体巨大的鼓包
				PRINTFORMW 认为%CALLNAME:MASTER%在兴奋着而感到厌恶
				PRINTFORMW %CALLNAME:MASTER%让小兄弟挺立起来
				PRINTFORMW %CALLNAME:ARG%没想到竟如此巨大！
				CALL COLORMESSAGE(@"好感和亲密上升了",C_YELLOW,2,1)
				ABL:ARG:親密 += 2
				CFLAG:ARG:好感度 += 300
			ELSEIF TALENT:MASTER:形状 == 10
				PRINTFORMW ！！！
				PRINTFORMW %CALLNAME:ARG%的目光被%CALLNAME:MASTER%马一样的阴茎撑起的鼓包吸引
				PRINTFORMW 被吸引的目光无法移开
				CALL COLORMESSAGE(@"好感和亲密上升了",C_YELLOW,2,1)
				ABL:ARG:親密 += 2
				CFLAG:ARG:好感度 += 300
			ENDIF
		ELSE
			IF TALENT:MASTER:形状 < 3
				PRINTFORMW %CALLNAME:ARG%注意到%CALLNAME:MASTER%下体微小的鼓包
				PRINTFORMW 露出了很失望的样子
				CALL COLORMESSAGE(@"好感和亲密下降了",C_RED,2,1)
				ABL:ARG:親密 -= 2
				CFLAG:ARG:好感度 -= 300
			ELSEIF TALENT:MASTER:形状 == 4
				PRINTFORMW %CALLNAME:ARG%看到%CALLNAME:MASTER%的下体在发光
				PRINTFORMW 认为%CALLNAME:MASTER%是个奇怪的人
				CALL COLORMESSAGE(@"好感和亲密下降了",C_RED,2,1)
				ABL:ARG:親密 -= 2
				CFLAG:ARG:好感度 -= 300
			ELSEIF TALENT:MASTER:形状 == 5
				PRINTFORMW %CALLNAME:ARG%注意到%CALLNAME:MASTER%下体的鼓包奇形怪状
				PRINTFORMW 对%CALLNAME:MASTER%感到很害怕
				CALL COLORMESSAGE(@"好感和亲密下降了",C_RED,2,1)
				ABL:ARG:親密 -= 2
				CFLAG:ARG:好感度 -= 300
			ELSEIF TALENT:MASTER:形状 == 6
				PRINTFORMW %CALLNAME:ARG%注意到%CALLNAME:MASTER%下体巨大的鼓包
				PRINTFORMW 认为%CALLNAME:MASTER%在兴奋着而感到厌恶
				CALL COLORMESSAGE(@"好感和亲密下降了",C_RED,2,1)
				ABL:ARG:親密 -= 2
				CFLAG:ARG:好感度 -= 300
			ELSEIF TALENT:MASTER:形状 == 10
				PRINTFORMW %CALLNAME:ARG%盯着%CALLNAME:MASTER%马一样的阴茎撑起的巨大鼓包
				PRINTFORMW 认为%CALLNAME:MASTER%是个伪装成人类的马妖怪
				CALL COLORMESSAGE(@"好感和亲密下降了",C_RED,2,1)
				ABL:ARG:親密 -= 2
				CFLAG:ARG:好感度 -= 300
			ENDIF
		ENDIF
	ENDIF
ENDIF
IF !FLAG:扮演
	SETCOLOR 0xc53d43
	SIF NO:ARG != 拠点_家主
		PRINTFORMW %CALLNAME:ARG%向初次见面的%CALLNAME:MASTER%作了问候
	RESETCOLOR
	CALL KOJO_MESSAGE_SEND("ENCOUNTER",0,ARG,1,0)
ENDIF
CFLAG:ARG:面識 = 1

@FirstMeetToday(ARG)
#DIM PREV_TARGET
#DIM IRAI_ABLE
SIF CFLAG:ARG:現在位置 != CFLAG:MASTER:現在位置
	RETURN
SIF TCVAR:ARG:今天有否见面
	RETURN

TCVAR:ARG:今天有否见面 = 1
CFLAG:ARG:最後に会った日 = DAY
SIF TCVAR:ARG:另一种便装
	PRINTFORMW 今天的%CALLNAME:ARG%打扮得跟往常不一样
IF CFLAG:ARG:オナバレ
	;陷落素質あり、【接受快感】の时、わりと坦率な反应に
	IF CFLAG:ARG:オナバレ == 1
		IF (IS_LOVER(ARG) || TALENT:ARG:炮友) && TALENT:ARG:快感応答 == 1
			PRINTFORMW 前日被目击了自慰现场的%CALLNAME:ARG%%TEXTR("一副若无其事的表情…/好像有什么想说似得看着这边…/努力表现得坦然…")%
		ELSE
			PRINTFORMW 前日被目击了自慰现场的%CALLNAME:ARG%很难为情…
		ENDIF
	ELSEIF CFLAG:ARG:オナバレ == 2
		IF (IS_LOVER(ARG) || TALENT:ARG:炮友) && TALENT:ARG:快感応答 == 1
			PRINTFORMW 之前看到了%CALLNAME:ARG%的自慰现场的时候、顺便给搞了一发的%CALLNAME:ARG%%TEXTR("妩媚地向这边送着秋波…/看着这边坏坏地笑着…/脸上浮现出回味的笑容…")%
		ELSE
			PRINTFORMW 前日被目击了自慰现场的、于是身体没有得到满足的%CALLNAME:ARG%很难为情…
		ENDIF
	ELSEIF CFLAG:ARG:オナバレ == 3
		IF (IS_LOVER(ARG) || TALENT:ARG:炮友) && TALENT:ARG:快感応答 == 1
			PRINTFORMW 之前看到了%CALLNAME:ARG%的自慰现场的时候、展露了激烈高潮脸的%CALLNAME:ARG%%TEXTR("往这边抛了媚眼…/时不时向这边偷看…/看起来很不好意思的样子…")%
		ELSE
			PRINTFORMW 前日被目击了自慰现场的、而且还在别人眼前激烈高潮的%CALLNAME:ARG%很难为情…
		ENDIF
	ENDIF
	CFLAG:ARG:オナバレ = 0
ELSEIF TALENT:ARG:心情 == 1
	PRINTFORMW 不知是不是发生了什么好事呢、今天的%CALLNAME:ARG%心情很好的样子
ELSEIF TALENT:ARG:心情 == -1
	PRINTFORMW 总觉得今天的%CALLNAME:ARG%似乎心情不好的样子…
ENDIF
IF CFLAG:ARG:积攒度 >= 400 && TALENT:MASTER:劣情察知
	SETCOLOR 255,100,255
	FONTBOLD
	IF CFLAG:ARG:积攒度 >= 1000
		IF TALENT:MASTER:形状 == 4
		PRINTFORMW 阴茎探测到了什么发出五彩光芒！
		PRINTFORMW %CALLNAME:MASTER%得知了%CALLNAME:ARG%欲火焚身了……！
		ELSE
		PRINTFORMW 发现！
		PRINTFORMW %CALLNAME:MASTER%发现%CALLNAME:ARG%欲火焚身了……！
		ENDIF
	ELSEIF CFLAG:ARG:积攒度 >= 700
		IF TALENT:MASTER:形状 == 4
		PRINTFORMW 阴茎探测到了什么发出强光！
		PRINTFORMW %CALLNAME:MASTER%得知了%CALLNAME:ARG%饥渴难耐中……！
		ELSE
		PRINTFORMW 发现！
		PRINTFORMW %CALLNAME:MASTER%发现%CALLNAME:ARG%饥渴难耐中……！
		ENDIF
	ELSE
		IF TALENT:MASTER:形状 == 4
		PRINTFORMW 阴茎探测到了什么在一闪一闪发光！
		PRINTFORMW %CALLNAME:MASTER%得知了%CALLNAME:ARG%欲求不满中……！
		ELSE
		PRINTFORMW 发现！
		PRINTFORMW %CALLNAME:MASTER%发现%CALLNAME:ARG%欲求不满中……！
		ENDIF
	ENDIF
	RESETCOLOR
	FONTREGULAR
ENDIF
CALL SEASONAL(ARG)

PREV_TARGET = TARGET
TARGET = ARG
CALL CAN_IRAI_START
IRAI_ABLE = RESULT

IF IRAI_ABLE && IRAI_CHARA_TO_PROGRESS(ARG) == "依頼未受託" && !FLAG:依頼スルー
	;CALL先の判定でTARGET==CLIENT前提の处理が多々あるので一时变更、そのまま戻ってくるので退避しとく
	CALL IRAI_START(ARG)
	TARGET = PREV_TARGET
ELSEIF IRAI_ABLE && GROUPMATCH(IRAI_CHARA_TO_PROGRESS(ARG), "期限切れ", "報告忘れ")
	PRINTFORML %CALLNAME:ARG%责怪%CALLNAME:MASTER%没有按照要求履行委托
	IF TALENT:MASTER:形状 == 4
		PRINTFORMW 发光的阴茎羞愧的低下头
	ELSE
		PRINTFORMW 多少感到有些对不起对方
	ENDIF
	;TFLAG:104の使い方がよくわからん
	;TFLAG:104 = ARG
	CALL IRAI_REPORT(ARG)
	TARGET = PREV_TARGET
ELSEIF TALENT:ARG:心情 >= 0 && BASE:MASTER:気力 > 0 && BASE:ARG:気力 > 0 && !CFLAG:MASTER:同行 && NEMUKE() < 720 && !睡眠時間(ARG)
	CALL OSASOI(ARG)
	SIF !RESULT
		TARGET = PREV_TARGET
ELSE
	TARGET = PREV_TARGET
ENDIF
