@BIRTH(ARG)
#DIM CHILDNUM
#DIM DADDY
#DIM DYNAMIC INITCHILDNUM
#DIM DYNAMIC CHILDCOUNT
	;Custom code, make sure pregnancy is at least 1, set variables.
CFLAG:ARG:WithChildCount = MAX(CFLAG:ARG:WithChildCount,1)
PregnancyProgress:ARG:0 = MAX(PregnancyProgress:ARG:0,101)
INITCHILDNUM = (CFLAG:ARG:子供人数+1) % MAXCHILD
CHILDCOUNT = CFLAG:ARG:子供人数
CFLAG:ARG:LatestChildCount = COUNT_BIRTH_MATES(ARG)
CFLAG:ARG:BirthEventsCount++
DADDY = CFLAG:ARG:誰の子
DRAWLINE
$LOOP
;妊娠消去、出産経験、性別の決定、誕生日決定
;Custom code, reduce with child count and remove a pregnancy tracker once for each child.
CFLAG:ARG:WithChildCount -= 1
CALL REMOVE_PREGNANCY_PROGRESS(ARG)
; TALENT:ARG:妊娠 = 0
EXP:ARG:出産経験 += 1
CFLAG:ARG:兒童人数 ++
CFLAG:DADDY:兒童人数 ++
CFLAG:ARG:育児人数 ++
CFLAG:DADDY:育児人数 ++
Qol_Info_Count_Array:ARG:Qol_Count_Birth ++ ;qol custom code
CHILDNUM = CFLAG:ARG:兒童人数 % MAXCHILD
SIF !CHILDNUM
	CHILDNUM = MAXCHILD
IF CHILD_SEX_PREG:ARG:CHILDNUM
	CHILD_SEX:ARG:CHILDNUM = CHILD_SEX_PREG:ARG:CHILDNUM
ELSEIF FLAG:兒童の性別
	CHILD_SEX:ARG:CHILDNUM = FLAG:兒童の性別
ELSE
	CHILD_SEX:ARG:CHILDNUM = RAND:2 + 1
ENDIF
CHILD_DADDY:ARG:CHILDNUM = DADDY
CFLAG:ARG:誰の子 = 0
CHILD_BIRTHDAY:ARG:CHILDNUM = DAY
CHILD_BIRTHDATE:ARG:CHILDNUM = (DAY / 124 + (DAY % 124 ? 1 # 0)) * 1000
CHILD_BIRTHDATE:ARG:CHILDNUM += DAY:2 * 100 + DAY:3
CHILD_WEATHER:ARG:CHILDNUM = %GET_WEATHER()%

IF CFLAG:ARG:無自覚妊娠
	CHILD_LOVE:ARG:CHILDNUM = -100
ELSEIF TALENT:ARG:恋慕
	CHILD_LOVE:ARG:CHILDNUM = 300
	;marriage custom code
	SIF TALENT:ARG:妻
		CHILD_LOVE:ARG:CHILDNUM += 100 * TALENT:ARG:妻
	SIF TALENT:ARG:妾
		CHILD_LOVE:ARG:CHILDNUM += 50
ELSEIF TALENT:ARG:思慕
	CHILD_LOVE:ARG:CHILDNUM = 200
ELSE
	CHILD_LOVE:ARG:CHILDNUM = 100
ENDIF

SIF !CFLAG:ARG:無自覚妊娠 && ITEM:私達だけが知る哀嘆 == 1
	CHILD_LOVE:ARG:CHILDNUM += 150

	;Custom code, loop again if there are more children to be born.
SIF CFLAG:ARG:WithChildCount && PregnancyProgress:ARG:0 > 100
	GOTO LOOP
	;Custom code, only send KOJO event once, differentiate live birth and hatching, and pass the number of the first child being born as ARG:1.
IF CFLAG:ARG:無自覚妊娠
	LOCAL:1 = 4
ELSEIF TALENT:ARG:恋慕
	LOCAL:1 = 1
ELSEIF TALENT:ARG:思慕
	LOCAL:1 = 2
ELSE
	LOCAL:1 = 3
ENDIF
IF TALENT:ARG:NESTING
	CALL KOJO_MESSAGE_SEND("EVENT",0,ARG,LOCAL:1,INITCHILDNUM,"HATCHING")
ELSE
	CALL KOJO_MESSAGE_SEND("EVENT",25,ARG,LOCAL:1,INITCHILDNUM)
ENDIF

	;separate loop to name all children, use LOCAL instead of CHILDNUM, account for pregnancy types.
FOR LOCAL,INITCHILDNUM,INITCHILDNUM+CFLAG:ARG:LatestChildCount
	CHILDNUM = LOCAL % MAXCHILD
	SIF !CHILDNUM
		CHILDNUM = MAXCHILD
	;あらかじめ自立先を決めちゃう（自立するときにもっかい決める）
	CALL 自立先選定(ARG, CHILDNUM)
	IF FLAG:EGGNANCY && GROUPMATCH(TALENT:ARG:PREGNANCY_TYPE, PREGNANCY_EGGS, PREGNANCY_CLUTCH)
		PRINTFORMW %CALLNAME:ARG%产下卵，孩子很快也破壳而出
	ELSE
		PRINTFORMW \@ TALENT:ARG:NESTING ? %CALLNAME:ARG%的孩子顺利破壳而出 # %CALLNAME:ARG%平安无事的生下了%CALLNAME:DADDY%的孩子\@
	ENDIF
	PRINTFORMW \@ CHILD_SEX:ARG:CHILDNUM == 1 ? %CALLNAME:ARG%一样很元气的女孩子 # %CALLNAME:DADDY%一样很元气的男孩子 \@
	PRINTFORMW %CALLNAME:ARG%接过刚刚诞生的小宝宝、温柔地抱住并开始喂起母乳来
	IF TALENT:ARG:母性 == 0
		PRINTFORMW %CALLNAME:ARG%觉醒了[母性]！
		TALENT:ARG:母性 = 1
	ENDIF
	PRINTL ******************************************************************
	CHILDCOUNT++
	PRINTFORML 第{CHILDNUM}子　母：%CALLNAME:ARG%　父：%CALLNAME:DADDY%
	PRINTFORML 　性别：\@ CHILD_SEX:ARG:CHILDNUM == 1 ? 女 # 男 \@
	PRINTFORM 诞生日：
	CALL PRINT_DATE(DAY)
	PRINTFORML  %FESTIVAL_TR()% -%GET_WEATHER()%-
	;PRINTFORML 　身高：
	;PRINTFORML 　体重：
	;PRINTFORML 　種族：
	;PRINTFORML 　髪色：
	;PRINTFORML 　瞳色：
	PRINTL ******************************************************************
	CALL CHILD_NAME(ARG, LOCAL)
NEXT

;育児中・母性追加
;CFLAG:ARG:育児人数 += 1
;CFLAG:DADDY:育児人数 += 1
CFLAG:ARG:出産経過日 = 1 + FLAG:妊娠日程倍率
;CFLAG:ARG:妊娠経過日数 = 0
CFLAG:ARG:無自覚妊娠 = 0
CFLAG:ARG:身に覚え_母 = 0
CFLAG:DADDY:身に覚え_父 = 0
CFLAG:ARG:妊娠自覚状態 = 0 

CFLAG:ARG:産休中 = 0
	;Custom code, set pregnancy or NESTING progress to the next due child (if any) instead of zero.
IF GROUPMATCH(TALENT:ARG:PREGNANCY_TYPE, PREGNANCY_EGGS, PREGNANCY_CLUTCH) && !FLAG:EGGNANCY
	TALENT:ARG:NESTING = PregnancyProgress:ARG:0
	CFLAG:ARG:妊娠経過日数 = 0
ELSE
	CFLAG:ARG:妊娠経過日数 = PregnancyProgress:ARG:0
	TALENT:ARG:NESTING = 0
		;Only unset pregnancy talent if progress is below 20.
	SIF CFLAG:ARG:妊娠経過日数 < 20
		TALENT:ARG:妊娠 = 0
ENDIF
EXP:ARG:愛情経験 += 10
IF FLAG:甲斐性無 && (DADDY == MASTER || ARG == MASTER)
	PRINTFORML %CALLNAME:MASTER%因为自己孩子的诞生
	CALL ASK_YN("萌生起了作为父母的责任感","对今后可能发生起的各种各样的事态感到不安")
	IF !RESULT
		PRINTFORMW %CALLNAME:MASTER%的毫无志气消失了
			;only set the flag if it's turned off.
		FLAG:甲斐性無 = 0
	ELSE
		PRINTFORMW 人的性格会这么简单就改变的话那么就没有人会觉得辛苦了
		PRINTFORMW %CALLNAME:MASTER%还是以前那样不可靠的性格、幸福的和%CALLNAME:ARG%一起面临着育儿
	ENDIF
ENDIF

@CHILD_NAME(YOME, CHILDNUM)
#DIM YOME
#DIM CHILDNUM
#DIMS PROPOSED_NAME, MAXCHILD
#DIM DADDY
CHILDNUM = EXP:YOME:出産経験 % MAXCHILD
DADDY = CHILD_DADDY:YOME:CHILDNUM
SIF !EXP:YOME:出産経験 % MAXCHILD
	CHILDNUM = MAXCHILD
IF CHILD_SEX:YOME:CHILDNUM == 1
	PROPOSED_NAME:0 = %随机角色_命名(YOME)%
ELSE
	PROPOSED_NAME:0 = %男の子_命名(YOME, CHILDNUM)%
ENDIF
A:0 = LINECOUNT
$LOOP0
CLEARLINE LINECOUNT - A:0
PRINTL 请决定孩子的名字
PRINTFORML 
IF YOME == MASTER && (TALENT:DADDY:恋人 || TALENT:DADDY:恋慕) && (CFLAG:DADDY:允许无套 == 2 || TALENT:DADDY:妊娠願望)
	CALL ASK_M("自己决定",1, "一览随机候补",1, @"%CALLNAME:MASTER%和%CALLNAME:DADDY%把她们爱的结晶取名为「%PROPOSED_NAME:0%」",1)
ELSEIF (TALENT:YOME:恋人 || TALENT:YOME:恋慕) && (CFLAG:YOME:允许无套 == 2 || TALENT:YOME:妊娠願望)
	CALL ASK_M("自己决定",1, "一览随机候补",1, @"%CALLNAME:YOME%和%CALLNAME:MASTER%把他们爱的结晶取名为「%PROPOSED_NAME:0%」",1)
ELSEIF YOME == MASTER
	CALL ASK_M("自己决定",1, "一览随机候补",1, @"顺从%CALLNAME:DADDY%的提案取名为「%PROPOSED_NAME:0%」",1)
ELSE
	CALL ASK_M("自己决定",1, "一览随机候补",1, @"顺从%CALLNAME:YOME%的提案取名为「%PROPOSED_NAME:0%」",1)
	;CALL ASK_M("自己決定",1,"一覧随機候補",1,"取名為帯著「墨西哥吹來的熱風！」意味的「桑塔納」怎么樣！",1) 
ENDIF
SELECTCASE RESULT
	CASE 0
		A:1 = LINECOUNT
		$LOOP1
		CLEARLINE LINECOUNT - A:1
		PRINTL 请输入孩子的名字
		INPUTS
		IF RESULTS == ""
			GOTO LOOP1
		ELSE
			PROPOSED_NAME:0 = %RESULTS%
			CALL CHK_CHILDNAME_DUPLICATION(PROPOSED_NAME:0)
			PRINTFORML %PROPOSED_NAME:0%可以么？
			CALL ASK_YN
			IF RESULT
				GOTO LOOP1
			ELSE
				CHILDNAME:YOME:CHILDNUM = %PROPOSED_NAME:0%
			ENDIF
		ENDIF
	CASE 1
		A:2 = LINECOUNT
		$LOOP2
		CLEARLINE LINECOUNT - A:2
		FOR LOCAL,1,MAXCHILD
			IF CHILD_SEX:YOME:CHILDNUM == 1
				PROPOSED_NAME:LOCAL = %随机角色_命名(YOME)%
			ELSE
				PROPOSED_NAME:LOCAL = %男の子_命名(YOME, CHILDNUM)%
			ENDIF
			PRINTFORML [{LOCAL}] - %PROPOSED_NAME:LOCAL%
		NEXT
		DRAWLINE
		PRINTFORML [99] - 候补再生成
		PRINTFORML [100] - 返回
		A:3 = LINECOUNT
		$LOOP3
		CLEARLINE LINECOUNT - A:3
		INPUT
		SELECTCASE RESULT
			CASE 1 TO MAXCHILD
				LOCAL = RESULT
				CALL CHK_CHILDNAME_DUPLICATION(PROPOSED_NAME:LOCAL)
				PRINTFORML %PROPOSED_NAME:LOCAL%可以么？
				CALL ASK_YN
				IF RESULT
					GOTO LOOP0
				ELSE
					CHILDNAME:YOME:CHILDNUM = %PROPOSED_NAME:LOCAL%
				ENDIF
			CASE 99
				GOTO LOOP2
			CASE 100
				GOTO LOOP0
			CASEELSE
				GOTO LOOP3
		ENDSELECT
	CASE 2
		;PRINTFORML 生氣的%CALLNAME:YOME%自己決定好了名字…
		IF YOME == MASTER && (TALENT:DADDY:恋人 || TALENT:DADDY:恋慕) && (CFLAG:DADDY:允许无套 == 2 || TALENT:DADDY:妊娠願望)
			PRINTFORML %CALLNAME:YOME%和%CALLNAME:DADDY%高兴地呼唤着自己孩子的名字
		ELSEIF (TALENT:YOME:恋人 || TALENT:YOME:恋慕) && (CFLAG:YOME:允许无套 == 2 || TALENT:YOME:妊娠願望)
			PRINTFORML %CALLNAME:YOME%和%CALLNAME:MASTER%高兴地呼唤着自己孩子的名字
		ELSEIF YOME == MASTER
			PRINTFORML %CALLNAME:DADDY%高兴地呼唤着自己孩子的名字
		ELSE
			PRINTFORML %CALLNAME:YOME%高兴地呼唤着自己孩子的名字
		ENDIF
		CHILDNAME:YOME:CHILDNUM = %PROPOSED_NAME:0%
ENDSELECT

PRINTFORML 取好了%CHILDNAME:YOME:CHILDNUM%的名字
TALENT:YOME:育児中 = CHILDNUM
CALL ASK_DIARY(@"%CALLNAME:YOME%生下了一个\@ CHILD_SEX:YOME:CHILDNUM == 1? 女# 男\@孩子。<br>取名为%CHILDNAME:YOME:CHILDNUM%。")
;----------------------------------------------------
;その角色に特定の年齢の兒童がいたら返す関数
;----------------------------------------------------
@CHK_HAVE_CHILD(ARG,ARG:1,ARG:2)
#FUNCTION
#DIM 年齢
SIF !CFLAG:ARG:兒童人数
	RETURNF 0

FOR LOCAL, 1, MAXCHILD + 1
	SIF CHILDNAME:ARG:LOCAL == ""
		CONTINUE
	年齢 = DAY - CHILD_BIRTHDAY:ARG:LOCAL
	SIF INRANGE(年齢,ARG:1,ARG:2-1)
		RETURNF LOCAL
NEXT

RETURNF 0
;----------------------------------------------------
;誕生日の兒童がいるか返す関数
;eraTW4.570proto_bugfix_m05での仕様変更
;・REFを使用し、配列を書き換えるF関数にした（引数を全て渡さないとエラーになる）
;・仕様上、配列の想定最大要素数は10*人物数量上限となる（＝RESULTの要素数を突破してエラーが出たので仕様変更）
;・BREAKやCONTINUEを使用して処理を効率化
;----------------------------------------------------
@CHILD_HAPPY_BIRTHDAY(CNT_BIRTHDAY, NAME_CHILD, AGE_CHILD)
#FUNCTION
#DIM CHARA
#DIM CHILDNUM
#DIM  REF CNT_BIRTHDAY
#DIMS REF NAME_CHILD
#DIM  REF AGE_CHILD
FOR CHARA, 1, CHARANUM
	SIF !CFLAG:CHARA:兒童人数
		CONTINUE
	FOR CHILDNUM, 1, MIN(MAXCHILD + 1, CFLAG:CHARA:兒童人数 + 1)
		;名無し≒未登録なら下一位角色へ
		SIF !STRLENS(CHILDNAME:CHARA:CHILDNUM)
			BREAK
		;誕生日以外はスキップ
		SIF (DAY - CHILD_BIRTHDAY:CHARA:CHILDNUM) % 124
			CONTINUE
		CNT_BIRTHDAY ++
		NAME_CHILD:CNT_BIRTHDAY = %CHILDNAME:CHARA:CHILDNUM%
		AGE_CHILD:CNT_BIRTHDAY = (DAY - CHILD_BIRTHDAY:CHARA:CHILDNUM) / 124
	NEXT
NEXT
RETURNF CNT_BIRTHDAY

;兒童の名前被りチェック
@CHK_CHILDNAME_DUPLICATION(ARGS)
#DIM C_ID
FOR C_ID,1,CHARANUM
	;"FOR LOCAL,0,10"から修正
	FOR LOCAL,1,MAXCHILD + 1
		SIF CHILDNAME:C_ID:LOCAL == ARGS
			CALL COLORMESSAGE(@"%CALLNAME:C_ID%已经有一个同名的孩子了",C_YELLOW,1)
	NEXT
NEXT