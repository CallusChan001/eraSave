;────────────────────────────────────
;調教終了の処理
;────────────────────────────────────
@EVENTEND
#LATER
#DIM 変動値
#DIM 会ってない日数
#DIM YANUSHI
#DIM MAX_SHOOTER
#DIM 可能擁有陰莖的对象
#DIM DYNAMIC SEMEN_POISON_PLUS, 200
;魔改TSP相关，检测TSP等级
#DIM TSP_NOW_GRADE
;魔改完毕
#DIM D_BEFORE
;MODIFIED SAVING SYSTEM
VARSET NEW_SAVE_MARK
SAVEGLOBAL
PRINTL 一天结束了

;DLC相关↓
IF AUTOWATER
    PRINTL 多亏了河童的黑科技，田地被自动灌溉了
ENDIF
CALL DLCEVENT
;DLC相关↑

PRINTFORML 
;SIF STRLENS(TSTR:1)
;	CALL SELL_VIDEO

;魔改永琳通，重置患者访问和开始营业等变量。
CALL MEDICAL_BUSINESS_SETTLEMENT
;超级幽幽子大人重置饥饿
IF superYuyukoSamaMode >= 1
	superYuyukoSamaIsHungry ++
ELSE
	superYuyukoSamaIsHungry = 0
ENDIF

;睡眠フラグ
CALL SET_SLEEP(1,MASTER,0)

CFLAG:MASTER:膣内射精 = 0
CFLAG:MASTER:肛門射精 = 0
CFLAG:MASTER:口内精液 = 0
CFLAG:MASTER:脸上精液 = 0
CFLAG:MASTER:手上精液 = 0
CFLAG:MASTER:没穿内裤 = 0

;神簽を引けるようにフラグリセット
CLEARBIT FLAG:999, 1
;你懐妊処理
;CALL MasterPregnancy

;addition custom code, DAYEVENT handling
CALL DAYEVENT_MAIN("night")

;中出しフラグリセット
FOR LOCAL,0,CHARANUM
	CFLAG:LOCAL:膣内射精 = 0
	CFLAG:LOCAL:肛門射精 = 0
	CFLAG:LOCAL:口内精液 = 0
	CFLAG:LOCAL:脸上精液 = 0
	CFLAG:LOCAL:手上精液 = 0
	;祈願による感度上昇解除
	SIF GROUPMATCH(TCVAR:LOCAL:祈願補正,23,31,41,70,80,82,83,101,102,103,104,106)
		TALENT:LOCAL:(TCVAR:LOCAL:祈願補正) -= 1
	;喜欢的体位決定
	SIF CFLAG:LOCAL:喜欢的体位 > 10 
		CFLAG:LOCAL:喜欢的体位 = 0
	FOR COUNT,1,10
		SIF EXP:LOCAL:(COUNT + 100) > EXP:LOCAL:(CFLAG:LOCAL:喜欢的体位 + 100) + 50
			CFLAG:LOCAL:喜欢的体位 = COUNT
	NEXT
	;胖次フラグ
	CFLAG:LOCAL:没穿内裤 = 0
	CFLAG:LOCAL:衣装一時変更 = 0
	SIF TCVAR:LOCAL:本来的服装
		CFLAG:LOCAL:基本服装セット = TCVAR:LOCAL:本来的服装
	SELECTCASE ABL:LOCAL:精液中毒
		CASE 0
			LOCAL:1 = 5
		CASE 1 TO 3
			LOCAL:1 = 7
		CASE 4 TO 7
			LOCAL:1 = 10
		CASEELSE
			LOCAL:1 = 20
	ENDSELECT
	;心情
	TALENT:LOCAL:心情 = 0
	CALL KIGEN_CHANGE(LOCAL,10)
	VARSET SEMEN_POISON_PLUS:LOCAL 
	IF EX:LOCAL:膣内精液 > 0 || EX:LOCAL:肛門内精液 > 0
		IF EX:LOCAL:膣内精液 > 0
			FOR LOCAL:2, 0, CHARANUM
				IF CFLAG:LOCAL:(LOCAL:2 + 3000) > 0 && TALENT:(LOCAL:2):濃厚精液
					SEMEN_POISON_PLUS:LOCAL = 1
					BREAK
				ENDIF
			NEXT
		ENDIF
		IF EX:LOCAL:肛門内精液 > 0 || SEMEN_POISON_PLUS:LOCAL == 0
			FOR LOCAL:2, 0, CHARANUM
				IF CFLAG:LOCAL:(LOCAL:2 + 3500) > 0 && TALENT:(LOCAL:2):濃厚精液
					SEMEN_POISON_PLUS:LOCAL = 1
					BREAK
				ENDIF
			NEXT
		ENDIF
	ENDIF
	SIF SEMEN_POISON_PLUS:LOCAL == 1
		LOCAL:1 *= 2
	SETCOLOR 0xEE60EE
	IF EX:LOCAL:膣内精液 > 200 || EX:LOCAL:肛門内精液 > 200
		IF LOCAL > 0
			PRINTFORM %CALLNAME:MASTER%精子的温柔爱抚让%CALLNAME:LOCAL%的
		ELSE
			PRINTFORM 感受着精子的温柔爱抚，%CALLNAME:LOCAL%的
		ENDIF
		IF EX:LOCAL:膣内精液 > 200 && EX:LOCAL:肛門内精液 > 200
			PRINTL 阴道和肛门又热又痒❤…
		ELSEIF EX:LOCAL:膣内精液 > 200
			PRINTL 阴道又热又痒❤…
		ELSE
			PRINTL 肛门又热又痒❤…
		ENDIF
		IF EX:LOCAL:膣内精液 > 200
			JUEL:LOCAL:屈服 += LOCAL:1 * (ABL:LOCAL:膣射中毒 + 1) * EX:LOCAL:膣内精液
			JUEL:LOCAL:欲情 += LOCAL:1 * (ABL:LOCAL:膣射中毒 + 1) * EX:LOCAL:膣内精液
		ENDIF
		IF EX:LOCAL:肛門内精液 > 200
			JUEL:LOCAL:屈服 += LOCAL:1 * (ABL:LOCAL:肛射中毒 + 1) * EX:LOCAL:肛門内精液
			JUEL:LOCAL:欲情 += LOCAL:1 * (ABL:LOCAL:肛射中毒 + 1) * EX:LOCAL:肛門内精液
		ENDIF
	ENDIF
	IF (EX:LOCAL:Ｃ寸止め + EX:LOCAL:Ｖ寸止め + EX:LOCAL:Ａ寸止め + EX:LOCAL:Ｂ寸止め + EX:LOCAL:Ｍ寸止め) > 0
		PRINTFORM %CALLNAME:LOCAL%忍耐着
		SIF EX:LOCAL:Ｃ寸止め
			PRINT 阴蒂
		SIF EX:LOCAL:Ｃ寸止め && (EX:LOCAL:Ｖ寸止め || EX:LOCAL:Ａ寸止め || EX:LOCAL:Ｂ寸止め || EX:LOCAL:Ｍ寸止め)
			PRINT 和
		SIF EX:LOCAL:Ｖ寸止め
			PRINT 膣内
		SIF EX:LOCAL:Ｖ寸止め && (EX:LOCAL:Ａ寸止め || EX:LOCAL:Ｂ寸止め || EX:LOCAL:Ｍ寸止め)
			PRINT 和
		SIF EX:LOCAL:Ａ寸止め
			PRINT 肛门
		SIF EX:LOCAL:Ａ寸止め && (EX:LOCAL:Ｂ寸止め || EX:LOCAL:Ｍ寸止め)
			PRINT 和
		SIF EX:LOCAL:Ｂ寸止め
			PRINT 胸部
		SIF EX:LOCAL:Ｂ寸止め && EX:LOCAL:Ｍ寸止め
			PRINT 和
		SIF EX:LOCAL:Ｍ寸止め
			PRINT 嘴唇
			PRINTFORMW 颤抖的酥麻感…
		CFLAG:LOCAL:积攒度 += (EX:LOCAL:Ｃ寸止め + EX:LOCAL:Ｖ寸止め + EX:LOCAL:Ａ寸止め + EX:LOCAL:Ｂ寸止め + EX:LOCAL:Ｍ寸止め) * 100
	ENDIF
	{
		IF CFLAG:LOCAL:体目当て == 1 && TALENT:LOCAL:処女 == 1 && !TALENT:LOCAL:風騷 == 1 && CFLAG:LOCAL:积攒度 >= 500  && TCVAR:LOCAL:発情 == 1
		&& (TALENT:LOCAL:漏尿癖 == 1 || TALENT:LOCAL:自慰狂 == 1 || TALENT:LOCAL:精愛味覚 == 1 || TALENT:LOCAL:接吻魔 == 1 || TALENT:LOCAL:尻穴狂 == 1)
		}
		   SETCOLOR C_RED
		   PRINTL 处 女 的 觉 醒
		   RESETCOLOR
		   PRINTFORMW %CALLNAME:LOCAL%虽然仍然保持处女之身，但在%CALLNAME:MASTER%的不断调教下，隐藏在灵魂深处的欲求彻底觉醒了！
		   SETCOLOR C_PINK
		   PRINTFORMW 觉醒的%CALLNAME:LOCAL%变风骚了！身体素质发生了转变！思想变的开放且淫乱了！
		   RESETCOLOR
		   CFLAG:LOCAL:体目当て = 0
			   TALENT:LOCAL:風騷 = 1
			   TALENT:LOCAL:性別嗜好 = -1
			   ABL:LOCAL:欲望 = 20
			   TALENT:LOCAL:貞操 = -1
			   TALENT:LOCAL:妊娠願望 = 1
			   TALENT:LOCAL:容姿 = 1
			   TALENT:LOCAL:性的兴趣 = 1
			   TALENT:LOCAL:自己愛 = 1
			   TALENT:LOCAL:羞恥心 = 1
			   TALENT:LOCAL:弄濕難易 = 1
			   TALENT:LOCAL:快感応答 = 1
			   TALENT:LOCAL:容易中毒 = 1
			   TALENT:LOCAL:容易高潮 = 1
			   TALENT:LOCAL:Ｃ感度 = 1
			   TALENT:LOCAL:Ｖ感度 = 1
			   TALENT:LOCAL:Ａ感度 = 1
			   TALENT:LOCAL:Ｂ感度 = 1
			   TALENT:LOCAL:Ｍ感度 = 1
			   TALENT:LOCAL:回復速度 = 1
			   TALENT:LOCAL:淫乱 = 1
	   ENDIF
	{
	IF (TALENT:LOCAL:幼児／幼児退行 || TALENT:LOCAL:幼稚) && !TALENT:LOCAL:初潮 && EXP:LOCAL:Ｖ絶頂経験 > MAX(0, 200 - CFLAG:LOCAL:子宝補正)
	 && !RAND:(10 - MIN(9, CFLAG:LOCAL:子宝補正 / 10 + ABL:LOCAL:膣射中毒)) && EX:LOCAL:膣内精液
	 }
		PRINTFORML 在对幼小的身体而言为时尚早的快乐冲击下、%CALLNAME:LOCAL%从女孩变成了「女人」……
		PRINTFORMW 卵巢迫不及待的成熟…%CALLNAME:LOCAL%迎来了初潮…
		PRINTFORMW 可喜可贺…%CALLNAME:LOCAL%变得可以怀孕了…
		TALENT:LOCAL:初潮 = 1
		CALL KOJO_MESSAGE_SEND("EVENT", 34, LOCAL, 32) ;addition custom code
	ENDIF
	IF ABL:LOCAL:精液中毒 < 5
		IF CFLAG:LOCAL:累計精浴 + CFLAG:LOCAL:累計精飲 * 5 +CFLAG:LOCAL:累計膣内精液 + CFLAG:LOCAL:累計肛門精液 >= 20000 * (10 - 2 * TALENT:LOCAL:容易中毒) / 10 && ABL:LOCAL:精液中毒 == 4
			PRINTFORML 被强烈开发的%CALLNAME:LOCAL%的雌躯、在灵魂层面上刻下了对\@ LOCAL ? %CALLNAME:MASTER%# \@精液的渴求…
			PRINTFORML \@ LOCAL ? %CALLNAME:MASTER%的# \@精液已经成为了%CALLNAME:LOCAL%雌躯不可或缺的成分
			PRINTFORMW %CALLNAME:LOCAL%的精液中毒达到了5级
			ABL:LOCAL:精液中毒 = 5
		ELSEIF CFLAG:LOCAL:累計精浴 + CFLAG:LOCAL:累計精飲 * 5 +CFLAG:LOCAL:累計膣内精液 + CFLAG:LOCAL:累計肛門精液 >= 15000 * (10 - 2 * TALENT:LOCAL:容易中毒) / 10 && ABL:LOCAL:精液中毒 == 3
			PRINTFORML 被无数次注入精液的%CALLNAME:LOCAL%的躯体、无时无刻不在渴求着\@ LOCAL ? %CALLNAME:MASTER%的# \@精液…
			PRINTFORML 任何时候见到\@ LOCAL ? %CALLNAME:MASTER%# \@，子宫都会颤抖着渴求\@ LOCAL ? %CALLNAME:MASTER%的# \@精液…
			PRINTFORMW %CALLNAME:LOCAL%的精液中毒达到了4级
			ABL:LOCAL:精液中毒 = 4
		ELSEIF CFLAG:LOCAL:累計精浴 + CFLAG:LOCAL:累計精飲 * 5 +CFLAG:LOCAL:累計膣内精液 + CFLAG:LOCAL:累計肛門精液 >= 10000 * (10 - 2 * TALENT:LOCAL:容易中毒) / 10 && ABL:LOCAL:精液中毒 == 2
			PRINTFORML %CALLNAME:LOCAL%被开发的身体、已经与\@ LOCAL ? %CALLNAME:MASTER%# \@的精液十分熟稔了…
			PRINTFORML 今后即使是无意识下也会变得主动想要了吧
			PRINTFORMW %CALLNAME:LOCAL%的精液中毒达到了3级
			ABL:LOCAL:精液中毒 = 3
		ELSEIF CFLAG:LOCAL:累計精浴 + CFLAG:LOCAL:累計精飲 * 5 +CFLAG:LOCAL:累計膣内精液 + CFLAG:LOCAL:累計肛門精液 >= 5000 * (10 - 2 * TALENT:LOCAL:容易中毒) / 10 && ABL:LOCAL:精液中毒 < 2
			PRINTFORML %CALLNAME:LOCAL%淫荡的身体、已经记住了\@ LOCAL ? %CALLNAME:MASTER%# \@精液的味道…
			PRINTFORMW %CALLNAME:LOCAL%的精液中毒达到了2级
			ABL:LOCAL:精液中毒 = 2
		ELSEIF CFLAG:LOCAL:累計精浴 + CFLAG:LOCAL:累計精飲 * 5 +CFLAG:LOCAL:累計膣内精液 + CFLAG:LOCAL:累計肛門精液 >= 3000 * (10 - 2 * TALENT:LOCAL:容易中毒) / 10 && !ABL:LOCAL:精液中毒
			PRINTFORML %CALLNAME:LOCAL%的身体、忘不了沾染\@ LOCAL ? %CALLNAME:MASTER%# \@精液的感觉…
			PRINTFORMW %CALLNAME:LOCAL%的精液中毒达到了1级
			ABL:LOCAL:精液中毒 = 1
		ENDIF
		CALL KOJO_MESSAGE_SEND("EVENT", 34, LOCAL, 30) ;addition custom code
	ENDIF
	IF CFLAG:LOCAL:累計精飲 > 10000 * (10 - 2 * TALENT:LOCAL:容易中毒) / 10 && !TALENT:LOCAL:精愛味覚
		PRINTFORML 持续喝下\@ LOCAL ? %CALLNAME:MASTER%# \@精液的%CALLNAME:LOCAL%、变得会因精液的味道而感到喜悦…
		PRINTFORML %CALLNAME:LOCAL%开始无意识的渴求\@ LOCAL ? %CALLNAME:MASTER%# \@精液的味道
		PRINTFORMW %CALLNAME:LOCAL%得到了【精爱味觉】
		TALENT:LOCAL:精愛味覚 = 1
		CALL KOJO_MESSAGE_SEND("EVENT", 34, LOCAL, 31) ;addition custom code
	ENDIF
	IF ABL:LOCAL:精液中毒 >= 5
		IF CFLAG:LOCAL:累計膣内精液 > 3000 * ABL:LOCAL:膣射中毒 * (10 - 2 * TALENT:LOCAL:容易中毒) / 10 &&  EX:LOCAL:膣内精液 > 200 && ABL:LOCAL:膣射中毒 < 5
			PRINTFORMW %CALLNAME:LOCAL%的媚黏膜完全被精液浸染…
			PRINTFORMW %CALLNAME:LOCAL%的膣道变得\@ ABL:LOCAL:膣射中毒 ? 强烈# \@渴望着\@ LOCAL ? %CALLNAME:MASTER%的# \@精液
			ABL:LOCAL:膣射中毒 ++
			CALL KOJO_MESSAGE_SEND("EVENT", 34, LOCAL, 30, 1) ;addition custom code
		ELSEIF CFLAG:LOCAL:累計肛門精液 > 3000 * ABL:LOCAL:肛射中毒 * (10 - 2 * TALENT:LOCAL:容易中毒) / 10 && EX:LOCAL:肛門内精液 > 200 && ABL:LOCAL:肛射中毒 < 5
			PRINTFORMW %CALLNAME:LOCAL%的直肠完全被精液浸染…
			PRINTFORMW %CALLNAME:LOCAL%的肛门变得\@ ABL:LOCAL:肛射中毒 ? 强烈# \@渴望着\@ LOCAL ? %CALLNAME:MASTER%的# \@精液
			ABL:LOCAL:肛射中毒 ++
			CALL KOJO_MESSAGE_SEND("EVENT", 34, LOCAL, 30, 2) ;addition custom code
		ENDIF
	ENDIF
	RESETCOLOR
	;------ ALTER 殘留精液
	IF JYCL == 1
		CFLAG:LOCAL:膣内精液 = EX:LOCAL:膣内精液
		CFLAG:LOCAL:子宮内精子 = EX:LOCAL:子宮内精液
	ELSE
		CFLAG:LOCAL:膣内精液 = 0
		CFLAG:LOCAL:子宮内精子 = 0
	ENDIF

	;------ END 殘留精液

	SIF CFLAG:LOCAL:600
		CFLAG:LOCAL:600 = 0
	IF CFLAG:LOCAL:面識
		会ってない日数 = DAY - CFLAG:LOCAL:最後に会った日
		;addition custom code, DAYEVENT
		SIF DAYEVENT_TARGET == LOCAL
			CALL DAYEVENT_EXECUTE_EVENT()
		CALL KOJO_MESSAGE_SEND("EVENT", 31, LOCAL, TCVAR:LOCAL:今天有否见面 ? 0 # 会ってない日数)
	ENDIF
	CALL KOJO_MESSAGE_SEND("EVENT", 0, LOCAL, , , "AFTERTRAIN_BEGIN") ;addition custom code
	
	;肛内精液不残留，全部清理掉
	FOR LOCAL:2,0,CHARANUM
		;精液量リセット
		CFLAG:LOCAL:(LOCAL:2 + 3500) = 0
	NEXT

NEXT

FOR LOCAL, MINROOM(), MAXROOM
	YOGORE:LOCAL += RAND:50
	YANUSHI = PRIVATEROOM:(LOCAL % 100)
	IF YANUSHI && YOGORE:LOCAL > (4000 - ABL:YANUSHI:清掃技能 * 500)
		PRINTFORML %CALLNAME:YANUSHI%打扫了自己的房间
		EXP:YANUSHI:清掃経験 ++
		YOGORE:LOCAL /= 4
	ENDIF
	;公共の場もしくは你が入れない場所でプライベートルームじゃないなら上限を付ける,多少抜けはあるけどだいたいこれでカバー
	IF ROOMDATA:(LOCAL % 100) & 場所_公共の場 || (!MAP_CAN_MOVE(LOCAL, MASTER) && !YANUSHI)
;		PRINTFORMW 上限アリ：%NAME_FROM_PLACE(LOCAL)%（{LOCAL}）
		YOGORE:LOCAL = MIN(YOGORE:LOCAL, 500)
	ENDIF
NEXT
IF FLAG:加護
	FLAG:加護 = 0
	DRAWLINE
	PRINTFORMW 神的加护的效果消失了
ENDIF
;(天候パッチ)台風・猛吹雪を除外
IF FLAG:異常気象 >= 1 && FLAG:異常気象 <= 8
	FLAG:異常気象 = 0
	DRAWLINE
	PRINTFORMW 异常气象消失了
ENDIF
IF FLAG:ファッション
	FLAG:ファッション = 0
	DRAWLINE
	PRINTFORMW 追求时尚热潮平息了
ENDIF
IF GatheringSeason
	GatheringSeason = 0
	PRINTFORMW 回到了原来的季节
ENDIF
IF TFLAG:上门清扫
	DRAWLINE
	PRINTFORML 应委托前去执行上门清扫服务
	PRINTFORMW …
	PRINTFORMW ……
	PRINTFORMW %GET_MAPNAME(MAIN_MAP)%里面变得干净了！
	FOR LOCAL,MINROOM(),MAXROOM
		YOGORE:LOCAL /= 4
	NEXT
ENDIF
IF FLAG:市場の神 >= 10
	FLAG:市場の神 = 0
	DRAWLINE
	PRINTL 明月照耀下的虹光正在闭合……
	PRINTW 土特产店的货物种类回归原样了。
ENDIF

;重置潜伏
FLAG:潜伏 = 0
BASE:MASTER:潜伏率 = 0

[SKIPSTART]
;依頼の期限切れをチェック
FOR LOCAL,1,6
	SIF !IRAI_SLOT:LOCAL
		CONTINUE
	SIF IRAI_DEADLINE:LOCAL > DAY
		CONTINUE
	DRAWLINE
	PRINTFORML %CALLNAME:(IRAI_SLOT_TO_CLIENT(LOCAL))%的委托期限到了……
	CALL COLORMESSAGE("【依頼失敗】",C_YELLOW,2,1)
	CFLAG:(IRAI_CLIENT:LOCAL):依頼状況 = 4
NEXT
[SKIPEND]

;デイリー
;SIF CFLAG:MASTER:現在位置 != OMANEKIBEYA() && CFLAG:MASTER:约会中 == MAIN_MAP
SIF CFLAG:MASTER:現在位置 == CFLAG:MASTER:初期位置
	CALL DAILYEVENT

;畑の管理
CALL YASAI

;借书屋
CALL BOOKLEND

CALL 酒造

CALL MOENY_HENDOU

;罠猟
CALL TRAP_PROGRESS
;昆虫诱捕装置
CALL MUSHI_TRAP_PROGRESS
;材木市場
CALL WOODYARD_PROGRESS ;logging patch custom code
;養鶏
CALL EGG_PROGRESS

MONEY:1 += MAXBASE:MASTER:TSP - BASE:MASTER:TSP
IF MONEY:1 >= 18900 && TSP_GRADE_MODIFICATION() == 6
	DRAWLINE
	PRINTFORML 时间停止能力进化到了极限！
	CLEARBIT EXTRA_TSP_EXPANDING, 1
	CLEARBIT EXTRA_TSP_EXPANDING, 3
	MAXBASE:MASTER:TSP = 6666
ELSEIF MONEY:1 >= 9200 && TSP_GRADE_MODIFICATION() == 5
	DRAWLINE
	PRINTFORML 时间停止能力的使用已经炉火纯青了！
	MAXBASE:MASTER:TSP = 3200
	CLEARBIT EXTRA_TSP_EXPANDING, 1
	CLEARBIT EXTRA_TSP_EXPANDING, 3
ELSEIF MONEY:1 >= 3000 && TSP_GRADE_MODIFICATION() == 4
	DRAWLINE
	PRINTFORML 时间停止能力的使用已经相当在行了！
	MAXBASE:MASTER:TSP = 1600
	CLEARBIT EXTRA_TSP_EXPANDING, 1
	CLEARBIT EXTRA_TSP_EXPANDING, 3
ELSEIF MONEY:1 >= 1050 && TSP_GRADE_MODIFICATION() == 3
	DRAWLINE
	PRINTFORML 时间停止能力的使用已经很拿手了！
	MAXBASE:MASTER:TSP = 800
	CLEARBIT EXTRA_TSP_EXPANDING, 1
	CLEARBIT EXTRA_TSP_EXPANDING, 3
ELSEIF MONEY:1 >= 300 && TSP_GRADE_MODIFICATION() == 2
	DRAWLINE
	PRINTFORML 时间停止能力的使用已经有点门道了！
	MAXBASE:MASTER:TSP = 400
	CLEARBIT EXTRA_TSP_EXPANDING, 1
	CLEARBIT EXTRA_TSP_EXPANDING, 3
ELSEIF MONEY:1 >= 50 && TSP_GRADE_MODIFICATION() == 1
	DRAWLINE
	PRINTFORML 时间停止能力的使用已经有点入门了！
	MAXBASE:MASTER:TSP = 200
	CLEARBIT EXTRA_TSP_EXPANDING, 1
	CLEARBIT EXTRA_TSP_EXPANDING, 3
ENDIF

IF GETBIT(EXTRA_TSP_EXPANDING, 0) && !GETBIT(EXTRA_TSP_EXPANDING, 1)
	SETBIT EXTRA_TSP_EXPANDING, 1
	MAXBASE:MASTER:TSP += 500
ENDIF
IF GETBIT(EXTRA_TSP_EXPANDING, 2) && !GETBIT(EXTRA_TSP_EXPANDING, 3)
	SETBIT EXTRA_TSP_EXPANDING, 3
	MAXBASE:MASTER:TSP += 800
ENDIF


BASE:MASTER:TSP = MAXBASE:MASTER:TSP

;魔改禁止自慰
SIF NO_ONANISM == 0
	CALL ONANISM

FOR LOCAL,0,CHARANUM
	IF FLAG:妊娠開关 == 1 && (LOCAL || SELF_PREGNANCY)
			;enable pregnancy check while pregnant if the character isn't using the normal or litters pregnancy type.
		SIF (CFLAG:LOCAL:妊娠経過日数 == 0 || (FLAG:USE_EXPANDED_PREGNANCY_OPTIONS && TALENT:LOCAL:PREGNANCY_TYPE > 1 || TALENT:LOCAL:HYPERFERTILE)) && !FLAG:避妊 && !TCVAR:LOCAL:口服避孕薬 && TALENT:LOCAL:路人 != 2
			CALL GET_CHILD(LOCAL)
	ELSE
	
	ENDIF
	CALL 出産経過(LOCAL)
NEXT

IF TALENT:MASTER:2 & 2
	;------ ALTER 殘留精液
	IF JYCL == 1 && TALENT:MASTER:濃厚精液
	 	TALENT:MASTER:濃厚精液殘留 = TALENT:MASTER:濃厚精液
	ELSE
		TALENT:MASTER:濃厚精液殘留 = 0
	ENDIF
	;------ END 殘留精液
	IF BASE:MASTER:精力 == MAXBASE:MASTER:精力 && !GETBIT(TFLAG:一日一回,8)
		FLAG:禁自慰 ++
		IF FLAG:禁自慰 >= 5 && !RAND:3
			DRAWLINE
			PRINTFORML 因为最近没有做过、所以感到烦躁……
			PRINTFORML %CALLNAME:MASTER%在被子里滚来滚去
			PRINTFORML 塞翁失马、由于憋太久导致容量提升了
			CALL COLORMESSAGE("气力的最大值因为烦躁暂时下降了",C_YELLOW,2,1)
			変動値 = MIN(MAXBASE:MASTER:気力 * 20 / 100, 500)
			CALL BUFF_BASE(MASTER,BASE_気力,-変動値)
			MAXBASE:MASTER:精力 += 10
		ELSEIF FLAG:禁自慰 >= 3
			TALENT:MASTER:濃厚精液 = 1
			PRINTL 话说这边的小兄弟也蓄势已久了呢…
			MAXBASE:MASTER:精力 += 5
		ENDIF
	ELSE
		FLAG:禁自慰 = 0
		TALENT:MASTER:濃厚精液 = 0
	ENDIF
	IF BASE:MASTER:精力 < MAXBASE:MASTER:精力 * 2 / 10 && BASE:MASTER:体力 > 500
		DRAWLINE
		PRINTFORML 今天满满的射出来了……
		PRINTFORML %CALLNAME:MASTER%舒舒服服的睡着了
		PRINTFORML 因为很爽，所以小兄弟更加努力的工作了……
		CALL COLORMESSAGE("TSP和气力最大值暂时增加了",C_YELLOW,2,1)
		MAXBASE:MASTER:精力 += 5
		変動値 = MIN(MAXBASE:MASTER:気力 / 10, 300)
		CALL BUFF_BASE(MASTER,BASE_気力,変動値)
		変動値 = MIN(MAXBASE:MASTER:TSP / 2, 500)
		CALL BUFF_BASE(MASTER,BASE_TSP,変動値)
	ENDIF
ENDIF

;------ ALTER 殘留精液
IF JYCL == 1
	FOR 可能擁有陰莖的对象,1,CHARANUM
		IF HAS_PENIS(可能擁有陰莖的对象)
	;------ ALTER 殘留精液
			IF TALENT:可能擁有陰莖的对象:濃厚精液
				TALENT:可能擁有陰莖的对象:濃厚精液殘留 = TALENT:可能擁有陰莖的对象:濃厚精液
			ELSE
				TALENT:可能擁有陰莖的对象:濃厚精液殘留 = 0
			ENDIF
	;------ END 殘留精液
			IF TALENT:可能擁有陰莖的对象:恋人
				IF !RAND:4
					TALENT:可能擁有陰莖的对象:濃厚精液 = 1
				ELSE
					TALENT:可能擁有陰莖的对象:濃厚精液 = 0
				ENDIF
			ELSEIF TALENT:可能擁有陰莖的对象:恋慕
				IF !RAND:8
					TALENT:可能擁有陰莖的对象:濃厚精液 = 1
				ELSE
					TALENT:可能擁有陰莖的对象:濃厚精液 = 0
				ENDIF
			ELSEIF TALENT:可能擁有陰莖的对象:思慕
				IF !RAND:10
					TALENT:可能擁有陰莖的对象:濃厚精液 = 1
				ELSE
					TALENT:可能擁有陰莖的对象:濃厚精液 = 0
				ENDIF
			ELSE
				IF !RAND:20
					TALENT:可能擁有陰莖的对象:濃厚精液 = 1
				ELSE
					TALENT:可能擁有陰莖的对象:濃厚精液 = 0
				ENDIF
			ENDIF
		ELSE
			TALENT:可能擁有陰莖的对象:濃厚精液殘留 = 0
			TALENT:可能擁有陰莖的对象:濃厚精液 = 0
		ENDIF
	NEXT
ELSE
	FOR 可能擁有陰莖的对象,1,CHARANUM
		TALENT:可能擁有陰莖的对象:濃厚精液 = 0
		TALENT:可能擁有陰莖的对象:濃厚精液殘留 = 0
	NEXT
ENDIF
;------ END 殘留精液

;魔改功能，诱导逆推
SIF 诱导逆推_开关
	VARSET 诱导逆推_进程

CALL GIFT_FAVORITE

;寝る前にみんなの日記を…
CALL CHARA_DIARY_AFTERTRAIN

SIF FLAG:酒屋買取強化 > 0
	FLAG:酒屋買取強化 --
IF FLAG:土産屋特价销卖 >= 1000
	FLAG:土産屋特价销卖 = FLAG:土産屋特价销卖 % 1000
ELSEIF FLAG:土産屋特价销卖 >= 100
	FLAG:土産屋特价销卖 = 0
ENDIF
IF FLAG:市場の神 == 1
	FLAG:市場の神 = 0
ELSEIF FLAG:市場の神 == 2
	FLAG:市場の神 = 10
ENDIF
;何の珠を得られたか
FOR LOCAL,0,CHARANUM
	SIF CFLAG:LOCAL:出禁
		CONTINUE
	CALL SOURCE_ABLUP(LOCAL)
	CALL JUEL_CHECK(LOCAL)
	CALL DENIAL_CHECK(LOCAL)
	SIF TALENT:LOCAL:路人 
		CFLAG:LOCAL:出禁 = 2
	;清理掉体内自己的精液
	;精液量リセット
	CFLAG:LOCAL:(LOCAL + 2000) = 0
	CFLAG:LOCAL:(LOCAL + 3000) = 0
	;debug，孩子行为存储清理
	SIF CFLAG:LOCAL:兒童人数 <= 0
		CONTINUE
	VARSET CHILD_ACT:LOCAL:0
	CALL KOJO_MESSAGE_SEND("EVENT", 0, LOCAL, , , "AFTERTRAIN_END") ;addition custom code
NEXT
VARSET CUSTOM_JOB_DESC
SIF FLAG:睡四小时
	CALL 最低睡眠時間設定()


BEGIN ABLUP



@JUEL_CHECK(ARG)
#LOCALSIZE 2
#LOCALSSIZE 1
FOR LOCAL,0,100
	IF PALAM:ARG:LOCAL < PALAMLV:1
		LOCAL:1 = 0
	ELSEIF PALAM:ARG:LOCAL < PALAMLV:1*3
		LOCAL:1 = 1
	ELSEIF PALAM:ARG:LOCAL < PALAMLV:2
		LOCAL:1 = 2
	ELSEIF PALAM:ARG:LOCAL < PALAMLV:2*3
		LOCAL:1 = 10
	ELSEIF PALAM:ARG:LOCAL < PALAMLV:3
		LOCAL:1 = 20
	ELSEIF PALAM:ARG:LOCAL < PALAMLV:3*2
		LOCAL:1 = 100
	ELSEIF PALAM:ARG:LOCAL < PALAMLV:4
		LOCAL:1 = 200
	ELSEIF PALAM:ARG:LOCAL < PALAMLV:5
		LOCAL:1 = 1000
	ELSEIF PALAM:ARG:LOCAL < PALAMLV:6
		LOCAL:1 = 2000
	ELSEIF PALAM:ARG:LOCAL < PALAMLV:7
		LOCAL:1 = 3000
	ELSEIF PALAM:ARG:LOCAL < PALAMLV:8
		LOCAL:1 = 5000
	ELSEIF PALAM:ARG:LOCAL < PALAMLV:9
		LOCAL:1 = 8000
	ELSEIF PALAM:ARG:LOCAL < PALAMLV:10
		LOCAL:1 = 12000
	ELSEIF PALAM:ARG:LOCAL < PALAMLV:11
		LOCAL:1 = 25000
	ELSEIF PALAM:ARG:LOCAL < PALAMLV:12
		LOCAL:1 = 40000
	ELSEIF PALAM:ARG:LOCAL < PALAMLV:13
		LOCAL:1 = 60000
	ELSEIF PALAM:ARG:LOCAL < PALAMLV:14
		LOCAL:1 = 100000
	ELSE
		LOCAL:1 = 150000
	ENDIF
	;快感系
	IF LOCAL >= 0 && LOCAL < 9
		GOTJUEL:ARG:LOCAL = LOCAL:1 + EX:ARG:LOCAL * 1000
	;其他
	ELSEIF LOCAL < 30
		GOTJUEL:ARG:LOCAL = LOCAL:1
	;否定の珠
	ELSEIF LOCAL == 30 || LOCAL == 31 || LOCAL == 32
		GOTJUEL:ARG:100 += LOCAL:1
	ENDIF
NEXT
JUEL:ARG:100 += GOTJUEL:ARG:100
FOR LOCAL,0,100
	SIF !STRLENS(PALAMNAME:LOCAL)
		CONTINUE
	SIF LOCAL != 9 &&LOCAL != 30 && LOCAL != 31 && LOCAL != 32
		JUEL:ARG:LOCAL += GOTJUEL:ARG:LOCAL
NEXT
@DENIAL_CHECK(ARG)
#LOCALSIZE 3
#LOCALSSIZE 1
SIF JUEL:ARG:100 == 0
	RETURN 0
LOCAL = 1
WHILE LOCAL
	SIF !JUEL:ARG:否定 || (!JUEL:ARG:恭順 && !JUEL:ARG:欲情 && !JUEL:ARG:屈服)
		BREAK
	LOCAL:1 = JUEL:ARG:否定 == 1 ? 1 # JUEL:ARG:否定 / 2
	;JUEL:10～12で消費
	LOCAL:2 = RAND:3 + 10
	SIF LOCAL:1 > JUEL:ARG:(LOCAL:2)
		LOCAL:1 = JUEL:ARG:(LOCAL:2)
	JUEL:ARG:否定 -= LOCAL:1
	JUEL:ARG:(LOCAL:2) -= LOCAL:1
WEND
LOCAL = 1
WHILE LOCAL
	SIF !JUEL:ARG:否定 || (!JUEL:ARG:恥情 && !JUEL:ARG:苦痛 && !JUEL:ARG:恐怖 && !JUEL:ARG:好意 && !JUEL:ARG:優越)
		BREAK
	LOCAL:1 = JUEL:ARG:否定 == 1 ? 1 # JUEL:ARG:否定 / 2
	;JUEL:14～16で消費
	LOCAL:2 = RAND:5
	SELECTCASE LOCAL:2
		CASE 0
			LOCAL:2 = 14
		CASE 1
			LOCAL:2 = 15
		CASE 2
			LOCAL:2 = 16
		CASE 3
			LOCAL:2 = 20
		CASE 4
			LOCAL:2 = 21
	ENDSELECT
	SIF LOCAL:1 > JUEL:ARG:(LOCAL:2)
		LOCAL:1 = JUEL:ARG:(LOCAL:2)
	JUEL:ARG:否定 -= LOCAL:1
	JUEL:ARG:(LOCAL:2) -= LOCAL:1
WEND

@ONANISM
#LOCALSIZE 3
#LOCALSSIZE 1
#DIM 欲求不満度
#DIM 満足度
#DIM 嫉妬度
#DIM 珠入手倍率
#DIM 部位指定
#DIM 追加Ｖ自慰
#DIM 追加Ａ自慰
#DIM 追加Ｂ自慰
#DIM 自慰タイプ
#DIM 自慰発生閾値
#DIM CONST 文字尺寸 = 15
#DIMS 自慰画像
VARSET LOCAL

FOR LOCAL,1,CHARANUM
	SIF CFLAG:LOCAL:出禁
		CONTINUE
	部位指定 = 0
	自慰タイプ = 0
	追加Ｖ自慰 = 0
	追加Ａ自慰 = 0
	追加Ｂ自慰 = 0
	自慰発生閾値 = LIMIT(750 - 200 * TALENT:LOCAL:容易自慰 - 100 * TALENT:LOCAL:快感応答 - 100 * TALENT:LOCAL:自己愛 + 200 * TALENT:LOCAL:自制心,500,999)
	自慰発生閾値 -= 300 * TALENT:LOCAL:自慰狂
	IF CFLAG:LOCAL:オナバレ
		PRINTFORML %CALLNAME:LOCAL%把脸埋在了枕头里面…
	ELSEIF CFLAG:LOCAL:积攒度 > 自慰発生閾値
		IF !RAND:4 && GETBIT(CFLAG:LOCAL:既成事實, 1) && !TALENT:LOCAL:無知 && 诱导逆推_开关
			SETBIT 诱导逆推_进程:LOCAL:0, 0
			CONTINUE
		ENDIF
		SIF !FLAG:自慰画像非表示
			CALL PrintOnanismImage(LOCAL)
		LOCAL:1 = 0
		IF TALENT:LOCAL:愛欲
			LOCAL:1 = 4
		ELSEIF TALENT:LOCAL:恋慕
			LOCAL:1 = 3
		ELSEIF TALENT:LOCAL:炮友
			;愛人もここ
			LOCAL:1 = 2
		ELSEIF TALENT:LOCAL:思慕
			LOCAL:1 = 1
		ENDIF
		IF TALENT:LOCAL:自制心 && LOCAL:1 < 5
			LOCAL:1 += 1
		ENDIF
		IF CFLAG:LOCAL:既成事實 & 合意_诶嘿嘿 && !RAND:(8 - LOCAL:1)
			PRINTFORML %CALLNAME:LOCAL%忍耐着身体的蠢动、幻想着明天和%CALLNAME:MASTER%一起做的场景、吮吸着手指睡着了……
			自慰タイプ = 7
			;CALL KOJO_MESSAGE_SEND("EVENT",22,LOCAL,7)
			CFLAG:LOCAL:积攒度 = LIMIT(CFLAG:LOCAL:积攒度 + 100, 0 ,1000)
			SIF !CFLAG:LOCAL:神社在住 
				CFLAG:LOCAL:翌日来訪 = 1
			PRINTL
			ELSEIF CFLAG:MASTER:現在位置 == CFLAG:LOCAL:初期位置
			PRINTFORML 夜里、%CALLNAME:MASTER%听到了一些动静而睁开了眼睛
			PRINTFORML %CALLNAME:MASTER%♡……%CALLNAME:MASTER%♡……
			PRINTFORML ……%CALLNAME:LOCAL%一边抓着%CALLNAME:MASTER%的手一边压抑着呻吟声沉溺于自慰之中……
			自慰タイプ = 2
		ELSEIF ABL:LOCAL:精液中毒 > 0 && EX:LOCAL:膣内精液 && !RAND:4
			IF ABL:LOCAL:精液中毒 > 3
				PRINTFORML ……
				PRINTFORML 躁动难耐的%CALLNAME:LOCAL%、将膣内残留的%CALLNAME:MASTER%的精液涂抹在手指上开始了自慰……
				PRINTFORML 吸溜……♡……啵♡……
			ELSEIF ABL:LOCAL:精液中毒 > 1
				PRINTFORML 难忍的%CALLNAME:LOCAL%、利用膣内残留的%CALLNAME:MASTER%精液、嗅着味道开始了自慰……
				PRINTFORML 嗅嗅……♡
			ELSE
				PRINTFORML 无法忍耐的%CALLNAME:LOCAL%、伴着膣内残留的%CALLNAME:MASTER%的精液开始了自慰……
			ENDIF
			自慰タイプ = 3
		ELSEIF TALENT:LOCAL:無知
			LOCAL:1 = 0
			LOCAL:1 = EXP:LOCAL:自慰経験 - EXP:LOCAL:調教自慰経験
			IF LOCAL:1 >= 120 - (MIN(ABL:LOCAL:親密,6) * 10) && !FLAG:無知自動喪失禁止
				PRINTFORMW %CALLNAME:LOCAL%的自慰行为被监护人发现了……
				PRINTFORMW 监护人认真和%CALLNAME:LOCAL%交流了……
				PRINTFORMW %CALLNAME:LOCAL%失去了无知……
				TALENT:LOCAL:無知 = 0
				部位指定 = 1 ;qol custom code
			ELSEIF LOCAL:1 >= 80 - (MIN(ABL:LOCAL:親密,4) * 10)
				PRINTFORML 尚不明白自慰是什么的%CALLNAME:LOCAL%埋头自慰中♡……
				部位指定 = 2 ;qol custom code
			ELSEIF LOCAL:1 >= 50 - (MIN(ABL:LOCAL:親密,3) * 10)
				PRINTFORML %CALLNAME:LOCAL%在桌子角扭动腰肢、一边用手刺激秘所♡……
				部位指定 = 3 ;qol custom code
			ELSEIF LOCAL:1 >= 30 - (MIN(ABL:LOCAL:親密,2) * 10)
				PRINTFORML %CALLNAME:LOCAL%把书夹在胯间刺激着幼小的秘裂♡……
				部位指定 = 4 ;qol custom code
			ELSEIF LOCAL:1 >= 15 - (MIN(ABL:LOCAL:親密,1) * 10)
				PRINTFORML %CALLNAME:LOCAL%在学习的时候把笔贴在了胯下、就这样摩擦起秘缝♡……
				部位指定 = 5 ;qol custom code
			ELSE
				PRINTFORML %CALLNAME:LOCAL%躺在被窝里一边侧躺使大腿互相摩擦一边闷闷♡……
				部位指定 = 6 ;qol custom code
			ENDIF
			自慰タイプ = 4
		ELSEIF ABL:LOCAL:自慰中毒 >= 3 && (TALENT:LOCAL:思慕 || IS_LOVER(LOCAL) || TALENT:LOCAL:恋人 || TALENT:LOCAL:炮友) && !RAND:3
			PRINTFORML 怎么也忍耐不住了的%CALLNAME:LOCAL%、一边嗅着沾在衣服上的%CALLNAME:MASTER%的气味一边激烈的自慰着……♡
			LOCAL:2 = RAND:5
			IF LOCAL:2 == 4
				IF (TALENT:LOCAL:Ｃ感度 == 1 || ABL:LOCAL:Ｃ感覚 >= 10)
					PRINTFORM 用力揉搓着阴核、潮吹把爱液喷的到处都是
					部位指定 = 1
				ELSE
					PRINTFORM 玩弄着阴核、即使潮吹了还一味地持续着动作
					部位指定 = 1
				ENDIF
			ELSEIF LOCAL:2 == 3
				IF  (TALENT:LOCAL:Ｖ感度 == 1 || ABL:LOCAL:Ｖ感覚 >= 10) 
					PRINTFORM 用手指高速的在蜜壶之中搅动、潮吹把爱液喷的到处都是
					部位指定 = 2
					追加Ｖ自慰 = 1
				ELSE
					PRINTFORM 把手指深深地插入到蜜壶之中持续扣弄着爱液
					部位指定 = 2
					追加Ｖ自慰 = 1
				ENDIF
			ELSEIF LOCAL:2 == 2
				IF  (TALENT:LOCAL:Ａ感度 == 1 || ABL:LOCAL:Ａ感覚 >= 10) 
					PRINTFORM 交替玩弄蜜穴和菊穴，隔着互相刺激另一边
					部位指定 = 3
					追加Ａ自慰 = 1
				ELSE
					PRINTFORM 持续玩弄着蜜穴和菊穴
					部位指定 = 3
					追加Ａ自慰 = 1
				ENDIF
			ELSEIF LOCAL:2 == 1
				IF (TALENT:LOCAL:Ｂ感度 == 1 || ABL:LOCAL:Ｂ感覚 >= 10) 
					IF TALENT:LOCAL:胸圍 >= 1
						PRINTFORM 拉扯着自己那丰满的乳房、轻咬并吮吸着乳头
					ELSEIF TALENT:LOCAL:胸圍 <= -1
						PRINTFORM 爱抚着贫瘠但敏感的胸部来回揉捏拉扯乳头
					ELSE
						PRINTFORM 变换各种下流的手法玩弄着敏感的乳房、
					ENDIF
				ELSE
					IF TALENT:LOCAL:胸圍 >= 1
						PRINTFORM 吸吮着自己那丰满的乳房、
					ELSEIF TALENT:LOCAL:胸圍 <= -1
						PRINTFORM 抚摸着贫瘠的胸部来回揉搓着乳头、
					ELSE
						PRINTFORM 用下流的手法揉弄着柔软的乳房、
					ENDIF
				ENDIF
				PRINTFORM 高潮了好几次♡
				部位指定 = 4
				追加Ｂ自慰 = 1
			ELSE
				PRINTFORM 颤抖着身体对%CALLNAME:MASTER%的渴求丝毫没能减弱……
				PRINTFORM %CALLNAME:LOCAL%度过了苦闷的时光直到早上……
				部位指定 = 5
			ENDIF
			IF TALENT:LOCAL:思慕 || TALENT:LOCAL:恋慕 || TALENT:LOCAL:恋人
				PRINTFORML 、对%CALLNAME:MASTER%的思念平息了……
			ELSE
				PRINTFORML 、燃烧了身体的热量还没有平息……
			ENDIF
			追加Ｖ自慰 = 1
			自慰タイプ = 5
		ELSEIF ABL:LOCAL:自慰中毒 >= 3 && !RAND:2
			PRINTFORM 已经无法用手指满足的%CALLNAME:LOCAL%、用从河童那买来的玩具
			IF ABL:LOCAL:Ａ感覚 >= 5
				IF TALENT:LOCAL:貞操 == -1 && TALENT:LOCAL:処女 == 0
					PRINTFORM 尽可能深的插入阴道和肛门的深处
					追加Ｖ自慰 = 1
				ELSEIF TALENT:LOCAL:貞操 == -1 && TALENT:LOCAL:処女 == 1
					PRINTFORM 尽可能深的插入肛门的深处隔着刺激仍还保持着处子身的子宫
					追加Ｖ自慰 = 1
				ELSEIF TALENT:LOCAL:貞操 == 1
					PRINTFORM 抵住私处和肛门
				ELSE
					PRINTFORM 玩弄着私处和尻穴
				ENDIF
				部位指定 = 1
				追加Ａ自慰 = 1
			ELSE
				IF TALENT:LOCAL:貞操 == -1 && TALENT:LOCAL:処女 == 0
					PRINTFORM 插入阴道的深处
					追加Ｖ自慰 = 1
				ELSEIF TALENT:LOCAL:貞操 == -1 && TALENT:LOCAL:処女 == 1
					PRINTFORM 丝毫不害怕被弄破的旋转着搅动处女膜
					追加Ｖ自慰 = 1
				ELSEIF TALENT:LOCAL:貞操 == 1
					PRINTFORM 抵住私处
				ELSE
					PRINTFORM 玩弄着私处
				ENDIF
				部位指定 = 2
			ENDIF
			PRINTFORML 沉溺于自慰之中…♡
			自慰タイプ = 6
		ELSE
			LOCAL:2 = RAND:5
			IF LOCAL:2 == 4
				PRINTFORML 冷静不下来的%CALLNAME:LOCAL%用手指在私处揉、爱液把手指打湿了…♡
			ELSEIF LOCAL:2 == 3
				PRINTFORML 躁动的%CALLNAME:LOCAL%温柔的用手指爱抚着私处、爱液缓缓流出…♡
			ELSEIF LOCAL:2 == 2
				PRINTFORML 欲火焚身的%CALLNAME:LOCAL%自己用手指安慰起了自己。爱液流的哪里都是…♡
			ELSEIF LOCAL:2 == 1
				PRINTFORML 躁动难忍的%CALLNAME:LOCAL%、用舔湿的手指玩弄着蜜裂…♡
			ELSE
				PRINTFORML 身体变的躁动难安的%CALLNAME:LOCAL%、自己用手指玩弄起了私处…♡
			ENDIF
			自慰タイプ = 1
		ENDIF
		CALL KOJO_MESSAGE_SEND("EVENT",22,LOCAL,自慰タイプ,部位指定)
		SIF 自慰タイプ == 7
			CONTINUE
		EXP:LOCAL:自慰経験 += ABL:LOCAL:自慰中毒 * 2 + 1
		CALL COLORMESSAGE(@"自慰经验＋{ABL:LOCAL:自慰中毒 * 2 + 1}",C_PINK,1)
		SELECTCASE ABL:LOCAL:自慰中毒
			CASE 0 TO 2
				珠入手倍率 = 5
			CASE 3 TO 5
				珠入手倍率 = 3
			CASEELSE
				珠入手倍率 = 1
		ENDSELECT
		;快C
		CALL JUEL_UP_SPECIAL(LOCAL,"快Ｃ","Ｃ感覚",珠入手倍率,1)
		SIF 追加Ｖ自慰
			CALL JUEL_UP_SPECIAL(LOCAL,"快Ｖ","Ｖ感覚",珠入手倍率,1)
		SIF 追加Ａ自慰
			CALL JUEL_UP_SPECIAL(LOCAL,"快Ａ","Ａ感覚",珠入手倍率,1)
		SIF 追加Ｂ自慰
			CALL JUEL_UP_SPECIAL(LOCAL,"快Ｂ","Ｂ感覚",珠入手倍率,1)
		;恥情
		CALL JUEL_UP_SPECIAL(LOCAL,"恥情","自慰中毒",珠入手倍率 * 2,1)
		;欲情
		CALL JUEL_UP_SPECIAL(LOCAL,"欲情","欲望",珠入手倍率,1)
		CFLAG:LOCAL:积攒度 /= 4
		PRINTW
		IF CFLAG:LOCAL:积攒度 >= 2000
			SETCOLOR C_CREAM
			PRINTFORMW %CALLNAME:LOCAL%的身体不断渴求受孕...
			RESETCOLOR
			CFLAG:LOCAL:子宝補正 ++
			PRINTFORMW
		ENDIF
	ENDIF
	;まず欲望で基礎値を決める
	SELECTCASE ABL:LOCAL:欲望
		CASE 0 TO 2
			欲求不満度 = 10
		CASE 3 TO 5
			欲求不満度 = 20
		CASE 6 TO 10
			欲求不満度 = 30
		CASE 10 TO 15
			欲求不満度 = 50
		CASEELSE
			欲求不満度 = 80
	ENDSELECT

	欲求不満度 += ABL:LOCAL:自慰中毒 * 7 + (ABL:LOCAL:膣射中毒 + ABL:LOCAL:肛射中毒) * 3
	欲求不満度 = 欲求不満素質補正(LOCAL,欲求不満度)
	SELECTCASE 欲求不満度
		CASE IS > 400
			欲求不満度 = (欲求不満度 - 400) / 10 + 400
		CASE IS > 200
			欲求不満度 = (欲求不満度 - 200) / 4 + 200
		CASE IS > 100
			欲求不満度 = (欲求不満度 - 100) / 2 + 100
	ENDSELECT
	SIF 自慰タイプ
		欲求不満度 = 0
	SIF TCVAR:LOCAL:今天有否见面
		欲求不満度 /= 2
	SIF TALENT:LOCAL:妊娠
		欲求不満度 /= 5
	SIF TALENT:LOCAL:育児中
		欲求不満度 /= 2
	;积攒度は0~1000の間に
	CFLAG:LOCAL:积攒度 = LIMIT(CFLAG:LOCAL:积攒度 + 欲求不満度, 0, 1000)
	DEBUGPRINTFORML %CALLNAME:LOCAL%的満足度：{満足度}　欲求不満上昇値：{欲求不満度}　积攒度：{CFLAG:LOCAL:积攒度}
NEXT

@DAILYEVENT
#LOCALSIZE 1
#LOCALSSIZE 1
;デイリー最大数は イベント関連\デイリーイベント\DAILY_VARS.ERH で指定
#DIM EVENT_DAY,DAILY_EV_MAX

CALL FISHER_YATES_SHAFFLE(DAILY_EV_MAX)
ARRAYCOPY "SHAFFLE_ARRAY" , "EVENT_DAY"
;PRINTFORML デイリー発生判定
DRAWLINE
FOR LOCAL,0,DAILY_EV_MAX
	RESULT = 0
	TRYCALLFORM DAILY_CONDITION_EV{EVENT_DAY:LOCAL}
	IF RESULT > 0
		DEBUGPRINTFORML 每日活动{EVENT_DAY:LOCAL}发生了
		DRAWLINE
		BREAK
	ENDIF
	SIF RESULT == -1
		DEBUGPRINTFORML 每日活动{EVENT_DAY:LOCAL}未满足发生条件
NEXT
;PRINTFORMW 
PRINTFORML 


@欲求不満素質補正(ARG,ARG:1)
#FUNCTION
#LOCALSIZE 1
#LOCALSSIZE 1
LOCAL = ARG:1
SIF TALENT:ARG:自制心
	LOCAL /= 2
SIF TALENT:ARG:自己愛 < 0
	LOCAL /= 2
SIF TALENT:ARG:自己愛 > 0
	LOCAL = LOCAL * 5 / 4
SIF TALENT:ARG:幼稚 && !TALENT:ARG:初潮
	LOCAL /= 4
IF !TALENT:ARG:HYPERFERTILE
	SIF TALENT:ARG:妊娠
		LOCAL /= 4
	SIF TALENT:ARG:育児中
		LOCAL /= 2
ENDIF
SIF CFLAG:ARG:オナバレ
	LOCAL /= 4
RETURNF LOCAL
