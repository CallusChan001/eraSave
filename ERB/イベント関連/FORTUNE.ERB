;提到神社，果然还是神签啊
;所以现在是抽神签的时间啦

;LOCAL 0:整体运, 1:财运, 2:健康运, 3:恋爱运, 4:旅行运, 99:额外附赠一句话
;各自对应的选择编号。
;目前设计成可后续参照抽到的结果进行处理的结构，若想追加内容，可在各项目末尾补充
@FORTUNE
#DIM DYNAMIC 神签ID减1
#DIM DYNAMIC 刮刮奖
#LOCALSIZE 1
#LOCALSSIZE 1
#DIM DYNAMIC GID
;先把签的结果展示出来吧
;整体运的内容之后会再补充
神签ID减1 = RAND:神签_MAXNUM
PRINTFORML 　今日的幻存神签：
CALL 神签打印(神签ID减1 + 1)
GID = RESULT
DRAWLINE
WAIT
SETCOLOR 0xc0c0c0
PRINT 签后广告时间：
PRINTDATAW 刮刮奖
	DATAFORM 恭喜你中奖了！如果对今天的签不满意的话可以再抽一次（下次抽神签时会退款·对定期契约无效）
	DATAFORM 博丽神社！超过守矢神社的，幻想乡最强的神社！
	DATAFORM 现订阅《文文。新闻》还送美女记者特殊♥服务——（所谓特殊服务，指的是订阅者可以作为本报的独家新闻素材——主编S.Aya氏）
	DATAFORM 雾雨牌魔女扫把——一把可以拿来打扫卫生的扫把（飞行功能暂未实装）
	DATAFORM 博丽神社最新幻存神签服务——十分准！不准不要钱！（广告夸张说法，请以实际情况为准）
	DATAFORM FumoFumo～（下面画了一个巨大的Fumo）
	DATAFORM 如果对结果有所不满请明天再试一次吧——请继续支持幻存神签！
	DATAFORM 铃奈庵借书服务——知名作家稗……阿加莎克里斯Q新书绝赞公开中！
	DATAFORM 骚灵乐团演唱会二手门票低价出售——详情请咨询二岩女士（售票地点：妖怪狸森林，卖假票的不是人）
	DATAFORM 通缉：鬼人正邪 Dead or Alive 赏金：到时候再说
	DATAFORM 鲵吞亭推出最新酒水超Zun啤酒，第二杯半价。
	DATAFORM 博丽神社狛犬出租服务——时租金请到博丽神社与现任博丽巫女面谈。
	DATAFORM 博丽神社能乐表演——主演团队：KKR 48（冉冉升起的新星！）
	DATAFORM 博丽神社夏日祭典
	DATAFORM 在结界内发现了神秘的眼镜啤酒男？！
	DATAFORM 能力卡片——居家旅行必备的绝佳收藏品（详情请咨询AAA卡牌批发天弓姐）
	DATAFORM 地灵殿火焰猫殡葬服务——死人安息，活人放心
	DATAFORM 《东方非想天则》全乡争霸大赛将于不久后于玄武之泽举行
	DATAFORM 支持东方M1漫才，这就是现在的你们要积累的善行。（四季映姬）
	DATAFORM 永远亭烈小龙侦探事务所（侦探打烈车拳为正常现象，无需慌张）
	DATAFORM 宫出口牌咒具，百年老字号。纸人形，稻草人形，蛊毒，让你的仇人如被怨灵缠身般痛苦！（对怨灵凭依可能导致的副作用概不负责）
ENDDATA

SELECTCASE 神签ID减1
	CASE 6,12,23,24,34,36,41,42,44,57,69,70,73,93,94,98,103,104,105,106,109,113,119,127
		TFLAG:幸運補正 = 5
	CASE 3,118
		TFLAG:幸運補正 = 0
	CASE 1,5,9,13,14,15,17,18,26,31,38,39,43,49,53,54,55,58,60,61,64,74,78,79,82,83,84,92,95,96,100,101,102,107,114,117,120,121,122,125
		TFLAG:幸運補正 = 3
	CASE 4,11,16,21,29,33,35,47,62,63,65,67,71,72,75,76,91,115,124,126
		TFLAG:幸運補正 = 1
	CASE 22,30,45,66,110
		TFLAG:幸運補正 = -1
	CASE 0,10,20,25,27,32,48,59,80,81,86,97,99
		TFLAG:幸運補正 = -3
	CASE 2,8,28,37,46,50,56,77,87,89,90,111,116,123
		TFLAG:幸運補正 = -5
	CASE 7,19,85,112
		IF RAND:2 == 1
			TFLAG:幸運補正 = 3
		ELSE
			TFLAG:幸運補正 = -3
		ENDIF
	CASE 40
		PRINTFORMW %CALLNAME:MASTER%感觉自己似乎受到了来自守矢神社风祝的奇迹护佑……
		FOR LOCAL, 1, CHARANUM
			TALENT:(LOCAL):心情 = 1
			BASE:(LOCAL):憤怒 = 0
		NEXT
		PRINTL 情绪也变得格外的平稳
		PRINTW 另外感觉自己的身体也变轻了呢
		BASE:MASTER:体力 = MAXBASE:MASTER:体力
		BASE:MASTER:気力 = MAXBASE:MASTER:気力
		BASE:MASTER:精力 = MAXBASE:MASTER:精力
		BASE:MASTER:TSP  = MAXBASE:MASTER:TSP
		BASE:MASTER:酒気 = 0
	CASE 51
		TFLAG:幸運補正 = -5
		CALL BUFF_BASE(MASTER,0,50,1)
		CALL BUFF_BASE(MASTER,1,50,1)
		CALL BUFF_BASE(MASTER,6,50,1)
		CALL BUFF_BASE(MASTER,8,5,1)
	CASE 52
		IF RAND:2 == 1
			TFLAG:幸運補正 = RAND:5
		ELSE
			TFLAG:幸運補正 = -1*RAND:5
		ENDIF
	CASE 68
		PRINTFORML 那么，就决定一下今天的运势吧～
		CALL ASK_M("大吉",1,"吉",1,"末吉",1,"末凶",1,"凶",1,"大凶",1,"不需要",1)
		SELECTCASE RESULT
		CASE 0
			TFLAG:幸運補正 = 5
			PRINTFORMW 果然只要脑子没问题的话就会选这个呢……
		CASE 1
			TFLAG:幸運補正 = 3
			PRINTFORMW 嘛，也算是妥当的结果呢……
		CASE 2
			TFLAG:幸運補正 = 1
			PRINTFORMW 只要是吉就很不错……话是这么说但选大吉的话会不会更好一点？
		CASE 3
			TFLAG:幸運補正 = -1
			PRINTFORMW 这是为了所谓的运气守恒定律吗？还是单纯的想把“自己决定”这个运势所有的结果都看一遍？
			PRINTFORMW 哪怕你看过了所有的结果也不会有什么成就的……不过你能抽到七次这张签，你的运气（现实里的）是不是有点太好了呢？ 
		CASE 4
			TFLAG:幸運補正 = -3
			PRINTFORMW 明明可以选择大吉却非要选择凶，哈基%CALLNAME:MASTER%，你这家伙……
		CASE 5
			TFLAG:幸運補正 = -5
			PRINTFORMW 以为选大凶这样的运势会获得什么其它的补正？
			PRINTFORMW 这种事情怎么可能发生嘛！大凶就是大凶而已……
		CASE 6
			TFLAG:幸運補正 = 0
			PRINTFORMW 你这是，懒得决定了吗……？
		ENDSELECT
	CASE 88
		SELECTCASE RAND:3
		CASE 0
			TFLAG:幸運補正 = 3
			PRINTFORMW 在“地球之赫卡提亚”的影响下，%CALLNAME:MASTER%的运势变成了“吉”
		CASE 1
			TFLAG:幸運補正 = -3
			PRINTFORMW 在“月球之赫卡提亚”的影响下，%CALLNAME:MASTER%的运势变成了“凶”
		CASE 2
			TFLAG:幸運補正 = -5
			PRINTFORMW 在“异界之赫卡提亚”的影响下，%CALLNAME:MASTER%的运势变成了“大凶”
		ENDSELECT
	CASE 108
		PRINTFORMW 那么，就请猜测一下今天的运势吧？
		CALL ASK_M("吉",1,"凶",1)
		SELECTCASE RESULT
		CASE 0
			IF RAND:2 == 1
				PRINTFORMW 猜对了！运势确实是吉！
				TFLAG:幸運補正 = 3
			ELSE
				PRINTFORMW 猜错了！运势是凶哦！
				TFLAG:幸運補正 = -3
			ENDIF
		CASE 1
			IF RAND:2 == 1
				PRINTFORMW 猜对了！运势确实是凶！
				TFLAG:幸運補正 = -3
			ELSE
				PRINTFORMW 猜错了！运势是吉哦！
				TFLAG:幸運補正 = 3
			ENDIF
		ENDSELECT
		PRINTFORMW 嘛，不过无论你猜对猜错，也不会改变运势本身就是了……
		PRINTFORMW 猜运势不过是个猜谜小游戏而已，怎样，很有意思吧？
	CASE 128
		PRINTFORMW 为什么这张签上会写着你的名字……
		TFLAG:幸運補正 = 3
		EXP:MASTER:清掃経験 += 1
	CASEELSE
ENDSELECT

IF 1
;逆天改命
	SELECTCASE TFLAG:幸運補正
		CASE IS <= -5
			CFLAG:MASTER:今日の運勢 = RAND(0,480)
		CASE IS <= -3
			CFLAG:MASTER:今日の運勢 = RAND(100,520)
		CASE IS <= -1
			CFLAG:MASTER:今日の運勢 = RAND(200,520)
		CASE IS <= 0
			CFLAG:MASTER:今日の運勢 = RAND(480,520)
		CASE IS <= 1
			CFLAG:MASTER:今日の運勢 = RAND(500,800)
		CASE IS <= 3
			CFLAG:MASTER:今日の運勢 = RAND(700,980)
		;大吉有点多
		CASEELSE
			CFLAG:MASTER:今日の運勢 = RAND(700,1000)
	ENDSELECT
	PRINTFORML
	SELECTCASE CFLAG:MASTER:今日の運勢
		CASE 0 TO 199
			PRINTFORML 今日的运势：差({CFLAG:MASTER:今日の運勢})
		CASE 200 TO 479
			PRINTFORML 今日的运势：较差({CFLAG:MASTER:今日の運勢})
		CASE 480 TO 519
			PRINTFORML 今日的运势：平({CFLAG:MASTER:今日の運勢})
		CASE 520 TO 799
			PRINTFORML 今日的运势：较好({CFLAG:MASTER:今日の運勢})
		CASE 800 TO 979
			PRINTFORML 今日的运势：好({CFLAG:MASTER:今日の運勢})
		CASE 980 TO 999
			PRINTFORML 今日的运势：非常幸运！({CFLAG:MASTER:今日の運勢})
		CASEELSE

	ENDSELECT
	PRINTFORMW 再提醒一句：根据这个去行动吧
ENDIF
RESETCOLOR

;中奖用
SELECTCASE 刮刮奖
	;中奖啦，再送一次（下次抽神签时会退款）
	;不过，若处于定期契约中则无退款（因为处理起来太麻烦）
	CASE 0
		SIF !FLAG:神簽契約
			SETBIT FLAG:999, 10
	CASE 5
		PRINTFORML 
		CALL PRINT_AA(28)
		PRINTFORML 
	CASEELSE
ENDSELECT
SIF 更丰富的神签效果 && !神签_FILTER:(神签ID减1 + 1)
	CALL 更丰富的神签效果(神签ID减1)
;GDISPOSE GID
RETURN

;关于神签里“特殊运势”的处理
;目前只挑了几个喜欢的做了，后续可能会增加？（挖坑）
@更丰富的神签效果(神签ID减1)
#DIM 神签ID减1
#LOCALSIZE 3
#LOCALSSIZE 1
SELECTCASE 神签ID减1
	CASE 2
		SETCOLOR 0X09f9ff
		PRINTFORMW 突然，抽到的神签上，标注着“学业”的那一条运势发起了象征着“智慧”的青蓝光……
		SETCOLOR 0xc0c0c0
		PRINTFORMW 看来什么事情悄然发生了呢……
		EXP:MASTER:学習経験 -= 9
	CASE 7
		SETCOLOR 0Xff0000
		PRINTFORMW 突然，抽到的神签上，标注着“恋爱”的那一条运势发起了象征着命运的红光……
		SETCOLOR 0xc0c0c0
		IF LOVER_COUNT() > 1
			IF LOVER_COUNT() == 2
				PRINTFORMW 看来%CALLNAME:MASTER%因为脚踏两艘船而遭受了来自“命运之恶魔”所降下的不幸诅咒……
				BASE:MASTER:体力 -= 666
				BASE:MASTER:気力 -= 666
			ELSE
				PRINTFORML 『虽然我有不止一个恋人，但是既然恋人数量多于两个的话，也就不算“脚踏两艘船”了吧？』
				PRINTFORMW %CALLNAME:MASTER%手握着神签这样想着……
				PRINTFORMW 神签像是感应到了%CALLNAME:MASTER%的思考，渐渐地，运势散发的红光黯淡了下去……
			ENDIF
		ELSE
			PRINTFORMW 随后，神签像是察觉到了%CALLNAME:MASTER%只有一个恋人的事，运势散发的红光满意地黯淡了下去……
		ENDIF
	CASE 8
		SETCOLOR 0Xff0000
		PRINTFORMW 突然，抽到的神签上，标注着“恋爱”的那一条运势发起了象征着破灭的红光……
		PRINTFORMW ………
		PRINTFORMW ……
		PRINTFORMW …
		PRINTFORMW 仿佛是在一瞬间内，神签的红光突然亮得刺眼，
		SETCOLOR 0xc0c0c0
		FOR LOCAL,1,CHARANUM
			IF TALENT:(LOCAL):恋人
				BASE:(LOCAL):体力 = 0
				BASE:(LOCAL):気力 = 0
			ENDIF
		NEXT
		IF LOVER_COUNT() > 1
			PRINTFORMW 随着于天南海北传来的几声巨响，宛如相当于十个甚至九个红魔馆当量程度的爆炸就这样在世界各处发生了……
			PRINTFORMW 显然，%CALLNAME:MASTER%的女友们无一幸存……
		ELSE
			PRINTFORMW 随着某处传来的一声巨响，宛如相当于十个甚至九个红魔馆当量程度的爆炸就这样在哪里发生了……
			PRINTFORMW 显然，%CALLNAME:MASTER%的女友未能幸存……
		ENDIF
		PRINTFORMW ……
		PRINTFORMW 所以原来那个“破毁”是这么个意思吗？？？
	CASE 16
		SETCOLOR 0X3fe8a2
		PRINTFORMW 突然，抽到的神签上，标注着“恋爱”的那一条运势发起了象征着斩断的青绿光……
		SETCOLOR 0xc0c0c0
		FOR LOCAL,1,CHARANUM
			IF TALENT:(LOCAL):爱欲
				TALENT:(LOCAL):爱欲 = 0
			ENDIF
		NEXT
		PRINTFORMW 看来什么事情悄然发生了呢……
	CASE 21
		SETCOLOR 0Xffe553
		PRINTFORMW 突然，抽到的神签上，标注着“财运”的那一条运势发起了象征着财富的金光……
		SETCOLOR 0xc0c0c0
		FOR LOCAL,1,MUSHI_MAXCAGE+1
			IF MUSHI_CAGE:LOCAL
				CALL MUSHI_DATA(GET_MUSHIDATA(MUSHI_CAGE:LOCAL,"ムシ番号"))
				;IF MUSHI_NAME == "椰子犀角金龟" || MUSHI_NAME == "大角金龟" || MUSHI_NAME == "艳金龟" || MUSHI_NAME == "铜绿金龟子" || MUSHI_NAME == "宝石金龟" || MUSHI_NAME == "金龟子"
				IF STRCOUNT(MUSHI_NAME,"金龟")
					MONEY += 500
				ENDIF
			ENDIF
		NEXT
		PRINTFORMW 看来什么事情悄然发生了呢……
	CASE 26
		SETCOLOR 0X0000ff
		PRINTFORMW 突然，抽到的神签上，标注着“学业”的那一条运势发起了象征着智慧的蓝光……
		SETCOLOR 0xc0c0c0
		PRINTFORMW 看来什么事情悄然发生了呢……
		EXP:MASTER:学習経験 += 8
		EXP:MASTER:調合経験 += 8
	CASE 30
		SETCOLOR 0X993ac5
		PRINTFORMW 突然，抽到的神签上，标注着“恋爱”的那一条运势发起了象征着剧毒的紫光……
		SETCOLOR 0xc0c0c0
		FOR LOCAL,1,CHARANUM
			IF MARK:(LOCAL):3 >= 1
				CFLAG:(LOCAL):2 += 10*(MARK:(LOCAL):3)
				CFLAG:(LOCAL):4 += 5*(MARK:(LOCAL):3)
			ENDIF
		NEXT
		PRINTFORMW 看来什么事情悄然发生了呢……
	CASE 32
		PRINTFORMW 今天的神签看上去平平无奇……
		PRINTFORMW 噗——
		PRINTFORMW 一份《文文。新闻》就这么水灵灵地砸到了%CALLNAME:MASTER%脸上。
		PRINTFORMW
		FONTSTYLE 1
			PRINTFORML 独 家 报 道
			PRINTFORML 已然成为热门话题的，外乡人%CALLNAME:MASTER%氏竟是罹患性压抑晚期的色狼？！
		FONTSTYLE 2
			PRINTFORML 对外乡人%CALLNAME:MASTER%氏的秘密调查：
		FONTREGULAR
			PRINTFORML 据本报主编S.Aya氏的秘密调查。昨夜，%CALLNAME:MASTER%氏于熟睡之时，呢喃着诸如下文所述的梦话：
			PRINTFORML 这些梦话很可能是据说于外界流行的精神疾病“性压抑”的证明，
			PRINTFORM 据不知名线人阿纹·射命丸氏所
		FONTSTYLE 4
			PRINTFORM 杜撰
		FONTREGULAR
			PRINTFORML 提供的线索，
			PRINTFORML 此疾病很可能导致患者拥有色狼倾向。因此我们可以十分合理的推测%CALLNAME:MASTER%氏为色狼。
		PRINTFORML （以下为%CALLNAME:MASTER%氏昨晚的梦话：）
		PRINTFORML
		PRINTFORML 『……我和我的宝可梦不会输……大舌贝，使用水枪……』
		PRINTFORML 贝壳显然是对女性器的暗示，至于“大舌贝”，想必乃暗指以舌舐阴之事，至于所谓“水枪”暗示之物，不言而喻。（S.Aya）
		PRINTFORML
		PRINTFORML 『……哼，想逃？……闪电炫风*批……』
		PRINTFORML *读音如此，所指之物不详。
		PRINTFORML 炫批者，亦为舔阴之雅称。至于所舔之人“风”者，所指不明——大概是花田妖怪风见幽香氏？（S.Aya）
		PRINTFORML 
		PRINTFORML 『……啊啊……不能再玩宝可梦了……我还有口上没写……』
		PRINTFORML 大概是梦中的胡话，意义不明。（S.Aya）
		PRINTFORML
		PRINTFORML 总之，上述内容显然足以作为%CALLNAME:MASTER%氏为性压抑色狼一事之证据，
		PRINTFORMW 愿乡内少女保护好自己，洁身自好，切勿落入色狼魔爪！
		PRINTFORMW
		FOR LOCAL,1,CHARANUM
				CFLAG:(LOCAL):2 -= 100
				CFLAG:(LOCAL):4 -= 50
		NEXT	
		PRINTFORMW ……
		PRINTFORMW 『搞什么啊这报纸？！』
		PRINTFORMW %CALLNAME:MASTER%又仔细看向神签，标注着“恋爱”的那一条运势正微微发着和其它字一样的墨水颜色的光……
	CASE 33
		SETCOLOR 0Xffe553
		PRINTFORMW 突然，抽到的神签上，标注着“财运”的那一条运势发起了象征着财富的金光……
		SETCOLOR 0xc0c0c0
		MONEY += 30
		;江户时代的六文钱换算成现代日元差不多就是30日元。
		;当然我也知道大结界建立于明治17年但那样就是0.0078日元了，我觉得钱的话还是能多给点就多给点比较好（）
		;真是慷慨的神签啊（棒读）
		PRINTFORMW 看来什么事情悄然发生了呢……
	CASE 50
		SETCOLOR 0Xbb2424
		PRINTFORMW 突然，抽到的神签上，标注着“健康”的那一条运势发起了象征着死亡的暗红光……
		SETCOLOR 0xc0c0c0
		LOCAL:1 = 0
		LOCAL:2 = 0
		IF BASE:MASTER:体力 > 666
			LOCAL:2 += ((BASE:MASTER:体力)-666)/2
			CALL BUFF_BASE(MASTER,0,(-1*(BASE:MASTER:体力)+666),1)
		ELSE
			LOCAL:1 += 1
		ENDIF
		IF BASE:MASTER:気力 > 666
			LOCAL:2 += ((BASE:MASTER:気力)-666)/2
			CALL BUFF_BASE(MASTER,1,(-1*(BASE:MASTER:気力)+666),1)
		ELSE
			LOCAL:1 += 1
		ENDIF
		IF LOCAL:1 == 2
			PRINTFORMW 然而这暗红光没亮多久，就又黯淡了下去……
			PRINTFORMW 『这是什么意思？难道我现在这样已经和死人差不多了吗？？！』
		ELSE
			CALL BUFF_BASE(MASTER,6,LOCAL:2,1)
			PRINTFORMW 看来什么事情悄然（？）发生了呢……
		ENDIF
	CASE 51
		SETCOLOR 0X36cb36
		PRINTFORMW 突然，抽到的神签上，标注着“学业”的那一条运势发起了象征着核弹鸟脑袋的绿光……
		SETCOLOR 0xc0c0c0
		EXP:MASTER:学習経験 -= 6
		IF RAND:100 >= 95
			;大成功！
			SETCOLOR 0X00ff00
			PRINTFORMW 随后，仿佛是在一瞬间，标注着“财运”的那一条运势发起了象征着梦幻般清洁能源的碧绿光……
			SETCOLOR 0xc0c0c0
			IF MONEY <= 5000
				MONEY += 5000
			ELSEIF MONEY >= 25000
				MONEY += 25000
			ELSE
				MONEY *= 2
			ENDIF
		ENDIF
		PRINTFORMW 看来什么事情悄然发生了呢……
	CASE 59
		SETCOLOR 0X9611bc
		PRINTFORMW 突然，抽到的神签上，标注着“恋爱”的那一条运势发起了象征着寡欲的紫光……
		SETCOLOR 0xc0c0c0
		FOR LOCAL:1,1,CHARANUM
			LOCAL:2 = (CFLAG:(LOCAL:1):2)/10*4
			IF TALENT:(LOCAL:1):恋人 || TALENT:(LOCAL:1):爱欲
				CFLAG:(LOCAL:1):2 -= LOCAL:2
				IF !(TALENT:(LOCAL:1):爱欲)
					CFLAG:(LOCAL:1):4 += (LOCAL:2)/8
				ENDIF
				CFLAG:(LOCAL:1):340 = 0
				TCVAR:(LOCAL:1):139 = 0
				TCVAR:(LOCAL:1):140 = 0
				TCVAR:(LOCAL:1):142 = 0
			ELSEIF TALENT:(LOCAL:1):恋慕
				CFLAG:(LOCAL:1):2 -= (LOCAL:2)/2
				CFLAG:(LOCAL:1):4 += (LOCAL:2)/16
				CFLAG:(LOCAL:1):340 = 0
				TCVAR:(LOCAL:1):139 = 0
				TCVAR:(LOCAL:1):140 = 0
				TCVAR:(LOCAL:1):142 = 0
			ENDIF
		NEXT
		PRINTFORMW 看来什么事情悄然发生了呢……
	CASE 64
		SETCOLOR 0X1fdb92
		PRINTFORMW 突然，抽到的神签上，标注着“呼喝”的那一条运势发起了象征着大喊的绿光……
		SETCOLOR 0xc0c0c0
		PRINTFORML 『纳贡之时已至！』
		PRINTFORMW %CALLNAME:MASTER%喊道。
		PRINTFORMW 神签上的绿光很满意地黯淡了下去……
	CASE 69
		SETCOLOR 0Xf8ff8b
		PRINTFORMW 突然，抽到的神签上，标注着“恋爱”的那一条运势发起了象征着圣人的金光……
		SETCOLOR 0xc0c0c0
		IF LOVER_COUNT() >= 10
			PRINTFORMW 似乎是察觉到了%CALLNAME:MASTER%广阔的后宫，神签似乎很满意……
			FOR LOCAL,1,CHARANUM
				IF TALENT:(LOCAL):恋人
					TCVAR:(LOCAL):142 = 1
				ENDIF
			NEXT
			PRINTFORMW 然后，就在一瞬间，在神签的影响下，%CALLNAME:MASTER%的恋人们全都开始发情了……
			PRINTFORMW 要确保后宫和谐哦～
		ELSE
			PRINTFORMW 随后，神签像是察觉到了%CALLNAME:MASTER%的后宫数量，运势散发的金光略显失望地黯淡了下去……
			PRINTFORMW tm的，真是被看扁了啊……
		ENDIF
	CASE 74
		SETCOLOR 0Xfbfac4
		IF DAY:3 == 15 || DAY:3 == 16
			PRINTFORMW 突然，抽到的神签上，标注着“学业”和“财运”的运势发起了象征着月光的淡黄光……
		ELSE
			PRINTFORMW 突然，抽到的神签上，标注着“学业”的那一条运势发起了象征着月光的淡黄光……
		ENDIF
		SETCOLOR 0xc0c0c0
		;由于没有月相，只能公历当农历用了（悲）
		SELECTCASE DAY:3
			CASE 1,30,31
				PRINTFORMW 这么说起来今天是朔月……也就是完全看不到月亮的日子来着吧？
				EXP:MASTER:学習経験 -= 8
			CASE 2 TO 6
				PRINTFORMW 这么说起来今天是新月……月亮开始向满月接近了，虽然还是很黯淡的月相呢……
				EXP:MASTER:学習経験 += 2
			CASE 7 TO 8
				PRINTFORMW 这么说起来今天是上弦月……月亮的形状越来越接近满月了，月亮的形状也已经可以说是半圆了呢……
				EXP:MASTER:学習経験 += 4
			CASE 9 TO 14
				PRINTFORMW 这么说起来今天是上凸月……月亮已经变得圆起来了，距离满月可以说只差一点了吧？
				EXP:MASTER:学習経験 += 6
			CASE 15 TO 16
				PRINTFORMW 这么说起来已经是满月（或者说望月）了呢！狼人的力量加强了什么的……
				EXP:MASTER:学習経験 += 8
				MONEY += 2000
			CASE 17 TO 21
				PRINTFORMW 这么说起来今天是下凸月……满月要凹下去什么的，对于妖怪什么的想必不是什么好事吧？
				EXP:MASTER:学習経験 -= 2
			CASE 22 TO 23
				PRINTFORMW 这么说起来今天是下弦月……月亮只剩下一半了呢……
				EXP:MASTER:学習経験 -= 4
			CASE 24 TO 29
				PRINTFORMW 这么说起来今天是残月……这个月最后的月光了哦？再过几天，月亮可就又要看不见呢……
				EXP:MASTER:学習経験 -= 6
		ENDSELECT
		PRINTFORMW 看来什么事情悄然发生了呢……
	CASE 77
		SETCOLOR 0Xff5252
		PRINTFORMW 突然，抽到的神签上，标注着“恋爱”的那一条运势发起了象征着逆反的红光……
		SETCOLOR 0xc0c0c0
		FOR LOCAL:1,1,CHARANUM
			IF MARK:(LOCAL:1):2 > 0
				MARK:(LOCAL:1):2 = 0
				CFLAG:(LOCAL:1):2 -= 400
				CFLAG:(LOCAL:1):4 -= 200
			ENDIF
		NEXT
		PRINTFORMW 看来什么事情悄然发生了呢……
	CASE 85
		SETCOLOR 0Xff5252
		PRINTFORMW 突然，抽到的神签上，标注着“健康”的那一条运势发起了象征着逆反的红光……
		SETCOLOR 0xc0c0c0
		IF BASE:MASTER:体力 > (MAXBASE:MASTER:体力)/2
			CALL BUFF_BASE(MASTER,0,(-1*((MAXBASE:MASTER:体力)/2)),1)
		ELSE
			CALL BUFF_BASE(MASTER,0,((MAXBASE:MASTER:体力)/2),1)
		ENDIF
		IF BASE:MASTER:気力 > (MAXBASE:MASTER:気力)/2
			CALL BUFF_BASE(MASTER,1,(-1*((MAXBASE:MASTER:気力)/2)),1)
		ELSE
			CALL BUFF_BASE(MASTER,1,((MAXBASE:MASTER:気力)/2),1)
		ENDIF
		PRINTFORMW 看来什么事情悄然发生了呢……
	CASE 88
		SETCOLOR 0Xbb205e
		PRINTFORMW 突然，抽到的神签上，标注着“恋爱”的那一条运势发起了象征着地狱的红光……
		SETCOLOR 0xc0c0c0
		IF LOVER_COUNT() < 3
			IF TALENT:113:恋人
				PRINTFORMW %CALLNAME:MASTER%的后宫不满三人，这使得地狱的女神也为%CALLNAME:MASTER%而可怜……
				PRINTFORMW 然而除此之外地狱女神还能做什么呢？给%CALLNAME:MASTER%安排几场相亲吗？
			ELSE
				CFLAG:113:2 += 200
				CFLAG:113:4 += 100
				PRINTFORMW %CALLNAME:MASTER%的后宫不满三人，这使得地狱的女神也为%CALLNAME:MASTER%而可怜……
				PRINTFORMW 地狱女神念在%CALLNAME:MASTER%如此可怜的份上，略微降低了自己成为%CALLNAME:MASTER%恋人的标准……
			ENDIF
		ELSE
			PRINTFORMW 随后，神签像是察觉到了%CALLNAME:MASTER%的后宫数量，运势散发的光芒黯淡了下去……
			PRINTFORMW 这是满意还是不满意呢……？
		ENDIF
	CASE 89
		SETCOLOR 0Xffe553
		PRINTFORMW 突然，抽到的神签上，标注着“恋爱”的那一条运势发起了象征着财富的金光……
		SETCOLOR 0xc0c0c0
		LOCAL:2 = MONEY/200
		MONEY = 0
		IF LOVER_COUNT() > 0
			FOR LOCAL:1,1,CHARANUM
				IF TALENT:(LOCAL:1):恋人
					CFLAG:(LOCAL:1):2 += LOCAL:2/LOVER_COUNT()
				ENDIF
			NEXT
		ELSE
			CFLAG:125:2 += LOCAL:2
		ENDIF
		PRINTFORMW 看来什么事情悄然发生了呢……
	CASE 100
		SETCOLOR 0Xedf683
				PRINTFORMW 突然，抽到的神签上，标注着“学业”的那一条运势发起了象征着小鸡的金光……
				EXP:MASTER:学習経験 -= 6
				EXP:MASTER:歌唱経験 += 6
		SETCOLOR 0xc0c0c0
	CASE 106
		SETCOLOR 0Xffe553
		PRINTFORMW 突然，抽到的神签上，标注着“恋爱”的那一条运势发起了象征着财富的金光……
		SETCOLOR 0xc0c0c0
		FOR LOCAL:1,1,CHARANUM
			IF TALENT:(LOCAL:1):恋人
				IF CFLAG:(LOCAL:1):2 >= 100
					MONEY += 200
					CFLAG:(LOCAL:1):2 -= 100
				ELSE
					MONEY += (CFLAG:(LOCAL:1):2)*2
					CFLAG:(LOCAL:1):2 = 0
				ENDIF
			ELSEIF TALENT:(LOCAL:1):恋慕
				IF CFLAG:(LOCAL:1):2 >= 100
					MONEY += 100
					CFLAG:(LOCAL:1):2 -= 100
				ELSE
					MONEY += (CFLAG:(LOCAL:1):2)
					CFLAG:(LOCAL:1):2 = 0
				ENDIF
			ELSEIF TALENT:(LOCAL:1):思慕 || TALENT:(LOCAL:1):爱欲
				IF CFLAG:(LOCAL:1):2 >= 100
					MONEY += 50
					CFLAG:(LOCAL:1):2 -= 100
				ELSE
					MONEY += (CFLAG:(LOCAL:1):2)/2
					CFLAG:(LOCAL:1):2 = 0
				ENDIF
			ENDIF
		NEXT
		PRINTFORMW 看来什么事情悄然发生了呢……
	CASE 108
		SETCOLOR 0Xffe553
		PRINTFORMW 突然，抽到的神签上，标注着“经商”的那一条运势发起了象征着财富的金光……
		SETCOLOR 0xc0c0c0
		IF RAND:2 == 1
			IF RAND:777 == 1
			;头奖，不过这个真的有人能抽到吗……？
				MONEY += 1000000
				PRINTFORMW JACKPOT！头奖！
				PRINTFORMW %CALLNAME:MASTER%中了整整一百万日元啊！
				PRINTFORMW
				PRINTFORMW 此时此刻，%CALLNAME:MASTER%的心情，会是什么样子的呢……？
				PRINTFORMW 
				CALLF 生成图片,@"剧本自定义附件/东方幻存神签/ZhuanQianLa.webp","ZhuanQianLa"
				CALL PRINT_IMAGE,"ZhuanQianLa",1000
				PRINTFORML 『我赚钱啦赚钱啦，我都不知道怎么去花。
				PRINTFORML 我左手买个诺基亚，右手买个摩托罗拉。
				PRINTFORML 我移动联通小灵通，一天换一个电话号码啊。
				PRINTFORML 我坐完奔驰开宝马，没事洗桑拿吃龙虾。
				PRINTFORML 
				PRINTFORML 我赚钱啦赚钱啦，光保姆就请了仨。
				PRINTFORML 哎一个扫地一个做饭一个是当奶妈。
				PRINTFORML 我厕所墙上挂国画，倍儿像艺术家啊。
				PRINTFORML 我贷款按揭名牌西服手表和电脑啊。』
				PRINTFORML 
				PRINTFORML 节选自现代诗《我赚钱了》
				PRINTFORML 
				PRINTFORMW 而且由于是正儿八经的中奖（还不用交个人所得税）而不是贷款，%CALLNAME:MASTER%的心情只会比诗里描写的还要激动……
			ELSE
				MONEY += 5000
				PRINTFORMW 看来是中奖了呢。
				PRINTFORMW 多少能得到点钱吧。
			ENDIF
		ELSE
			IF MONEY > 5000
				MONEY -= 5000
			ELSE
				MONEY = 0
			ENDIF
			PRINTFORMW ……随后又绝望般的熄灭了……
			PRINTFORMW 赌输了啊！
		ENDIF
	CASE 113
		SETCOLOR 0Xdddddd
		PRINTFORMW 突然，抽到的神签上，标注着“学业”的那一条运势发起了象征着采掘的银光……
		SETCOLOR 0xc0c0c0
		PRINTFORMW 看来什么事情悄然发生了呢……
		EXP:MASTER:学習経験 += 8
		EXP:MASTER:採集経験 += 8
	CASE 119
		SETCOLOR 0X0000ff
		PRINTFORMW 突然，抽到的神签上，标注着“兴趣”的那一条运势发起了象征着月海的蓝光……
		SETCOLOR 0xc0c0c0
		EXP:MASTER:釣魚経験 += 10
	CASEELSE
ENDSELECT
RESETCOLOR
RETURN 0