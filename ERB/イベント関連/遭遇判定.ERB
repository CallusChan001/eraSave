@遭遇判定
#LOCALSIZE 1
#LOCALSSIZE 1
#DIM 基本遭遇率
#DIM 遭遇率

遭遇率 = 0
CVARSET CFLAG, GETNUM(CFLAG, "遭遇位置")

;出禁人数が多いほど遭遇率は高くなる（あんまり密にならないようにする）    出禁人数越多，遭遇率就越高（尽量不要过于密集）
;※従来は低いほど遭遇しやすかったけど分かりにくいんで直感的％に,全体的にやや上方修正    以往虽是数值越低越容易遇到但越难判断，因此整体上做了些许上调，以符合直观百分比
SELECTCASE (FLAG:出禁人数 * 100 / CHARANUM)
	CASE IS >= 60
		基本遭遇率 = 51
	CASE IS >= 45
		基本遭遇率 = 48
	CASE IS >= 30
		基本遭遇率 = 45
	CASE IS >= 15
		基本遭遇率 = 42
	CASE IS >= 10
		基本遭遇率 = 39
	CASEELSE
		基本遭遇率 = 36
ENDSELECT

;連動設定もあるのでこの処理は出禁角色も行う    由于也有联动设定，因此出禁角色也会进行这一处理
FOR LOCAL, 1, CHARANUM
	;自宅在住角色は遭遇しない    不会遇到自宅角色和路人，来访和宴会也不进行
	SIF CFLAG:LOCAL:神社在住
		CONTINUE
	SIF TALENT:LOCAL:路人
		CONTINUE
	IF !(CFLAG:LOCAL:拠点来訪 && CFLAG:LOCAL:宴会参加)
		遭遇率 = 基本遭遇率 + TALENT:LOCAL:思慕 * 3 + TALENT:LOCAL:恋慕 * 6 + CFLAG:LOCAL:依頼状況 * 6
		SIF 遭遇率 > RAND:100
			CALL 遭遇フラグ決定(LOCAL)
	ENDIF
NEXT

;全角色決定後、連動角色を設定する    所有角色确定后，再设定联动角色
FOR LOCAL, 1, CHARANUM
	VARSET RESULT
	;新增剧本侧接口，调用剧本侧设定的角色移动数值
	CALL KOJO_CHECK(LOCAL, "CHARADATA", "MOVE", 1)
	IF RESULT == -1 
		;剧本侧禁用该机制，但是请作者务必清楚自己在做什么
		VARSET RESULT
	ELSEIF RESULT || RESULT:1 
		;采用剧本设定的值
	ELSE
		;剧本没有采用CHECK函数或者剧本需要是结果为0，从而采用原版移动数值
		CALLFORM CHARAMOVE_DATA_{NO:LOCAL}(1)
	ENDIF
	;依存連動
	IF RESULT
		CFLAG:LOCAL:遭遇位置 = CFLAG:RESULT:遭遇位置
	;対等連動
	ELSEIF RESULT:1
		;連動設定する
		CFLAG:LOCAL:遭遇位置 = CFLAG:連動角色設定(RESULT:1, RESULT:2, RESULT:3):遭遇位置
	ENDIF
NEXT
[IF_DEBUG]
	PRINTL 【今日的遭遇位置】※DEBUG用
	FOR LOCAL, 1, CHARANUM
		IF CFLAG:LOCAL:遭遇位置
			PRINTFORM %CALLNAME:LOCAL, 18, LEFT%%NAME_FROM_PLACE(CFLAG:LOCAL:遭遇位置)%
			VARSET RESULT
			;新增剧本侧接口，调用剧本侧设定的角色移动数值
			CALL KOJO_CHECK(LOCAL, "CHARADATA", "MOVE", 1)
			IF RESULT == -1 
				;剧本侧禁用该机制，但是请作者务必清楚自己在做什么
				VARSET RESULT
			ELSEIF RESULT || RESULT:1 
				;采用剧本设定的值
			ELSE
				;剧本没有采用CHECK函数或者剧本需要是结果为0，从而采用原版移动数值
				CALLFORM CHARAMOVE_DATA_{NO:LOCAL}(1)
			ENDIF
			IF RESULT
				PRINTFORM （依存连动）
			ELSEIF RESULT:1
				PRINTFORM （对等连动）
			ENDIF
			PRINTL 
		ENDIF
	NEXT
	WAIT
[ENDIF]
;-----------------------------------------------------------
;角色の遭遇位置を設定する関数    设定角色遭遇位置的函数，注意遭遇位置应该在外出的其他地图上，MAINMAP应该走来访或者本地而非遭遇
;-----------------------------------------------------------
@遭遇フラグ決定(CHARA)
#LOCALSIZE 2
#LOCALSSIZE 1
#DIM CHARA
#DIM P_ID
#DIM LPCOUNT
VARSET LOCAL
LPCOUNT = 0
;最初に出現タイプを決定,他のタイプになったらその位置には出現不会うに    首先确定出现类型，若变为其他类型则不会在该位置出现
;自宅位置：經常去的地方：其他＝１：２：１
WHILE !LOCAL
	SELECTCASE RAND:1000
	CASE IS < 250
		;自宅位置はそのまま決定    自宅位置保持不变并返回
		IF CFLAG:CHARA:自宅位置
			LOCAL = 1
			CFLAG:CHARA:遭遇位置 = CFLAG:CHARA:自宅位置
			;RETURN
		ENDIF
	CASE IS < 750
		SIF CFLAG:CHARA:經常去的地方 >= 0 && CFLAG:CHARA:經常去的地方 != MAIN_MAP
			LOCAL = 2
	CASEELSE
		LOCAL = 3
	ENDSELECT
	;防死结界（？）
	LPCOUNT++
	IF LPCOUNT > 100 && !LOCAL
		LOCAL = 3
		BREAK
	ENDIF
WEND

LPCOUNT = 0
;遭遇位置の決定
WHILE 1
	;这里把前面自宅位置情况的return移到后面去，走公共return，方便在原版设置完成后引入特殊设置
	SIF LOCAL == 1
		BREAK
	;防死结界（？） 这里要是一直得不出来那就干脆点设成自宅或者主地图外出的第一间（意味着一般来说肯定遇不上了？）好了
	IF LPCOUNT > 150
		IF CFLAG:CHARA:自宅位置
			CFLAG:CHARA:遭遇位置 = CFLAG:CHARA:自宅位置
		ELSE
			;CFLAG:CHARA:遭遇位置 = MAIN_MAP*100+10
		ENDIF
		BREAK
	ENDIF
	LPCOUNT ++
	P_ID = RANDOM_ODEKAKE()
	;經常去的地方に自宅しかない、經常去的地方しか行かない場合は無限ループになるので100で妥協する    如果经常去的地方就是自宅位置，就会形成无限循环，所以妥协到 100。
	IF LPCOUNT < 100
		;自宅位置はなるべく行かない    避免在自宅的情况
		IF P_ID == CFLAG:CHARA:自宅位置
			CONTINUE
		;經常去的地方の場合,經常去的地方以外には行かない    就经常去的地方这个情况而言，除了那个地图不会去其他地方
		ELSEIF LOCAL == 2
			SIF GET_MAPID(P_ID) != CFLAG:CHARA:經常去的地方
				CONTINUE
		;其他の地域の場合,經常去的地方にはなるべく行かない    就其他地方这个情况而言，避免去到经常去的那个地图
		ELSE
			SIF GET_MAPID(P_ID) == CFLAG:CHARA:經常去的地方
				CONTINUE
		ENDIF
	ENDIF
	;出現場所かチェック    确认出现位置
	RESULT = 0
	;新增剧本侧接口，调用剧本侧设定的角色移动数值
	CALL KOJO_CHECK(CHARA, "CHARADATA", "MOVE", 7, P_ID)
	IF RESULT == -1
		;对于出现区域设定，保证能禁用掉原版设定中启用的真值
	ELSEIF RESULT
		;采用剧本设定的值，这里必然是真值
	ELSE
		;剧本没有采用CHECK函数或者剧本需要是结果为0，从而采用原版的移动数值
		CALLFORM CHARAMOVE_DATA_{NO:CHARA}(7, P_ID)
	ENDIF 
	SIF !RESULT
		CONTINUE
	CFLAG:CHARA:遭遇位置 = P_ID
	BREAK
WEND
RETURN

;-----------------------------------------------------------
;角色が他の角色に依存した挙動をするのを設定する
;暫定3人まで。遭遇位置でのみ使用されている
;暫定恋慕と思慕のみ（遭遇率に影響があるので）
;同列なら小さい方が優先
;CHARA:1～3  この三人（二人）のうちもっとも評価が高い角色に依存する
;-----------------------------------------------------------
@連動角色設定(CHARA:1, CHARA:2, CHARA:3)
#LOCALSIZE 1
#LOCALSSIZE 1
#FUNCTION
#DIM CHARA, 4
FOR LOCAL, 1, 4
	SIF !CHARA:LOCAL
		BREAK
	SIF TALENT:(CHARA:LOCAL):恋慕
		RETURNF CHARA:LOCAL
NEXT
FOR LOCAL, 1, 4
	SIF !CHARA:LOCAL
		BREAK
	SIF TALENT:(CHARA:LOCAL):思慕
		RETURNF CHARA:LOCAL
NEXT
;全部無いならCHARA:1で決定
RETURNF CHARA:1

;-----------------------------------------------------------
;角色の出現フラグを判定する関数    判断角色出现标志的函数
;=1拠点には来ない =2来る =3 住んでる    =1不常来 =2会来 =3住在这里
;-----------------------------------------------------------
@出現状態(ARG)
#FUNCTION
#LOCALSIZE 1
#LOCALSSIZE 1
IF CFLAG:ARG:出禁 || TALENT:ARG:路人
	RETURNF 0
ELSEIF CFLAG:ARG:神社在住
	RETURNF 3
ELSEIF CFLAG:ARG:拠点来訪
	RETURNF 2
ELSEIF CFLAG:ARG:宴会参加
	RETURNF 2
ELSEIF !CHARA_HOLIDAY(ARG)
	RETURNF 1
ELSEIF CFLAG:ARG:招待
	RETURNF 1
ELSEIF CFLAG:ARG:遭遇位置
	RETURNF 1
ELSE
	RETURNF 0
ENDIF

;-----------------------------------------------------------
;OPTIONから呼ばれる角色の出現場所列表
;-----------------------------------------------------------
@SHOW_APPEAR_LIST
#LOCALSIZE 1
#LOCALSSIZE 1
#DIM LPCOUNT
#DIM LPCOUNT2
#DIM LPCOUNT3
#DIM P_ID
#DIM MAPID
#DIM メイン保管
メイン保管 = MAIN_MAP
MAIN_MAP = MAXHOME
LPCOUNT3 = 0
SIF FLAG:並び替え
	CALL CHARASORT_EX
FOR LPCOUNT, 1, EX_CHARANUM
	IF FLAG:並び替え
		LOCAL = RESULT:LPCOUNT
		SIF LOCAL < 0
			CONTINUE
	ELSE
		LOCAL = LPCOUNT
		SIF LOCAL >= CHARANUM
			BREAK
	ENDIF
	SIF TALENT:LOCAL:路人
		BREAK
	LPCOUNT3 ++
	SIF LPCOUNT3 % 10 == 1
		PRINTFORML 　　 %" ", 18, LEFT% 移率 分寸　博　寺　里　湖　竹　森　冥　山　守　地　月　自宅位置
	PRINTFORM [{LOCAL,3 }] %CALLNAME:LOCAL, 18, LEFT% {CFLAG:LOCAL:移動率補正, 2} 　
	IF CFLAG:LOCAL:移動節度 > 0
		PRINTFORM +{ABS(CFLAG:LOCAL:移動節度), 2, LEFT}　
	ELSEIF CFLAG:LOCAL:移動節度 < 0
		PRINTFORM -{ABS(CFLAG:LOCAL:移動節度), 2, LEFT}　
	ELSE
		PRINTFORM  0 　
	ENDIF
	FOR MAPID, 0, MAXHOME
		IF MAPID == GET_MAPID(CFLAG:LOCAL:自宅位置) && CFLAG:LOCAL:自宅位置 && CFLAG:LOCAL:自宅位置 != 99
			PRINT ☆
		ELSEIF MAPID == CFLAG:LOCAL:經常去的地方
			PRINT ◎
		ELSE
			FOR LPCOUNT2, 1, 10
				P_ID = MAPID * 100 + LPCOUNT2 * 10
				;新增剧本侧接口，调用剧本侧设定的角色移动数值
				CALL KOJO_CHECK(LOCAL, "CHARADATA", "MOVE", 7, P_ID)
				IF RESULT == -1
					;对于出现区域设定，禁用标志(-1)能保证能禁用掉原版设定中启用的真值1
				ELSEIF RESULT
					;采用剧本设定的值，这里必然是真值1
				ELSE
					;剧本没有采用CHECK函数或者剧本需要是结果为0，从而采用原版的移动数值
					CALLFORM CHARAMOVE_DATA_{NO:LOCAL}(7, P_ID)
				ENDIF 
				IF RESULT
					PRINT ○
					BREAK
				ELSEIF LPCOUNT2 == 9
					PRINT －
					BREAK
				ENDIF
			NEXT
		ENDIF
		PRINT 　
	NEXT
	PRINTFORM %NAME_FROM_PLACE(CFLAG:LOCAL:自宅位置)%
	PRINTL 
NEXT
MAIN_MAP = メイン保管
DRAWLINE
PRINTL 所有角色的出现区域。选择仅用于高亮显示，无实际效果。有些角色会根据条件变化。
PRINTL 移率：在据点时的移动倾向
PRINTL 分寸：在据点时顾及隐私的程度
PRINTL ☆：自宅区域　◎：常去区域　○：出现区域　－：不出现区域
PRINTL [  0] 返回
INPUT
