@PfScale(LOCALS,ARG)
CALL RELINE(2,1)
PRINTFORML 对%LOCALS%的印象是？
SETCOLOR C_AQUA
PRINTBUTTON @"[热爱] ",5
SETCOLOR C_GREEN
PRINTBUTTON @"[喜欢] ",3	
SETCOLOR C_PINK
PRINTBUTTON @"[有趣] ",1
RESETCOLOR
PRINTBUTTON @"[一般] ",0
PRINTL
SETCOLOR C_GRAY
PRINTBUTTON @"[无趣] ",-1
SETCOLOR C_ORANGE
PRINTBUTTON @"[讨厌] ",-3
SETCOLOR C_RED
PRINTBUTTON @"[憎恶] ",-5
RESETCOLOR
IF ARG==1
	LOCALS:1'="[选择形容词] "
ELSEIF ARG==2
	LOCALS:1'="[结束设定] "
ELSEIF !ARG
	LOCALS:1'="[返回tag分类] "
ENDIF
PRINTBUTTON LOCALS:1,999
INPUT
SELECTCASE RESULT
CASE 5,0,999
	LOCAL=RESULT
CASE -3,-1,1,3
	;-3->-4/-3,1->1/2
	LOCAL=RESULT+RAND:2*RESULT/ABS(RESULT)
CASE -5
	;-5/-10
	LOCAL=RESULT*(RAND:2+1)
CASEELSE 
	PRINTW 无效输入
	CALL RELINE(2)
	RESTART
ENDSELECT
CALL REVIEW_GIFT(0)
LOCAL:1=STR_MULTI_COUNT(LOCALS,RESULTS:1)
LOCAL:2=STR_MULTI_COUNT(LOCALS,RESULTS:2)
IF LOCAL:1+LOCAL:2
	WPf:1'=REPLACE(WPf:1,LOCALS,"")	
	WPf'=REPLACE(WPf,LOCALS,"")	
ENDIF
IF LOCAL==999
ELSEIF (LOCAL>0&&(LOCAL:1+LOCAL)>5)||(LOCAL<0&&(LOCAL:2-LOCAL)>10)
	WPf:1'=REPLACE(WPf:1,LOCALS,"")	
	WPf'=REPLACE(WPf,LOCALS,"")	
	PRINTW 超出范围，重置喜好
ELSEIF (LOCAL>0&&LOCAL:2*(LOCAL:1+LOCAL))||(LOCAL<0&&(LOCAL:2-LOCAL)*LOCAL:1)
	WPf:1'=REPLACE(WPf:1,LOCALS,"")	
	WPf'=REPLACE(WPf,LOCALS,"")			
	PRINTW 喜好冲突，重置喜好
ELSEIF INRANGE(LOCAL,-10,-1)
	WPf:1+=LOCALS*(-LOCAL)
ELSEIF INRANGE(LOCAL,1,5)
	WPf+=LOCALS*LOCAL
ENDIF
CALL RELINE(2)
RETURN LOCAL
@PfSetDetailed
PRINTL 试想某天自己收到一份礼物，对礼物会有怎样的印象呢？
PRINTW 以下，超出范围（喜欢Lv5、讨厌Lv10）则重置该tag喜好，同时喜欢与讨厌同理。
CALL GIFT_ORDER_MADE_FROM_MASTER(0)
PRINTL 设定完毕了吗？
CALL ASK_YN()
SIF RESULT
	RESTART
;PRINTFORMW 喜好:%WPf%，讨厌%WPf:1%
@PresetTagChoice
WPf'="虫/"*3+"魔法/時の/可愛的/妖怪/"
WPf:1'="死生/"*10+"危険の/"*5+"可怕的/带来死亡の/被诅咒の/破滅の/"*3+"誇張/"
FOR LOCAL,40,50						
	SELECTCASE LOCAL
		CASE 40
			CALL PresetTag(LOCAL,,"秩序/")
			CALL PresetTag(LOCAL,1,"混沌/")
		CASE 41
			CALL PresetTag(LOCAL,,"茶/茶具/")	
		CASE 43
			CALL PresetTag(LOCAL,,"書物/書/文具/知的/")
		CASE 44
			CALL PresetTag(LOCAL,,"食器/")
		CASE 45
			CALL PresetTag(LOCAL,,"楽器/音楽/演奏の/乐谱/")
		CASE 47
			CALL PresetTag(LOCAL,,"魚/")
	ENDSELECT	
NEXT
SIF INRANGE(TALENT:0:121,1,3)
	WPf+="酒具/酒/"*(TALENT:0:121*2-1)	
@PresetTag(ARG,ARG:1,ARGS)
;ARG:0数值，ARG:1喜恶，0喜1恶；ARGS tag
SELECTCASE ARG
	CASE 40 TO 45
		SIF !INRANGE(ABL:0:ARG,1,6)
			RETURN 
		ARG:2=(ABL:0:ARG+1)/2*2-1
	CASE 46 TO 49
		SIF !INRANGE(TALENT:0:ARG,1,6)
			RETURN 
		ARG:2=(TALENT:0:ARG+1)/2*2-1
ENDSELECT
SIF ARG:2!=5
	ARG:2+=RAND:2
WPf:(ARG:1)+=ARGS*ARG:2

@SimPfSet
VARSET LOCAL
CALL RELINE(3,1)
PRINTL 请选择需要同步的对象
FOR LOCAL,1,CHARANUM
	SIF !TALENT:LOCAL:7
		CONTINUE
	PRINTBUTTON @"%@"[%CALLNAME:LOCAL%]",12,LEFT%",LOCAL
	LOCAL:1++
	SIF !(LOCAL:1%4)
		PRINTL
NEXT
PRINTL
PRINTBUTTON @"%"[退出]",12,LEFT%",0
INPUT
IF !RESULT
	RETURN
ELSEIF !INRANGE(RESULT,1,CHARANUM)||!TALENT:RESULT:7
	PRINTW 输入无效，现在返回
	CALL RELINE(3,1)
	RESTART
ELSE
	WPf = %GET_STR(, "キャラデータ", RESULT, "感性：好き")%
	WPf:1 = %GET_STR(, "キャラデータ", RESULT, "感性：嫌い")%
	PRINTFORMW 已设定为与%CALLNAME:RESULT%相同的喜好
ENDIF
@ResetPf
PRINTFORML 重置%CALLNAME:MASTER%的喜恶吗？
CALL ASK_YN()
SIF !RESULT
	VARSET WPf,"",1,3
@SetPfChoice
CALL RELINE(4,1)
PRINTFORML 设定%CALLNAME:MASTER%的喜好吧
PRINTBUTTON @"[使用预设] ",0
SIF TALENT:0:7
	PRINTBUTTON @"[与恋人同步] ",1
PRINTBUTTON @"[详细设定] ",2
PRINTBUTTON @"[返回] ",9
INPUT
SELECTCASE RESULT
	CASE 9
		RETURN
	CASE 0
		PRINTFORML 根据%CALLNAME:MASTER%的技能等级设定喜好，确定吗？
		CALL ASK_YN()
		SIF !RESULT
			CALL PresetTagChoice
	CASE 1
		SIF TALENT:0:7
			CALL SimPfSet
	CASE 2
		CALL PfSetDetailed
ENDSELECT
CALL RELINE(4)
RESTART