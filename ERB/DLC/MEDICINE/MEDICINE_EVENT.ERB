;医疗事件
@PreMEv
#LOCALSIZE 2
;今日患者总人数
#DIM DYNAMIC MAX_PATIENTS
VARSET LOCAL
CALL MAble
SIF !RESULT
	RETURN
SIF !MeDf:1
	LIMIT EP:1,-499,999
;基础随机1~3个人+名望奖励随机人数
MAX_PATIENTS = RAND:3 + 1 + RAND:(PopBonus(EP:1)+1)
;PRINTFORML 今天来了{MAX_PATIENTS}位患者的样子
CALL Message_Box(@"今天来了{MAX_PATIENTS}位预约患者的样子")
PRINTL
CALL MEv(MAX_PATIENTS)
@MAble
RESULT=0
IF EP < 2500
	CALL Message_Box_mk2("药品不足没法正常开业的哦！", "PINK", 20)
	PRINTW 请兑换足额的EP点数...
ELSEIF !MeDf:1&&IFDr:1
	CALL Message_Box_mk2(@"%CALLNAME:0%，患者们已经离开了", "PINK", 20)
	PRINTW 今天已经营业过了...
ELSEIF BASE:0:0<500||BASE:0:1<500
	CALL Message_Box_mk2(@"%CALLNAME:0%，太累可没法治病哦！", "PINK", 20)
	WAIT
ELSE
	;PRINTL 要开始今天的营业吗？一天只能开业一次哦！
	IF !MeDf:1
		CALL Message_Box_mk2("要开始今天的营业吗？一天只能开业一次哦！", "PINK", 20)
	ELSE
		CALL Message_Box_mk2("要开始今天的营业吗？没接诊完预约患者就下班会被投诉哦！", "PINK", 20)
	ENDIF
	PRINTL
	PRINTL [1]是    [0]否
	INPUT
	SIF RESULT != 1
		RETURN 0
	IFDr:1=1
	RESULT=1
ENDIF
RETURN RESULT
@MEv(MAX_PATIENTS,ARG)
;ARG教程，1轻症2急诊
;今日患者总人数
#DIM DYNAMIC MAX_PATIENTS
#DIM PatCnt
;每次生成患者，最大患者数-1
IF !MeDf:1&&!ARG&&MAX_PATIENTS <= 0
	;PRINTL 没有更多患者了，今天营业结束
	CALL Message_Box("没有更多患者了，今天营业结束")
	PRINTL
	RETURN
ENDIF
IF ARG
	mCID=52
ELSE
	CALL RDis
	SIF !mCID&&!ARG
		mCID=RAND:(CHARANUM-2)+1
ENDIF
IF ARG
	IF ARG==1
		BASE:52:42=1,0,101,19009,1900,1900,100,19e3,50,100
		mCID:1=19e2
		;感冒，体力101，病症1000，防御100，营养50，感冒100
		PRINTFORML 试药员%CALLNAME:mCID%进来了...
		PRINTW
	ELSEIF ARG==2
		BASE:52:42=1,0,-1000,19037,53e3,53e3,325,53e4,0,0,25
		BASE:52:57=50,0,50
		BASE:52:63=25
		BASE:52:73=100
		BASE:52:77=50
		mCID:1=53e3
		;不明，体力-1000，病症60000，防御325，疼痛25，关节挫伤外伤50，H性25，黏液100
		PRINTFORML 试药员%CALLNAME:mCID%被担架抬进来了……
		PRINTW
		;随时结束
		MAX_PATIENTS=-1
		CALL PATIENT_COMA
		SIF RESULT==-1
			RESTART
	ENDIF
;随机生成病症，如果旧病没有治好就跳过并恢复体力
ELSEIF BASE:mCID:患病<=0&&!BASE:mCID:今日问诊&&!BASE:mCID:近期问诊
	PatCnt++
	BASE:mCID:症状类型 = RAND:70 + 19000
	;无尽补正
	BASE:mCID:病症大小 = (RAND:(15+PopBonus(EP:1)+MeDf:1*EP:1/100) + 1) * 100
	SIF MeDf&&!RAND:5
		BASE:mCID:病症大小+=RAND:(1+PatCnt%5*10)*100
	SIF MeDf>=2&&!RAND:(1+PatCnt%(10/(MeDf-1)))
		BASE:mCID:病症大小+=RAND:(1+20*(1+4*(MeDf-2)))*100
	SIF MeDf>=4&&!RAND:(1+PatCnt%10)
		BASE:mCID:病症大小+=RAND:(200*(1+(MeDf-4)*4))*100
	BASE:mCID:患者体力 = MAX(-1000*MeDf+1,RAND:1001-RAND:(BASE:mCID:病症大小)/10)
	;急诊补正
	SIF BASE:mCID:患者体力<=0
		BASE:mCID:病症大小-=MAX(BASE:mCID:患者体力/50*100,500)
	BASE:mCID:病症残余 = BASE:mCID:病症大小
	BASE:mCID:疾病防御 = (RAND:(MeDf*2+7) + 1) * 25;125~375,7*25~9*50
	BASE:mCID:医疗费 = BASE:mCID:病症大小 + 200 + RAND:101
	;依据病症难度进行适当奖励修正
	SIF BASE:mCID:病症大小 >= 1200;12~15->3000~7000
		BASE:mCID:医疗费 += 300+RAND:(1+(BASE:mCID:病症大小-1100)/100)*100
	SIF BASE:mCID:疾病防御 >= 150;150~175->3000~5000
		BASE:mCID:医疗费 += 300+RAND:(1+(BASE:mCID:疾病防御-125)/25)*100
	SIF BASE:mCID:患者体力 <= 0;-200~300->1000~11000
		BASE:mCID:医疗费 += 100+RAND:(1+(300-BASE:mCID:患者体力)/5)
	;BASE:mCID:医疗费*=5/2
	SIF BASE:mCID:病症大小>2e3&&MeDf>=2
		BASE:mCID:医疗费*=(MeDf+1)/2
	;生成疾病详细数据
	CALL DISEASE_TYPE(mCID)
	;记录本次治疗前的疾病大小用于结算费用
	mCID:1 = BASE:mCID:病症残余
	;添加患者添加患病随机数和今日问诊次数
	BASE:mCID:患病 = RAND:16 + 15
	BASE:mCID:今日问诊 ++
	BASE:mCID:明日再来 = 0
	IF BASE:mCID:患者体力 > 0
		PRINTFORML 患者%CALLNAME:mCID%进来了...
		;放行，最大患者数量-1
		MAX_PATIENTS --
		PRINTW
	ELSE
		PRINTFORML 紧急情况！患者%CALLNAME:mCID%被担架抬进来了！
		;急诊不占预约
		PRINTW
		CALL PATIENT_COMA
		SIF RESULT==-1
			RESTART
	ENDIF
ELSEIF BASE:mCID:患病>0 && !BASE:mCID:今日问诊 && !BASE:mCID:近期问诊
	;记录本次治疗前的疾病大小用于结算费用
	mCID:1 = BASE:mCID:病症残余
	;为患者恢复体力，并添今日问诊次数
	BASE:mCID:患者体力 += RAND:1001 + 200
	BASE:mCID:今日问诊 ++
	;放行，最大患者数量-1，明日再来复位
	MAX_PATIENTS --
	BASE:mCID:明日再来 = 0
	IF BASE:mCID:患者体力>0
		PRINTFORML %CALLNAME:mCID%又回来接受治疗了...
		PRINTW
	ELSE
		PRINTFORML %CALLNAME:mCID%又被担架抬进来了…
		PRINTW
		CALL PATIENT_COMA
		SIF RESULT==-1
			RESTART
	ENDIF
ELSE
	;终极兜底，防止循环卡死，平时无视
	LOCAL:1=!LOCAL:1
	SIF LOCAL:1
		RESTART
	PRINTL
	PRINTW 奇怪，人呢？总之今天没有更多病人了，营业结束
	RETURN
ENDIF
WHILE 1
	;首先打印出病人信息
	CALL MedUI
	CALL MIDInfo
	PRINTL
	PRINTL 请选择要执行的项目
	SIF BASE:mCID:诊断完成||BASE:0:1<500||BASE:0:0<500
		SETCOLOR C_GRAY
	PRINT [1]进行诊断
	RESETCOLOR
	PRINT_SPACE 100
	SIF EP<=2500||BASE:0:1<500||BASE:0:0<500
		SETCOLOR C_GRAY
	PRINT [2]开始治疗
	RESETCOLOR
	PRINT_SPACE 100
	PRINT [3]劝离
	PRINT_SPACE 100
	SIF MAX_PATIENTS+1>0
		SETCOLOR C_GRAY
	SIF MAX_PATIENTS+1<=0&&MeDf:1
		SETCOLOR C_GREEN
	PRINTFORM [0]终止\@!ARG?营业#训练\@
	RESETCOLOR
	INPUT
	IF GROUPMATCH(RESULT,1,2)&&(BASE:0:1<500||BASE:0:0<500)
		CALL IFTire
		CONTINUE
	ELSEIF RESULT == 1
		IF BASE:mCID:诊断完成
			PRINTW 已经诊断过了
		ELSE
			PRINTW 开始进行诊断
			BASE:mCID:诊断完成 = 1
			;添加诊断耗时
			CALL TPas
		ENDIF
	ELSEIF RESULT == 2
		IF EP<600
			PRINTFORMW EP不足
			CONTINUE
		ENDIF
		;重置药品使用次数
		VARSET TFLAG, 0, 951,958
		CALL DRUG_MENU
		IF BASE:mCID:病症残余 <= 0
			PRINTL 闪~闪~发~光~！
			PRINTL
			PRINTFORML %CALLNAME:mCID%康复了！！！
			CALL MEDICAL_BILLS
			;患者康复，重置疾病数据，保留问诊和住院数据用于其它结算
			VARSET BASE:mCID:40, 0, 40, 86
			;给康复的患者生成0~5天随机问诊间隔
			BASE:mCID:近期问诊 = RAND:6
			PRINTW
			RESTART
		ELSE
			CALL MEDICAL_BILLS
			RESTART
		ENDIF
	ELSEIF RESULT == 3
		PRINTL
		;PRINTFORML 确定要让%CALLNAME:mCID%离开吗？这样会导致名望下降哦
		CALL Message_Box_mk2(@"确定要让%CALLNAME:mCID%离开吗？这样会导致名望下降哦", "PINK", 20)
		PRINTL
		PRINTL [1]是    [0]否
		INPUT
		SIF RESULT != 1
			CONTINUE
		PRINTFORML %CALLNAME:mCID%走了
		PRINTL
		LOCAL = RAND:5 + 1
		PRINTFORML %CALLNAME:0% 名望 - {LOCAL}
		PRINTW
		EP:1 -= LOCAL
		;添加劝离耗时
		CALL TPas
		RESTART
	ELSE
		;PRINTFORML 还有{MAX_PATIENTS + 1}位患者等待治疗呢，确定要终止营业吗？这样会导致名望下降哦
		IF ARG
			CALL Message_Box_mk2(@"要结束训练吗？", "PINK", 20)
		ELSEIF MAX_PATIENTS+1>0
			CALL Message_Box_mk2(@"还有{MAX_PATIENTS + 1}位患者等待治疗呢，确定要终止营业吗？这样会导致名望下降哦", "PINK", 20)
		ELSEIF MeDf:1
			CALL Message_Box_mk2(@"今天预约治疗的患者已经就诊完毕，确定要不顾新来的患者终止营业吗？", "PINK", 20)
		ENDIF
		PRINTL
		PRINTL [1]是    [0]否
		INPUT
		IF RESULT==1
			SIF MAX_PATIENTS+1<=0
				RETURN
			LOCAL = (RAND:5 + 1) * (MAX_PATIENTS + 1)
			PRINTFORML %CALLNAME:0% 名望 - {LOCAL}
			PRINTW
			RETURN
		ENDIF
	ENDIF
WEND
;生成患者,RandomDiseaseSufferer
@RDis
#LOCALSIZE 3
;检查所有角色的明日再来标志，并赋值跳出循环，如果都没有，就随机生成患者；随机10次失败则遍历
FOR LOCAL,1,CHARANUM-1
	SIF BASE:LOCAL:今日问诊>0||BASE:LOCAL:近期问诊>0||!LOCAL:1
		CONTINUE
	IF BASE:LOCAL:明日再来
		mCID=LOCAL
		RETURN
	ELSEIF LOCAL:1
		FOR LOCAL:2,0,10
			RESULT=RAND:(CHARANUM-2)+1
			SIF BASE:RESULT:今日问诊||BASE:RESULT:近期问诊
				CONTINUE
			mCID=RESULT
			RETURN
		NEXT
	ENDIF
	mCID=LOCAL
NEXT
LOCAL:1=!LOCAL:1
SIF LOCAL:1
	RESTART
;依据名望多寡获得问诊人数奖励
@PopBonus(ARG)
#FUNCTION
RESULT=MIN(10,(ARG>=100)*(ARG/100+1))
RETURNF RESULT