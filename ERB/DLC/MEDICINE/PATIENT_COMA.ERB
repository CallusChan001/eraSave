;患者昏迷
@PATIENT_COMA
CALL MedUI
CALL MAssi
PRINTL
PRINTFORML %CALLNAME:mCID%倒地昏迷不起！要进行救援吗？
PRINTL
PRINTL [1]紧急救援！！！
PRINTFORML
LOCAL=(mCID:1-BASE:mCID:病症残余) * 100 / mCID:1;治疗后满意度
SIF BASE:mCID:病症残余>0
	PRINTFORML [2]支付{BASE:mCID:医疗费*(100-LOCAL)/100}现金作为转院医药费，放弃这次治疗
SIF LOCAL<=20
	SETCOLOR C_GRAY
SIF LOCAL>=80
	SETCOLOR C_AQUA
PRINTL [0]弃之不顾
RESETCOLOR
INPUT
IF RESULT == 1
	CALL Aid
	SIF RESULT==-1
		RESTART
	RETURN 0
ELSEIF RESULT == 2 && BASE:mCID:病症残余>0
	PRINTL
	PRINTFORML 将昏迷的%CALLNAME:mCID%抬出去了，%CALLNAME:0%带着歉意将{BASE:mCID:医疗费*(100-LOCAL)/100}现金塞进%CALLNAME:mCID%的钱包里
	PRINTL
	PRINTFORML %CALLNAME:0% 现金 - {BASE:mCID:医疗费*(100-LOCAL)/100}
	PRINTL
	MONEY -= BASE:mCID:医疗费*(100-LOCAL)/100
	LOCAL=MAX(BASE:mCID:病症残余/2-200,100);满意度
	BASE:mCID:医疗费-=(BASE:mCID:病症残余-LOCAL)/BASE:mCID:病症残余
	BASE:mCID:病症残余-=LOCAL
ELSE
	PRINTL
	PRINTFORML 将昏迷的%CALLNAME:mCID%抬了出去，任其自生自灭；
	PRINTL
	PRINTFORML 不知过了多久，%CALLNAME:mCID%才醒过来
ENDIF
FailCnt:mCID=0
RETURN -1
@Aid
;失败补正
IF BASE:mCID:患者体力 > 0
	PRINTFORMW  %CALLNAME:mCID%醒过来了！
	PRINTL
	RETURN 1
ELSEIF BASE:0:0<500||BASE:0:1<500
	CALL IFTire
	RESULT=-1
	RETURN RESULT
ENDIF
WHILE 1
CALL RELINE(2,1)
CALL MedUI
CALL MAssi
PRINTL
DRAWLINEFORM ━
PRINTFORML %"",7%%"药品",12,LEFT%疗效 成功率 患者体力  %"EP消费",9,LEFT%药效
DRAWLINE
FOR LOCAL, 800, 807
	CALL DRUG_TYPE(LOCAL)
	PRINTFORM [{LOCAL}]%STR:(LOCAL + 18000), 14,LEFT%{CKMax(LOCAL),4,RIGHT} {MIN(99,AidSucRate(LOCAL)+FailCnt:mCID*5),4,RIGHT}％ 恢复：{DRUG_HP(LOCAL)} 
	SELECTCASE DRUG_PRICE(LOCAL)
		CASE IS > EP
			SETCOLOR C_GRAY
		CASE IS > 3000
			SETCOLOR C_RED
		CASE 2000 TO 2900
			SETCOLOR C_D_ORANGE
		CASE 1600 TO 1900
			SETCOLOR C_YELLOW
		CASE 1000 TO 1500
			SETCOLOR C_GREEN
		CASE 200 TO 900
			SETCOLOR C_PINK
	ENDSELECT
	PRINTFORM EP {DRUG_PRICE(LOCAL), 4, RIGHT}  
	RESETCOLOR
	SIF BASE:mCID:诊断完成
		CALL CK_Ef(LOCAL)
	PRINTL
NEXT
DRAWLINEFORM ━
SIF BASE:mCID:病症残余>=2e3
	SETCOLOR C_GREEN
PRINTFORML [  8]  SPECIAL
RESETCOLOR
PRINTFORML [999]  放弃治疗
PRINTL
	INPUT
	IF RESULT==999
		RETURN -1
	ELSEIF RESULT==8
		CALL SPECIAL_SKILL
		CALL RELINE(2)
	ELSEIF !INRANGE(RESULT,800,807)
	ELSEIF EP<DRUG_PRICE(RESULT)
		PRINTFORMW EP不足
	ELSE
		EP-=DRUG_PRICE(RESULT)
		LOCAL=RESULT
		;添加急救耗时
		CALL TPas
		IF MIN(99,AidSucRate(LOCAL)+FailCnt:mCID*5) > (RAND:100 + 1)
			CALL EfCalc(LOCAL)
			IF BASE:mCID:患者体力 <= 0
				CONTINUE
			ELSE
				RETURN
			ENDIF
		ELSE
			PRINTL 没能成功！！！
			PRINTFORML 积累了抢救患者%CALLNAME:mCID%的经验！
			PRINTL 抢救成功率+5%
			FailCnt:mCID++
			CALL MDBase
			CALL RELINE(2)
		ENDIF
	ENDIF
WEND
RESTART
@MDBase
;MedDownBase
FOR LOCAL,0,2
	;RAND20~99，难度补正E0.75,H1.25,L1.5，超执刀/5
	RESULT:LOCAL=(RAND:MAX(80-(EP:1+EP:2)/100,20)+20)*(3+MeDf)/4/(1+4*Drus)
	BASE:0:LOCAL-=RESULT:LOCAL
NEXT
PRINTL
PRINTFORML 医生：%CALLNAME:0%  ↓体力-{RESULT}↓  ↓气力-{RESULT:1}↓
PRINTW
@AidSucRate(ARG)
#FUNCTION
#DIM Rt,7 =50,65,60,75,75,55,99
ARG-=800
SIF !INRANGE(ARG,0,6)
	RETURNF 0
RETURNF Rt:ARG