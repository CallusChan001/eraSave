;结算医疗费用
@MEDICAL_BILLS
;医疗满意度
#DIM DYNAMIC SATISFACTION
;仁义值额外奖励
#DIM DYNAMIC BONUS
[SKIPSTART]
IF BASE:mCID:病症残余 <= 0
	LOCAL = BASE:mCID:医疗费
	SATISFACTION = 100
ELSE
	SATISFACTION = (mCID:1 - BASE:mCID:病症残余) * 100 / mCID:1
	LOCAL = BASE:mCID:医疗费*SATISFACTION/100
ENDIF
[SKIPEND]
VARSET Drus
;求百分比，（历史记录-当前剩余数值）乘100，最后再除以历史记录ERA只能接整数，只有小数点后的情况直接视为0
SATISFACTION=100*(mCID:1-MAX(BASE:mCID:病症残余,0))/mCID:1
;按照治疗百分比计算医疗费和满意度
LOCAL=BASE:mCID:医疗费*SATISFACTION/100
;计算仁义值修正奖励，分别对应仁义的正负两种情况
SIF !MeDf:1
	LIMIT EP:2,-499,999
BONUS = LOCAL * BnvBonus(EP:2) / 100
;生成付款事件，正常付款或别的支付方式
IF !RAND:10 && SATISFACTION > 20
	PRINTL
	PRINTFORML 医疗满意度：{SATISFACTION}％
	PRINTL
	CALL MEDICAL_SATISFACTION(SATISFACTION, LOCAL, BONUS)
	SIF !LOCAL
		RETURN
	PRINTFORML %CALLNAME:mCID%好像忘记带钱包了
ELSE
	PRINTL
	PRINTFORML 医疗满意度：{SATISFACTION}％
	PRINTL
	CALL MEDICAL_SATISFACTION(SATISFACTION, LOCAL, BONUS)
	SIF LOCAL
		PRINTFORML %CALLNAME:mCID%支付了医疗费，共计：{LOCAL} 和 {BONUS} / 仁义修正
	MONEY += (LOCAL + BONUS)
	BASE:mCID:医疗费 -= LOCAL
	PRINTL
	;历史症状2000以上且非常满意返还部分EP，最低诊金/10，最高诊金/2；同时加声望
	SIF mCID:1>=2e3&&SATISFACTION>=80
		CALL EPReb(LOCAL,BONUS,MAX(12-mCID:1/1e3,2),1)
	RETURN
ENDIF
PRINTL
PRINTFORML [1] 这次就不用付钱了！要好好保重身体啊，%CALLNAME:mCID%！（仁义）
PRINTL [2] 能帮忙采集些药材吗？（EP）
PRINTL
WHILE 1
	INPUT
	CLEARLINE 1
	SIF !INRANGE(RESULT,1,2)
		CONTINUE
	IF RESULT == 1
		PRINTFORML %CALLNAME:mCID%感激地向%CALLNAME:0%道别
		PRINTL
		LOCAL:1 = (LOCAL+BONUS)/1e3
		PRINTFORML %CALLNAME:0%  仁义+{LOCAL:1}
		EP:2 += LOCAL:1
		SIF mCID:1>=2e3&&SATISFACTION>=80
			CALL EPReb(LOCAL,BONUS,MAX(12-mCID:1/1e3,2))
	RETURN
	ELSEIF RESULT == 2
		CALL EPReb(LOCAL,BONUS)
	ENDIF
	RETURN
WEND
@EPReb(ARG,ARG:1,ARG:2=2,ARG:3)
;Rebate；ARG诊金，ARG:1bonus，ARG:2系数，3是否已支付
PRINTFORML 为了报答%CALLNAME:0%的恩情，%CALLNAME:mCID%前去采集药材
PRINTL
PRINTFORML 获得{ARG/ARG:2}EP 和 {ARG:1/ARG:2}EP / 仁义修正
PRINTL
PRINTFORML %CALLNAME:mCID%将%CALLNAME:0%治好自己病的事情传开了！
PRINTL 
PRINTFORML %CALLNAME:0%  名望+{(ARG+ARG:1)/ARG:2/1e3}
PRINTL 
SIF !ARG:3
	BASE:mCID:医疗费 -= ARG
EP += (ARG+ARG:1)/ARG:2
EP:1 += (ARG+ARG:1)/ARG:2/1e3
;医疗满意度，依据满意度获得奖励ARG:0 = 满意度 ARG:1 = 医疗费 ARG:2 = 仁义修正
@MEDICAL_SATISFACTION(ARG:0, ARG:1, ARG:2)
#LOCALSIZE 2
IF ARG:0 >= 80
	PRINTFORML %CALLNAME:mCID%对本次治疗非常满意
	PRINTL
	LOCAL:0 = MIN(100,(ARG:1 + ARG:2) / 500)
	LOCAL:1 = MAX(mCID:1/1e3,1)
	PRINTFORML %CALLNAME:mCID% 好感度 + {LOCAL}  信賴度 + {LOCAL/2}
	PRINTL
	PRINTFORML %CALLNAME:0% 名望 + {LOCAL:1} 仁义 + {LOCAL:1}
	PRINTL
	EP:1 +=LOCAL:1
	EP:2+=LOCAL:1
	CFLAG:mCID:好感度 += LOCAL
	CFLAG:mCID:信賴度 += LOCAL/2
	TALENT:mCID:心情 = 1
	BASE:mCID:憤怒 = 0
	PALAM:mCID:好意 += BASE:mCID:医疗费 * 10
ELSEIF ARG:0 <= 20
	PRINTFORML %CALLNAME:mCID%对本次治疗特别失望
	PRINTL
	LOCAL:0 = RAND:21 + 20
	LOCAL:1 = RAND:11 + 10
	PRINTFORML %CALLNAME:mCID% 好感度 - {LOCAL:0}  信賴度 - {LOCAL:1}
	CFLAG:mCID:好感度=MAX(CFLAG:mCID:好感度-LOCAL:0,0)
	CFLAG:mCID:信賴度=MAX(CFLAG:mCID:信賴度-LOCAL:1,0)
	PRINTL
	LOCAL=RAND:3+3
	LOCAL:1=RAND:3+3
	;亲密补正
	IF ABL:mCID:9*10>RAND:100+1
		PRINTFORML 但看在和你关系的份上，没有多说什么
		RETURN
	ELSEIF ABL:mCID:9*20>RAND:100+1
		LOCAL=RAND:2+1
		LOCAL:1=RAND:2+1
	ENDIF
	PRINTFORML %CALLNAME:0% 名望 - {LOCAL} 仁义 - {LOCAL:1}
	EP:1 -= LOCAL
	EP:2 -= LOCAL:1
	PRINTL
;ELSE暂时没有，以后可能会进行补充
ENDIF
;仁义值奖励加成，干坏事有惩罚
@BnvBonus(ARG)
#FUNCTION
RESULT=5*(ARG/100+(2*(ARG>=0)-1)*!INRANGE(ARG,0,899)*!MeDf:1)
RETURNF RESULT