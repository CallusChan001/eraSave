;药品菜单
@DRUG_MENU
;打印出患者
CALL RELINE(1,1)
CALL MedUI
CALL MIDInfo
PRINTL
VARSET LOCAL
;治疗方式选择，只能用一次
FOR LOCAL,1,8
	LOCAL:1++
	CALL USED_DRUG(LOCAL)
	SIF !(LOCAL:1%3)
		PRINTL
NEXT
;IF TFLAG:958 == 0
SIF BASE:mCID:病症残余>=2e3
	SETCOLOR C_GREEN
PRINTFORMLC [8] SPECIAL
RESETCOLOR
;ELSE
;	PRINTLC 已使用
;ENDIF
PRINTFORMLC [9] 查看帮助
PRINTL
LOCAL=(mCID:1-BASE:mCID:病症残余) * 100 / mCID:1;治疗后满意度
IF LOCAL>=80
	SETCOLOR C_AQUA
ELSEIF LOCAL<=20
	SETCOLOR C_GRAY
ENDIF
PRINTL
PRINTL [998]结束了，明天再来哦！（大概明天还会来）
PRINTL [999]结束了，不来也没关系！（近期不会再出现了）
RESETCOLOR
INPUT
IF RESULT == 998
	BASE:mCID:明日再来 = 1
	RETURN
ELSEIF RESULT == 999
	BASE:mCID:近期问诊 = 10 + RAND:6
	RETURN
ELSEIF BASE:0:0<500||BASE:0:1<500
	CALL IFTire
	RESTART
ELSEIF !INRANGE(RESULT,1,9)
	RESTART
ELSEIF TFLAG:(RESULT+950)&&!Drus
	PRINTFORMW 已使用
	RESTART
ENDIF
;打印出对应治疗方式的药品
IF RESULT==9
	CALL MedHelp
	CALL RELINE(1)
	RESTART	
ELSEIF RESULT==8
	CALL SPECIAL_SKILL
	CALL RELINE(1)
	RESTART
ENDIF
LOCAL:1=RESULT
WHILE 1
	CALL PRINT_DRUG(LOCAL:1)
	DRAWLINEFORM ━
	PRINTL [0]返回
	INPUT
	IF !RESULT
		CALL RELINE(1)
		RESTART
	ELSEIF EP <= DRUG_PRICE(RESULT)
		PRINTW EP不足
		CALL RELINE(1)
		RESTART
	ENDIF
	;使用药品后为对应的类型，添加已使用标志
	LOCAL=RESULT/100+950
	TFLAG:LOCAL=1
	;使用药品扣除对应EP点数
	EP -= DRUG_PRICE(RESULT)
	;选择好药品后，进行药效结算
	CALL EfCalc(RESULT)
	SIF BASE:mCID:病症残余<=0
		RETURN
	;如果患者体力耗尽询问是否抢救
	IF BASE:mCID:患者体力 <= 0
		CALL PATIENT_COMA
		SIF RESULT==-1
			RETURN
		RESTART
	ENDIF
	SIF !Drus;无超执刀/治愈返回
		RESTART
WEND
;打印药品清单和药品最强药效
@PRINT_DRUG(ARG)
VARSET LOCAL
DRAWLINEFORM ━
PRINTFORML %"",7%%"药品",19,LEFT%%"疗效",7,LEFT%%"EP消费",9,LEFT%%"患者体力",10,LEFT%药效
DRAWLINE
FOR LOCAL, ARG*100, ARG*100+19
	CALL DRUG_TYPE(LOCAL)
	SIF RESULT==-1
		RETURN
	IF BASE:mCID:诊断完成
		IF GROUPMATCH(LOCAL,104,105,106,108,109,203,208,209,307,402,405,406,407,408,409,507,511,605,607,609,704,705,708,711,716)&&CgAb(LOCAL)
			SETCOLOR C_H_PINK
		ELSEIF LOCAL==607&&Ck607()
			SETCOLOR C_PINK
		ELSEIF GROUPMATCH(LOCAL,700,713,715)&&IFHeal(LOCAL)
			SETCOLOR C_GREEN
		ENDIF
	ENDIF
	PRINTFORM [{LOCAL}]%STR:(LOCAL + 18000), 20,LEFT% {CKMax(LOCAL),4,RIGHT}   
	RESETCOLOR
	SELECTCASE DRUG_PRICE(LOCAL)/100
		CASE IS > EP/100
			SETCOLOR C_GRAY
		CASE IS >= 30
			SETCOLOR C_RED
		CASE 20 TO 29
			SETCOLOR C_D_ORANGE
		CASE 15 TO 19
			SETCOLOR C_YELLOW
		CASE 10 TO 14
			SETCOLOR C_GREEN
		CASE 6 TO 10
			SETCOLOR C_PINK
	ENDSELECT
	PRINTFORM EP {DRUG_PRICE(LOCAL), 4, RIGHT}  
	RESETCOLOR
	IF DRUG_HP(LOCAL)>0
		SETCOLOR C_GREEN
	ELSEIF DRUG_HP(LOCAL)+BASE:mCID:患者体力<=0
		SETCOLOR C_RED
	ELSEIF DRUG_HP(LOCAL)<0
		SETCOLOR C_D_ORANGE	
	ENDIF	
	PRINTFORM \@DRUG_HP(LOCAL)<0?消耗#恢复\@ {ABS(DRUG_HP(LOCAL)), 3, RIGHT}  
	RESETCOLOR
	SIF BASE:mCID:诊断完成
		CALL CK_Ef(LOCAL)
	PRINTL
NEXT
@CK_Ef(ARG)
;ARG，药品编号
#LOCALSIZE 4
VARSET LOCAL
FOR LOCAL, 0, 36
	SIF !TFLAG:(LOCAL+900)||TFLAG:(LOCAL+900)<(ARG<800?Drus:ARG*100#0)
		CONTINUE
	LOCAL:2=MAX(0,TFLAG:(LOCAL+900)-(ARG<800?Drus:ARG*100#0))
	LOCAL:1=LOCAL:2*BASE:mCID:(LOCAL+50)/100
	SIF Drus:2
		LOCAL:2+=mCID:1/100
	IF LOCAL:1
		SETCOLOR C_GRAY
		IF LOCAL:1>=BASE:mCID:疾病防御
			LOCAL:3=(mCID:1-(BASE:mCID:病症残余-LOCAL:2))*100/mCID:1;治疗后满意度
			IF LOCAL:3>=100
				SETCOLOR C_H_PINK
			ELSEIF LOCAL:3>=80
				SETCOLOR C_AQUA
			ELSEIF !Drus
				SELECTCASE LOCAL:2
					CASE 900
						SETCOLOR C_PINK					
					CASE 700
						SETCOLOR C_GREEN
					CASE 500
						SETCOLOR C_ORANGE
					CASE 300
						SETCOLOR C_YELLOW
					CASE 200,100
						RESETCOLOR
				ENDSELECT
			ELSE
				SELECTCASE LOCAL:2*100/DRUG_PRICE(ARG)
					;一线用药
					CASE IS >= 100
						SETCOLOR C_PINK
					CASE 70 TO 99
						SETCOLOR C_GREEN
					;二线					
					CASE 50 TO 69
						SETCOLOR C_ORANGE
					CASE 30 TO 49
						SETCOLOR C_YELLOW
					;三线
					CASE 20 TO 29
						SETCOLOR C_D_RED
					CASE IS<20
						RESETCOLOR
				ENDSELECT
			ENDIF
		ENDIF
		PRINTFORM %STR:(LOCAL + 19550)%:{LOCAL:1,4,RIGHT}  
		RESETCOLOR
	ENDIF
NEXT
;特殊技能
@SPECIAL_SKILL
#DIM DCost,3 
#LOCALSIZE 1
DCost=MAX(0,8e3-Drus:1*200),MAX(0,4e3-Drus:1*200),MAX(0,2e3-Drus:1*200)
CALL RELINE(,1)
IF !Drus
	SIF BASE:MASTER:TSP < 100
		SETCOLOR C_GRAY
	PRINTFORML [1]  超執刀 消耗：100 TSP 每次治疗时间、自身气体力消耗降为1/5；可重复使用治疗手段，但每次疗效降低200
	RESETCOLOR
ELSE
	PRINTFORML [2]%@"■绀珠之药",14,LEFT% EP {DCost,4,RIGHT} 重置所有药物使用次数
ENDIF
PRINTFORML [3]%@"■蝴蝶梦丸噩梦",14,LEFT% EP {DCost:1,4,RIGHT} 随机化患者的症状
SIF mCID:1>1e4
	SETCOLOR C_GREEN
SIF !Drus:2
	PRINTFORML [4]%@"■生命爆炸之药",14,LEFT% EP {DCost:2,4,RIGHT} 每次治疗附加1\%病症大小的疗效，但体力消耗额外+100
RESETCOLOR
PRINTL [0]返回
INPUT
IF !RESULT
	RETURN
ELSEIF !INRANGE(RESULT,1,4)||(RESULT==1&&Drus)||(RESULT==4&&Drus:2)
	CALL RELINE()
	RESTART
ELSEIF RESULT==1
	IF BASE:MASTER:TSP >= 100
		PRINTFORML %CALLNAME:0%身影一阵模糊，新治疗器具重新摆放整齐
		PRINTL
		PRINTFORML %CALLNAME:0% TSP - 100
		PRINTW
		;超執刀 TPS-500 重置TFLAG使用标记
		BASE:MASTER:TSP -= 100
		VARSET TFLAG, 0, 951, 958
		Drus=1
	ELSE
		PRINTW TSP不足
	ENDIF
ELSEIF RESULT==2&&EP>=DCost
	PRINTFORML 对%CALLNAME:mCID%使用■绀珠之药进行治疗
	PRINTFORMW 疾病对药物的抵抗重置了！
	VARSET Drus,0,1
	EP-=DCost
ELSEIF RESULT==3&&EP>=DCost:1
	PRINTFORML 对%CALLNAME:mCID%使用■蝴蝶梦丸噩梦进行治疗
	EP-=DCost:1
	;+1000EP
	Drus:1-=5
	LOCAL=RAND:70 + 19000
	IF LOCAL==BASE:mCID:45
		PRINTFORML %CALLNAME:mCID%的【%STR:(BASE:mCID:45)%】症状似乎发生了什么变化
	ELSE
		PRINTFORML %CALLNAME:mCID%的【%STR:(BASE:mCID:45)%】症状发生了变化！
		BASE:mCID:45=LOCAL
		PRINTFORML %CALLNAME:mCID%的症状变成了【%STR:(BASE:mCID:45)%】！
	ENDIF
	VARSET BASE:mCID:50,0,50,86
	CALL DISEASE_TYPE(mCID)
	WAIT
ELSEIF RESULT==4&&EP>=DCost:2&&!Drus:2
	PRINTFORML 对%CALLNAME:mCID%使用■生命爆炸之药进行治疗
	PRINTFORMW %CALLNAME:mCID%的生命开始熊熊燃烧！
	Drus:2=1
	EP-=DCost:2
ELSEIF INRANGE(RESULT,2,4)&&EP<DCost:(RESULT-2)
	PRINTW EP不足
ENDIF
PRINTL
@USED_DRUG(ARG)
#DIMS Meth,7 ="注射","食疗","内服","外用","理疗","语疗","其他"
SIF !INRANGE(ARG,1,7)
	RETURN
PRINTFORMLC \@!TFLAG:(ARG+950)||Drus?[{ARG}] %Meth:(ARG-1)%#已使用\@
@MedHelp
PRINTFORML 想查看哪方面的帮助？
PRINTFORML [0]治疗目标 [1]难度效果 [2]药品选择 [3]Special [9]返回
INPUT
SELECTCASE RESULT
	CASE 9
		RETURN	
	CASE 0
		PRINTFORML 治疗目标
		PRINTFORML 根据初始疾病大小分为轻症与重症
		PRINTFORML 具体分界点在于能否不使用SPECIAL道具达到治疗目标
		PRINTFORML
		PRINTFORML 治疗目标分为避免患者不满意与使患者满意
		PRINTFORML 即二八（20\%与80\%）满意度为分界线，根据就诊时病症大小与结束时比值结算
		PRINTFORML 假如初始病症大小为{1e4}，那么当治疗结束时残余<{2e3}患者满意，>{8e3}则反之
		PRINTFORML
		PRINTFORML 不满意会降低名望仁义，初始疾病大小>={2e3}时患者满意有额外奖励，中间值没有特殊效果
		PRINTFORM SPECIAL会在病症残余>={2e3}时标绿
		SETCOLOR C_GREEN
		PRINTFORM SPECIAL
		RESETCOLOR
		PRINTFORML ，仅供参考
		PRINTFORML
		PRINTFORM 在未达到二成时，
		SETCOLOR C_GRAY
		PRINTFORM 结束治疗
		RESETCOLOR
		PRINTFORM 为灰色，八成及以上
		SETCOLOR C_AQUA
		PRINTFORM 结束治疗
		RESETCOLOR
		PRINTFORML 为水色
		PRINTFORML 为了丰厚的额外奖励（EP、额外名望等），请朝使患者满意努力
		PRINTFORML
	CASE 1
		PRINTFORML 各难度效果
		PRINTFORML 消耗：每次治疗自身体力气力消耗基础值20~99，随治疗熟练度（即仁义、名声）降低上限
		PRINTFORML 难度补正，E为0.75倍，每提升一个难度提高0.25倍消耗值
		PRINTFORML 即E0.75 N1.00 H1.25 L1.50 PH1.75 HE2.00
		PRINTFORML 奖励倍率根据病症大小与难度系数决定
		PRINTFORML
		PRINTFORML 初始病症大小：基础值100~1500，根据名望/100提高上限
		PRINTFORML N起每个病人1/5概率随机+100~{4e3}，H起额外+100~{2e3}/{1e4}(L)/{2e4}(PH)/{1e5}(HE)
		PRINTFORML
		PRINTFORML 急诊：初始体力<0的患者，不占用预约名额
		PRINTFORML 初始体力基础值{1e3}，下限为-{1e3}*难度，病症大小与初始体力会相互影响
		PRINTFORML 如L难初始病症大小{3e3}，初始体力{1e3}~{1e3}-{3e3}
		PRINTFORML 若初始体力为-{1e3}，初始病症大小+{1e3}*2
		PRINTFORML 初始体力>=0时不影响病症大小
		PRINTFORML
		PRINTFORML 特殊机制
		PRINTFORML H 患者体力可能因病降低 L 症状可能恶化 PH 治疗消费EP点数增加 HE 治疗时间增加
	CASE 2
		PRINTFORML 药品选择
		PRINTFORML 不使用超执刀时，每大类药品只可使用一次
		PRINTFORML 因此对于轻症，重点在于疗效（单次治疗削减病症残余量）高，以快速结束治疗
		PRINTFORML 不同疗效已用颜色标注，且诊断完成后预期疗效将显示在药品名称后
		PRINTFORML
		PRINTFORML 反之，则考虑性价比，可分为一线用药、二线、三线，在此简称T1-3
		PRINTFORML 请参考药效栏颜色，T1为粉绿，T2为橙黄，T3为红白
		PRINTFORML
	CASE 3
		PRINTFORML Special
		PRINTFORML 超执刀的疗效降低对急救无效
		PRINTFORML ■绀珠之药基础消耗EP{1e4}/，■蝴蝶梦丸噩梦EP{5e3}/；每次用药消耗-200
		PRINTFORML ■蝴蝶梦丸噩梦使用后两药EP消耗+{1e3}，■绀珠之药重置两药消耗
		PRINTFORML
ENDSELECT
WAIT
RESTART
;@ResetMedfee
;FOR LOCAL,1,CHARANUM
;	SIF BASE:LOCAL:病症大小>0
;		CONTINUE
;	BASE:LOCAL:病症大小=MAX(BASE:LOCAL:病症残余,0,BASE:LOCAL:病症大小)
;	;BASE:LOCAL:医疗费=MAX(0,BASE:LOCAL:医疗费,-BASE:LOCAL:医疗费)
;	LOCAL:1++
;NEXT
;PRINTFORMW 已修正{LOCAL:1}人诊金