@IFTire
TARGET=0
PRINTFORML %CALLNAME:0%\@BASE:0:0<500?体#气\@力不足！
PRINTFORML 怎么办呢？
PRINT [0] 返回 
PRINT [1] 休息一下 
PRINTL [2] 使用道具
INPUT
IF RESULT==1
	CALL COM403
ELSEIF RESULT==2
	CALL COM490
ENDIF
;ARG接收药品类型编号
@EfCalc(ARG)
VARSET LOCAL
;3用于存储计算出的最强医疗效果数值
;1、2用于存储当前步长的医疗效果数值
LOCALS = 无效
IF BASE:0:0<500||BASE:0:1<500
	CALL IFTire
	RETURN 0
ENDIF
;重新导入药效变量
CALL DRUG_TYPE(ARG)
;0~35递进,分别引用各项数据，如果忘记了就直接去看XXXX.csv
FOR LOCAL, 0, 36
	SIF !TFLAG:(LOCAL+900)||TFLAG:(LOCAL+900)<(ARG<800?Drus:ARG*100#0)
		CONTINUE
	LOCAL:1=TFLAG:(LOCAL+900)-(ARG<800?Drus:ARG*100#0)
	LOCAL:2=LOCAL:1*BASE:mCID:(LOCAL+50)/100
	SIF LOCAL:2 < BASE:mCID:疾病防御
		CONTINUE
	;L1和L3对比，如果L1药效更强，则将L1赋值到L3进行记录，并将当前L0步长的药效值和在STR中对应的字符串保存到本地
	IF LOCAL:3<LOCAL:1
		LOCAL:3=LOCAL:1
		LOCAL:4=LOCAL:2
		LOCALS = %STR:(LOCAL + 19550)%
	ENDIF
NEXT
PRINTL
PRINTFORML 对%CALLNAME:mCID%使用%STR:(ARG + 18000)%进行治疗
PRINTL
;----------------治疗演出----------------
CALL Patient_Feedback(ARG)
CALL MDBase
PRINTFORML 疾病防御：{BASE:mCID:疾病防御}   药效【%LOCALS%】：{LOCAL:4}
PRINTL
;如果疾病防御被击穿，就会降低防御值，最低降到25
IF LOCAL:4 >= BASE:mCID:疾病防御
	PRINTL 击穿疾病防御！！！
	PRINTL
	IF BASE:mCID:疾病防御 > 25
		PRINTL 疾病防御被削弱！！！
		PRINTL
		PRINTFORML 疾病防御：{BASE:mCID:疾病防御}
		PRINTL ↓↓↓↓
		BASE:mCID:疾病防御 -= 25
		PRINTFORML 疾病防御：{BASE:mCID:疾病防御}
		PRINTL
	ENDIF
	;结算药品对患者体力消耗
	CALL CkBase(ARG)
	SIF Drus:2
		LOCAL:3+=mCID:1/100
	PRINTL
	PRINTFORML 病症残余：{BASE:mCID:病症残余} - {LOCAL:3}
	PRINTL ↓↓↓↓
	BASE:mCID:病症残余 -= LOCAL:3
	PRINTFORML 病症残余：{BASE:mCID:病症残余}
	PRINTL
ELSE
	PRINTL 没有击穿疾病的防御！！！
	PRINTL
	CALL CkBase(ARG)
	PRINTL
ENDIF
IF !BASE:mCID:诊断完成
	BASE:mCID:诊断完成 = 1
	PRINTFORML 在治疗过程中%CALLNAME:0%对%CALLNAME:mCID%的症状，已经有了大致的判断
	PRINTL
ENDIF
IF GROUPMATCH(ARG,104,105,106,108,109,203,208,209,307,402,405,406,407,408,409,507,511,605,607,609,700,704,705,708,711,713,715,716)
	CALL CgDis(ARG)
	SIF RESULT
		PRINTFORML %CALLNAME:mCID%的症状，耐性发生了变化
ENDIF
Drus:1+=1
Drus:ARG+=2
;难度机制
IF MeDf>=1&&!RAND:(11-MeDf*2)&&IFWorsen()
	;体力衰减，每1/(11-2*难度)概率触发，1/9、1/7->1
	PRINTFORML 由于疾病的原因，%CALLNAME:mCID%的体力降低了…
	LOCAL=RAND:(10*MeDf)+5*MeDf
	PRINTFORML 体力-{LOCAL}
	BASE:mCID:患者体力-=LOCAL
ENDIF
IF MeDf>=2&&!RAND:(6-MeDf)&&IFWorsen()
	LOCAL=(RAND:MeDf+1)*20
	;症状恶化，每1/(6-难度)概率触发，1/4、1/3->1
	PRINTFORML %CALLNAME:mCID%的症状恶化了…
	PRINTFORML 病症残余+{LOCAL}
	BASE:mCID:病症残余+=LOCAL
ENDIF
;添加用药耗时
CALL TPas
PRINTW
@IFWorsen
#FUNCTION
;病症残余阈值上限90%->30%，每级-15%，下限1000
SIF BASE:mCID:病症残余>1000&&BASE:mCID:病症残余>mCID:1*(7-MeDf)*3/20
	RETURNF 1
RETURNF 0
@TPas
TIME+=(RAND:(1+5*(MeDf==5))+5)/(1+4*Drus)
SIF NEMUKE() < 840
	RETURN
IF NEMUKE() >= 960
	BASE:MASTER:体力 = MAX(BASE:MASTER:体力 - 5 * TIME_PROGRESS(TIME:1) , 0)
	BASE:MASTER:気力 = MAX(BASE:MASTER:気力 - 10 * TIME_PROGRESS(TIME:1) , 0)
ELSE
	BASE:MASTER:気力 = MAX(BASE:MASTER:気力 - 2 * TIME_PROGRESS(TIME:1) , 0)
ENDIF
@CkBase(ARG)
PRINTFORML 患者体力：{BASE:mCID:患者体力}
BASE:mCID:患者体力 += DRUG_HP(ARG)
IF DRUG_HP(ARG) > 0
	PRINTL  恢复  ↑↑↑↑
ELSEIF DRUG_HP(ARG) < 0
	PRINTL  下降  ↓↓↓↓
ELSE
	PRINTL  平稳  ●●●●
ENDIF
PRINTFORML 患者体力：{BASE:mCID:患者体力}
@CgDia(ARG)
SIF !INRANGE(ARG,0,3)
	RETURN
SELECTCASE ARG
	CASE 0
		SETCOLOR C_H_PINK
		PRINTFORML %CALLNAME:mCID%接受治疗后躁动不安，投来了火热的目光
	CASE 1
		SETCOLOR C_PINK
		PRINTFORML 用圣水洗掉了%CALLNAME:mCID%身上的黏液
	CASE 2
		SETCOLOR C_GREEN
		PRINTFORML 处理好了%CALLNAME:mCID%身上的伤
	CASE 3
		SETCOLOR C_PINK
		PRINTFORML 对%CALLNAME:mCID%的症状得到了更深入的理解
ENDSELECT
RESETCOLOR
@CgDis(ARG)
#LOCALSIZE 4
;ChangeDisease，疾病症状变化
LOCAL=0
IF GETBIT(CgAb(ARG),0)
	CALL BCVUp(GROUPMATCH(ARG,104,108,203,208,406,607,705,716),GROUPMATCH(ARG,105,405),GROUPMATCH(ARG,109,407))
	CALL CgDia()
	LOCAL=1
ENDIF
SELECTCASE ARG
	CASE 203,307;酒、媚药，H性
		SIF !BASE:mCID:63
			RETURN
		BASE:mCID:63 += 25
		SIF !LOCAL
			CALL CgDia()
	CASE 209;精力，V感
		SIF BASE:mCID:71 < 74
			RETURN
		BASE:mCID:71 += 25
		SIF !LOCAL
			CALL CgDia()
	;CASE 307;媚药，精神、H性、游戏、冷冻
	CASE 402,405,408,704;按摩、史莱姆、A按摩器、大葱，A感
		SIF CgAb(ARG)
			BASE:mCID:72 += 25
	;CASE 507;护士服
	CASE 511;圣水，黏液换美容
		SIF BASE:mCID:73<74
			RETURN
		BASE:mCID:79=MAX(BASE:mCID:73-25,BASE:mCID:79)
		BASE:mCID:73=25
		CALL CgDia(1)
	CASE 700,713,715;绷带、创可贴、石膏，外伤
		SIF !IFHeal(ARG)
			RETURN
		IF ARG!=715
			BASE:mCID:77 = 25
		ELSE
			;石膏，关节、挫伤
			LOCAL:1=57,59,77
			FOR LOCAL,1,3
				SIF BASE:mCID:(LOCAL:LOCAL) < 25
					CONTINUE
				BASE:mCID:(LOCAL:LOCAL) = 25				
			NEXT
		ENDIF
		CALL CgDia(2)
ENDSELECT
IF ARG==607&&Ck607();听诊器，营养、感冒、腹痛、寄生、母乳
	FOR LOCAL,0,5
		SIF !GETBIT(RESULT,LOCAL)
			CONTINUE
		BASE:mCID:(RESULT:(LOCAL+1))+=25
	NEXT
	CALL CgDia(3)
ENDIF
;CASE 609;kiss
;CASE 708;抱住
;CASE 711;入浴剂
;CASE 716;B测定
@IFHeal(ARG)
#FUNCTION
SIF BASE:mCID:77 > 25
	RETURNF 1
SIF ARG==715&&(BASE:mCID:57 > 25||BASE:mCID:59 > 25)
	RETURNF 1
@CgAb(ARG)
#FUNCTION
VARSET RESULT,0,0,2
IF BASE:mCID:63>=50&&CgAble(GROUPMATCH(ARG,307,402,708,711),GROUPMATCH(ARG,609,708),ARG==203)
	FOR LOCAL,0,3
		SIF BASE:mCID:(LOCAL+69)>99
			CONTINUE
		RESULT:1++
	NEXT
	SIF RESULT:1
		SETBIT RESULT,0
ENDIF
SIF GROUPMATCH(ARG,203,307)&&BASE:mCID:63
	SETBIT RESULT,1
SIF ARG==209&&BASE:mCID:71 > 74
	SETBIT RESULT,2
SIF ARG==511&&BASE:mCID:73 > 74
	SETBIT RESULT,3
RETURNF RESULT
@CgAble(ARG,ARG:1,ARG:2)
#DIM ToCk=53,62,63,74,75,79;精神、消耗、H性、游戏、冷冻、美容
#FUNCTION
FOR LOCAL,0,6
	SIF (LOCAL==1&&!ARG:2)||(LOCAL==3&&ARG:2)||(LOCAL==4&&!ARG)||(LOCAL==5&&!ARG:1)||(BASE:mCID:(ToCk:LOCAL) < 50)
		CONTINUE
	RETURNF 1
NEXT
@Ck607
#FUNCTION
#DIM ToCk=50,51,56,63,68,80;营养、感冒、腹痛、H性、寄生、母乳
#LOCALSIZE 1
RESULT=0
FOR LOCAL,0,5
	SIF !INRANGE(BASE:mCID:(ToCk:LOCAL),25,99)
		CONTINUE
	RESULT:(LOCAL+1)=ToCk:LOCAL
	SETBIT RESULT,LOCAL
NEXT
RETURNF RESULT
@BCVUp(ARG:0,ARG:1,ARG:2)
#LOCALSIZE 1
RESULT=0
FOR LOCAL,0,3
	SIF BASE:mCID:(LOCAL+69)>99
		CONTINUE
	BASE:mCID:(LOCAL+69) += MAX(10,ARG:LOCAL*25)
	RESULT++
NEXT
RETURN RESULT