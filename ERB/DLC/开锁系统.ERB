;400移动 原逻辑1
[SKIPSTART]
IF TFLAG:移動不能メッセージ == 999 && LOCKPICK_SYSTEM > 0
	CAN_BE_UNLOKED_ROOM_SAVE = RESULT
	FOR LOCAL, MINROOM(), MAXROOM
		IF CAN_MOVE(LOCAL, CAN_BE_UNLOKED_ROOM_SAVE) & 1 && IN_HOME(LOCAL) == 1
			CALL SKIP_MOVE(CFLAG:MASTER:現在位置, LOCAL)
			IF CFLAG:MASTER:現在位置 == LOCAL
				PRINTFORML 锁上了
				PRINTFORML 要尝试开锁吗？
				VARSET RESULT
				CALL ASK_YN
				SIF RESULT == 1
					BREAK
				VARSET RESULT
				IF PRIVATEROOM:(LOCAL % 100) > 0
					IF CFLAG:(PRIVATEROOM:(LOCAL % 100)):地位 >= 500
						CALL LOCKPICK(1)
					ELSE
						CALL LOCKPICK()
					ENDIF
				ELSE
					CALL LOCKPICK()
				ENDIF
				IF RESULT == 1
					PRINTFORMW 开锁成功
					CALL SKIP_MOVE(CFLAG:MASTER:現在位置, CAN_BE_UNLOKED_ROOM_SAVE)
				ELSE
					PRINTFORMW 没能打开锁……
				ENDIF
				CLEARLINE 2
				REUSELASTLINE
			ENDIF
			BREAK
		ENDIF
		IF LOCAL == MAXROOM - 1
			PRINTFORMW 找不到门……
			CLEARLINE 2
			REUSELASTLINE
			TFLAG:移動不能メッセージ = 0
			GOTO INPUT_LOOP
		ENDIF
	NEXT
	TFLAG:移動不能メッセージ = 0
	RETURN
ELSE
[SKIPEND]
;400移动 原逻辑2
[SKIPSTART]
IF TFLAG:移動不能メッセージ == 999 && LOCKPICK_SYSTEM > 0
	CAN_BE_UNLOKED_ROOM_SAVE = RESULT
	FOR LOCAL, MINROOM(), MAXROOM
		IF CAN_MOVE(LOCAL, CAN_BE_UNLOKED_ROOM_SAVE) & 1 && IN_HOME(LOCAL) == 1
			SIF CFLAG:MASTER:現在位置 != LOCAL && !(CAN_MOVE(CFLAG:MASTER:現在位置, CAN_BE_UNLOKED_ROOM_SAVE) & 1)
				CALL SKIP_MOVE(CFLAG:MASTER:現在位置, LOCAL)
			PRINTFORML 锁上了
			PRINTFORML 要尝试开锁吗？
			VARSET RESULT
			CALL ASK_YN
			SIF RESULT == 1
				BREAK
			VARSET RESULT
			IF PRIVATEROOM:(LOCAL % 100) > 0
				IF CFLAG:(PRIVATEROOM:(LOCAL % 100)):地位 >= 500
					CALL LOCKPICK(1)
				ELSE
					CALL LOCKPICK()
				ENDIF
			ELSE
				CALL LOCKPICK()
			ENDIF
			IF RESULT == 1
				PRINTFORMW 开锁成功
				CALL SKIP_MOVE(CFLAG:MASTER:現在位置, CAN_BE_UNLOKED_ROOM_SAVE)
			ELSE
				PRINTFORMW 没能打开锁……
			ENDIF
			CLEARLINE 2
			REUSELASTLINE
			BREAK
		ENDIF
		IF LOCAL == MAXROOM - 1
			PRINTFORMW 找不到门……
			CLEARLINE 2
			REUSELASTLINE
			TFLAG:移動不能メッセージ = 0
			CONTINUE
		ENDIF
	NEXT
	TFLAG:移動不能メッセージ = 0
	RETURN
ELSE
[SKIPEND]

;qol 将移动的开锁提示信息写在了这里
@LOCKPICK_INFO(UNLOCK_LOCATION_ID)
#LOCALSIZE 1
#LOCALSSIZE 1
#DIM DYNAMIC UNLOCK_LOCATION_ID
;原本是不管不顾直接进循环，按理应该是先判定当前位置是否相通再进循环移动到相邻的，应该按原逻辑2里面的类似思路
IF !(CAN_MOVE(CFLAG:MASTER:現在位置, UNLOCK_LOCATION_ID) & 1)
	FOR LOCAL, MINROOM(), MAXROOM
		IF (CAN_MOVE(LOCAL, UNLOCK_LOCATION_ID) & 1)
			CALL SKIP_MOVE(CFLAG:MASTER:現在位置, LOCAL)
			GOTO UNLOCK
		ENDIF
	NEXT
	PRINTFORMW 找不到门……
	CLEARLINE 2
	REUSELASTLINE
	TFLAG:移動不能メッセージ = 0
	RETURN
ENDIF
$UNLOCK
PRINTFORML 锁上了
PRINTFORML 要尝试开锁吗？
VARSET RESULT
CALL ASK_YN
IF RESULT == 1
	TFLAG:移動不能メッセージ = 0
	RETURN
ENDIF
VARSET RESULT
;这里之前是CFLAG:MASTER:現在位置而非UNLOCK_LOCATION_ID，问题这时候玩家位置应该是在上锁房间的相邻位置？有点不太理解……
;IF PRIVATEROOM:(CFLAG:MASTER:現在位置 % 100) > 0 && CFLAG:(PRIVATEROOM:(CFLAG:MASTER:現在位置 % 100)):地位 >= 500
IF PRIVATEROOM:(UNLOCK_LOCATION_ID % 100) > 0 && CFLAG:(PRIVATEROOM:(UNLOCK_LOCATION_ID % 100)):地位 >= 500
	CALL LOCKPICK(1)
ELSE
	CALL LOCKPICK()
ENDIF
IF RESULT == 1
	PRINTFORMW 开锁成功
	CALL SKIP_MOVE(CFLAG:MASTER:現在位置, UNLOCK_LOCATION_ID)
ELSE
	PRINTFORMW 没能打开锁……
ENDIF
CLEARLINE 2
REUSELASTLINE
TFLAG:移動不能メッセージ = 0
RETURN

@LOCKPICK(LOCK_DIFFICULTY)
#LOCALSIZE 1
#LOCALSSIZE 1
#DIM DYNAMIC LOCK_DIFFICULTY
#DIM DYNAMIC MEDIAN_OF_RIGHT_UNLOCK_ANGLE_RANGE
#DIM DYNAMIC HALF_OF_RIGHT_UNLOCK_ANGLE_RANGE
#DIM DYNAMIC HALF_OF_SURROUNDING_RANGE, 5
#DIM DYNAMIC ANGLE_OF_LOCKPICK
#DIM DYNAMIC SAVE_ANGLE_OF_LOCKPICK
#DIM DYNAMIC UNLOCK_PROGRESSION
#DIM DYNAMIC ANIMATED_UNLOCK_PROGRSSION
#DIM DYNAMIC LINE_COUNT_FOR_LOCKPICK
#DIM PREV_REDRAW
#DIM DYNAMIC UNLOCK_BLOCKED
SIF LOCKPICK_SYSTEM <= 0
	RETURN 0
SIF UNLOCK_BLOCKED == 1
	ANGLE_OF_LOCKPICK = 90
PREV_REDRAW = CURRENTREDRAW()
REDRAW 0
LINE_COUNT_FOR_LOCKPICK = LINECOUNT
CALL LOCK_DIFFICULTY_SET(LOCK_DIFFICULTY, MEDIAN_OF_RIGHT_UNLOCK_ANGLE_RANGE, HALF_OF_RIGHT_UNLOCK_ANGLE_RANGE, HALF_OF_SURROUNDING_RANGE)
DO
	SAVE_ANGLE_OF_LOCKPICK = ANGLE_OF_LOCKPICK
	SIF ANIMATED_UNLOCK_PROGRSSION > UNLOCK_PROGRESSION
		ANIMATED_UNLOCK_PROGRSSION = UNLOCK_PROGRESSION
	CUSTOMDRAWLINE ━
	HTML_PRINT @"<p align='center'>开锁难度： <font color='#%LOCK_DIFFICULTY_COLOR(LOCK_DIFFICULTY)%'>%LOCK_DIFFICULTY_GRADE(LOCK_DIFFICULTY)%</font></p>"
	CUSTOMDRAWLINE ━

	HTML_PRINT @"<p align='center'><nonbutton title='调整开锁器角度尝试开锁'>开锁器角度：　　{ANGLE_OF_LOCKPICK}°</nonbutton></p>"
	PRINTL
	IF !UNLOCK_BLOCKED
		IF ANIMATED_UNLOCK_PROGRSSION < UNLOCK_PROGRESSION
			ANIMATED_UNLOCK_PROGRSSION ++
			HTML_PRINT @"<p align='center'>%HTML_COLORBAR(ANIMATED_UNLOCK_PROGRSSION, 42, 42, UNICODE(0x2588), UNICODE(0x2588), BARCOLORSET("青緑"), 0x202020)%</p>"
		ELSE
			HTML_PRINT @"<p align='center'>%HTML_COLORBAR(UNLOCK_PROGRESSION, 42, 42, UNICODE(0x2588), UNICODE(0x2588), BARCOLORSET("青緑"), 0x202020)%</p>"
		ENDIF
	ELSE
		HTML_PRINT @"<p align='center'>%HTML_COLORBAR(UNLOCK_PROGRESSION, 42, 42, UNICODE(0x2588), UNICODE(0x2588), 0xFF0000, 0x202020)%</p>"
	ENDIF
	PRINTL
	HTML_PRINT @"<p align='center'><\@ LOCKPICK_SYSTEM == 2 ? nonbutton # button \@ title='尝试开锁' \@ LOCKPICK_SYSTEM == 2 ? # value='10'\@>［转动锁\@ LOCKPICK_SYSTEM == 2 ? (A) #\@］</\@ LOCKPICK_SYSTEM == 2 ? nonbutton # button \@></p>"
	IF LOCKPICK_SYSTEM == 1
		{
		HTML_PRINT @"<br><p align='center'><nonbutton title='调整开锁器角度'>转动开锁器：</nonbutton>
		<button title='角度减小12°' value='0'>　%"[----]", 6%　</button>
		<button title='角度减小4°' value='1'>　%"[---]", 6%　</button>
		<button title='角度减小2°' value='2'>　%"[--]", 6%　</button>
		<button title='角度减小1°' value='3'>　%"[-]", 6%　</button>
		<button title='角度调整回90°' value='8'>　%"[重置角度]", 6%　</button>
		<button title='角度增大1°' value='4'>　%"[+]", 6%　</button>
		<button title='角度增大2°' value='5'>　%"[++]", 6%　</button>
		<button title='角度增大4°' value='6'>　%"[+++]", 6%　</button>
		<button title='角度增大12°' value='7'>　%"[++++]", 6%　</button></p>
		<nonbutton title='调整开锁器角度'>　　　　　　</nonbutton>"
		}
	ENDIF
	HTML_PRINT @"\@ LOCKPICK_SYSTEM == 2 ? # <br>\@<p align='center'><\@ LOCKPICK_SYSTEM == 2 ? nonbutton # button \@ title='停止开锁' \@ LOCKPICK_SYSTEM == 2 ? # value='9'\@>　[EXIT\@ LOCKPICK_SYSTEM == 2 ? (Esc) #\@]　</\@ LOCKPICK_SYSTEM == 2 ? nonbutton # button \@></p>"
	CUSTOMDRAWLINE ━
	CUSTOMDRAWLINE ━
	IF UNLOCK_BLOCKED == 1
		TWAIT 100, 0
		UNLOCK_BLOCKED = 0
		CLEARLINE LINECOUNT - LINE_COUNT_FOR_LOCKPICK
		CONTINUE
	ELSEIF ANIMATED_UNLOCK_PROGRSSION < UNLOCK_PROGRESSION
		INPUTMOUSEKEY 20
		SIF RESULT:0 == 1 && (RESULT:1 == 0x100000 || RESULT:1 == 0x200000)
			ANIMATED_UNLOCK_PROGRSSION = UNLOCK_PROGRESSION
		CLEARLINE LINECOUNT - LINE_COUNT_FOR_LOCKPICK
		CONTINUE
	ENDIF
	IF LOCKPICK_SYSTEM == 1
		INPUT
		SELECTCASE RESULT
			CASE 10
				IF INRANGE(ANGLE_OF_LOCKPICK, MEDIAN_OF_RIGHT_UNLOCK_ANGLE_RANGE - HALF_OF_RIGHT_UNLOCK_ANGLE_RANGE, MEDIAN_OF_RIGHT_UNLOCK_ANGLE_RANGE - 1 + HALF_OF_RIGHT_UNLOCK_ANGLE_RANGE)
					UNLOCK_PROGRESSION = MIN(UNLOCK_PROGRESSION + 6, 42)
				ELSEIF INRANGE(ANGLE_OF_LOCKPICK, MEDIAN_OF_RIGHT_UNLOCK_ANGLE_RANGE - HALF_OF_SURROUNDING_RANGE:0, MEDIAN_OF_RIGHT_UNLOCK_ANGLE_RANGE - 1 + HALF_OF_SURROUNDING_RANGE:0)
					SIF UNLOCK_PROGRESSION + 6 > 36
						UNLOCK_BLOCKED = 1
					UNLOCK_PROGRESSION = MIN(UNLOCK_PROGRESSION + 6, 36)
				ELSEIF INRANGE(ANGLE_OF_LOCKPICK, MEDIAN_OF_RIGHT_UNLOCK_ANGLE_RANGE - HALF_OF_SURROUNDING_RANGE:1, MEDIAN_OF_RIGHT_UNLOCK_ANGLE_RANGE - 1 + HALF_OF_SURROUNDING_RANGE:1)
					SIF UNLOCK_PROGRESSION + 6 > 30
						UNLOCK_BLOCKED = 1
					UNLOCK_PROGRESSION = MIN(UNLOCK_PROGRESSION + 6, 30)
				ELSEIF INRANGE(ANGLE_OF_LOCKPICK, MEDIAN_OF_RIGHT_UNLOCK_ANGLE_RANGE - HALF_OF_SURROUNDING_RANGE:2, MEDIAN_OF_RIGHT_UNLOCK_ANGLE_RANGE - 1 + HALF_OF_SURROUNDING_RANGE:2)
					SIF UNLOCK_PROGRESSION + 6 > 24
						UNLOCK_BLOCKED = 1
					UNLOCK_PROGRESSION = MIN(UNLOCK_PROGRESSION + 6, 24)
				ELSEIF INRANGE(ANGLE_OF_LOCKPICK, MEDIAN_OF_RIGHT_UNLOCK_ANGLE_RANGE - HALF_OF_SURROUNDING_RANGE:3, MEDIAN_OF_RIGHT_UNLOCK_ANGLE_RANGE - 1 + HALF_OF_SURROUNDING_RANGE:3)
					SIF UNLOCK_PROGRESSION + 6 > 18
						UNLOCK_BLOCKED = 1
					UNLOCK_PROGRESSION = MIN(UNLOCK_PROGRESSION + 6, 18)
				ELSEIF INRANGE(ANGLE_OF_LOCKPICK, MEDIAN_OF_RIGHT_UNLOCK_ANGLE_RANGE - HALF_OF_SURROUNDING_RANGE:4, MEDIAN_OF_RIGHT_UNLOCK_ANGLE_RANGE - 1 + HALF_OF_SURROUNDING_RANGE:4)
					SIF UNLOCK_PROGRESSION + 6 > 12
						UNLOCK_BLOCKED = 1
					UNLOCK_PROGRESSION = MIN(UNLOCK_PROGRESSION + 6, 12)
				ELSE
					SIF UNLOCK_PROGRESSION + 6 > 6
						UNLOCK_BLOCKED = 1
					UNLOCK_PROGRESSION = MIN(UNLOCK_PROGRESSION + 6, 6)
				ENDIF
				SIF ANIMATED_UNLOCK_PROGRSSION == UNLOCK_PROGRESSION && UNLOCK_PROGRESSION == 42
					BREAK
			CASE 0
				ANGLE_OF_LOCKPICK = LIMIT(ANGLE_OF_LOCKPICK - 12, 0, 180)
			CASE 1
				ANGLE_OF_LOCKPICK = LIMIT(ANGLE_OF_LOCKPICK - 4, 0, 180)
			CASE 2
				ANGLE_OF_LOCKPICK = LIMIT(ANGLE_OF_LOCKPICK - 2, 0, 180)
			CASE 3
				ANGLE_OF_LOCKPICK = LIMIT(ANGLE_OF_LOCKPICK - 1, 0, 180)
			CASE 4
				ANGLE_OF_LOCKPICK = LIMIT(ANGLE_OF_LOCKPICK + 1, 0, 180)
			CASE 5
				ANGLE_OF_LOCKPICK = LIMIT(ANGLE_OF_LOCKPICK + 2, 0, 180)
			CASE 6
				ANGLE_OF_LOCKPICK = LIMIT(ANGLE_OF_LOCKPICK + 4, 0, 180)
			CASE 7
				ANGLE_OF_LOCKPICK = LIMIT(ANGLE_OF_LOCKPICK + 12, 0, 180)
			CASE 8
				ANGLE_OF_LOCKPICK = 90
			CASE 9
				BREAK
		ENDSELECT
	ELSEIF LOCKPICK_SYSTEM == 2
		VARSET RESULT
		INPUTMOUSEKEY 40
		SELECTCASE RESULT
			CASE 3
				IF RESULT:1 == 65
					IF INRANGE(ANGLE_OF_LOCKPICK, MEDIAN_OF_RIGHT_UNLOCK_ANGLE_RANGE - HALF_OF_RIGHT_UNLOCK_ANGLE_RANGE, MEDIAN_OF_RIGHT_UNLOCK_ANGLE_RANGE - 1 + HALF_OF_RIGHT_UNLOCK_ANGLE_RANGE)
						UNLOCK_PROGRESSION = MIN(UNLOCK_PROGRESSION + 6, 42)
					ELSEIF INRANGE(ANGLE_OF_LOCKPICK, MEDIAN_OF_RIGHT_UNLOCK_ANGLE_RANGE - HALF_OF_SURROUNDING_RANGE:0, MEDIAN_OF_RIGHT_UNLOCK_ANGLE_RANGE - 1 + HALF_OF_SURROUNDING_RANGE:0)
						SIF UNLOCK_PROGRESSION + 6 > 36
							UNLOCK_BLOCKED = 1
						UNLOCK_PROGRESSION = MIN(UNLOCK_PROGRESSION + 6, 36)
					ELSEIF INRANGE(ANGLE_OF_LOCKPICK, MEDIAN_OF_RIGHT_UNLOCK_ANGLE_RANGE - HALF_OF_SURROUNDING_RANGE:1, MEDIAN_OF_RIGHT_UNLOCK_ANGLE_RANGE - 1 + HALF_OF_SURROUNDING_RANGE:1)
						SIF UNLOCK_PROGRESSION + 6 > 30
							UNLOCK_BLOCKED = 1
						UNLOCK_PROGRESSION = MIN(UNLOCK_PROGRESSION + 6, 30)
					ELSEIF INRANGE(ANGLE_OF_LOCKPICK, MEDIAN_OF_RIGHT_UNLOCK_ANGLE_RANGE - HALF_OF_SURROUNDING_RANGE:2, MEDIAN_OF_RIGHT_UNLOCK_ANGLE_RANGE - 1 + HALF_OF_SURROUNDING_RANGE:2)
						SIF UNLOCK_PROGRESSION + 6 > 24
							UNLOCK_BLOCKED = 1
						UNLOCK_PROGRESSION = MIN(UNLOCK_PROGRESSION + 6, 24)
					ELSEIF INRANGE(ANGLE_OF_LOCKPICK, MEDIAN_OF_RIGHT_UNLOCK_ANGLE_RANGE - HALF_OF_SURROUNDING_RANGE:3, MEDIAN_OF_RIGHT_UNLOCK_ANGLE_RANGE - 1 + HALF_OF_SURROUNDING_RANGE:3)
						SIF UNLOCK_PROGRESSION + 6 > 18
							UNLOCK_BLOCKED = 1
						UNLOCK_PROGRESSION = MIN(UNLOCK_PROGRESSION + 6, 18)
					ELSEIF INRANGE(ANGLE_OF_LOCKPICK, MEDIAN_OF_RIGHT_UNLOCK_ANGLE_RANGE - HALF_OF_SURROUNDING_RANGE:4, MEDIAN_OF_RIGHT_UNLOCK_ANGLE_RANGE - 1 + HALF_OF_SURROUNDING_RANGE:4)
						SIF UNLOCK_PROGRESSION + 6 > 12
							UNLOCK_BLOCKED = 1
						UNLOCK_PROGRESSION = MIN(UNLOCK_PROGRESSION + 6, 12)
					ELSE
						SIF UNLOCK_PROGRESSION + 6 > 6
							UNLOCK_BLOCKED = 1
						UNLOCK_PROGRESSION = MIN(UNLOCK_PROGRESSION + 6, 6)
					ENDIF
					SIF ANIMATED_UNLOCK_PROGRSSION == UNLOCK_PROGRESSION && UNLOCK_PROGRESSION == 42
						BREAK
				ELSEIF RESULT:1 == 27
					BREAK
				ENDIF
		ENDSELECT
		IF MOUSEX() <= 0
			ANGLE_OF_LOCKPICK = 0
		ELSEIF MOUSEX() > 0
			ANGLE_OF_LOCKPICK = MOUSEX() * 181 / GETCONFIG("ウィンドウ幅")
			SIF ANGLE_OF_LOCKPICK > 180
				ANGLE_OF_LOCKPICK = 180
		ENDIF
	ENDIF
	SIF SAVE_ANGLE_OF_LOCKPICK != ANGLE_OF_LOCKPICK
		UNLOCK_PROGRESSION = 0
	CLEARLINE LINECOUNT - LINE_COUNT_FOR_LOCKPICK
LOOP 1
REDRAW PREV_REDRAW
CLEARLINE LINECOUNT - LINE_COUNT_FOR_LOCKPICK
SIF ANIMATED_UNLOCK_PROGRSSION == UNLOCK_PROGRESSION && UNLOCK_PROGRESSION == 42
	RETURN 1
RETURN 0


@LOCK_DIFFICULTY_SET(LOCK_DIFFICULTY, MEDIAN_OF_RIGHT_UNLOCK_ANGLE_RANGE, HALF_OF_RIGHT_UNLOCK_ANGLE_RANGE, HALF_OF_SURROUNDING_RANGE)
#LOCALSIZE 1
#LOCALSSIZE 1
#DIM REF LOCK_DIFFICULTY
#DIM REF MEDIAN_OF_RIGHT_UNLOCK_ANGLE_RANGE
#DIM REF HALF_OF_RIGHT_UNLOCK_ANGLE_RANGE
#DIM REF HALF_OF_SURROUNDING_RANGE

SELECTCASE LOCK_DIFFICULTY
	;easy
	CASE 0
		HALF_OF_RIGHT_UNLOCK_ANGLE_RANGE = 5
		HALF_OF_SURROUNDING_RANGE:0 = 6
		HALF_OF_SURROUNDING_RANGE:1 = 7
		HALF_OF_SURROUNDING_RANGE:2 = 8
		HALF_OF_SURROUNDING_RANGE:3 = 10
		HALF_OF_SURROUNDING_RANGE:4 = 12
	;normal
	CASE 1
		HALF_OF_RIGHT_UNLOCK_ANGLE_RANGE = 4
		HALF_OF_SURROUNDING_RANGE:0 = 5
		HALF_OF_SURROUNDING_RANGE:1 = 6
		HALF_OF_SURROUNDING_RANGE:2 = 7
		HALF_OF_SURROUNDING_RANGE:3 = 8
		HALF_OF_SURROUNDING_RANGE:4 = 9
	;hard
	CASE 2
		HALF_OF_RIGHT_UNLOCK_ANGLE_RANGE = 3
		HALF_OF_SURROUNDING_RANGE:0 = 4
		HALF_OF_SURROUNDING_RANGE:1 = 5
		HALF_OF_SURROUNDING_RANGE:2 = 5
		HALF_OF_SURROUNDING_RANGE:3 = 6
		HALF_OF_SURROUNDING_RANGE:4 = 6
	;extremely hard
	CASE 3
		HALF_OF_RIGHT_UNLOCK_ANGLE_RANGE = 2
		HALF_OF_SURROUNDING_RANGE:0 = 3
		HALF_OF_SURROUNDING_RANGE:1 = 3
		HALF_OF_SURROUNDING_RANGE:2 = 4
		HALF_OF_SURROUNDING_RANGE:3 = 4
		HALF_OF_SURROUNDING_RANGE:4 = 4
ENDSELECT

MEDIAN_OF_RIGHT_UNLOCK_ANGLE_RANGE = RAND(HALF_OF_RIGHT_UNLOCK_ANGLE_RANGE, 181 - HALF_OF_RIGHT_UNLOCK_ANGLE_RANGE)

@LOCK_DIFFICULTY_GRADE(LOCK_DIFFICULTY)
#FUNCTIONS
#LOCALSIZE 1
#LOCALSSIZE 1
#DIM REF LOCK_DIFFICULTY

SELECTCASE LOCK_DIFFICULTY
	;easy
	CASE 0
		RETURNF "简单"
	;normal
	CASE 1
		RETURNF "中等"
	;hard
	CASE 2
		RETURNF "困难"
	;extremely hard
	CASE 3
		RETURNF "非常困难"
ENDSELECT

@LOCK_DIFFICULTY_COLOR(LOCK_DIFFICULTY)
#FUNCTIONS
#DIM REF LOCK_DIFFICULTY

SELECTCASE LOCK_DIFFICULTY
	;easy
	CASE 0
		RETURNF "0x00FF00"
	;normal
	CASE 1
		RETURNF "0x0078B4"
	;hard
	CASE 2
		RETURNF "0xCC99FF"
	;extremely hard
	CASE 3
		RETURNF "0xFF0000"
ENDSELECT

@TREASURE_CHEST
#LOCALSIZE 1
#LOCALSSIZE 1
#DIM DYNAMIC CHEST_GRADE = -1
#DIM DYNAMIC CHEST_RANDOM_VALUE
VARSET CHEST_LOOT
VARSET AMOUNT_OF_CHEST_LOOT
SELECTCASE TALENT:MASTER:採集Lv
	CASE 1
		CHEST_GRADE = 0
	CASEELSE
		CHEST_RANDOM_VALUE = RAND:100
		IF CHEST_RANDOM_VALUE < 2 * (TALENT:MASTER:採集Lv - 1)
			CHEST_GRADE = 3
		ELSEIF CHEST_RANDOM_VALUE < 4 * (TALENT:MASTER:採集Lv - 1)
			CHEST_GRADE = 2
		ELSEIF CHEST_RANDOM_VALUE < 6 * (TALENT:MASTER:採集Lv - 1)
			CHEST_GRADE = 1
		ELSE
			CHEST_GRADE = 0
		ENDIF
ENDSELECT

SIF CHEST_GRADE == -1
	RETURN 0

VARSET RESULT
HTML_PRINT @"发现了一个<font color='#%LOCK_DIFFICULTY_COLOR(CHEST_GRADE)%'>上锁的宝箱</font>！<br>要尝试打开吗？"
CALL ASK_YN

SIF RESULT == 1
	RETURN 0

VARSET RESULT
CALL LOCKPICK(CHEST_GRADE)
IF RESULT == 1
	PRINTFORMW 开锁成功
	SELECTCASE CHEST_GRADE
		CASE 0
			DO
				LOCAL:0 = RAND(600, 711)
				SIF ITEMNAME:(LOCAL:0) == ""
					CONTINUE
				SIF ITEMPRICE:(LOCAL:0) >= 500
					CONTINUE
				SIF ITEM:(LOCAL:0) >= 999
					CONTINUE
				IF CHEST_LOOT:0 == 0
					CHEST_LOOT:0 = LOCAL:0
					SIF RAND:2
						CONTINUE
				ENDIF
				SIF CHEST_LOOT:1 == 0
					CHEST_LOOT:1 = LOCAL:0
				SIF CHEST_LOOT:0 > 0 || CHEST_LOOT:1 > 0
					BREAK
			LOOP 1
		CASE 1
			DO
				IF RAND:2
					LOCAL:0 = RAND(600, 711)
				ELSE
					LOCAL:0 = RAND(151, 384)
				ENDIF
				SIF ITEMNAME:(LOCAL:0) == ""
					CONTINUE
				SIF ITEMPRICE:(LOCAL:0) >= 5000
					CONTINUE
				SIF ITEM:(LOCAL:0) >= 999
					CONTINUE
				IF CHEST_LOOT:0 == 0
					CHEST_LOOT:0 = LOCAL:0
					SIF RAND:2
						CONTINUE
				ENDIF
				SIF CHEST_LOOT:1 == 0
					CHEST_LOOT:1 = LOCAL:0
				SIF CHEST_LOOT:0 > 0 || CHEST_LOOT:1 > 0
					BREAK
			LOOP 1
		CASE 2
			DO
				IF RAND:2
					LOCAL:0 = RAND(600, 711)
				ELSE
					LOCAL:0 = RAND(151, 384)
				ENDIF
				SIF ITEMNAME:(LOCAL:0) == ""
					CONTINUE
				SIF INRANGE(LOCAL:0, 600, 706) && INRANGE(ITEMPRICE:(LOCAL:0), 5000, 50000)
					CONTINUE
				SIF ITEM:(LOCAL:0) >= 999
					CONTINUE
				IF CHEST_LOOT:0 == 0
					CHEST_LOOT:0 = LOCAL:0
					SIF RAND:4
						CONTINUE
				ENDIF
				SIF CHEST_LOOT:1 == 0
					CHEST_LOOT:1 = LOCAL:0
				SIF CHEST_LOOT:0 > 0 || CHEST_LOOT:1 > 0
					BREAK
			LOOP 1
		CASE 3
			DO
				IF ITEM:避水の玉 == 0 && !RAND:10
					LOCAL:0 = 404
				ELSEIF !RAND:10
					LOCAL:0 = 531
				ELSEIF RAND:2
					LOCAL:0 = RAND(502, 519)
				ELSE
					IF RAND:2
						LOCAL:0 = RAND(560, 587)
					ELSE
						LOCAL:0 = RAND(850, 858)
					ENDIF
				ENDIF
				SIF ITEMNAME:(LOCAL:0) == ""
					CONTINUE
				SIF ITEM:(LOCAL:0) >= 999
					CONTINUE
				SIF CHEST_LOOT:0 == 404
					CONTINUE
				IF CHEST_LOOT:0 == 0
					CHEST_LOOT:0 = LOCAL:0
					CONTINUE
				ENDIF
				SIF CHEST_LOOT:1 == 0
					CHEST_LOOT:1 = LOCAL:0
				SIF CHEST_LOOT:0 > 0 || CHEST_LOOT:1 > 0
					BREAK
			LOOP 1
	ENDSELECT
ELSE
	RETURN 0
ENDIF
IF CHEST_LOOT:0 > 0
	LOCALS = %ITEMNAME:(CHEST_LOOT:0)%
	IF CHEST_LOOT:0 != 404
		AMOUNT_OF_CHEST_LOOT:0 = RAND(4 - CHEST_GRADE, 8 - CHEST_GRADE)
	ELSE
		AMOUNT_OF_CHEST_LOOT:0 = 1
	ENDIF
	CALL GET_ITEM(LOCALS, AMOUNT_OF_CHEST_LOOT:0, 2)
ENDIF
IF CHEST_LOOT:1 > 0
	LOCALS = %ITEMNAME:(CHEST_LOOT:1)%
	IF CHEST_LOOT:1 != 404
		AMOUNT_OF_CHEST_LOOT:1 = RAND(4 - CHEST_GRADE, 8 - CHEST_GRADE)
	ELSE
		AMOUNT_OF_CHEST_LOOT:1 = 1
	ENDIF
	CALL GET_ITEM(LOCALS, AMOUNT_OF_CHEST_LOOT:1, 2)
ENDIF
RETURN 1
