;-------------------------------------------
;调教后の文本
;-------------------------------------------
@PALAM_MESSAGE_C
#LOCALSIZE 1
#LOCALSSIZE 1
RESETCOLOR
;二重表示抑制
IF GETBIT(DOUBLE_MESSAGE_BLOCKED:TARGET, 4)
	CALL SET_KOJO_COLOR
	RETURN
ENDIF
SETBIT DOUBLE_MESSAGE_BLOCKED:TARGET, 4

;处女丧失
FOR LOCAL,0,CHARANUM
	IF TCVAR:LOCAL:破瓜 == 1 || TCVAR:LOCAL:破瓜 == 2
		PRINTFORML %CALLNAME:LOCAL%的秘唇、作为处女证明的鲜红之血滴落下来……
	ELSEIF TCVAR:LOCAL:破瓜 == -1
		PRINTFORML 令人不可思议的是%CALLNAME:LOCAL%的秘唇，竟然没有流出一滴血……
	ENDIF
	SIF TCVAR:LOCAL:破瓜 == 2
		PRINTFORMW %CALLNAME:LOCAL%那再次被撕开处女膜的秘唇依旧那么妖艳与美丽……
NEXT
;Ａ处女丧失
FOR LOCAL,0,CHARANUM
	IF FLAG:70 && TCVAR:LOCAL:Ａ破瓜
		PRINTFORMW %CALLNAME:LOCAL%紧绷的后庭被强行打开
	ELSE
		IF TCVAR:LOCAL:Ａ破瓜 == 1 
			PRINTFORM %CALLNAME:LOCAL%因为尻穴的违和感
			IF TARGET == 57 && !TALENT:恋慕
				PRINTFORMW 换上了猿和狮子口的面具
			ELSE
				PRINTFORMW 紧缩眉头
			ENDIF
		ELSEIF TCVAR:LOCAL:Ａ破瓜 == -1 && !CFLAG:LOCAL:Ａ破瓜違和感確認
			PRINTFORMW %CALLNAME:LOCAL%因为菊花轻而易举地吞下了异物而感到了违和感
			CFLAG:LOCAL:Ａ破瓜違和感確認 = 1
		ENDIF
	ENDIF
NEXT
;恋人取得
IF SELECTCOM == 352
	IF TFLAG:193
		PRINTFORM %CALLNAME:TARGET%
		IF TARGET == 57 && !TALENT:恋慕
			PRINTFORM 换上笑嘻嘻的老翁面具、
		ELSE
			PRINTFORM 满脸通红地
		ENDIF
		PRINTFORML 接受了%CALLNAME:MASTER%的告白……
		PRINTL  
		PRINTFORM 和%CALLNAME:TARGET%
		PRINT 成为了
		SETCOLOR C_PINK
		PRINTW 恋人
		RESETCOLOR
		PRINTL  
	ELSE
		PRINTFORMW %CALLNAME:TARGET%一脸惊讶……
		PRINTL  
	ENDIF
ENDIF
;什么也不做
IF SELECTCOM == 21
	IF (FLAG:70 == 1 && CFLAG:TARGET:344 == 0)
	ELSE
	SIF PALAM:欲情 >= PALAMLV:4
		PRINTFORM %CALLNAME:TARGET%呼吸慌乱
	IF PALAM:欲情 >= PALAMLV:6
		IF TARGET == 57 && !TALENT:恋慕
			PRINTFORM 虽然面无表情
		ENDIF
		PRINT 用灼热的瞳孔凝视着这边
	ENDIF
	SIF PALAM:欲情 >= PALAMLV:4 && TCVAR:媚薬
		PRINT 、抱住微微颤抖着的身体的
	SIF PALAM:欲情 >= PALAMLV:6
		PRINT 同时、频繁摩擦着大腿
	SIF PALAM:欲情 >= PALAMLV:4
		PRINTL ……
	ENDIF
ENDIF
;口交で射精后
;IF TFLAG:8 && NOWEX:PLAYER:11 && (!FLAG:70 || CFLAG:TARGET:344 == 1)
IF (TCVAR:MASTER:112 == 1 || TCVAR:MASTER:112 == 2) && SELECTCOM == 81 && (!FLAG:70 || CFLAG:TARGET:344 == 1) && !CFLAG:恶作剧
	;LOCAL = MASTER_POSE(SET_M,SET_P,1)
	;PRINT 那之后、
	;IF SELECTCOM == 511
		;PRINTFORML %CALLNAME:TARGET%は%CALLNAME:MASTER%と%CALLNAME:ASSI%の阴茎を交互に咥え、污れを舐め取っていった……
	;ELSE
		;IF LOCAL && LOCAL != TARGET
			;PRINTFORM %CALLNAME:LOCAL%と%CALLNAME:TARGET%は
		;ELSEIF LOCAL && LOCAL == TARGET
			;PRINTFORM %CALLNAME:LOCAL%は
		;ELSEIF !LOCAL
			PRINTFORM %CALLNAME:TARGET%
		;ENDIF
		SIF NOWEX:PLAYER:射精
			PRINT 一边吸吮着残余的精液，
		PRINTL 把肉棒的污垢舔得干干净净……
	;ENDIF
	;SIF TFLAG:8 == 3 && NOWEX:MASTER:11
		;PRINTL 二人はそれだけでは足りないかのように互相用口に溜まった精液を舐めあっている……
ENDIF
CALL SET_KOJO_COLOR()
;-------------------------------------------
;パラメータ変化時文本　作成中
;-------------------------------------------
@PALAM_MESSAGE_D
#LOCALSIZE 1
#LOCALSSIZE 1
RESETCOLOR
CALL SET_KOJO_COLOR()
;欲情がLV2になったとき　などなど
;-------------------------------------------------
;関数名:OPPAI_DESCRIPTION
;概　要:言葉でなく心で理解しろ
;引　数:ARG…角色登録番号
;戻り値:夢
;備　考:文中関数
;使用例:%OPPAI_DESCRIPTION(TARGET)%
;例文:胸
;ARG:1を1にすると状態に応じた形容詞がつく
;使用例:%OPPAI_DESCRIPTION(TARGET,1)%
;例文:快要装不下的乳塊
;条件文に TEQUIP:50 == PLAYER || TEQUIP:51 == PLAYER
;を指定しているのは合体中の描写
;-------------------------------------------------
@OPPAI_DESCRIPTION(ARG,ARG:1)
#FUNCTIONS
#LOCALSIZE 1
#LOCALSSIZE 1
VARSET LOCALS
IF ARG:1
	IF FLAG:Look表示用乳房
		;爆乳
		IF TALENT:ARG:胸圍 == 2
			LOCALS += "呼之欲出的"
		;巨乳
		ELSEIF TALENT:ARG:胸圍 == 1
			LOCALS += "丰满的"
		;并乳
		ELSEIF TALENT:ARG:胸圍 == 0
			LOCALS += "美型的"
		;贫乳
		ELSEIF TALENT:ARG:胸圍 == -1
			LOCALS += "小巧的"
		;绝壁
		ELSEIF TALENT:ARG:胸圍 == -2
			LOCALS += "可爱的"
		ELSE
			LOCALS += "错误报告、出现了预想之外的数值――以上"
		ENDIF
	ELSE
		;爆乳
		IF TALENT:ARG:胸圍 == 2
			IF TEQUIP:50 == PLAYER || TEQUIP:51 == PLAYER
				SELECTCASE RAND:4
					CASE 0
						LOCALS += "满溢而出地乱颤的"
					CASE 1
						LOCALS += "呼哇哇地颤动的"
					CASE 2
						LOCALS += "噗隆噗隆地摇曳乱颤的"
					CASE 3
						LOCALS += "软绵绵地摇晃的"
				ENDSELECT
			ENDIF
			IF TALENT:PLAYER:胸圍 == -2 && RAND:5 == 0
				LOCALS += "硕大得令人咋舌的"
			ELSEIF RAND:4 == 0
				LOCALS += "乳牛一般的"
			ELSEIF RAND:3 == 0
				LOCALS += "呼之欲出的"
			ELSE
				LOCALS += "无法一手掌控的"
			ENDIF
		;巨乳
		ELSEIF TALENT:ARG:胸圍 == 1
			IF RAND:3 == 0
				IF TEQUIP:50 == PLAYER || TEQUIP:51 == PLAYER
					LOCALS += "颤巍巍地晃动、一手完全无法掌握的"
				ELSE
					LOCALS += "难以完全包覆的"
				ENDIF
			ELSEIF RAND:2 == 0
				IF TEQUIP:50 == PLAYER || TEQUIP:51 == PLAYER
					LOCALS += "沉甸甸地剧烈晃动的"
				ELSE
					LOCALS += "沉甸甸地垂坠着的"
				ENDIF
			ELSE
				IF TEQUIP:50 == PLAYER || TEQUIP:51 == PLAYER
					LOCALS += "晃晃悠悠，仿佛要从手中溢出的"
				ELSE
					LOCALS += "仿佛要从手中溢出的"
				ENDIF
			ENDIF
		;并乳
		ELSEIF TALENT:ARG:胸圍 == 0
			IF TEQUIP:ARG:50 == PLAYER || TEQUIP:ARG:51 == PLAYER
				IF !RAND:4
					LOCALS += "噗噜噗噜颤动着的"
				ELSEIF !RAND:3
					LOCALS += "轻快晃动着的"
				ELSE
					LOCALS += "噗哟噗哟颤颤着的"
				ENDIF
			ENDIF
			IF TALENT:ARG:体型 < 0
				IF !RAND:2
					LOCALS += "相较体格发育良好的"
				ELSE
					LOCALS += "从身材上难以想象的丰满"
				ENDIF
			ELSEIF RAND:4 == 0
					LOCALS += "水润饱满的"
			ELSEIF RAND:3 == 0
					LOCALS += "盈盈一握的"
			ELSEIF RAND:2 == 0
					LOCALS += "优美匀称的"
			ELSE
				LOCALS += "曲线优美的"
			ENDIF
		;绝壁 只描写乳头，没有胸部可以描写
		ELSEIF TALENT:ARG:胸圍 == -2
			IF RAND:4 == 0
				LOCALS += "隐隐约约有一点点的"
			ELSEIF RAND:2 == 0
				LOCALS += "几乎没有隆起的"
			ELSE
				LOCALS += "感觉只有一点点凸起的"
			ENDIF
		;贫乳
		ELSEIF TALENT:ARG:胸圍 == -1
			IF TALENT:ARG:体型 < 0
				LOCALS += "与身材相称的"
			ELSEIF RAND:2 == 0
				LOCALS += "曲线平缓的"
			ELSE
				LOCALS += "可爱的"
			ENDIF
		;想定外
		ELSE
			LOCALS += "错误报告、出现了预想之外的数值――以上"
		ENDIF
	ENDIF
ENDIF
CALLF 上半身内衣チェック(ARG)
IF FLAG:Look表示用乳房
	;绝壁
	IF TALENT:胸圍 == -2
		LOCALS += "乳头"
	;贫乳~爆乳
	ELSEIF TALENT:胸圍 >= -1 && TALENT:胸圍 <= 2
		LOCALS += "胸部"
	ELSE
		LOCALS += "错误报告、出现了预想之外的数值――以上"
	ENDIF
ELSE
	;爆乳
	IF TALENT:ARG:胸圍 == 2 && TALENT:ARG:母乳体質 && TALENT:ARG:淫乳 && TALENT:ARG:受虐狂 && RAND:27 == 0
		LOCALS += "I字乳"
	ELSEIF TALENT:ARG:胸圍 == 2 && TALENT:ARG:母乳体質 && RAND:26 == 0
		LOCALS += "大奶罐"
	ELSEIF TALENT:ARG:胸圍 == 2 && (TALENT:ARG:半白澤 || TALENT:ARG:母乳体質) && RAND:25 == 0
		LOCALS += "奶牛乳"
	ELSEIF TALENT:ARG:胸圍 == 2 && TALENT:ARG:淫乳 && RAND:24 == 0
		LOCALS += "淫爆乳"
	ELSEIF TALENT:ARG:胸圍 == 2 && TALENT:ARG:体型 == 1 && RAND:23 == 0
		LOCALS += "超乳"
	ELSEIF TALENT:ARG:胸圍 == 2 && RAND:22 == 0
		LOCALS += "豪乳"
	ELSEIF TALENT:ARG:胸圍 == 2 && RAND:20 == 0
		LOCALS += "爆乳"
	ELSEIF TALENT:ARG:胸圍 == 2 && RAND:19 == 0
		LOCALS += "乳肉"
	;巨乳
	ELSEIF TALENT:ARG:胸圍 == 1 && RAND:18 == 0
		LOCALS += "丰乳"
	ELSEIF TALENT:ARG:胸圍 == 1 && RAND:17 == 0
		LOCALS += "巨乳"
	ELSEIF TALENT:ARG:胸圍 == 1 && RAND:16 == 0
		LOCALS += "丰乳"
	ELSEIF TALENT:ARG:胸圍 == 1 && RAND:15 == 0
		LOCALS += "柔肉"
	ELSEIF TALENT:ARG:胸圍 >= 1 && RAND:14 == 0
		LOCALS += "柔乳"
	;绝壁
	ELSEIF TALENT:ARG:胸圍 == -2 && TALENT:ARG:性別 != 2 && RAND:13 == 0
		LOCALS += "绝壁"
	ELSEIF TALENT:ARG:胸圍 == -2 && TALENT:ARG:性別 != 2 && RAND:12 == 0
		LOCALS += "洗衣板"
	ELSEIF TALENT:ARG:胸圍 == -2 && TALENT:ARG:性別 != 2 && RAND:11 == 0
		LOCALS += "平坦胸部"
	;贫乳
	ELSEIF TALENT:ARG:胸圍 == -1 && RAND:10 == 0
		LOCALS += "贫乳"
	ELSEIF TALENT:ARG:胸圍 == -1 && RAND:9 == 0
		LOCALS += "贫胸"
	ELSEIF TALENT:ARG:胸圍 == -1 && RAND:8 == 0
		LOCALS += "小巧胸部"
	ELSEIF TALENT:ARG:胸圍 == -1 && RAND:7 == 0
		LOCALS += "平胸"
	ELSEIF TALENT:ARG:胸圍 == -1 && RAND:6 == 0
		LOCALS += "小胸"
	;乳
	ELSEIF TALENT:ARG:胸圍 == 0 && RAND:5 == 0
		LOCALS += "美乳"
	ELSEIF TALENT:ARG:胸圍 == 0 && RAND:4 == 0
		LOCALS += "胸部"
	ELSEIF TALENT:ARG:胸圍 >= 0 && RAND:3 == 0
		LOCALS += "乳房"
	ELSEIF TALENT:ARG:胸圍 >= 0 && RAND:2 == 0
		LOCALS += "乳房"
	ELSE
		;念のため男の乳书いとく
		IF TALENT:ARG:胸圍 == -2 && TALENT:ARG:性別 == 2
			IF ABL:ARG:戦闘能力 >= 6
				LOCALS += "大胸肌峡谷"
			ELSEIF ABL:ARG:戦闘能力 >= 4
				LOCALS += "厚实胸膛"
			ELSEIF ABL:ARG:戦闘能力 <= 1
				LOCALS += "单薄胸膛"
			ELSE
				LOCALS += "胸膛"
			ENDIF
		ELSEIF TALENT:ARG:胸圍 >= 0 && TALENT:ARG:胸圍 <= 2
			LOCALS += "乳房"
		ELSEIF TALENT:ARG:胸圍 < 0 && TALENT:ARG:胸圍 >= -2
			LOCALS += "小小乳房"
		ELSE
			LOCALS += "错误报告、出现了预想之外的数值――以上"
		ENDIF
	ENDIF
ENDIF
RETURNF LOCALS

;-------------------------------------------------
;函数名:NIPPLE_DESCRIPTION
;参　数:ARG…角色登录番号
;使用例:%NIPPLE_DESCRIPTION(TARGET)%
;例文:乳头
;备　考:文中函数
;ARG:1を1にすると状态に应じた形容词がつく
;使用例:%NIPPLE_DESCRIPTION(TARGET,1)%
;例文:梆硬になっている乳头
;-------------------------------------------------
@NIPPLE_DESCRIPTION(ARG,ARG:1)
#FUNCTIONS
#LOCALSIZE 1
#LOCALSSIZE 1
VARSET LOCALS

IF ARG:1
	IF EX:ARG:Ｂ絶頂 * 10000 + PALAM:ARG:快Ｂ + PALAM:ARG:欲情 >= 30000
	;ELSEIF PALAM:欲情 >= PALAMLV:5
		IF RAND:4 == 0
			LOCALS += "硬挺挺的"
		ELSEIF RAND:3 == 0
			LOCALS += "从乳晕开始隆起的"
		ELSEIF RAND:2 == 0
			LOCALS += "连同乳晕一起饱满鼓胀起来的"
		ELSE
			LOCALS += "因快感下流地肿胀的"
		ENDIF
	ELSEIF EX:ARG:Ｂ絶頂 * 10000 + PALAM:ARG:快Ｂ + PALAM:ARG:欲情 >= 10000
	;ELSEIF PALAM:欲情 >= PALAMLV:4
		IF RAND:4 == 0
			LOCALS += "像蓓蕾般翘起的"
		ELSEIF RAND:3 == 0
			LOCALS += "尖尖的"
		ELSEIF RAND:2 == 0
			LOCALS += "完全硬邦邦的"
		ELSE
			LOCALS += "勃起的"
		ENDIF
	ELSEIF EX:Ｂ絶頂 * 10000 + PALAM:快Ｂ + PALAM:欲情 >= 3000
	;ELSEIF PALAM:欲情 >= PALAMLV:3
		IF RAND:4 == 0
			LOCALS += "硬起来的"
		ELSEIF RAND:3 == 0
			LOCALS += "开始变硬的"
		ELSEIF RAND:2 == 0
			LOCALS += "慢慢勃起的"
		ELSE
			LOCALS += "不断充血的"
		ENDIF
	ELSE
		IF TALENT:体型 <= -1
			LOCALS += "小小的"
		ELSE
			LOCALS += "还很柔嫩的"
		ENDIF
	ENDIF
ENDIF
IF FLAG:Look表示用乳頭
	LOCALS += "乳头"
ELSE
	IF RAND:4 == 0
		LOCALS += "乳头"
	ELSEIF RAND:3 == 0
		LOCALS += "蓓蕾"
	ELSEIF RAND:2 == 0
		LOCALS += "乳蕾"
	ELSE
		LOCALS += "乳头"
	ENDIF
ENDIF
RETURNF LOCALS
;-------------------------------------------------
;函数名:PrintMilkedNipple
;参　数:ARG…角色登录番号
;母乳体质乳头の记述が何度も出てきたから函数化
;-------------------------------------------------
@PrintMilkedNipple(ARG,ARGS)
#LOCALSIZE 1
#LOCALSSIZE 1
IF ARGS == "衣服描写つき"
	IF TEQUIP:ARG:1 == 0
		PRINT 
	ELSE
		FOR LOCAL,17,6,-1
			IF GROUPMATCH(LOCAL, 15, 12, 11)
				CONTINUE
			ENDIF
			IF EQUIP:ARG:LOCAL
				IF !TEQUIP:ARG:上半身裸露状態
					PRINTFORM 露在%CLOTHNAME(LOCAL,EQUIP:ARG:LOCAL)%外面、
					BREAK
				ELSEIF TEQUIP:ARG:上半身裸露状態
					PRINTFORM 露在敞开的%CLOTHNAME(LOCAL,EQUIP:ARG:LOCAL)%外、
					BREAK
				ENDIF
			ENDIF
		NEXT
	ENDIF
ENDIF
PRINTFORM %OPPAI_DESCRIPTION(ARG)%上
IF TALENT:ARG:母乳体質 && RAND:3 == 0
	PRINTFORM 渗着%TEXTR("雌乳/母乳/奶汁")%的
ENDIF
PRINTFORM %NIPPLE_DESCRIPTION(ARG,1)%


;-------------------------------------------------
;函数名:SAMEN_DESCRIPTION
;参　数:ARG…角色登录番号
;备　考:文中函数
;ARG:1を1にすると素質に应じた形容词がつく
;-------------------------------------------------
@SAMEN_DESCRIPTION(ARG,ARG:1)
#FUNCTIONS
#LOCALSIZE 1
#LOCALSSIZE 1
VARSET LOCALS

IF TALENT:ARG:濃厚精液 && ARG:1
	SELECTCASE RAND:10
		CASE 0
			LOCALS += "极为浓厚的"
		CASE 1
			LOCALS += "半固态的"
		CASE 2
			LOCALS += "包含着无数精子的"
		CASE 3
			LOCALS += "糊状的"
		CASE 4
			LOCALS += "经过数日酝酿的"
		CASE 5
			LOCALS += "散发出强烈雄性气味的"
		CASE 6
			LOCALS += "让雌性闻了就想怀孕的"
		CASE 7
			LOCALS += "啫喱状的"
		CASE 8
			LOCALS += "超浓雄性气息的"
		CASE 9
			LOCALS += "为了让雌性受孕的"
	ENDSELECT
ENDIF

SELECTCASE RAND:9
	CASE 0
		LOCALS += "子种汁"
	CASE 1
		LOCALS += "白浊"
	CASE 2
		LOCALS += "精液"
	CASE 3
		LOCALS += "妊娠汁"
	CASE 4
		LOCALS += "肉棒牛奶"
	CASE 5
		LOCALS += "孕汁"
	CASE 6
		LOCALS += "受孕牛奶"
	CASE 7
		LOCALS += "白浊乳液"
	CASE 8
		LOCALS += "生命之源"
ENDSELECT

RETURNF LOCALS

;-------------------------------------------------
;函数名:HIP
;概　要:三番煎じ上等
;参　数:ARG…角色登录番号；ARG:1…形容词（向后兼容是默认打开）
;返回值:
;备　考:文中函数
;使用例:%HIP(TARGET)%
;-------------------------------------------------
@HIP(ARG,ARG:1 = 1,ARG:2 = 0)
#FUNCTIONS
#LOCALSIZE 1
#LOCALSSIZE 1
VARSET LOCALS
VARSET LOCAL
IF ARG:1
	IF PALAM:ARG:欲情 >= PALAMLV:4 && !RAND:4
		IF EQUIP:ARG:レオタード || EQUIP:ARG:ボディースーツ
			LOCALS = 紧贴着湿透%SHOW_下半身内衣(ARG,3)%、
		ELSEIF EQUIP:ARG:下半身内衣１
			LOCALS = 紧贴着\@ GET_INT(ARG, "下半身内衣_ずらし不可", EQUIP:ARG:下半身内衣１, "半透明(后)") ?湿透#汗湿 \@%PANTSNAME(EQUIP:ARG:下半身内衣１, ARG)%、
		ELSEIF EQUIP:ARG:下半身内衣２
			LOCALS = 紧贴着\@ GET_INT(ARG, "下半身内衣_ずらし可能", EQUIP:ARG:下半身内衣２, "半透明(后)")?湿透#汗湿\@%PANTSNAME(EQUIP:ARG:下半身内衣２, ARG)%、
		ELSE
			LOCALS = 浮现出晶莹汗珠、
		ENDIF
	ELSEIF !RAND:5 && EXP:ARG:出産経験 && TALENT:ARG:ヒップサイズ <= 0
		LOCALS = 很难想象属于%TOKANJIT(EXP:ARG:出産経験)%个孩子母亲、
	ELSEIF !RAND:5 && CFLAG:ARG:乳品质提升 && TALENT:ARG:ヒップサイズ > 0
		LOCALS = 怀孕后\@TALENT:ARG:ヒップサイズ > 0 ?更显媚惑#变得有女人味\@、
	ENDIF

IF TALENT:ARG:ヒップサイズ < -1
	LOCAL = RAND(0,13)
ELSEIF TALENT:ARG:ヒップサイズ == -1
	LOCAL = RAND(5,20)
ELSEIF TALENT:ARG:ヒップサイズ == 0
	LOCAL = RAND(13,23)
ELSEIF TALENT:ARG:ヒップサイズ == 1
	LOCAL = RAND(13,31)
ELSEIF TALENT:ARG:ヒップサイズ < 4
	LOCAL = RAND(23,38)
ELSE
	LOCAL = RAND(23,50)
ENDIF
SELECTCASE LOCAL
;-1以下的幼儿体型
CASE 0
	LOCALS += "犹如去壳鸡蛋般白嫩的"
CASE 1
	LOCALS += "没什么肉但手感极佳的"
CASE 2
	LOCALS += "孩童般稚嫩的"
CASE 3
	LOCALS += "娇小的"
CASE 4
	LOCALS += "可爱的"
;-1开始的纤细体型
CASE 5
	LOCALS += "娇小可爱的"
CASE 6
	LOCALS += "纤细匀称的"
CASE 7
	LOCALS += "形状优美的"
CASE 8
	LOCALS += "小巧纤细的"
CASE 9
	LOCALS += "玲珑小巧的"
CASE 10
	LOCALS += "白嫩嫩的"
CASE 11
	LOCALS += "雪白弹嫩的"
CASE 12
	LOCALS += "吹弹可破的娇嫩"
;幼儿体型结束，正常与有肉体型开始
CASE 13
	LOCALS += "洋溢着少女气息的紧致"
CASE 14
	LOCALS += "弹性极好洋溢着青春活力的"
CASE 15
	LOCALS += "弹性极好洋溢着青春活力的"
CASE 16
	LOCALS += "弹嫩紧致的"
CASE 17
	LOCALS += "不带一丝赘肉的娇嫩"
CASE 18
	LOCALS += "手感好到舍不得松手的"
CASE 19
	LOCALS += "美型且紧致的"
CASE 20
	LOCALS += "圆润紧致的"
CASE 21
	LOCALS += "线条匀称优美的"
CASE 22
	LOCALS += "雪白水润"
;1普通体型结束，大屁股开始
CASE 23
	LOCALS += "肉肉的"
CASE 24
	LOCALS += "丰满桃形的"
CASE 25
	LOCALS += "从身材上难以想象的丰满"
CASE 26
	LOCALS += "曲线诱人且弹性十足的"
CASE 27
	LOCALS += "一看就是安产型的"
CASE 28
	LOCALS += "丰满浑圆的"
CASE 29
	LOCALS += "丰腴却没有赘肉的"
CASE 30
	LOCALS += "带着成熟魅力的"
;巨臀领域，肉臀结束
CASE 31
	LOCALS += "肉感十足的"
CASE 32
	LOCALS += "成熟魅惑的丰满"
CASE 33
	LOCALS += "丰腴浑圆、肉感极佳的"
CASE 34
	LOCALS += "带着女性媚香的硕大"
CASE 35
	LOCALS += "臀浪满溢而出的"
CASE 36
	LOCALS += "熟透了的水蜜桃般的丰盈"
CASE 37
	LOCALS += "令人欲罢不能的丰硕"
;现在是幻想时间
CASE 38 TO 40
	LOCALS += "令人无法移开视线的硕大"
CASE 41 TO 43
	LOCALS += "荡漾着白花花臀浪的"
CASE 44 TO 46
	LOCALS += "夺走人理性的巨大"
CASE 47 TO 49
	LOCALS += "仿佛将人吸住的超大"
ENDSELECT
ENDIF

IF TALENT:ARG:体型 == -2 && TALENT:ARG:ヒップサイズ <= -2
	LOCALS += TEXTR("屁屁/屁股/粉臀/玉臀/俏臀")
ELSEIF TALENT:ARG:ヒップサイズ < 0
	LOCALS += TEXTR("屁屁/屁股/臀部/粉臀/玉臀/俏臀")
ELSEIF TALENT:ARG:ヒップサイズ < 2
	LOCALS += TEXTR("屁股/臀部/粉臀/玉臀/俏臀/雪臀/翘臀")
ELSE
	LOCALS += TEXTR("屁股/臀部/雪臀/玉臀/肉尻/肥臀/翘臀")
ENDIF
RETURNF LOCALS

;-------------------------------------------------
;函数名:THIGHS
;概　要:太ももは英语でTHIGHSと言うらしい、HIPのパクリ
;参　数:ARG…角色登录番号
;返回值:
;备　考:文中函数
;使用例:%THIGHS(TARGET)%
;-------------------------------------------------
@THIGHS(ARG)
#FUNCTIONS
#LOCALSIZE 1
#LOCALSSIZE 1
VARSET LOCALS
VARSET LOCAL
;大轮盘改造
IF TALENT:ARG:ヒップサイズ < -1
	LOCAL = RAND(0,5)
ELSEIF TALENT:ARG:ヒップサイズ < 1
	LOCAL = RAND(2,8)
ELSE
	LOCAL = RAND(4,11)
ENDIF
SELECTCASE LOCAL
;原幼儿体型
	CASE 0
		LOCALS = 滑溜溜的大腿
	CASE 1
		LOCALS = 纤细的大腿
;原贫乳
	CASE 2
		LOCALS = 修长的大腿
	CASE 3
		LOCALS = 匀称的大腿
	CASE 4
		LOCALS = 摸起来感觉很好的大腿
;原胸围等于0
	CASE 5
		LOCALS = 优美紧致的大腿
	CASE 6
		LOCALS = 白玉无瑕般的大腿
	CASE 7
		LOCALS = 柔软的大腿
;原胸围大于1
	CASE 8
		LOCALS = 丰盈的大腿
	CASE 9
		LOCALS = 丰腴的大腿
	CASE 10
		LOCALS = 丰满的大腿
ENDSELECT
RETURNF LOCALS

;-------------------------------------------------
;函数名:UNDER_SKIRT_DESCRIPTION
;概　要:裙子の中の描写
;参　数:ARG…角色登录番号
;返回值:
;备　考:文中函数
;使用例:%UNDER_SKIRT_DESCRIPTION(TARGET)%
;-------------------------------------------------
@UNDER_SKIRT_DESCRIPTION(ARG)
#FUNCTIONS
#LOCALSIZE 1
#LOCALSSIZE 2
VARSET LOCALS
;扶她以外は有り得ない
IF GETBIT(TALENT:ARG:性別,1)
	LOCALS += TINKO(ARG) + "和"
ENDIF
LOCALS += SLIT(ARG, 1)

IF CFLAG:ARG:没穿内裤 == 0
	IF EQUIP:ARG:レオタード || EQUIP:ARG:ボディースーツ
		 ;LOCALS:1 = %CALLNAME:ARG%の下に何も穿いていないせいで、%SHOW_下半身内衣(ARG,3)%の上からでもくっきりと形が浮かび上がる%LOCALS%
		 LOCALS:1 = 即使隔着%SHOW_下半身内衣(ARG,3)%也能清晰地勾勒出肉感的%LOCALS%
	ELSEIF EQUIP:ARG:下半身内衣１ || EQUIP:ARG:下半身内衣２
		LOCALS:1 = \@ EQUIP:ARG:下半身内衣２ ? %PANTS_DESCRIPTION(EQUIP:ARG:下半身内衣２, ARG)%%PANTSNAME(EQUIP:ARG:下半身内衣２, ARG)% # %PANTSNAME(EQUIP:ARG:下半身内衣１, ARG)%\@
	ELSE
		LOCALS:1 = %CALLNAME:ARG%的真空%LOCALS%
	ENDIF
ELSE
	IF EQUIP:ARG:レオタード ||  EQUIP:ARG:ボディースーツ
		LOCALS:1 = 藏在%SHOW_BOTTOM(ARG,1)%下的%SHOW_下半身内衣(ARG,3)%
	ELSE
		LOCALS:1 = %CALLNAME:ARG%的真空%LOCALS%
	ENDIF
ENDIF
RETURNF LOCALS:1

;-------------------------------------------------
;函数名:SLIT
;概　要:秘裂の地の文
;-------------------------------------------------
@SLIT(ARG,有定语)
#FUNCTIONS
#LOCALSIZE 1
#LOCALSSIZE 1
#DIM DYNAMIC 有定语

VARSET LOCALS
SIF !ARG
	ARG = TARGET
CALLF 下半身内衣チェック(TARGET)
IF 有定语
	IF TALENT:ARG:処女 == 1
		LOCALS += "不识男人滋味"
		SIF PALAM:潤滑 > PALAMLV:1
			LOCALS += "但仍然"
	ELSEIF EX:ARG:処女喪失记号 == 1
		LOCALS += "刚刚失去处女"
	ELSEIF EX:ARG:処女喪失记号 == 2
		LOCALS += "刚刚被再次破处"
	ELSEIF TCVAR:ARG:発情
		LOCALS += "放出浓厚雌性气味"
		IF PALAM:潤滑 > PALAMLV:1
			LOCALS += "的同时、"
		ELSE
			LOCALS += "、"
		ENDIF
	ENDIF
	;润滑LV4以上
	IF PALAM:潤滑 > PALAMLV:4
		LOCALS += "爱蜜流淌到大腿上"
	ELSEIF PALAM:潤滑 > PALAMLV:3
		LOCALS += "咕啾咕啾地湿透了"
	ELSEIF PALAM:潤滑 > PALAMLV:2
		LOCALS += "被黏滑爱液浸湿了"
	ELSEIF PALAM:潤滑 > PALAMLV:1
		LOCALS += "微微濡湿"
	ENDIF
	;矮小体型の有无で分岐
	IF TALENT:ARG:体型 == -1
		LOCALS += "小小的"
	ELSEIF TALENT:ARG:体型 == -2
		LOCALS += TEXTR("滑溜溜的/稚嫩的/光溜溜的/未成熟的/小巧的/令人怜爱的")
	ELSE
		LOCALS += "的"
	ENDIF
ENDIF
IF TALENT:ARG:処女 == 1 && !RAND:3
	LOCALS += "处女嫩穴"
ELSE
	SELECTCASE RAND:8
		CASE 0
			LOCALS += "秘缝"
		CASE 1
			LOCALS += "小穴"
		CASE 2
			LOCALS += "膣穴"
		CASE 3
			LOCALS += "雌穴"
		CASE 4
			LOCALS += "肉壶"
		CASE 5
			LOCALS += "蜜壶"
		CASE 6
			LOCALS += "蜜穴"
		CASEELSE
			LOCALS += "秘裂"
	ENDSELECT
ENDIF
RETURNF LOCALS
;-------------------------------------------------
;函数名:寝起き更衣
;概　要:寝起き更衣地の文
;参　数:ARG=TARGET
;返回值:
;备　考:
;-------------------------------------------------
@寝起き着替え(ARG)
#LOCALSIZE 1
#LOCALSSIZE 1
IF 睡眠時間(ARG) && !CFLAG:ARG:眠姦発覚
	IF ARG && TALENT:ARG:恋慕 && !GETBIT(CFLAG:ARG:着替え実行, 1) && CFLAG:MASTER:現在位置 == CFLAG:ARG:現在位置 && MAIN_MAP == GET_MAPID(CFLAG:MASTER:現在位置)
		PRINTFORM %CALLNAME:ARG%
		IF TALENT:ARG:恋人
			PRINTFORM 仿佛在诱惑这边、慢慢地
		;气弱系
		ELSEIF CFLAG:ARG:性格傾向 == 1
			PRINTFORM 在意着这边的视线
		;强气系
		ELSEIF CFLAG:ARG:性格傾向 == 2
			PRINTFORM 叹了口气、堂堂正正地
		;坦率系
		ELSEIF CFLAG:ARG:性格傾向 == 3
			PRINTFORM 露出苦笑
		;真面目系
		ELSEIF CFLAG:ARG:性格傾向 == 4
			PRINTFORM 把背影朝向这边
		;其他
		ELSEIF CFLAG:ARG:性格傾向 == 5
			PRINTFORM 带着惊讶的表情
		ENDIF
		PRINTFORML 换上了睡衣、钻进了被窝
	ENDIF
	SETBIT CFLAG:ARG:着替え実行, 1
	CLEARBIT CFLAG:ARG:着替え実行, 0
ELSE
	IF ARG && TALENT:ARG:恋慕 && !GETBIT(CFLAG:ARG:着替え実行, 0) && CFLAG:MASTER:現在位置 == CFLAG:ARG:現在位置 && MAIN_MAP == GET_MAPID(CFLAG:MASTER:現在位置)
		PRINTFORM %CALLNAME:ARG%
		IF TALENT:ARG:恋人
			PRINTFORM 仿佛在诱惑这边、慢慢地
		;气弱系
		ELSEIF CFLAG:ARG:性格傾向 == 1
			PRINTFORM 在意着这边的视线
		;强气系
		ELSEIF CFLAG:ARG:性格傾向 == 2
			PRINTFORM 叹了口气、堂堂正正地
		;坦率系
		ELSEIF CFLAG:ARG:性格傾向 == 3
			PRINTFORM 露出苦笑
		;真面目系
		ELSEIF CFLAG:ARG:性格傾向 == 4
			PRINTFORM 把背影朝向这边
		;其他
		ELSEIF CFLAG:ARG:性格傾向 == 5
			PRINTFORM 带着惊讶的表情
		ENDIF
		PRINTFORML 换上了便服
	ENDIF
	SETBIT CFLAG:ARG:着替え実行, 0
	CLEARBIT CFLAG:ARG:着替え実行, 1
ENDIF
RETURN ARG

;-------------------------------------------
;一日の结束时口上の发生表示
;-------------------------------------------
@EVENTEND_TSUBUYAKI
#LOCALSIZE 1
#LOCALSSIZE 1
#DIM CHARA
CHARA = TARGET
SIF TCVAR:CHARA:デイリー表示
	RETURN
PRINTL 
GETCOLOR
LOCAL = RESULT
RESETCOLOR
DRAWLINEFORM ━
PRINTFORML ▼%CALLNAME:CHARA%的呢喃
;DRAWLINEFORM －
SETCOLOR LOCAL
TCVAR:CHARA:デイリー表示 = 1
;WAIT
