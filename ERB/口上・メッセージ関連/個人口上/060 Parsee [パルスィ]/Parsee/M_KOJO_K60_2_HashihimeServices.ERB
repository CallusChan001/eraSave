;更新次数：2024年11月3日
;-------------------------------------------------
;帕露西用
;Hashihime services
;-------------------------------------------------
@K60_HASHIHIME_SERVICES
#DIM UnwantedLover
#DIM DYNAMIC LOVERPEP
CALL M_KOJO_EGG_COLOR_K60
SELECTCASE K60_ATTITUDE()
    CASE 0, 1, 3
        PRINTFORMDW 帕露西向你露出了微笑
        PRINTFORMW 「需要我做些什么吗？」
    CASE 2
        PRINTFORMDW 帕露西面无表情的瞪着你
        PRINTFORMW 「有何贵干」
ENDSELECT


DO
    RESETCOLOR
    DRAWLINE

    CALL K60_HashihimeServices_CalculateLOVERPEP()
    LOVERPEP = RESULT:0
    UnwantedLover = RESULT:1
    IF LOVERPEP > 1
        PRINTFORML [  1] - 请帮我和我的其中一个恋人分手
    ELSEIF UnwantedLover == 60
        PRINTFORML [  1] - 我想和你分手
    ELSEIF LOVERPEP == 1
        PRINTFORML [  1] - 帮我和其他人分手%CALLNAME:UnwantedLover%
    ELSE
        SETCOLOR C_L_GRAY
        PRINTFORML [  1] - ????? (拥有一个恋人)
        RESETCOLOR
    ENDIF

    IF ABL:[[帕露西]]:親密 >= 2
        PRINTFORML [  2] - 告诉我…
    ELSE
        SETCOLOR C_L_GRAY
        PRINTFORML [  2] - ????? (结交帕露西)
        RESETCOLOR
    ENDIF
    IF (K60_FUCKED_PARSEE())
        PRINTFORML [  3] - 我希望能重新改变你的体毛
    ELSE
        SETCOLOR C_L_GRAY
        PRINTFORML [  3] - ????? (被帕露西推倒或得到帕露西的认可)
        RESETCOLOR
    ENDIF

    IF K60_YOU_ARE_CHEATING_ON_ONE_OF_PARSEES_FRIENDS()
        SETCOLOR C_L_GRAY
        IF !CFLAG:[[帕露西]]:生解禁
            PRINTFORML [  4] - ????? (取得【左拥右抱】，或者与帕露西的好友们分手吧)
        ELSE
            PRINTFORML [  4] - 除非你取得【左拥右抱】或与她的好友们分手，否则帕露西不会同意无保护插入行为
        ENDIF
        RESETCOLOR
    ELSEIF CFLAG:[[帕露西]]:生解禁 == 2
        SETCOLOR C_L_GRAY
        PRINTFORML [  4] - 帕露西已经有了与你繁衍后代的觉悟
        RESETCOLOR
    ELSEIF (TALENT:[[帕露西]]:恋人 && CFLAG:[[帕露西]]:生解禁 == 1) && ESTRUS_CYCLE([[帕露西]]) == 150
        PRINTFORML [  4] - 我想和你生个孩子
    ELSEIF CFLAG:[[帕露西]]:生解禁 == 1
        SETCOLOR C_L_GRAY
        PRINTFORML [  4] - ????? (成为帕露西的恋人，获得无保护的非危险期的插入同意，并等到她的危险日)
        RESETCOLOR
    ELSEIF !CFLAG:[[帕露西]]:生解禁 && (TALENT:[[帕露西]]:恋人 || TALENT:[[帕露西]]:愛欲 || TALENT:[[帕露西]]:セフレ)
        PRINTFORML [  4] - 我希望在非危险期进行无保护插入行为
    ELSE
        SETCOLOR C_L_GRAY
        PRINTFORML [  4] - ????? (成为帕露西的恋人或炮友，并等待至非危险日)
        RESETCOLOR
    ENDIF
    PRINTFORML [999] - 算了吧
    INPUT
    SELECTCASE RESULT
        CASE 999
            CALL M_KOJO_EGG_COLOR_K60
            TIME += 5
            RETURN 1
        CASE 1
            IF !TALENT:MASTER:恋人
                CONTINUE
            ELSE
                CALL K60_HashihimeServices_Breakup(LOVERPEP, UnwantedLover)
                SIF RESULT == -2
                    RETURN -1
            ENDIF
        CASE 2
            IF ABL:[[帕露西]]:親密 < 5
                CONTINUE
            ELSE
                CALL K60_HashihimeServices_ParseeChanDoesNotShutUp()
            ENDIF
        CASE 3
            IF !K60_FUCKED_PARSEE()
                CONTINUE
            ELSE
                CALL M_KOJO_EGG_COLOR_K60
                CALL K60_STYLE_PUBIC_HAIR()
                TIME += 5
                RESETCOLOR
            ENDIF
        CASE 4
            IF K60_YOU_ARE_CHEATING_ON_ONE_OF_PARSEES_FRIENDS()
                CONTINUE
            ELSEIF CFLAG:[[帕露西]]:生解禁 == 2
                CONTINUE
            ELSEIF (TALENT:[[帕露西]]:恋人 && CFLAG:[[帕露西]]:生解禁 == 1) && ESTRUS_CYCLE([[帕露西]]) == 150
                CALL OGAMITAOSHI([[帕露西]])
            ELSEIF CFLAG:[[帕露西]]:生解禁 == 1
                CONTINUE
            ELSEIF !CFLAG:[[帕露西]]:生解禁 && (TALENT:[[帕露西]]:恋人 || TALENT:[[帕露西]]:愛欲 || TALENT:[[帕露西]]:セフレ)
                CALL OGAMITAOSHI([[帕露西]])
            ELSE
                CONTINUE
            ENDIF
    ENDSELECT
LOOP 1

@K60_HashihimeServices_CalculateLOVERPEP()
#DIM UnwantedLover
#DIM TMP
#DIM DYNAMIC LOVERPEP
;Copied from MORELOVER.ERB
;TODO: Refactor that file to split its functionality into separate functions and call them instead

FOR TMP,1,CHARANUM
    IF TALENT:TMP:7 > 0
        LOVERPEP ++
        UnwantedLover = TMP
    ENDIF
NEXT
DEBUGPRINTFORML LOVERPEP is {LOVERPEP}
RETURN LOVERPEP, UnwantedLover, TMP

@K60_HashihimeServices_Breakup(LOVERPEP, UnwantedLover)
#DIM BreakupPrice
#DIM UnwantedLover
#DIM TMP
#DIM DYNAMIC LOVERPEP
#DIM nLineCnt_Head
CALL M_KOJO_EGG_COLOR_K60
IF FIRSTTIME("ParseeBreakup")
    nParseeLoversBrokenUpWith ++
    IF LOVERPEP == 1
        IF UnwantedLover == 60
            GOTO BreakParseesHeart
        ELSEIF GROUPMATCH(UnwantedLover, 83, 84, 64)
            GOTO FriendBreakup
        ENDIF
    ENDIF
    SELECTCASE K60_ATTITUDE()
        CASE 0, 1
            PRINTFORMW 「我知道这听起来会很奇怪，作为一个桥姬来说…」
            PRINTFORMW 「但…你确实也让我改变了不少」
            PRINTFORMW 「你真的真的确定要这么做吗？没有办法了吗？不能再和%HE_SHE_CN(UnwantedLover)%商量了吗？毕竟我出手就——」
            PRINTFORMW 「…这样吗？已经确定没法继续维持这段关系了啊」
            PRINTFORMDW 帕露西叹了口气
            PRINTFORMW 「嘛，好吧。起码应该比你们僵在这段尴尬的关系里面互相折磨对方更好吧」
            PRINTFORMW 「钱我还是会照收不误的」
            BreakupPrice = 5000
        CASE 2
            PRINTFORMW 「哈，懦了，不敢直面%HE_SHE_CN(UnwantedLover)%，是这样吧？」
            PRINTFORMDW 帕露西轻轻一笑
            PRINTFORMW 「那也行。但我的服务也不是免费的」
            BreakupPrice = 20000
        CASE 3
            PRINTFORMW 「看来你们两个注定无缘…真遗憾」
            PRINTFORMDW 帕露西摇了摇头
            PRINTFORMW 「嗯，我确实可以解决这个问题…但也会好好收钱的」
            BreakupPrice = 10000
    ENDSELECT
ELSE
    SIF TALENT:TARGET:恋人 && !TALENT:MASTER:左拥右抱
        GOTO BreakParseesHeart
    SELECTCASE K60_ATTITUDE()
        CASE 0, 1
            BreakupPrice = nParseeLoversBrokenUpWith * 5000
        CASE 2
            BreakupPrice = nParseeLoversBrokenUpWith * 20000
        CASE 3
            BreakupPrice = nParseeLoversBrokenUpWith * 10000
    ENDSELECT
ENDIF

$BREAKUP_SELECT
SIF LOVERPEP <= 1
    GOTO BREAKUP
PRINTFORML 「你想和谁断绝关系？」
RESETCOLOR
DRAWLINE
nLineCnt_Head = LINECOUNT
REDRAW 0
FOR TMP,1,CHARANUM
    IF TALENT:TMP:恋人 > 0
        PRINTFORMLC [{TMP, 3}] %CALLNAME:TMP%
    ENDIF
NEXT
PRINTFORML
PRINTFORML [-1] 取消
INPUT
UnwantedLover = RESULT
CALL M_KOJO_EGG_COLOR_K60
$BREAKUP
IF UnwantedLover == -1
    PRINTFORMW 「怎么？突然就没有胆量了吗？」
    RESETCOLOR
    RETURN -1
ELSEIF UnwantedLover < 0 || UnwantedLover >= CHARANUM
    PRINTFORMW 「你说的人是谁？」
    GOTO BREAKUP_SELECT
ELSEIF !TALENT:UnwantedLover:恋人
    PRINTFORMW 「在逗我玩吗？你和%HE_SHE_CN(UnwantedLover)%没在交往啊」
    GOTO BREAKUP_SELECT
ELSEIF UnwantedLover == 0
    PRINTFORMW 「搞什么鬼啊？你是想和自己分手吗？」
    GOTO BREAKUP_SELECT
;uncomment when implmented
[SKIPSTART]
ELSEIF TALENT:UnwantedLover:妻
    IF TALENT:UnwantedLover:妻 == 1
        PRINTFORMW 「你已经和%HE_SHE_CN(UnwantedLover)%订婚了。这种事不归我管，自己好好负起责任吧」
    ELSEIF TALENT:UnwantedLover:妻 == 2
        PRINTFORMW 「俗话说清官都难断家务事，实在不行你去是非曲直亭得了。自己想办法解决吧」
    ENDIF
    RESETCOLOR
    RETURN -1
[SKIPEND]
ELSEIF GROUPMATCH(UnwantedLover, 83, 84, 64)
    $FriendBreakup
    IF UnwantedLover != 64
        PRINTFORMDW 帕露西交叉双臂，斜眼怒视着你
    ELSE
        PRINTFORMDW 帕露西看起来有点矛盾…随后她抱起了双臂，瞪着你
    ENDIF
    CALL PRINT_STRL,@"「你知道…你是在和@ITALIC@我@说，要和@ITALIC@我的朋友，对吧？」"
    PRINTFORMW 「…随你吧，我也会好好做好该做的事，但——我认为我有理由让你多出点钱」
    BreakupPrice *= 2
ELSEIF UnwantedLover == 60
    $BreakParseesHeart
    CALL PRINT_STRL, @"「@ITALIC@什么？！@」"
    IF !CFLAG:[[帕露西]]:K60EN_PARSEE_TIMES_BROKEN_UP_WITH
        PRINTFORMDW 帕露西的脸突然变得煞白，翠绿色的眼瞳如地震般动摇，脸上充斥着惊恐的神色
       PRINTFORMW %HE_SHE_CN(TARGET)%的身体止不住的颤抖，颤颤巍巍的开始说话
        PRINTFORMDW 「你…你…你是认真的吗…？」
        PRINTFORMW 「开玩笑的…吧？对…对吧？」
        CALL ASK_YN("我认真的", "骗你的啦")
        IF RESULT
            PRINTFORMDW 帕露西长长的舒了一口气，简直就像刚刚正处于生死边缘
            CALL PRINT_STRW, @"「感谢神明…我都不敢想。如果…如果连你也开始厌恶我，那我…」"
            RESETCOLOR
            RETURN -1
        ELSE
            PRINTFORMDW 帕露西的萃绿色眼瞳死死的盯着你，她看起来十分希望这只是她做的噩梦
            PRINTFORMDW 当确认你真的想要和她分手时，她深深吸了一口气，试图抑制身体的颤抖，随后浑身颤抖着说出了话
            CALL PRINT_STRW,@"「…我明白了。如果你也开始厌恶我这个——@ITALIC@桥姬@。我也不会…强迫你继续这段扭曲的关系」"
            CALL PRINT_STRW,@"「这次，@ITALIC@免费@」"
            BreakupPrice = 0
            GOTO BreakupRitual
        ENDIF
    ELSE
        PRINTFORMDW 帕露西的目光如刀子般狠狠刺向你
        CALL PRINT_STRW, @"「哈…哈哈…哦哦，等会等会，也就是说你的意思是？在你和我在一起过，然后分手过，接着现在又找我复合了——的这个前提下，@ITALIC@你  他  妈  的@现在又要来找我分手？！！！」"
        PRINTFORMDW 帕露西看起来十分愤怒，甚至开始怒极反笑。你后颈的汗毛都不禁立了起来
        PRINTFORMW 「你觉得这很好笑是吗？还是觉得我就是个负责逗你乐的小丑？！你把我当什么了？嗯？只需要用嘴上随便说说就随叫随用的肉便器？当我是用完就能随便丢弃的物件吗？你就一点都不喜欢我，只在乎我的肉体吗？！！！」
        CALL ASK_YN("不是", "是")
        PRINTFORMDW 帕露西冷冷的笑了
        IF RESULT
            PRINTFORMW 「随你吧，如你所愿，都结束了。反正我也早就受够你了，你，还有任何和你有关的那些破事都是」
        ELSE
            PRINTFORMW 「行，那正合我意。结束了，都结束了。我厌倦了，现在…」
        ENDIF
        FONTITALIC
        SETCOLOR C_RED
        CALL KEYTYPING(@"「@ITALIC@死吧。畜生@」", "W", 200)
        FONTREGULAR
        CALL M_KOJO_EGG_COLOR_K60
        PRINTFORMDW 帕露西飘浮在空中，指尖捏住了一张发光的符卡
        PRINTFORMDW 漫天的弹幕和恶毒的话语一起打向了你，为了保住你的小命，现在还是走为上策最好
        LOCAL = CFLAG:[[帕露西]]:2
        PRINTFORMDW Favor -{LOCAL} (Parsee)
        CFLAG:[[帕露西]]:2 = 0
        LOCAL = CFLAG:[[帕露西]]:4
        PRINTFORMDW Trust -{LOCAL} (Parsee)
        CFLAG:[[帕露西]]:4 = 0
        ABL:[[帕露西]]:従順 = 0
        ABL:[[帕露西]]:親密 = 0
        JUEL:[[帕露西]]:10 = 0
        JUEL:[[帕露西]]:16 = 0
        JUEL:[[帕露西]]:12 = 0
        JUEL:[[帕露西]]:20 = 0
        PRINTFORMDW 顺从度降至LV0…
        PRINTFORMDW 亲密度降低至LV0…
        TALENT:[[帕露西]]:恋人 = 0
        TALENT:[[帕露西]]:恋慕 = 0
        TALENT:[[帕露西]]:セフレ = 0
        TALENT:[[帕露西]]:愛欲 = 0
        TALENT:[[帕露西]]:思慕 = 0
        CALL K60_GET_ANGRY(3)
        IF CFLAG:TARGET:同行
            CFLAG:TARGET:同行 = 0
            CFLAG:MASTER:同行 = 0
        ENDIF
        CALL RUNOFF(60, 0)
        CFLAG:[[帕露西]]:K60EN_PARSEE_TIMES_BROKEN_UP_WITH ++
        RESETCOLOR
        RETURN -2
    ENDIF
ELSEIF INRANGE(RELATION:[[帕露西]]:UnwantedLover, 1, 99)
    SELECTCASE K60_ATTITUDE()
        CASE 0, 1
            PRINTFORMW 「我明白了…」
            PRINTFORMDW 帕露西努力隐藏她松了一口气的表情
        CASE 2, 3
            PRINTFORMW 「嗯，这样最好吧。说实话我也不明白你最开始到底是看上%HE_SHE_CN(UnwantedLover)%哪一点了」
    ENDSELECT
    BreakupPrice *= ((100 - RELATION:[[帕露西]]:UnwantedLover) / 100)
ENDIF
PRINTFORML 「我的收费是%金額表示(BreakupPrice)%」
IF MONEY < BreakupPrice
    PRINTFORML 「…看来似乎付不起呢」
    PRINTFORMW 「等你兜里再富裕点的时候再来吧」
    RESETCOLOR
    RETURN -1
ENDIF
CALL ASK_M("我改主意了", 1, @"和%CALLNAME:UnwantedLover%分手", 1)
SELECTCASE RESULT
    CASE 0
        IF GROUPMATCH(UnwantedLover, 83, 84, 64)
            PRINTFORMDW 帕露西怒视着你
            PRINTFORMW 「你最好有」
        ELSE
            SELECTCASE K60_ATTITUDE()
                CASE 0, 1
                    PRINTFORMW 「那可能最好」
                CASE 2
                    PRINTFORMW 「那你为什么要浪费我的时间？」
                CASE 3
                    PRINTFORMW 「我明白了」
                    PRINTFORMDW 帕露西的表情透露出一丝失落
            ENDSELECT
        ENDIF
        RESETCOLOR
        RETURN -1
    CASE 1
        $BreakupRitual
        IF FIRSTTIME("RitualCutscene")
            PRINTFORMDW 帕露西从口袋里拿出了一对看起来十分不详的剪刀
            PRINTFORMDW 这些剪刀是由一种你从未听说过的奇异银白色金属打造而成的
            PRINTFORMW 「为了保证这个仪式能成功，你需要在心中努力想象你即将成为前任的爱人的形象」
            PRINTFORMW 「努力回忆你们之间的矛盾，小到晚上吃什么大到信任危机都行，这样我做起来也方便点」
            IF UnwantedLover == 60
                PRINTFORMDW 帕露西发出了一声刺骨的冷笑
                CALL PRINT_STRW,@"「哈，不过应该也不需要我提醒你吧？毕竟，想的是关于@ITALIC@我@的事啊」"
            ENDIF
            PRINTFORMW 「闭上眼睛」
            PRINTFORMDW 你闭上眼睛，回想和%CALLNAME:UnwantedLover%一起度过的不愉快的时光
            PRINTFORMDW 那些因意见不合的争吵，惹你生气的时候，所有你不能接受的小毛病…
            PRINTFORMDW 你感觉到\@ IS_MALE(MASTER) ? 小指 # 大拇指\@被什么东西紧紧缠住了，你惊讶地睁开了眼睛
            IF IS_MALE(TARGET)
                LOCALS = 小指
            ELSE
                LOCALS = 拇指
            ENDIF
            PRINTFORMDW %BREAKENG(@"有一根蓝色的线绑在上面。\@ UnwantedLover == 60 ? 它一直延伸向帕露西那边, 另一端也绑在她的指头上。%LOCALS%当你试图看清它时，它不见了 # 它向着远处延伸，在你注视着它时消失了\@")%
            PRINTFORMDW 帕露西叹了口气，你抬头望向她。她的表情显得颇为不悦
            PRINTFORMW 「刚刚和你说的什么？专心啊！你要是不集中注意力，我怎么做这个仪式！」
            IF UnwantedLover == 60
                PRINTFORMW 「…你在犹豫——」
                PRINTFORMDW 帕露西没有继续说下去，闭上眼睛摇了摇头
            ENDIF
            PRINTFORMDW 你再次闭上眼睛，继续回想和%CALLNAME:UnwantedLover%一起度过的不愉快的时光
            PRINTFORMDW 这回，你感觉到线在你的\@ IS_MALE(MASTER) ? 小指 # 拇指\@上越来越紧，你没有因此分心，继续集中注意力
            ;The Ahuna 「在琐罗亚斯德教的婚礼上，人们都会年念诵《Vairya》，在这个仪式中我得逆向念诵」
            PRINTFORMW 「愿你赐予生命，阿胡拉·马兹达…」
            PRINTFORMDW 帕露西正在用一种你不了解的语言念诵着什么
            PRINTFORMDW 线勒的越来越紧，几乎让你手上的血流都停止了，但你咬紧牙，持续将注意力集中于%CALLNAME:UnwantedLover%
            PRINTFORMDW 接着你听到帕露西的剪刀“咔嚓”一声，压力瞬间消失了
            PRINTFORMW 「行了，都结束了」
            PRINTFORMDW 你睁大眼睛，那条蓝色线再次映入眼帘。这次它被切断了，在你的眼前从头部缓缓消散于空中
        ELSE
            PRINTFORMDW 仪式和上次一样进行
        ENDIF
        PRINTFORMW 「一切都结束了。你与\@ UnwantedLover == 60 ? 我 # %CALLNAME:UnwantedLover%\@再也没有任何关系了」
        IF UnwantedLover == 60
            PRINTFORMW 「若你心中还有所愿…就再来看看我吧…」
            PRINTFORMDW 帕露西把脸埋进双手中，泪水顺着指缝滴到了地上，抽泣声回荡于空中…
            CFLAG:[[帕露西]]:K60EN_PARSEE_TIMES_BROKEN_UP_WITH ++
            CALL K60_PARSEE_GOT_HER_HEART_BROKEN
        ELSE
            PRINTFORMW 「如果还想和其他人断绝关系，可以再来找我」
            CALL PAY_MONEY_YEN(BreakupPrice, 2)
        ENDIF
        LOVERPEP --
        IF LOVERPEP == 0
            CLEARBIT CFLAG:MASTER:既成事実, 2
            TALENT:MASTER:7 = 0
        ENDIF
        TALENT:UnwantedLover:7 = 0
        CLEARBIT CFLAG:UnwantedLover:13, 2
        CLEARBIT CFLAG:UnwantedLover:13, 1
        CLEARBIT CFLAG:UnwantedLover:既成事実, 2
        IF GROUPMATCH(UnwantedLover, 83, 84, 64)
            CFLAG:[[帕露西]]:推倒禁止 = CFLAG:[[帕露西]]:K60EN_PARSEE_OLD_NO_PUSHDOWN_VALUE
            CFLAG:[[帕露西]]:既成事実 = CFLAG:[[帕露西]]:K60EN_PARSEE_OLD_CONSENT_VALUE
            CFLAG:[[帕露西]]:ヤラせちゃった = CFLAG:[[帕露西]]:K60EN_PARSEE_OLD_BEEN_PUSHED_DOWN_VALUE
            CFLAG:[[帕露西]]:K60EN_PARSEE_OLD_NO_PUSHDOWN_VALUE = 0
            CFLAG:[[帕露西]]:K60EN_PARSEE_OLD_CONSENT_VALUE = 0
            CFLAG:[[帕露西]]:K60EN_PARSEE_OLD_BEEN_PUSHED_DOWN_VALUE = 0
        ENDIF
        TCVAR:[[帕露西]]:K60ENC_TCVAR_DATE_REACTION = 0
        UnwantedLover = 0
        nParseeLoversBrokenUpWith ++
        TIME += 30
        RESETCOLOR
        RETURN 1
ENDSELECT

;Parsee's longer conversations go here
@K60_HashihimeServices_ParseeChanDoesNotShutUp()
IF K60_ATTITUDE() == 2 || MARK:[[帕露西]]:反発刻印 == 3 || CFLAG:[[帕露西]]:ブチギレ == 1
    PRINTFORMDW 帕露西双臂交叉，怒视着你
    PRINTFORMD 她似乎十分
    IF K60_ATTITUDE() == 2
        PRINTFORMD 傲娇
    ELSE
        PRINTFORMD 生气
    ENDIF
    PRINTFORMDW 正向你展示真实的自己…
    TIME += 5
    RETURN 1
ELSE
    DO
        RESETCOLOR
        DRAWLINE
        SIF !FIRSTTIME("ParseeYamameConvo", 1)
            SETCOLOR C_L_GRAY
        PRINTL [  0]黑谷山女

        RESETCOLOR
        SIF !FIRSTTIME("ParseeKisumeConvo", 1)
            SETCOLOR C_L_GRAY
        PRINTL [  1]琪斯美

        RESETCOLOR
        SIF !FIRSTTIME("ParseeYuugiBackstory", 1)
            SETCOLOR C_L_GRAY
        PRINTL [  2]你是怎么认识勇仪的？
        RESETCOLOR

        SIF !FIRSTTIME("ParseeSatoriPartialBackstory", 1)
            SETCOLOR C_L_GRAY
        PRINTL [  3]古明地觉
        RESETCOLOR

        SIF !FIRSTTIME("ParseeKoishiConvo", 1)
            SETCOLOR C_L_GRAY
        PRINTL [  4]古明地恋
        RESETCOLOR

        SIF !FIRSTTIME("ParseeOkuuBackstory", 1)
            SETCOLOR C_L_GRAY
        PRINTL [  5]灵乌路空
        RESETCOLOR

        SIF !FIRSTTIME("ParseeJoonConvo", 1)
            SETCOLOR C_L_GRAY
        PRINTL [  6]依神女苑
        RESETCOLOR

        SIF !FIRSTTIME("ParseeSeijaConvo", 1)
            SETCOLOR C_L_GRAY
        PRINTL [  7]正邪
        RESETCOLOR

        SIF !FIRSTTIME("ParseeAlternateWorldsConvo", 1)
            SETCOLOR C_L_GRAY
        PRINTL [ 8]深奥的东西
        RESETCOLOR

        SIF !FIRSTTIME("ParseeThoughtsOnHumansConvo", 1)
            SETCOLOR C_L_GRAY
        PRINTL [ 9]你对人类的看法
        RESETCOLOR
    ; PRINTL []How you met Yuugi [todo convo about yuugi that isn't backstory-related]
			;-Rin
			;-Suika
			;-Kanako
			;-Yuuma
			;-Nue
			;-Hatate and Aya (they met in Double Spoiler)
			;-Noroiko
			;-Eiki
			;-Sunny, if Sunny's at Lust (her fall dialogue mentions her running into Parsee while looking for you)
        PRINTL [999]别在意
        INPUT
        LOCAL = RESULT
        CALL M_KOJO_EGG_COLOR_K60
        SELECTCASE LOCAL
            CASE 0
                CALL K60_YAMAME_BACKSTORY()
                TIME += 5
                RESETCOLOR
            CASE 1
                CALL K60_KISUME_CONVO()
                TIME += 5
                RESETCOLOR
            CASE 2
                IF TALENT:[[帕露西]]:恋人
                    CALL K60_YUUGI_BACKSTORY
                    TIME += 5
                ELSE
                    $NOT_CLOSE_ENOUGH
                    PRINTFORMW 「…我不想谈那件事」
                    PRINTFORMDW 看起来你需要先和帕露西变得更亲近，她才会告诉你关于那件事的东西…
                ENDIF
                RESETCOLOR
            CASE 3
                CALL K60_SATORI_PARTIAL_BACKSTORY()
                TIME += 5
                RESETCOLOR
            CASE 4
                CALL K60_KOISHI_CONVO()
                TIME += 5
                RESETCOLOR
            CASE 5
                CALL K60_OKUU_CONVO()
                TIME += 5
                RESETCOLOR
            CASE 6
                CALL K60_JOON_CONVO()
                TIME += 5
                RESETCOLOR
            CASE 7
                CALL K60_SEIJA_CONVO()
                TIME += 5
                RESETCOLOR
            CASE 8
                CALL K60_ALTERNATE_WORLDS_CONVO()
                TIME += 10
                RESETCOLOR
            CASE 9
                CALL K60_THOUGHTS_ON_HUMANS()
                TIME += 5
                RESETCOLOR
            CASE 999
                RESETCOLOR
                TIME += 5
                RETURN
        ENDSELECT
    LOOP 1
ENDIF


