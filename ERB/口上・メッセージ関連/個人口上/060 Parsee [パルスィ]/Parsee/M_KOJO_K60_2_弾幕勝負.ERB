;更新日期：2024年11月3日
;==================================================
;弾幕勝負
;==================================================
@M_KOJO_EGG_MESSAGE_COM_K60_DANMAKU(ARGS, 相手残機, ARG:1)
#DIM 相手残機
;全体の流れ
;①【戦闘前】→②ダイスロールとプレイヤー選択肢、ハンデをつけた場合は【ハンデ口上】→③対応スキルが発動した場合【スキル口上】→④被弾判定、キャラが被弾したなら【被弾口上】→
;どちらかの残機が0になっていなければ②に戻る→⑤【戦闘後口上】

;--------------------------------------------------
;記入チェック（=0, 非表示、1, 表示）
LOCAL = 1
;-------------
IF LOCAL
    SIF FLAG:口上色
        TRYCALLFORM M_KOJO_EGG_COLOR_K{NO:TARGET}
	;-------------------------------------------------
	;小分岐
	;-------------------------------------------------
	;The larger the handicap, the more of a disadvantage the player is at
	;Negative numbers mean the player has an advantage
	;戦闘前
	;HANDICAP_FIXED = 0
	;HANDICAP_RAND = 0
	;ハンデをつけることができ、数値が大きいほど你が不利になる　数値が負なら你が有利に
	;キャラの能力に関わらずクッソ強くしたり弱くしたりできる
	;記入チェック（=0, 非表示、1, 表示）
    LOCAL:1 = 1
	;-------------------------------------------------
	;TODO: balance this
	;Parsee gets stronger based on her jealousy, or if she's stronger than you, the jealousy produced from the difference between your and her combat skill
    IF LOCAL:1 && ARGS == "戦闘前"
        HANDICAP_FIXED = 0
        HANDICAP_RAND = 0
        LOCAL:10 = ABL:MASTER:戦闘能力 - ABL:TARGET:戦闘能力
        IF LOCAL:10 < 0
            LOCAL:10 *= -2
        ELSE
            LOCAL:10 = K60_JEALOUSY()
            SELECTCASE K60_ATTITUDE()
                CASE 0
                    LOCAL:10 /= 2
                CASE 1
                    SIF TALENT:TARGET:恋人
                        LOCAL:10 /= 2
                CASE 2,3
                    LOCAL:10 *= 2
            ENDSELECT
        ENDIF
        HANDICAP_FIXED = LOCAL:10
        HANDICAP_RAND = 100

        IF MARK:TARGET:反発刻印 > 0
            SELECTCASE K60_ATTITUDE()
                CASE 0, 1
                    PRINTFORMW 「弹幕对战？当然可以」
                    PRINTFORMDW 帕露西僵硬地微笑
                    PRINTFORM 「正好啊，我还点事对你
                    SELECTCASE MARK:TARGET:反発刻印
                        CASE 1
                            PRINTFORM 有些反感，
                        CASE 2
                            PRINTFORM 有点生气，
                        CASE 3
                            PRINTFORM 很生气，
                    ENDSELECT
                    PRINTFORMW 呢」
                    PRINTFORMW 「既然言语没有那么容易消除隔阂……我们就用弹幕来解决问题，然后就让这些事随风散去」
                    HANDICAP_FIXED = HANDICAP_FIXED * (2 * MARK:TARGET:反発刻印)
                CASE 2, 3
                    PRINTFORMW 「弹幕对决？可以啊，我会好好地打败你，就为你对我的那些所作所为」
                    PRINTFORMW 「但我可要警告你……我一点都不会留手呢」
                    PRINTFORMDW 帕露西冷冷地笑了笑
                    HANDICAP_FIXED = HANDICAP_FIXED * (3 * MARK:TARGET:反発刻印)
            ENDSELECT
        ELSE
            SELECTCASE K60_ATTITUDE()
			;Loving
                CASE 0
                    PRINTFORML 「弹幕对战？好啊，听起来很有趣呢」
                    PRINTFORMW 「可别以为我喜欢你就会对你手下留情哦！要来了！」
			;Nice, horny
                CASE 1
                    PRINTFORML 「弹幕对战？当然没问题」
                    PRINTFORML 「但是要分出胜负的话果然加点赌注才更有意思，你不觉得吗？」
				;yes, this is a joke about eraTohoReverse
                    PRINTFORML 「我想想……如果我赢了，你就要舔我的脚，如果你赢了，那么你可以——
                    PRINTFORMW 「哈？真的假的，就这么反感舔我的脚吗？真不解风情……」
			;Grumpy tsundere
                CASE 2
                    IF ABL:MASTER:戦闘能力 > ABL:TARGET:戦闘能力
                        $PARSEE_DANMAKU_CHALLENGE_MASTER_IS_STRONGER
                        CALL M_KOJO_EGG_COLOR_K60_JEALOUS
                        PRINTFORMW 「弹幕？你是喜欢欺负比你弱的人吗？」
                        PRINTFORMDW 帕露西对你做出了一丝嘲讽的笑容
                        PRINTFORMW 「好吧，我得赌你出现什么意外情况了……」
                        CALL M_KOJO_EGG_COLOR_K60
                    ELSEIF ABL:MASTER:戦闘能力 < ABL:TARGET:戦闘能力
                        PRINTFORMW 「弹幕？但你比我弱啊」
                        PRINTFORMDW 帕露西对你做出了一丝温怒表情
                        CALL M_KOJO_EGG_COLOR_K60_JEALOUS
                        PRINTFORMW 「尽管如此，你还是选择挑战我啊……我很嫉妒你的勇气呢」
                        CALL M_KOJO_EGG_COLOR_K60
                    ELSE
                        PRINTFORMW 「弹幕？那也行」
                        PRINTFORMW 「嗯……实力好像差不多呢」
                        PRINTFORMW 「祝我们都好运吧」
                        PRINTFORMDW 帕露西对你做出了一丝怒容
                        CALL M_KOJO_EGG_COLOR_K60_JEALOUS
                        PRINTFORMW 「如果你赢了……那请你做个好人，赢了也别大肆嘲讽我还吹嘘自己，好吗？」
                        CALL M_KOJO_EGG_COLOR_K60
                    ENDIF
			;Fake nice
                CASE 3
                    IF ABL:MASTER:戦闘能力 > ABL:TARGET:戦闘能力
                        GOTO PARSEE_DANMAKU_CHALLENGE_MASTER_IS_STRONGER
                    ELSEIF ABL:MASTER:戦闘能力 < ABL:TARGET:戦闘能力
                        PRINTFORMW 「弹幕？但你比我弱啊」
                        PRINTFORMDW 帕露西对你微笑
                        CALL M_KOJO_EGG_COLOR_K60_JEALOUS
                        PRINTFORMW 「尽管如此，还是要挑战我吗……我很嫉妒你的勇气哦，加油吧！」
                        CALL M_KOJO_EGG_COLOR_K60
                    ELSE
                        PRINTFORMW 「弹幕？我接受」
                        PRINTFORMW 「嗯……看起来我们的实力差不多」
                        PRINTFORMW 「祝我们都好运吧！」
                    ENDIF
            ENDSELECT
        ENDIF
        PRINTFORMW
        PRINTFORMDL 帕露西悬浮在空中，%HE_SHE_CN(TARGET)%的绿眼中散发出了闪耀的光芒，还真漂亮啊……
        PRINTFORML
        PRINTDATAW
            DATAFORM 「那就见识一下吧！我的诅咒之力！」
            DATAFORM 「感受一下我作为恐怖妖怪的力量！」
            DATAFORM 「虽然我不是什么大妖怪，战斗方面我可一点都不会马虎！」
            DATAFORM 「可不要太快倒下哦，让我感受更多的乐趣吧！」
			;From LostWord
            DATAFORM 「我如果是你可不会想挨上这发！」
        ENDDATA
        PRINTFORML
        RETURN 0
    ENDIF
	;-------------------------------------------------
	;Handicap (doesn't apply if the character's handicap is 0)
	;ハンデ
	;記入チェック（=0, 非表示、1, 表示）
    LOCAL:1 = 1
	;-------------------------------------------------
    IF LOCAL:1 && ARGS == "ハンデ"
		;ハンデつけなかったら表示されないので無記入でよい
        PRINTFORML
        IF ABL:MASTER:戦闘能力 - ABL:TARGET:戦闘能力 > 0
            SELECTCASE K60_ATTITUDE()
                CASE 0, 1
                    PRINTFORMDL 帕露西盯着你的眼睛，脸上带着微笑
                    PRINTFORMDW %HE_SHE_CN(TARGET,1)%对你的弹幕能力 有点嫉妒呢……
                CASE 2
                    PRINTFORMDL 帕露西与你对视，盯着你瞪了一眼
                    PRINTFORMDW %HE_SHE_CN(TARGET,1)%对你的弹幕能力感到嫉妒……
                CASE 3
                    PRINTFORMDL 帕露西盯着你的眼睛，脸上露出冷笑
                    PRINTFORMDW %HE_SHE_CN(TARGET,1)%对你的弹幕能力感到嫉妒……
            ENDSELECT
        ELSE
            SELECTCASE K60_ATTITUDE()
                CASE 0, 1
                    PRINTFORMDL 帕露西盯着你的眼睛，脸上带着微笑
                CASE 2
                    PRINTFORMDL 帕露西与你对视，盯着你瞪了一眼帕露西与你对视，盯着你瞪了一眼
                CASE 3
                    PRINTFORMDL 帕露西盯着你的眼睛，脸上露出冷笑
            ENDSELECT
            PRINTFORMDW 你对帕露西的弹幕能力有点嫉妒呢……
        ENDIF
        PRINTFORML
        RETURN 0
    ENDIF
	;-------------------------------------------------
	;戦闘中TARGET被弾時口上
	;記入チェック（=0, 非表示、1, 表示）
    LOCAL:1 = 1
	;-------------------------------------------------
    IF LOCAL:1 && ARGS == "被弾" 
		;別に一律で「やられたー」だけでもいいよ
        SELECTCASE 相手残機
			;残り残機3が表示されるのは【洽覧深識】持ちのみ
            CASE 2
                PRINTFORML 「哼……不错嘛」
                SELECTCASE K60_ATTITUDE()
                    CASE 2, 3
                        CALL M_KOJO_EGG_COLOR_K60_JEALOUS
                        PRINTFORML 「我嫉妒你的力量……」
                        CALL M_KOJO_EGG_COLOR_K60
                ENDSELECT
                PRINTFORMW 「这招如何？！」
                IF CFLAG:[[帕露西]]:K60EN_SEEN_GEM == 0
                    PRINTFORMDW ――妒符「Green-Eyed Monster」!!!
                    CFLAG:[[帕露西]]:K60EN_SEEN_GEM = 1
                ELSEIF CFLAG:[[帕露西]]:K60EN_SEEN_TCS == 0
                    PRINTFORMDW ――剪舌麻雀「对谦虚的富者之记恨」!!!
                    CFLAG:[[帕露西]]:K60EN_SEEN_TCS = 1
                ELSE
                    PRINTDATADW
                        DATAFORM ――妒符「Green-Eyed Monster」!!!
                        DATAFORM ――开花爷爷「对华丽的仁者之嫉妒」!!!
                        DATAFORM ――剪舌麻雀「对谦虚的富者之记恨」!!!
                        DATAFORM ――恨符「丑时参拜」!!!
                        DATAFORM ――嫉妒「Jealousy Bomber」!!!
                        DATAFORM ――怨恨念法「积怨返」!!!
						;from The Outsider Who Loved Gensokyo
                        DATAFORM ――「愤怒的一挥」!!!
                        DATAFORM ――「痛恨的归宿」!!!
                    ENDDATA
                ENDIF
            CASE 1
                SELECTCASE K60_ATTITUDE()
                    CASE 0, 1
                        PRINTFORMDW 帕露西嫉妒地看着你，但只持续了一会儿
                        PRINTFORMW 「行吧……既然如此我也必须拿出底牌了！」
                    CASE 2
                        PRINTFORMDW 帕露西咬紧了牙关
                        CALL M_KOJO_EGG_COLOR_K60_JEALOUS
                        PRINTDATADW
                            DATAFORM 「不……我不会输的！尤其是——不能输给你！」
							;From LostWord
                            DATAFORM 「我有太多遗憾了！不能就这样倒下！」
                        ENDDATA
                        CALL M_KOJO_EGG_COLOR_K60
                    CASE 3
                        PRINTFORMDW 帕露西嫉妒地看着你
                        CALL M_KOJO_EGG_COLOR_K60_JEALOUS
                        PRINTFORMW 「好的……我不会再收手了！做好觉悟吧！」
                        CALL M_KOJO_EGG_COLOR_K60
                ENDSELECT
                PRINTFORMW 「这招如何？！」
                IF CFLAG:[[帕露西]]:K60EN_SEEN_GEM == 0
                    PRINTFORMDW ――嫉妒「看不见的绿眼怪兽」!!!
                    CFLAG:[[帕露西]]:K60EN_SEEN_GEM = 1
                ELSEIF CFLAG:[[帕露西]]:K60EN_SEEN_TCS == 0
                    PRINTFORMDW ――剪舌麻雀「大葛笼与小葛笼」!!!
                    CFLAG:[[帕露西]]:K60EN_SEEN_TCS = 1
                ELSE
                    PRINTDATADW
                        DATAFORM ――嫉妒「看不见的绿眼怪兽」!!!
                        DATAFORM ――开花爷爷「小白的灰烬」!!!
                        DATAFORM ――剪舌麻雀「大葛笼与小葛笼」!!!
                        DATAFORM ――恨符「丑时参拜第七日」!!!
                        DATAFORM ――嫉妒「Jealousy Bomber」!!!
                        DATAFORM ――怨恨念法「积怨返」!!!
						;from Touhou LostWord
                        DATAFORM ――髭切改鬼切「髭切的一刀」!!!
						;from The Outsider Who Loved Gensokyo
                        DATAFORM ――「空虚的诅咒」!!!
                        DATAFORM ――「盛怒」!!!
						;from Fantasy Maiden Wars
                        DATAFORM ――「满心妒嫉之公主」!!!
                    ENDDATA
                ENDIF
            CASE 0
                SELECTCASE K60_ATTITUDE()
                    CASE 0, 1
                        PRINTFORMW 「看起来你在弹幕方面和赢得桥姬的心一样在行……」
                    CASE 2
                        CALL M_KOJO_EGG_COLOR_K60_VERY_JEALOUS
                        PRINTFORMW 「早应该知道会这样的……」
                        PRINTFORMDW 帕露西的眼睛燃烧着嫉妒的火焰
                        CALL M_KOJO_EGG_COLOR_K60
                    CASE 3
                        CALL M_KOJO_EGG_COLOR_K60_JEALOUS
                        PRINTFORMW 「你赢了……」
                        CALL M_KOJO_EGG_COLOR_K60
                        PRINTFORMDW 帕露西嫉妒地看着你
                ENDSELECT
        ENDSELECT
    ENDIF

	;-------------------------------------------------
	;After the fight
	;The case statement is for the number of lives the character has remaining
	;戦闘後
	;記入チェック（=0, 非表示、1, 表示）
    LOCAL:1 = 1
	;-------------------------------------------------
    IF LOCAL:1 && ARGS == "戦闘後"
        PRINTFORML
		;Case 4 only applies to 2hus with【Deep Knowledge Of Life】, which Parsee doesn't have
		;Cases 3-1 are for when you lose, Case 0 is for when you win
		;残り残機4が表示されうるのは【洽覧深識】持ちのみ
		;残機が1以上なら你の負け、0の場合は你の勝利


        IF MARK:TARGET:反発刻印 > 0
            IF 相手残機 > 0
                SELECTCASE K60_ATTITUDE()
                    CASE 0, 1
                        PRINTFORMW 「好了，我们扯平了」
                        PRINTFORMDW 帕露西微笑着和你握手
                        CALL K60_LOSE_HATE_MARKS(1)
                    CASE 2, 3
                        PRINTFORMW 「啊……打完一场真舒畅，现在扯平了」
                        PRINTFORMDW 帕露西微笑着和你握手
                        CALL K60_LOSE_HATE_MARKS(0)
                ENDSELECT
            ELSE
                CALL M_KOJO_EGG_COLOR_K60_VERY_JEALOUS
                PRINTFORMW 「全力以赴也没能打败你……」
                CALL M_KOJO_EGG_COLOR_K60
                PRINTFORMDW 帕露西瞪着你
                PRINTFORMW 「还没完，%CALLNAME:MASTER%，如果非要让我说点什么——我现在更烦你了」
                CALL K60_GET_ANGRY(MARK:TARGET:反発刻印 + 1)
            ENDIF
        ELSE
		;Player is stronger or equally strong
            IF ABL:MASTER:戦闘能力 >= ABL:TARGET:戦闘能力
                SELECTCASE 相手残機
                    CASE 3
                        SELECTCASE K60_ATTITUDE()
                            CASE 0
                                PRINTFORMW 「连一点伤都没受……来吧，%K60_MASTER()%！我不是说了别手下留情吗！」
                            CASE 1
                                PRINTFORMW 「连一点伤都没有……难道已经开始疲乏了吗？」
                            CASE 2
                                PRINTFORML 「竟然没受伤……是想通过放水来讨好我吗？」
                                PRINTFORMW 「那你的期望可得落空了」
                            CASE 3
                                PRINTFORML 「这都没受伤……你是分心了？」
                                PRINTFORMW 「还是说运气不好？这种事倒确实会发生……」
                        ENDSELECT
                    CASE 2
                        SELECTCASE K60_ATTITUDE()
						;Loving
                            CASE 0
                                PRINTFORMW 「居然打中了我一次……希望你不是在放水」
						;Nice, horny
                            CASE 1
                                PRINTFORML 「你打中了我一次……被我的魅力分心了吗？」
                                PRINTFORMW 「想把心思从你的身体放到弹幕上是挺难的……」
						;Grumpy tsundere
                            CASE 2
                                PRINTFORML 「你击中我一次是为了让我觉得你没有放水吗？」
                                PRINTFORMW 「我不是傻子，%K60_MASTER()%……但你看起来简直就是这词的代表」
						;Fake nice
                            CASE 3
                                PRINTFORMW 「我居然赢了，看来这回我运气不错」
                                PRINTFORMDW 帕露西向你鞠躬
                        ENDSELECT
                    CASE 1
                        SELECTCASE K60_ATTITUDE()
                            CASE 0, 1
                                PRINTFORML 「我……赢了？」
                                PRINTFORMW 「可能只是运气好吧……但赢了就是赢了，尤其是对上你这么强的人」
                                PRINTFORMDW 帕露西向你鞠了一躬，脸上悄悄露出喜悦的表情
						;Grumpy tsundere
                            CASE 2
                                PRINTFORMW 「让我猜猜，你是不是忘了自己计划要放水，所以慌了？」
                                PRINTFORMDW 帕露西向你微微鞠躬，瞪着你
						;Fake nice
                            CASE 3
                                PRINTFORMW 「打得不错，如果不是运气好就是我输了」
                                PRINTFORMDW 帕露西向你鞠躬，脸上带着一丝微笑
                        ENDSELECT
                    CASE 0
                        SELECTCASE K60_ATTITUDE()
						;Loving
                            CASE 0
                                PRINTFORMDW 帕露西向你鞠躬，但你能看到她脸上有些许怨恨
                                CALL M_KOJO_EGG_COLOR_K60_JEALOUS
                                PRINTFORMW 「%K60_MASTER()%，明天能和我一起训练吗？我想像你一样强大……」
                                CALL M_KOJO_EGG_COLOR_K60
						;Nice, horny
                            CASE 1
                                PRINTFORMDW 帕露西向你鞠躬，但你能看到她脸上有些许怨恨
                                CALL M_KOJO_EGG_COLOR_K60_JEALOUS
                                PRINTFORMW 「虽然我输了，但至少是个不错的锻炼」
                                PRINTFORMW 「想做到和你一样\@ IS_MALE(MASTER) ? 英俊 # 美丽 \@，就得好好保持身材……」
                                CALL M_KOJO_EGG_COLOR_K60
						;Grumpy tsundere
                            CASE 2
                                PRINTFORMDW 帕露西向你微微鞠躬
                                CALL M_KOJO_EGG_COLOR_K60_VERY_JEALOUS
                                PRINTFORMW 「你想在这样碾压我之后继续羞辱我吗？那要不要我舔你的脚啊？」
                                CALL M_KOJO_EGG_COLOR_K60
						;Fake nice
                            CASE 3
                                CALL M_KOJO_EGG_COLOR_K60_VERY_JEALOUS
                                PRINTFORMW 「啊，好吧，我本来也没指望赢……打得不错」
                                CALL M_KOJO_EGG_COLOR_K60
                                PRINTFORMDW 帕露西向你鞠躬，但你能看到她脸上有些怨恨
                        ENDSELECT
                ENDSELECT
            ELSE
                SELECTCASE 相手残機
                    CASE 3
                        SELECTCASE K60_ATTITUDE()
						;Loving
                            CASE 0
                                PRINTFORMDW 帕露西向你鞠躬
                                PRINTFORMW 「别伤心，绝大多数人都做不到精通所有事情，这点我比谁都清楚……」
                                $PARSEE_TRAINING_OFFER
                                PRINTFORMW 「如果你愿意，我们明天也可以练练弹幕」
						;Nice, horny
                            CASE 1
                                PRINTFORMDW 帕露西向你鞠躬
                                PRINTFORMW 「不要难过，能擅长所有领域的人少之又少，这点我太清楚了……」
                                $PARSEE_BETTER_AT_SEX_REMINDER
                                PRINTFORMW 「而且，你的床上功夫可比我好很多呢，这也是一种本领，为这件事挺起胸膛吧！」
                            CASE 2
                                PRINTFORMDL 帕露西冷笑着看着你
                                PRINTFORMDW 你咬紧牙关
                                PRINTFORMDW %BREAKENG(@"你知道她比你强，但你没想到帕露西会和拍死一只蚊子一样轻松地打败你")%
                                IF IS_NONHUMAN(MASTER)
                                    PRINTFORMDL 你在想什么，竟然和她弹幕对决？
                                    PRINTFORMDW 你对弹幕了解的程度还没随便一个妖精多
                                ELSE
                                    PRINTFORMDL 你大概不太清醒，竟然去和妖怪弹幕对决
                                    PRINTFORMDW 你在来幻想乡之前甚至压根不知道什么是弹幕
                                ENDIF
                                PRINTFORMDW 当你凝视那双冷酷的绿色眼睛时，心中的妒火油然而起
                                PRINTFORMDW 你嫉妒她的力量，你嫉妒她的智慧
                                PRINTFORMDW 你嫉妒她的能力，你嫉妒她的容貌，你嫉妒——
                                PRINTFORML
                                PRINTFORMW 「真是美味的嫉妒之情啊，呵呵……多谢款待呢」
                                PRINTFORMDW 帕露西的笑容变深了，她舔了舔嘴唇
                                PRINTFORMW 「还能再强烈点吗？还想多尝尝这滋味呢」
                                PRINTFORMDW 妈的……竟然还被耍了……
                                PRINTFORMDW 哈，随她怎么笑吧，拥有时停能力的你，肯定早晚能让现在得意的她付出代价……
                            CASE 3
                                PRINTFORMDL 帕露西向你鞠躬
                                PRINTFORML 「别伤心，每个人都有自己擅长的事情，你只是现在不太擅长这个而已，我很能理解这种无力感……」
                                PRINTFORMDL 尽管她的话很温柔，你还是感到嫉妒在心中涌动——
                                PRINTFORMDW 等会？怎么回事，你的嫉妒心怎么突然都不见了？
                                PRINTFORML 「真美味……」
                                PRINTFORMDW 帕露西正带着陶醉的表情看着你
                        ENDSELECT

                    CASE 2
                        SELECTCASE K60_ATTITUDE()
				;Loving
                            CASE 0
                                PRINTFORMDW 帕露西向你鞠躬
                                IF IS_NONHUMAN(MASTER)
                                    PRINTFORMW 「居然能击中我一次！按照你现在的实力来说已经非常厉害了！」
                                ELSE
                                    PRINTFORMW 「真厉害！居然能打中我一次，你已经比绝大多数外来人要强了」
                                ENDIF
                                GOTO PARSEE_TRAINING_OFFER
				;Nice, horny
                            CASE 1
                                PRINTFORMDW 帕露西向你鞠躬
                                IF IS_NONHUMAN(MASTER)
                                    PRINTFORMW 「居然能击中我一次！按照你现在的实力来说已经非常厉害了！」
                                ELSE
                                    PRINTFORMW 「真厉害！居然能打中我一次，你已经比绝大多数外来人要强了」
                                ENDIF
                                GOTO PARSEE_BETTER_AT_SEX_REMINDER
				;Grumpy tsundere
                            CASE 2
                                PRINTFORML 「哼……你居然能打到我一次」
                                PRINTFORMW 「还真相当了不起……作为一个人类来说」
                                SIF IS_NONHUMAN(MASTER)
                                    PRINTFORMW 「不过要是你这种人，那也就没什么了不起了」
                                PRINTFORMDW 这家伙……
                                PRINTFORMDW 一想到你的嫉妒只会变成她的食物，就觉得更生气了
				;Fake nice
                            CASE 3
                                PRINTFORML 「哼……你居然打到我一次」
                                IF IS_NONHUMAN(MASTER)
                                    PRINTFORMW 「对于一个没有多少弹幕经验的人来说，很不错了」
                                ELSE
                                    PRINTFORMW 「这比大多数外来者要强多了」
                                ENDIF
                        ENDSELECT
                    CASE 1
                        SELECTCASE K60_ATTITUDE()
                            CASE 0, 1
                                PRINTFORML 「没想到能让我费这么大劲，还真是让我惊喜不断啊」
                                PRINTFORMW 「这让你显得更有魅力，更\@ IS_MALE(MASTER) ? 英俊 # 美丽 \@了呢……」
				;Grumpy tsundere
                            CASE 2
                                CALL M_KOJO_EGG_COLOR_K60_VERY_JEALOUS
                                PRINTFORML 「哼……差点就打赢我了」
                                PRINTFORMW 「任何其他妖怪都能在五秒内做掉你。真嫉妒……」
                                CALL M_KOJO_EGG_COLOR_K60
				;Fake nice
                            CASE 3
                                PRINTFORML 「还真是拼尽全力了啊……」
                                PRINTFORMW 「为何不提议再战一次呢？如果运气到家或许赢得就是你了」
                        ENDSELECT
                    CASE 0
                        SELECTCASE K60_ATTITUDE()
						;Loving
                            CASE 0
                                CALL M_KOJO_EGG_COLOR_K60_JEALOUS
                                PRINTFORMW 「打得好！没想到能躲过我这么多攻击」
                                PRINTFORMW 「动作真快……有点嫉妒」
                                PRINTFORMDW 帕露西伸出手把你轻轻抱住，脑袋埋入你的胸口蹭了蹭
                                CALL M_KOJO_EGG_COLOR_K60
                                PRINTFORMW 「明天要不要和我一起练习弹幕？我想让那些狸猫偷偷……嗯，伤害你」
						;Nice, horny
                            CASE 1
                                CALL M_KOJO_EGG_COLOR_K60_JEALOUS
                                PRINTFORMW 「打得好！没想到你能躲过我这么多攻击」
                                PRINTFORMW 「动作真快……有点嫉妒」
                                PRINTFORMDW 帕露西伸出手把你的脑袋按向自己，给了你一个微酸的吻
                                CALL M_KOJO_EGG_COLOR_K60
                                CALL MUTUAL_KISS
                                PRINTFORMW 「嗯……我想，如果你在弹幕上赢了我……」
                                PRINTFORMW 「那是不是说明今晚的床上我能反将一军呢？」
                                PRINTFORMW 「好像很难，但我还是想试试看哦……」
						;Grumpy tsundere
                            CASE 2
                                CALL M_KOJO_EGG_COLOR_K60_VERY_JEALOUS
                                PRINTFORML 「所以，\@ IS_MALE(MASTER) ? 英俊潇洒 # 惊艳绝伦 \@的挑战者胜利了，是吗？」
                                PRINTFORMW 「你是把对我的嫉妒转化为攻击了？」
                                CALL M_KOJO_EGG_COLOR_K60
						;Fake nice
                            CASE 3
                                CALL M_KOJO_EGG_COLOR_K60_JEALOUS
                                PRINTFORML 「别太得意，我远不是地底——甚至幻想乡最强的妖怪」
                                PRINTFORMW 「不过，尽管你比我弱，却还是战胜了我，真是嫉妒……」
                                CALL M_KOJO_EGG_COLOR_K60
                        ENDSELECT
                ENDSELECT
            ENDIF
        ENDIF

		;Unlock the mischief option if you saw her use GEM
        IF CFLAG:[[帕露西]]:K60EN_SEEN_GEM == 1
            PRINTFORML
            PRINTFORMDW 在战斗中，你看到帕露西召唤了一只看不见的怪物
            PRINTFORMDW 要是你也可以隐身的话，就有很多恶作剧可以做了……
            CFLAG:[[帕露西]]:K60EN_SEEN_GEM = 2
        ENDIF

		;Unlock the clonecest option if you saw her use TCS
        IF CFLAG:[[帕露西]]:K60EN_SEEN_TCS == 1
            PRINTFORML
            PRINTFORMDW 在战斗中，你看到帕露西召唤了一个她自己的克隆体
            PRINTFORMDW 你有点好奇她有没有用这个能力做过其他的……什么事……
            CFLAG:[[帕露西]]:K60EN_SEEN_TCS = 2
        ENDIF

    ENDIF
    RESETCOLOR
ENDIF
RETURN 1
