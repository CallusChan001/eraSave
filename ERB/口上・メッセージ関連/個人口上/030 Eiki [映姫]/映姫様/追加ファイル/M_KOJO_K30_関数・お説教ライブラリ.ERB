
;### 関数 ###################################
;==================================================
;人目があるか判定関数＠掀裙子判定のをまとめただけ
;人目がある =1 無い =0
;-------------------------------------------------
@K30_BE_SEEN()
#FUNCTION
SIF GET_TARGETNUM() > 1 || DATE_HITOGOMI(CFLAG:PLAYER:現在位置) || WITH_MOB()
	RETURNF 1
RETURNF 0 
;==================================================
;挨拶を返す式中関数
;4時～10時前・10時～18時前・それ以外
;-------------------------------------------------
@K30_GREETING_EIKI()
#FUNCTIONS
SELECTCASE TIME
	CASE 240 TO 599
		SIF TALENT:恋人
			RETURNF "早上好"
		RETURNF "早安"
	CASE 600 TO 1079
		RETURNF "你好"
	CASEELSE
		RETURNF "晚上好"
ENDSELECT
;==================================================
;MASTERの恋人がその場に居るかどうか返す式中関数
;=-1 映姫様が恋人 =0 元から恋人無 =1 その場に居ない =2 恋人と同席
;非恋人時の映姫様を面倒くさい女にさせる
;-------------------------------------------------
@K30_FIND_LOVER()
#FUNCTION
RETURNF FIND_LOVER(30) ;custom code
SIF TALENT:MASTER:恋人 == 0
	RETURNF 0
SIF TALENT:30:恋人
	RETURNF -1
SIF GET_TARGETNUM() < 2
	RETURNF 1
FOR LOCAL, 1, GET_TARGETNUM() + 1
	SIF TALENT:(TARGET:LOCAL):恋人 && CFLAG:MASTER:現在位置 == CFLAG:(TARGET:LOCAL):現在位置
		RETURNF 2
NEXT
RETURNF 1
;==================================================
;変Ｔで映姫様の目が死ぬか判別する式中関数
;=0 平気 =1 目が死ぬ 
;換装指令の存在忘れてた
;-------------------------------------------------
@K30_CHECK_HEN_T()
#FUNCTION
SIF DAY % 7 == 5
	RETURNF 1
SIF FLAG:ファッション == 62
	RETURNF 1
SIF CFLAG:30:衣装一時変更 == 62
	RETURNF 1
RETURNF 0
;==================================================
;酔っぱらい度を数字で返す式中関数
;=0 清醒 =1 微醺 =2 酔酒 3=不省人事
;-------------------------------------------------
@K30_DRUNK()
#FUNCTION
SIF BASE:30:酒気 <= 2
	RETURNF 0
SELECTCASE BASE:30:酒気
	;不省人事
	CASE IS > (MAXBASE:30:酒気 / 10) * 8
		RETURNF 3
	;酔酒
	CASE IS > (MAXBASE:30:酒気 / 10) * 5
		RETURNF 2
	;微醺
	CASE IS <= (MAXBASE:30:酒気 / 10) * 5
		RETURNF 1
ENDSELECT
;==================================================
;接吻其他を視線加味して通すか判断する式中関数 
;フレーバーのだから辛めでもいいや
;=0 失敗 =1 成功
;-------------------------------------------------
@K30_TRY_HARASS
#FUNCTION
;状態異常・诶嘿嘿時・招待時は通す
SIF SHIRAHU(30) != 1 || CFLAG:30:诶嘿嘿 || CFLAG:30:招待
	RETURNF 1
;接吻魔がついたら接吻は全通し
SIF TALENT:接吻魔 && SELECTCOM == 312
	RETURNF 1
LOCAL = RAND:110
LOCAL:1 = 25 + GET_SUCCESS_RATE() / 8
SIF LOCAL:1 > 109
	LOCAL:1 = 109
;キャップをかけた後に視線が通ってたら-33%、違ったら+20%
IF K30_BE_SEEN() == 0
	LOCAL:1 += 20
ELSE
	LOCAL:1 -= 33
ENDIF
;反発刻印1につき-20%
LOCAL:1 -= MARK:30:反発刻印 * 20
SIF LOCAL >= LOCAL:1
	RETURNF 0
RETURNF 1
;==================================================
;拾取同房间角色的公式中函数
;借用爱丽丝口上函数并进行了平文化。ERH 的话运行速度会更快更稳定吗
;完全复制不太好，所以除了熟人以外，随机选择并返回 ID。如果谁都没有，则返回 0
;参数为真 - TARGET 内随机，参数为假 - 带有优先顺序（持有[恋人] → 约会对象 → 熟人 → TARGET 内随机）
;映姬大人的熟人有 9 人，小町 > 久侘歌 > 觉 > 赫卡提亚 > 阿求 > 幽幽子 > 紫 > 璎花 > 残无 的顺序（擅自）设定
;-------------------------------------------------
@K30_FIND_AROUND(ARG = 0)
#FUNCTION
#DIM 知り合い = 76, 129, 49, 113, 80, 66, 26, 127, 157
#DIM 総人数
#DIM 抽選
#LOCALSIZE 1
#LOCALSSIZE 1
RETURNF FIND_AROUND(知り合い, ARG) ;custom code
;映姬-爱丽丝的检索函数已经被收录进函数库中标准化，往后各个角色的熟人查找都可以直接提供一个常量数组并跳转即可
VARSET LOCAL
抽選 = 0
総人数 = GET_TARGETNUM() 
;部屋に映姬大人一人なら当然0
SIF 総人数 == 1
	RETURNF 0
IF !ARG
	;如果存在[恋人]，则优先返回 ID @对应多恋人
	IF K30_FIND_LOVER() == 2
		FOR COUNT,1, 総人数 + 1
			SIF (CFLAG:(TARGET:COUNT):現在位置 != CFLAG:MASTER:現在位置) || (CFLAG:(TARGET:COUNT):現在位置 != CFLAG:30:現在位置)
				CONTINUE
			SIF !TALENT:(TARGET:COUNT):恋人 || TARGET:COUNT == 30
				CONTINUE
			LOCAL:抽選 = TARGET:COUNT
			抽選 ++
		NEXT
		SIF 抽選
			RETURNF LOCAL:(RAND(抽選))
	ENDIF
	;FromENから持ってきた
	SIF FLAG:デート相手 && FLAG:デート相手 != 30
		RETURNF FLAG:デート相手
	;知り合い優先度順に现在位置を调べMASTERと同じならIDを返す
	FOR COUNT, 0, VARSIZE("知り合い")
		SIF CFLAG:(知り合い:COUNT):現在位置 == CFLAG:MASTER:現在位置
			RETURNF 知り合い:COUNT
	NEXT
ENDIF
;知り合いがいなければ同室のTARGET_ID全部を拾う
FOR COUNT,1, 総人数 + 1
	;地狱の断頭台withジェロニモ状態を防ぐため口上主はスキップ
	SIF TARGET:COUNT == TARGET:0
		CONTINUE
	;MASTERと映姫様の现在位置にいなければスキップ
	SIF (CFLAG:(TARGET:COUNT):現在位置 != CFLAG:MASTER:現在位置) || (CFLAG:(TARGET:COUNT):現在位置 != CFLAG:30:現在位置)
		CONTINUE
	;配列のカウントは别で取りながら代入
	LOCAL:抽選 = TARGET:COUNT
	抽選 ++
NEXT
;GET_TARGETNUM()>1なのに抽選==0の场合あり、原因不明
SIF 抽選 < 1
	RETURNF 0
;抽選してIDを決定
RETURNF LOCAL:(RAND(抽選))
;==================================================
;映姬大人が他の角色を呼ぶ式中関数
;呼び捨て =小町 久侘歌：敬称 =赫卡提亚「样」
;阿求 =「御阿礼の子」：リリー姊妹 =略さない：他は皆「さん」づけ
;ARG=0でMASTER 条件付けで呼び名を变える。需要なさそうだけどおにいさんにも対应
;引数 =1or2をとると路人角色/MASTER/対象角色无し(ARG=0)が指定されたら「だれか」「他の人」と呼ぶ＠性骚扰が捗る
;引数 =3で种族・属性呼び。CSV读みでTALENT分けより雰囲气出る气がするけど、个别で设定しないからやっぱり变なのはある
;-------------------------------------------------
@K30_C_NAME(ARG, TYPE = 0)
#FUNCTIONS
#DIM TYPE
#DIM VARIANT
#DIMS STR_LIST, 10
VARSET LOCALS
VARSET STR_LIST
VARIANT = 0
SIF TYPE == 3
	GOTO TRIBE
IF TYPE && (ARG == RANDOM_CHARANUM || ARG == 0)
	SIF TYPE == 1
		RETURNF "某人"
	RETURNF "其他人"
ENDIF
SELECTCASE ARG
	;MASTERの呼び名
	CASE 0
		;反発持ち、ブチギレ時はよそよそしい感じに
		SIF CFLAG:30:ブチギレ || MARK:30:反発刻印 > 0
			RETURNF CALLNAME:MASTER + "桑"
		;恋慕無ければ通常形
		SIF !TALENT:30:恋慕
			RETURNF MASTERNAME:30 + "桑"
		;名有り角色周りにいない+恋慕時に設定で固有の呼び方
		SIF GET_TARGETNUM() <= 1 && CSTR:30:80 != ""
			RETURNF CSTR:30:80
		;デート中は固有の呼び名
		SIF CHK_DATENOW(CFLAG:デート中) && CSTR:30:80 != ""
			RETURNF CSTR:30:80
		;それ以外は通常形
		RETURNF MASTERNAME:30 + "桑"
	;リリー姉妹
	CASE 18, 19
		RETURNF @"莉莉%\@ ARG == 18 ? 白 # 黑\@%桑"
	;小町・久侘歌
	CASE 76, 129
		RETURNF CALLNAME:ARG
	;阿求
	CASE 80
		SIF CFLAG:30:現在位置 == CFLAG:80:現在位置
			RETURNF CALLNAME:ARG + "桑"
		RETURNF "御阿礼之子"
	;赫喀提亞
	CASE 113
		RETURNF CALLNAME:ARG + "大人"
	;其他
	CASEELSE
		RETURNF CALLNAME:ARG + "桑"
ENDSELECT
$TRIBE
;琪露諾だけ強烈に違和感あったので直接指定
SIF ARG == 14
	RETURNF "氷精"
;CSTR:10からの抽出はPRINT_STATEの流用。紹介文が無い、'●'で区切られてないなら飛ばす
LOCALS =  %CSVCSTR(ARG, 10)%
IF STRFIND(LOCALS, "●") > -1
	SPLIT LOCALS, "●", STR_LIST
	FOR LOCAL, 0, RESULT
		IF STRFIND(STR_LIST:LOCAL, "種族：") > -1
			LOCALS:1 = %REPLACE(STR_LIST:LOCAL, "種族：", "")%
			LOCALS:1 = %REPLACE(LOCALS:1, "　", "")%
			BREAK
		ENDIF
	NEXT
ENDIF
;'（）'で種族が括られたキャラは個別対応
IF STRFIND(LOCALS:1, "（") > -1
	SPLIT LOCALS:1, "（", LOCALS
	LOCALS:1 = %REPLACE(LOCALS:1, "）", "")%
	LOCALS:1 = %\@ GROUPMATCH(ARG,35,40,104,105) ? %LOCALS:1% # %LOCALS:0% \@%
;'人类''妖怪'はTALENTの方で更に詳しく見る
ELSEIF  LOCALS:1 == "人类"
	VARIANT = 1
	LOCALS:1 = 
ELSEIF LOCALS:1 == "妖怪"
	VARIANT = 2
	LOCALS:1 = 
ENDIF
SIF LOCALS:1 != ""
	RETURNF LOCALS:1
;'人类''妖怪'は追加種族→TALENTで判断
IF VARIANT
	IF TALENT:ARG:GETNUM(TALENT, "追加種族")
		LOCALS:1 = %GET_TRIBENAME(ARG, GETNUM(TALENT, "追加種族"))%
	ELSEIF VARIANT == 1
		LOCALS:1 = %GET_TRIBENAME(ARG, GETNUM(TALENT, "人类"))%
	ELSE
		LOCALS:1 = %GET_TRIBENAME(ARG, GETNUM(TALENT, "妖怪"))%
	ENDIF
;其他はしらみつぶしで拾いに行く
ELSE
	;条件できちんと分けるべきだが手抜き。仕様変わったらちゃんと合わせる
	FOR LOCAL, 0, 7
		IF TALENT:ARG:(190 + (LOCAL))
			LOCALS:1 = %GET_TRIBENAME(ARG, (190 + LOCAL))%
			BREAK
		ENDIF
	NEXT
	;'人类''妖怪'は追加種族を優先させる
	SIF TALENT:ARG:GETNUM(TALENT, "追加種族") && (TALENT:ARG:GETNUM(TALENT, "妖怪") == 1 || TALENT:ARG:GETNUM(TALENT, "人类") == 1)
		LOCALS:1 = %GET_TRIBENAME(ARG, GETNUM(TALENT, "追加種族"))%
ENDIF
RETURNF LOCALS:1
;==================================================
;中出しOKか判別する式中関数
;性交関連口上の準備（書けるかは不明）
;--------------------------------------------------
;=0 中出しOK =1 妊婦さんだからダメ =2 育児中だからダメ =3 合意が無い
;--------------------------------------------------
@K30_CREAMPIE()
#FUNCTION
;口服避孕薬さえ飲めば出すのは安心
SIF TCVAR:30:口服避孕薬
	GOTO PREG
;合意状況を見る
SIF (CFLAG:30:允许无套 == 0 && ESTRUS_CYCLE(30) != 15) || (CFLAG:30:允许无套 == 1 && ESTRUS_CYCLE(30) == 150)
	RETURNF 3
;育児状況を見る
SIF TALENT:30:育児中 && CFLAG:30:出産経過日 < CHILD_就学
	RETURNF 2
$PREG
;母胎の成育を見る
SIF TALENT:30:妊娠 && !INRANGE(CFLAG:妊娠経過日数, 31, 70)
	RETURNF 1
;ここまで抜けたらOK
RETURNF 0
;==================================================
;接吻ハメ実行可能か見る式中関数
;性交関連口上をシチュ固定して埋めやすくする
;--------------------------------------------------
;=0 OK =-1 猫舌だからダメ =-2 ばっちいからダメ
;--------------------------------------------------
@K30_KISS_CANCEL()
#FUNCTION
SIF TALENT:PLAYER:猫舌 && ABL:PLAYER:舌 < 2
	RETURNF -1
SIF TALENT:猫舌 && ABL:舌 < 2
	RETURNF -1
SIF TALENT:PLAYER:汚臭耐性 > 1
	RETURNF 0
SIF STAIN:0 & 污渍_精液 && ABL:PLAYER:精液中毒 < 3
	RETURNF -2
SIF STAIN:0 & 汚渍_肛門
	RETURNF -2
SIF STAIN:0 & 污渍_阴茎 && TALENT:PLAYER:汚臭耐性 < 0
	RETURNF -2
SIF TALENT:PLAYER:汚臭耐性 == -2 && (STAIN:0 & 汚渍_破瓜血 || STAIN:0 & 污渍_愛液)
	RETURNF -2
RETURNF 0
;==================================================
;REDRAW制御とCLEARLINEで直前の文章を消す関数
;擬似的にイベントをスキップする目的で使用
;--------------------------------------------------
@K30_CLEAR_TEXTLINE(ARG = 1)
LOCAL = CURRENTREDRAW()
REDRAW 0
CLEARLINE ARG
REDRAW LOCAL
RETURN
;==================================================
;兒童の口上色を随意決める関数
;名前の文字コードから起こして固定化する
;--------------------------------------------------
@K30_CHILDWORD()
#FUNCTION
SIF !FLAG:口上色
	RETURNF GETDEFCOLOR()
RETURNF GET_CN_WORD_COLOR(STR:(2050 + (ENCODETOUNI(SHOW_CHILDNAME(30)) % 13)))
;==================================================
;映姫様がどれだけヤル気か見る式中関数
;@ENDUREを仕事や身体接觸中に使用为に少し削って持ってきただけ
;本体ではこの値が RAND:300 + 300 より大きいと被逆推に移行
;-------------------------------------------------
@K30_PUSH_DOWN()
#FUNCTION
;素質により判定変化
LOCAL = CFLAG:30:合意判定
LOCAL += (EX:30:Ｃ寸止め + EX:30:Ｖ寸止め + EX:30:Ａ寸止め + EX:30:Ｂ寸止め + EX:30:Ｍ寸止め) * 20
;抱き着くと被逆推率上昇
SIF SELECTCOM == 311
	LOCAL += 30
LOCAL += TALENT:30:性的兴趣 * 20
SIF TALENT:30:処女
	LOCAL -= 50
SIF TALENT:30:恋慕
	LOCAL = LOCAL + 50
SIF TALENT:30:自尊心 > 0
	LOCAL -= 20
SIF CFLAG:30:允许无套 == 2
	LOCAL = LOCAL + 50
SIF CFLAG:30:强行内射
	LOCAL -= 100
SIF TALENT:30:胆量 < 0 || TALENT:30:応答 < 0 || TALENT:30:冷漠
	LOCAL = LOCAL - 30
SIF TALENT:30:羞恥心 > 0
	LOCAL -= 30
SIF TALENT:30:羞恥心 < 0
	LOCAL = LOCAL + 20
SIF TALENT:30:自制心 
	LOCAL = LOCAL - 20
SIF TALENT:30:貞操 < 0 || TALENT:30:小悪魔
	LOCAL = LOCAL + 20
SIF TALENT:30:貞操 > 0 || TALENT:30:難以越過的底線
	LOCAL -= 20
IF !GETBIT(CFLAG:30:既成事實, 1) && TALENT:30:貞操 > 0
	LOCAL -= 30
	SIF !TALENT:30:恋慕 && !TALENT:30:炮友
		LOCAL -= 50
ENDIF
SIF OTOKOGIRAI(30) && !(IsHeat(30) || IsInsenceValid(30))
	LOCAL = LOCAL / 2
SIF TALENT:30:難以越過的底線 && TALENT:30:恋慕
	LOCAL = LOCAL * 12 / 5
SIF MARK:30:反発刻印 == 3
	LOCAL = 0
SELECTCASE CFLAG:30:积攒度
	CASE IS >= 1000
		LOCAL *= 3
	CASE IS >= 800
		LOCAL = LOCAL * 3 / 2
	CASE IS >= 600
		LOCAL = LOCAL * 5 / 4
	CASE IS >= 500
	CASE IS >= 300
		LOCAL /= 2
	CASE IS >= 200
		LOCAL /= 4
	CASE IS >= 100
		LOCAL /= 10
	CASEELSE
		LOCAL = 0
ENDSELECT
;体力に依存して推倒率減少
SELECTCASE BASE:30:体力
	CASE IS < 750
		LOCAL = 0
	CASE IS < 1000
		LOCAL /= 2
	CASE IS < 1500
		LOCAL = LOCAL * 3 / 4
	CASEELSE
ENDSELECT
SELECTCASE BASE:30:気力
	CASE 0
		LOCAL = 0
	CASE IS < 500
		LOCAL /= 2
	CASEELSE
ENDSELECT
;理性に依存して推倒率減少
IF BASE:30:理性 > 800
	LOCAL = LOCAL / 5
ELSEIF BASE:30:理性 > 600
	LOCAL = LOCAL / 2
ENDIF
SELECTCASE BASE:30:情緒
	CASE IS < 200
		LOCAL = 0
	CASE 201 TO 400
		LOCAL /= 4
	CASE 401 TO 600
		LOCAL /= 2
	CASE 601 TO 800
	CASEELSE
		LOCAL = LOCAL * 6 / 5
ENDSELECT
SIF FLAG:甲斐性無
	LOCAL = LOCAL * 2
SIF CFLAG:30:ブチギレ
	LOCAL = 0
RETURNF LOCAL
;==================================================
;日記用に場所を文字列で返す式中関数＠月Map対応
;数値で残すと拠点引っ越しで変わる場合があるので
;ARG:1新增输出翻译文本
;-------------------------------------------------
@K30_DIARY_PLACE(ARG, ARG:1 = 0)
#FUNCTIONS
#DIM 対象Map
#DIMS TEMP_NAME
SIF CFLAG:30:初期位置 == ARG
	RETURNF "自己的房间"
対象Map =  GET_MAPID(ARG)
SELECTCASE 対象Map
	CASE 0
		SELECTCASE MAP_SEARCH_REPLACEMENT_0(ARG)
			CASE 妖精的大樹
				TEMP_NAME = 三妖精の家
			CASE 夢幻遺跡
				TEMP_NAME = 夢幻遺跡
			CASEELSE
				TEMP_NAME = 博麗神社
		ENDSELECT
	CASE 1
		SELECTCASE MAP_SEARCH_REPLACEMENT_1(ARG)
			CASE 神霊廟広場, 神霊廟道場
				TEMP_NAME = 神霊廟
			CASEELSE
				TEMP_NAME = 命蓮寺
		ENDSELECT
	CASE 2
		TEMP_NAME = 人里
	CASE 3
		SELECTCASE MAP_SEARCH_REPLACEMENT_3(ARG)
			CASE 霧の湖, 冰造的小雪屋
				TEMP_NAME = 霧の湖
			CASE 廃洋館
				TEMP_NAME = 騒霊の廃洋館
			CASEELSE
				TEMP_NAME = 紅魔館
		ENDSELECT
	CASE 4
		SELECTCASE MAP_SEARCH_REPLACEMENT_4(ARG)
			CASE 永遠亭, 永遠亭的内院
				TEMP_NAME = 永遠亭
			CASE 無名之丘
				TEMP_NAME = 無名之丘
			CASE 太陽花田
				TEMP_NAME = 太陽花田
			CASE 夢幻館
				TEMP_NAME = 夢幻館
			CASEELSE
				TEMP_NAME = 迷途竹林
		ENDSELECT
	CASE 5
		SELECTCASE MAP_SEARCH_REPLACEMENT_5(ARG)
			CASE 香霖堂
				TEMP_NAME = 香霖堂
			CASE 霧雨魔法店
				TEMP_NAME = 霧雨魔法店
			CASE 人偶的洋館
				TEMP_NAME = 人偶的洋館
			CASE 再思之道
				TEMP_NAME = 再思之道
			CASE 無縁塚
				TEMP_NAME = 無縁塚
			CASEELSE
				TEMP_NAME = 魔法之森
		ENDSELECT
;		SIF TEMP_NAME == "魔理沙の店"
;			TEMP_NAME = 霧雨魔法店
;		SIF TEMP_NAME == "艾伦の店"
;			TEMP_NAME = 魔女の店
	CASE 6
		SELECTCASE MAP_SEARCH_REPLACEMENT_6(ARG)
			CASE 迷途之家
				TEMP_NAME = 八雲家
			CASE 彼岸
				TEMP_NAME = 是非曲直庁
			CASE 白玉楼庭, 白玉楼
				TEMP_NAME = 白玉楼
			CASE 地獄
				TEMP_NAME = 地獄
			CASE 畜生界
				TEMP_NAME = 畜生界
			CASE 霊長園
				TEMP_NAME = 霊長園
			CASEELSE
				TEMP_NAME = 三途之川
		ENDSELECT
	CASE 7
		SELECTCASE MAP_SEARCH_REPLACEMENT_7(ARG)
			CASE 正邪的隠匿処前
				TEMP_NAME = 天邪鬼の住処
			CASE 仙人的宅邸
				TEMP_NAME = 仙人的宅邸
			CASEELSE
				TEMP_NAME = 妖怪之山の山麓
		ENDSELECT
;		SIF TEMP_NAME == "秋姉妹的家"
;			TEMP_NAME = 秋神様の住まい
;		SIF TEMP_NAME == "正邪的隠匿処前"
;			TEMP_NAME = 天邪鬼の住処
	CASE 8
		SELECTCASE MAP_SEARCH_REPLACEMENT_8(ARG)
			CASE 山之湖, 守矢神社境内, 守矢神社本殿
				TEMP_NAME = 守矢神社
			CASE 虹龍洞
				TEMP_NAME = 虹龍洞
			CASE 天界
				TEMP_NAME = 天界
			CASEELSE
				TEMP_NAME = 妖怪之山の山頂
		ENDSELECT
	CASE 9
		SELECTCASE MAP_SEARCH_REPLACEMENT_9(ARG)
			CASE 旧地獄街道, 旧地獄温泉, 情人旅館
				TEMP_NAME = 旧都
			CASE 地霊殿
				TEMP_NAME = 地霊殿
			CASE 灼熱地獄跡
				TEMP_NAME = 旧地獄
			CASEELSE
				TEMP_NAME = 地底
		ENDSELECT
	CASE 10
		SELECTCASE MAP_SEARCH_REPLACEMENT_10(ARG)
			CASE 槐安通路
				TEMP_NAME = 槐安通路
			CASE 月之都, 綿月亭, 玉兎之街
				TEMP_NAME = 月之都
			CASE 桃林～砂浜, 寧靜海
				TEMP_NAME = 月都の郊外
			CASEELSE
				TEMP_NAME = 月の世界
		ENDSELECT
	CASEELSE
		TEMP_NAME = 見知らぬ場所
ENDSELECT
SIF ARG:1
	RETURNF NAME_TR(TEMP_NAME)
RETURNF TEMP_NAME
;==================================================
;日記用データを呼び出す式中関数
;--------------------------------------------------
;ARG: =0 日期  =1 天気 =2 特記事項
;	  =3 名前  =4 場所 =5 別記事項
;--------------------------------------------------
@K30_DIARY_DATA(キー, 種別)
#FUNCTIONS
#DIM 種別
#DIMS キー
#DIMS 戻り値
#DIMS 取得文字列, 4
#DIMS 仮天気, 5 = "晴天", "晴朗", "多云", "阴天", "雨"
VARSET 取得文字列
LOCAL = 0
;読み込むCSTRの選別
IF 種別 > 2
	LOCAL ++
	種別 -= 3
ENDIF
;日記IDをキーとしてCSTRを検索
戻り値 = %DIC_GET(CSTR:30:(81 + LOCAL), キー)%
IF 戻り値 != ""
	;情報種別ごとに分割
	SPLIT 戻り値, "/", 取得文字列
	;取得値なければフォローへ移動
	SIF 取得文字列:種別 == ""
		GOTO FOLLOW
	;引数に応じて文字列を返す
	RETURNF 取得文字列:種別
;VerUP,不具合等で戻り値が空だった場合のフォロー
ELSE
	$FOLLOW
	SIF LOCAL
		種別 += 3
	SELECTCASE 種別
		CASE 0
			RETURNF "1"
		CASE 1
			LOCAL = TOINT(キー) % 5
			RETURNF 仮天気:LOCAL
		CASE 2, 5
			RETURNF ""
		CASE 3
			RETURNF MASTERNAME:30
		CASE 4
			RETURNF "那个地方"
	ENDSELECT
ENDIF
;==================================================
;日記に書き込む関数。第2引数を取ると未公開日記(DIARY:30:XX = 3)
;同時に日期等をCSTR:81,CSTR:82に記録。日記IDをキーとして使える@DIC_SETを拝借
;連想配列とかちゃんと意味わかってないけど、なんか使えて動いてるからこれでよし
;-------------------------------------------------
@K30_WRITE_DIARY(日記ID, ARG = 0)
#DIM 日記ID
#DIMS 日時
#DIMS 場所
#DIMS 天気
#DIMS 名前
#DIMS 特記
#DIMS 別記
#DIMS キー
#DIMS 書き込み値, 2
#DIMS 仮天気, 5 = "晴天", "晴朗", "多云", "阴天", "雨"
VARSET LOCALS
特記 =
別記 =
;残す記録を変換
キー = %TOSTR(日記ID)%
日時 = %TOSTR(DAY:0)%
場所 = %K30_DIARY_PLACE(CFLAG:30:現在位置)%
;異常気象は一部言い換え
SELECTCASE FLAG:異常気象
	CASE 1 TO 6
		天気 = %仮天気:(RAND:4)%　%\@ DAY:2 == 2 ? 黏糊糊地闷热 # 异常温暖 \@%
	CASE 7, 8
		天気 = %仮天気:(RAND:4)%　%\@ FLAG:異常気象 == 7 ? 内衣飞起来了？ # 风异常地大 \@%
	CASEELSE
		天気 = %GET_WEATHER()%
ENDSELECT
;UPDATEからなら天気は随机
SIF TFLAG:100 == 0
	天気 = %仮天気:(RAND:5)%
;名前はMASTERNAMEor恋慕後の愛称。陥落無しなら「さん」付け。変遷があると日記感がでる…かもしれない
名前 = %\@ CSTR:30:80 != "" ? %CSTR:30:80% # %MASTERNAME:30% \@%%\@ TALENT:30:思慕 || TALENT:30:恋慕 ? # 桑 \@%
;反発３で名前を呼んで是けないあの人扱い
名前 = %\@ MARK:反発刻印 == 3 ? 那个人 # %名前% \@%
;页ごとの特記事項
SELECTCASE 日記ID
	;接吻：色々選別
	CASE 1
		;「と思っているが、実は」の場合、前の段落だけ拾って無自覚を弾く
		IF STRFIND(CSTR:30:初吻喪失履歴, "￥ＢＲ") > -1
			SPLIT CSTR:30:初吻喪失履歴, "￥ＢＲ", LOCALS
		ELSE
			LOCALS:0 = %CSTR:30:初吻喪失履歴%
		ENDIF
		;强行だと日記書かない
		IF STRCOUNT(LOCALS:0, "强行") 
			SETBIT CFLAG:30:1910, 2
			RETURN
		ELSE
			;UPDATEからなら場所をぼかす
			SIF TFLAG:100 == 0
				場所 = 重要的地方
			SIF TALENT:30:恋慕
				特記 = 恋
			SIF STRCOUNT(LOCALS:0, "献上了") || GETBIT(CFLAG:30:1910, 6)
				別記 = 捧
		ENDIF
	;処女喪失：色々選別
	CASE 2
		;無意識と你以外で破瓜は以後スルー
		IF STRCOUNT(CSTR:30:処女喪失履歴, "时间停止中|醉|沉睡|不满") 
			SETBIT CFLAG:30:1910, 3
			RETURN
		ENDIF
		;破瓜口上用意してないせいでかなりガバガバだけど3段階で選別
		日時 = %TOSTR(CFLAG:30:処女喪失記念日)%
		IF TALENT:30:恋慕 && !STRCOUNT(CSTR:30:処女喪失履歴, "霸王硬上弓|振动棒") 
			特記 = 恋
		ELSEIF TALENT:30:思慕 || TALENT:30:恋慕
			特記 = 思
		ELSE
			特記 = 无
		ENDIF
	;お小言もらった：理由と説教の長さ
	CASE 5
		特記 = %TOSTR(TFLAG:193)%
	;お説教たいむ：小町のサボり現場
	CASE 8
		場所 = %K30_DIARY_PLACE(CFLAG:MASTER:現在位置)%
		SIF 場所 == "三途之川"
			場所 = 稍微不同的地方
	;ぶん殴られた：耐えたor倒れた
	CASE 10
		SIF BASE:MASTER:体力 <= 500
			特記 = 倒
	;贈り物(GOOD)：贈り物フルネーム、恋慕の有無
	CASE 12
		CALL GIFTNAME(TCVAR:30:今天的礼物)
		特記 = %RESULTS%
		別記 = %\@ TALENT:恋慕 ? 恋 # \@%
	;贈り物(NORMAL&BAD)：贈り物本体名
	CASE 13, 14
		CALL GIFTDATA_MAIN(GET_GIFTDATA(TCVAR:30:今天的礼物, "本体"))
		特記 = %RESULTS%
	;掀裙子：是てるぱんつと反応
	CASE 15
		特記 = %PANTSNAME(EQUIP:下半身内衣２, 30)%
		SIF 特記 == "没穿内裤"
			特記 = 没穿内裤
		SELECTCASE TFLAG:193
			CASE IS < 0
				別記 = 怒
			CASE IS > 10
				別記 = 喜
			CASEELSE
				別記 = 照
		ENDSELECT
	;説法10回終了：MASTER現住所
	CASE 16
		場所 = %K30_DIARY_PLACE(CFLAG:MASTER:初期位置)%
	;ヘカ様からお手紙：手紙の中身
	CASE 18
		SIF STRCOUNT(CSTR:30:0, "変Ｔ指令")
			特記 = 变Ｔ
	;何か釣れた：釣ったもの
	CASE 22
		SELECTCASE TFLAG:193
			CASE 1 TO 20, 22 TO 31
				特記 = 鱼
				SIF GROUPMATCH(TFLAG:193, 9, 23, 24, 28, 30, 31)
					特記 = 好大的鱼
			CASE 21
				特記 = 那种怪东西
			CASE 606, 609, 643
				特記 = 素材
			CASE 131, 610
				特記 = 有人丢的垃圾
			CASE 706
				特記 = 青蛙而已
		ENDSELECT
	;宴会のお知らせ：開催要項
	CASE 24
		SELECTCASE FLAG:宴会開催FLAG
			CASE 2
				場所 = 博麗神社
				特記 = 忘年会
			CASE 3
				場所 = 永遠亭
				特記 = 赏月会
			CASE 4
				場所 = 人里
				特記 = 收获祭
		ENDSELECT
	;单薄的本見ちゃった：本の中身、興味の有無
	CASE 25
		特記 = %\@ TFLAG:193 ? 惨 # \@%
		SIF ABL:欲望 > 6 || DIARY:30:31 != 0
			別記 = 有
	;恋慕：呼び名変えたか
	CASE 30
		IF !STRCOUNT(CSTR:30:0, "称呼")
			特記 = 1
		ELSEIF CSTR:30:80 == "" || CSTR:30:80 == MASTERNAME:30
			特記 = 2
		ELSE
			別記 = %MASTERNAME:30%
		ENDIF
	;出産：産まれた子どもの名前
	CASE 34
		特記 = %CHILDNAME:30:(TALENT:30:育児中)%
	;小町と同時：映姫様と小町の陥落状態
	CASE 36
		SELECTCASE K30_FIND_LOVER()
			CASE -1
				特記 = 映姬
			CASE 0
			CASE 1, 2
				SIF TALENT:76:恋人
					特記 = 小町
				;映姬大人优先
				SIF TALENT:30:恋人
					特記 = 映姬
		ENDSELECT
	;尻穴狂：振動棒約會経験、恋慕の有無
	CASE 37
		特記 = %\@ CFLAG:1608 ? 有 # \@%
		別記 = %\@ TALENT:恋慕 ? 恋 # \@%
	;炮友：取得手段、契約時の分岐
	CASE 39
		IF COM_STR == "K30_契約" && TFLAG:193
			特記 = 契约
			SELECTCASE TFLAG:193
				CASE 3
					別記 = 前世
				CASE 2
					別記 = Ａ调教
				CASE 1
					別記 = 无意识
			ENDSELECT
		ELSE
			特記 = 素质
		ENDIF
ENDSELECT
IF ARG == 0
	DIARY:30:日記ID = 2
ELSE
	DIARY:30:日記ID = 3
	CALL CHARA_DIARY_PAGESETTING(30, 日記ID)
ENDIF
書き込み値:0 = %日時%/%天気%/%特記%
書き込み値:1 = %名前%/%場所%/%別記%
CSTR:30:81 = %DIC_SET(CSTR:30:81, キー, 書き込み値:0)%
CSTR:30:82 = %DIC_SET(CSTR:30:82, キー, 書き込み値:1)%
RETURN
;==================================================
;地の文改変時につじつま合わせる関数
;-------------------------------------------------
@K30_COMPENSATE_MESSAGE
SIF CFLAG:MASTER:诶嘿嘿 != 2 && SELECTCOM != 461
	CALL Counter呼び出し処理
CALL SET_KOJO_COLOR(30)
RETURN 1
;==================================================
;白い線を引く。ただそれだけの関数
;-------------------------------------------------
@K30_D_LINE
RESETCOLOR
DRAWLINE
CALL SET_KOJO_COLOR(30)
RETURN RESULT
;==================================================
;诶嘿嘿入る時の準備関数
;絶頂回数等リセット,掌握把柄保存,
;さらに意識がある場合、前日行動反映をリセット・诶嘿嘿回数/時間記録・映姫様自室なら描写用フラグ立て
;-------------------------------------------------
@K30_PRESET_UFUFU
VARSET TCVAR:30:0, 0, 389, 399
TCVAR:30:381 = CFLAG:掌握把柄
IF SHIRAHU(30)
	CFLAG:1603 = 0
	TCVAR:30:380 ++
	TCVAR:30:383 = TIME:1
	SIF GROUPMATCH(CFLAG:MASTER:現在位置, CFLAG:30:初期位置, OMANEKIBEYA())
		TCVAR:30:378 = 1
ENDIF
RETURN RESULT
;==================================================
;诶嘿嘿終了の準備関数
;各種フラグ掃除
;意識あり合意ありなら終了時刻を記録,
;あなたがコマンドから終了させた場合、媚薬モード待機フラグ残す
;-------------------------------------------------
@K30_UFUFU_RECORD(ARG = 0)
SIF TCVAR:30:383 && !MAXARRAY(TCVAR:30:0, 385, 388) 
	CFLAG:30:1504 = TIME:1
CFLAG:30:風呂 = MIN(CFLAG:30:風呂 + (SUMARRAY(TCVAR:30:0, 390, 395) * 150), 2500)
CFLAG:30:延迟 += 15
VARSET TCVAR:30:0, 0, 383, 388
TCVAR:30:376 = 0
SIF !ARG
	CLEARBIT TCVAR:30:367, 2
RETURN RESULT
;==================================================
;TARGET外のまま複数人诶嘿嘿に入った場合のフォロー関数
;ひと指令おいて突然口調が変わるかもだが、そのままよりはマシなはず
;-------------------------------------------------
@K30_FOLLOW_UP
CALL K30_PRESET_UFUFU
SIF CFLAG:1801 == 0
	RETURN
IF !TALENT:恋慕
	TCVAR:30:385 = 1
ELSEIF K30_FIND_LOVER() == 2 && !GROUPMATCH(K30_FIND_AROUND(), 76, 129)
	TCVAR:30:385 = 1
	TCVAR:30:387 = 1
ENDIF
;==================================================
;画面を揺らす関数。再配布ＯＫなので流用
;ここから引用
;≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡
;「ぱにめーしょん」0527版　By ぱ。
;Emuera専用汎用、アニメーション・ビジュアル拡張関数ファイル。ほぼバリアント間互換あり
;無断再配布ＯＫ、その際改変・追記は元の機能を損なわない範囲でお願いします
;≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡
;-------------------------------------------------
;振動エフェクト関数@QUAKE
;	引数0：振動数。省略すると2
;
;画面を上下に揺らします。揺れ幅はある程度随机です
;-------------------------------------------------
@K30_QUAKE(ARG = 2)
#LOCALSIZE 3
;custom code, promoted to global function
CALL QUAKE(ARG)
RETURN RESULT

;表示オフだと戻る
;SIF !ANIME_CONFIG("QUAKE")
;	RETURN RESULT
; REDRAW 0
; FOR LOCAL, 0, ARG
; 	LOCAL:1 = RAND:4+1
; 	FOR LOCAL:2, 0, LOCAL:1
; 		PRINTL 
; 	NEXT
; 	TWAIT 100, 0
; 	CLEARLINE LOCAL:1
; 	TWAIT 100, 0
; NEXT
; REDRAW 3
RETURN RESULT
;引用ここまで
;==================================================
;主人公の呼び名を変更する関数
;スレで話題にあがってたから、なんとなく入れてみる
;構文は命OD勢の口上からほぼそのまま持ってきてます
@K30_SET_C_NAME(ARG)
#DIMS C_NAM
;--------------------------------------------------
;ARG =0:通常の初回入力 =1:恋慕呼び名入力 =2:祈願からの通常呼び名変更 =3:祈願からの恋慕呼び名変更
;--------------------------------------------------
RESETCOLOR
PRINTFORMDL 请输入称呼(请直接输入或按按钮选择)
IF ARG == 1 || ARG == 3
	C_NAM = %MASTERNAME:30%
	SIF ARG == 3 && CSTR:30:80 != ""
		C_NAM = %CSTR:30:80%
	PRINTBUTTON "[叫主人就好]", "主人"
	PRINTL 
	PRINTBUTTON "[叫老公就好]", "老公"
	PRINTL
	PRINTBUTTON @"[果然%C_NAM%就可以了]", @"%C_NAM%"
	PRINTL
ELSE
	C_NAM = %CALLNAME:MASTER%
	PRINTBUTTON @"[%C_NAM%\@ TALENT:30:恋人 ? # 桑 \@该怎么称呼]", @"%C_NAM%"
	PRINTL
ENDIF
$INPUT_LOOP
RESETCOLOR
INPUTS
;行頭行尾の空白文字削除
C_NAM = %RESULTS%
REPLACE C_NAM, "\(\^\\s\+\|\\s\+\$)", ""
CALL SET_KOJO_COLOR(30)
;2文字未満
IF STRLENS(C_NAM) < 2
	SIF STRLENS(RESULTS)
		CLEARLINE 1
	REUSELASTLINE 「…请多珍惜父母给的姓名」
	GOTO INPUT_LOOP
;12文字(全角6文字)超えた
ELSEIF STRLENS(C_NAM) > 12
	CLEARLINE 1
	REUSELASTLINE 「对不起。平日称呼的话，有点太长了…」
	GOTO INPUT_LOOP
;以下その角色の扮演でない時
;同姓同名
ELSEIF FLAG:扮演 != 30 && STRCOUNT(C_NAM, "\(四季\)\*\\s\*映姬")
	CLEARLINE 1
	REUSELASTLINE 「…请不要戏弄我」
	GOTO INPUT_LOOP
;小町or久侘歌
ELSEIF (FLAG:扮演 != 129 && STRCOUNT(C_NAM, "\(庭渡\)\*\\s\*久侘歌")) || (FLAG:扮演 != 76 && STRCOUNT(C_NAM, "\(小野冢\)\*\\s\*小町"))
	CLEARLINE 1
	REUSELASTLINE 「…那个名字很容易混淆，拜托请用别的称呼」
	GOTO INPUT_LOOP
;阿求
ELSEIF (FLAG:扮演 != 80 && STRCOUNT(C_NAM, "\(稗田\)\*\\s\*阿求"))
	CLEARLINE 1
	REUSELASTLINE 「…新的御阿礼之子？请不要开奇怪的玩笑」
	GOTO INPUT_LOOP
;赫喀提亞
ELSEIF (FLAG:扮演 != 113 && STRCOUNT(C_NAM, "赫喀提亚"))
	CLEARLINE 1
	REUSELASTLINE 「…戏言就到此为止吧。失礼也要有个限度」
	GOTO INPUT_LOOP
ENDIF
;変えるって言ったのに呼び名変えてなかったら返回
SIF (C_NAM == CSTR:30:80 && ARG == 3) || (C_NAM == MASTERNAME:30 && ARG == 2)
	RETURN -1
;直接入力対応＠響子口上からそのまま拝借。改造したいけど正規表現わかんね
IF MASTERNAME:30 == "" || ARG == 2
	IF STRCOUNT(C_NAM, "\(お\(\(兄\|にい\)\|\(姊\|ねえ\)\)\(桑\|酱\)\)\$")
		CLEARLINE 1
		REUSELASTLINE 「我实在不想听恶作剧」
		GOTO INPUT_LOOP
	ELSEIF STRCOUNT(C_NAM, "\(お\(\(兄\|にい\)\|\(姊\|ねえ\)\)\(大人\|さま\)\)\$")
		CLEARLINE 1
		REUSELASTLINE 「我实在不想听恶作剧」
		GOTO INPUT_LOOP
	ELSEIF STRCOUNT(C_NAM, "\(\(御\|ご\)\(主人\|しゅじん\)\(大人\|さま\)\)$")
		CLEARLINE 1
		IF TALENT:恋慕
			REUSELASTLINE 「那个，在别人面前那样称呼……真羞人…」
		ELSE
			REUSELASTLINE 「我实在不想听恶作剧」
		ENDIF
		GOTO INPUT_LOOP
	ELSEIF STRCOUNT(C_NAM, "\(\(旦那\|だんな\)\(大人\|さま\)\)\$")
		CLEARLINE 1
		IF TALENT:恋慕
			REUSELASTLINE 「那个，在别人面前那样称呼……真羞人…」
		ELSE
			REUSELASTLINE 「我实在不想听恶作剧」
		ENDIF
		GOTO INPUT_LOOP
	ENDIF
ENDIF
IF ARG == 1
	PRINTFORML 「%C_NAM%…那个、我想这样叫你，可以吗？」
ELSEIF ARG == 3
	PRINTFORML 「%C_NAM%…这、这样称呼就好了吧？」
ELSE
	PRINTFORML 「%C_NAM%\@ TALENT:30:恋人 ? # 桑 \@、是吧」
ENDIF
RESETCOLOR
CALL ASK_YN
IF !RESULT
	IF MASTERNAME:30 == "" || ARG == 2
		MASTERNAME:30 = %C_NAM%
	ELSE
		CSTR:30:80 = %C_NAM%
	ENDIF
ELSE
	PRINTFORMDL 请输入称呼
	GOTO INPUT_LOOP
ENDIF
CALL SET_KOJO_COLOR(30)
RETURN 1
;==================================================
;映姫様からぱんつを受け取る関数
;@PANTS_GETは隙間使用強制だったので別関数化
;-------------------------------------------------
@K30_PANTS_GETTING
TCVAR:30:377 = EQUIP:30:下半身内衣２
CFLAG:30:没穿内裤 = 2
EQUIP:30:下半身内衣２ = 0
CALL CLOTHES_SETTING_TRAIN(30)
; SIF CFLAG:30:睡衣 == 1
; 	CFLAG:30:睡衣胖次 = 0
CALLF ONCE("ぱんつなし")
CALLF ONCE("ぱんつ預かり")
RETURN 1
;==================================================
;顔絵付きメッセージをエフェクト込みで表示する関数
;パッチと競合したのでひとまず分けてみる
;-------------------------------------------------
@K30_SPTALK, 表情="", MESSAGE
#DIM エフェクト数
#DIM 行数
#DIMS 色
#DIMS 表情
#DIMS MESSAGE
#DIMS 服装
#DIMS DYNAMIC MESSAGE_PRINT,50
#LOCALSIZE 16
#LOCALSSIZE 5
服装 =
VARSET MESSAGE_PRINT
LOCAL:9 = CFLAG:30:差し替え適用
LOCAL:8 = GETCOLOR()
SIF CFLAG:30:差し替え適用 != 1 ;set alt portrait for the duration of this function for correct display
	CFLAG:30:差し替え適用 = 1
IF 顔絵付きメッセージ == 1
	TRYCALLFORM IMAGE_EX_I30_PREリソースチェック(30, "顔絵", "服", "自慰") ;custom code
	色 = 0%TOSTR(CFLAG:1501, "X")% 
	CALL GET_BASE_RESOURCE(30, "顔絵", 服装, 表情)
	SIF FLAG:多尺寸
		默认角色画像横幅 = RM_リソースの高さ(RESULTS)
	CALL 画像セット(RESULTS, 0, 0, 角色画像表示尺寸比, 0, 1, "100", CALLNAME:30)
	CALL GET_EFFECT_RESOURCE(30)
	エフェクト数 = RESULT
	FOR LOCAL, 0, エフェクト数
		CALL 画像セット(RESULTS:LOCAL, 0, 0, 角色画像表示尺寸比, LOCAL + 1)
	NEXT
	MESSAGE = %SPTALK整形(MESSAGE)%
	SPLIT MESSAGE, "/", MESSAGE_PRINT
	行数 = RESULT
	FOR LOCAL, 0, 行数
		SIF エフェクト数 - LOCAL < 0
			CALL ダミーセット(0, 0, 角色画像表示尺寸比, LOCAL)
		TEMP_HTML:LOCAL += @"<font color='#%色%'>%MESSAGE_PRINT:LOCAL%</font>"
	NEXT
	CALL 画像一斉表示(0, 0, 1, -1)
	PRINTL
ELSE
	;custom code
	SETCOLOR CFLAG:30:1501
	;顔絵メッセージOFF時は通常通りに表示させる
	SPLIT MESSAGE, "/", MESSAGE_PRINT
	FOR LOCAL, 0, RESULT
		SIF MESSAGE_PRINT:LOCAL != ""
			PRINTFORML %MESSAGE_PRINT:LOCAL%
	NEXT
ENDIF
;custom code
CFLAG:30:差し替え適用 = LOCAL:9
SETCOLOR LOCAL:8
;==================================================
;映姫様の口交をアニメーションさせる関数
;髪型とかめちゃくちゃなんであんまり映姫様に見えない
;--------------------------------------------------
;ARG	0=通常 1=エンドレス 2=オート:関数内で判定, 3= 顔射:指定時のみ 4=射精:関数内で描写判定
;		5~8 バキュームフェラ个別
;--------------------------------------------------
;PAT		強化ごっくんアニメパターン
;G_TIME		強化ごっくんのウエイトタイム
;S_TIME		顔射のアニメウエイトタイム
;SUCCESS	ごっくん成功判定
;CLEAN		清洁口交判定
;BLOCK		演出弱化フラグ、非陥落時や强行時
;FAVOR		演出強化フラグ、恋慕+高欲情+高精液中毒
;--------------------------------------------------
@K30_BLOW_JOB_ANIME(ARG)
#DIM PAT = 1, 2, 3, 4, 3, 2, 3, 4, 3, 2, 3, 4, 3, 2, 3, 4, 5 
#DIM G_TIME = 1400, 750, 450, 350, 350, 250, 250, 250, 250, 250, 250, 250, 250, 450, 550, 750, 1200
#DIM S_TIME = 300, 900, 1000, 1200, 1000
#DIM WAIT_T
#DIM LOOP_C
#DIM SUCCESS
#DIM SIZE
#DIM BLOCK
#DIM FAVOR
#DIM CLEAN
#DIM TEMP
#DIMS HTML_FORM
SIF !FLAG:画像表示 || !FLAG:挿絵表示 || !GETBIT(CFLAG:1500, 0)
	RETURN

VARSET LOCAL
;アニメ画像尺寸は顔絵表示に準拠
SIZE = K30_SET_IMAGESIZE()
BLOCK = TCVAR:30:385 || TCVAR:30:强行 || !(TALENT:恋慕 || TALENT:炮友 || TALENT:愛欲)
TEMP = CURRENTREDRAW()
REDRAW 0
IF !FLAG:アニメーション
	SELECTCASE ARG
		CASE 0 TO 2
		CALL PRINT_IMAGE("30_blow5", SIZE, "left")
		CASE 3, 4
			GOTO SKIP2
		CASE 5 TO 8
			LOCALS '= "success1", "success5", "dara5", "success3"
			CALL PRINT_IMAGE(@"30_%LOCALS:(ARG -5)%", SIZE, "left")
	ENDSELECT
		REDRAW TEMP
		RETURN
ENDIF
SELECTCASE ARG
	;通常
	CASE 0, 1, 2
		CLEAN = GROUPMATCH(TCVAR:MASTER:112, 1, 2) && !TCVAR:21 && TALENT:恋慕
		LOOP_C = 3 + (NOWEX:MASTER:射精 > 0) - BLOCK + ((TCVAR:30:357 !=0) * 5) + CLEAN
		;前ターン咥えて無ければひと舐め
		IF MASTER_POSE(SET_M, SET_P, 1) != 30 && TALENT:恋慕 && TCVAR:30:357 < 2
			CALL PRINT_IMAGE("30_start", SIZE, "left")
			TWAIT 800, 0
			CLEARLINE SIZE / 100
		ENDIF
		;通常パターン
		IF ARG == 0 || (ARG == 2 && NOWEX:MASTER:射精) || CLEAN
			WAIT_T = 100 - (NOWEX:MASTER:射精 > 0) * 25
			FOR COUNT:1 ,0, LOOP_C
				FOR COUNT, 1, 7
					CALL PRINT_IMAGE(@"30_blow{COUNT}", SIZE, "left")
					TWAIT WAIT_T, 0
					CLEARLINE SIZE / 100
					SIF GETKEY(1) && COUNT:1
						GOTO SKIP
				NEXT
			NEXT
			IF !CLEAN
				FOR COUNT, 1, 4
					CALL PRINT_IMAGE(@"30_blow{COUNT}", SIZE, "left")
					TWAIT WAIT_T, 0
					CLEARLINE SIZE / 100
					SIF GETKEY(1)
						BREAK
				NEXT
			ENDIF
			$SKIP
			IF CLEAN
				;清洁口交で口を離す描写ある(=Counter発生しない)パターン
				CALL PRINT_IMAGE(@"30_shower1", SIZE, "left")
			ELSE
				;射精直前パターン
				CALL PRINT_IMAGE(@"30_blow{COUNT}", SIZE, "left")
			ENDIF
		;エンドレスパターン
		;Emuera本体にあるアニメスプライト機能使用、ログ流しても咥えっぱ無にするテスト
		ELSE		
			;不能使用原有的循环动画，否则动画将在历史履历里堆积
			CALL PRINT_ANIME,"30_おしゃぶり映姫様"
			;HTML_FORM = <img src='30_おしゃぶり映姫様' height='{SIZE}' width='{SIZE}'>
			;HTML_PRINT HTML_FORM
			;FOR LOOP_C, 0, (SIZE / 100)
			;	PRINTFORML
			;NEXT
			;;描画間隔を制御する命令
			;SETANIMETIMER 150
		ENDIF
	;射精
	CASE 3, 4
		SUCCESS = (EXP_UP(15, 30) == NOWEX:MASTER:射精 * 3) || TFLAG:193 == -999 || TCVAR:30:357
		FAVOR = (TALENT:恋慕 && ABL:精液中毒 > 4 && PALAM:欲情 > PALAMLV:7 && RAND:3) || TCVAR:30:357
		;スパート
		FOR LOCAL, 0, 4 + (ARG == 4)
			FOR COUNT, 1, 7
				CALL PRINT_IMAGE(@"30_blow{COUNT}", SIZE, "left")
				TWAIT 55, 0
				CLEARLINE SIZE / 100
				SIF GETKEY(1) && LOCAL > 0
					GOTO SKIP2
			NEXT
		NEXT
		;ぶっかけ
		IF ARG == 3
			FOR COUNT, 1, 6
				CALL PRINT_IMAGE(@"30_shower{COUNT}", SIZE, "left")
				TWAIT (S_TIME:(COUNT -1)), 0
				CLEARLINE SIZE / 100
				SIF GETKEY(1) && COUNT > 1
					GOTO SKIP2
			NEXT
			GOTO SKIP2
		ENDIF
		;奥まで飲み込む動作
		FOR COUNT, 1, 5
			CALL PRINT_IMAGE(@"30_blow{COUNT}", SIZE, "left")
			TWAIT 55 + (COUNT == 4) * 100, 0
			CLEARLINE SIZE / 100
			SIF GETKEY(1)
				GOTO SKIP2
		NEXT
		WAIT_T = 1200 - (!SUCCESS * 300)
		FOR COUNT, 7, 8 + !BLOCK
			CALL PRINT_IMAGE(@"30_blow{COUNT}", SIZE, "left")
			TWAIT WAIT_T, 0
			CLEARLINE SIZE / 100
			SIF GETKEY(1)
				GOTO SKIP2
		NEXT
		;陥落ない場合は演出少なめ
		SIF BLOCK
			GOTO SKIP2
		;だらだらこぼす
		IF !SUCCESS && NOWEX:MASTER:射精 > 1 
			FOR COUNT, 1, 2 + (TALENT:恋慕 && ABL:精液中毒 > 4)
				CALL PRINT_IMAGE(@"30_failed{COUNT}", SIZE, "left")
				TWAIT 1200, 0
				CLEARLINE SIZE / 100
				SIF GETKEY(1)
					GOTO SKIP2
			NEXT
		;手に吐き出し
		ELSEIF !SUCCESS
			FOR COUNT, 2, 5
				CALL PRINT_IMAGE(@"30_success{COUNT}", SIZE, "left")
				TWAIT 550 , 0
				CLEARLINE SIZE / 100
				SIF GETKEY(1)
					GOTO SKIP2
			NEXT
			FOR COUNT, 1, 5 + FAVOR
				CALL PRINT_IMAGE(@"30_dara{COUNT}", SIZE, "left")
				TWAIT 550 + COUNT * 75 + (COUNT == 2) * 700, 0
				CLEARLINE SIZE / 100
				SIF GETKEY(1)
					GOTO SKIP2
			NEXT
		;ごっくんパターン1、陥落高くてMASTER大量射精
		ELSEIF FAVOR && SUCCESS
			FOR COUNT, 0, 17
				CALL PRINT_IMAGE(@"30_success{PAT:COUNT}", SIZE, "left")
				TWAIT (G_TIME:COUNT), 0
				CLEARLINE SIZE / 100
				SIF GETKEY(1)
					GOTO SKIP2
			NEXT
		;ごっくんパターン2
		ELSE
			LOCAL = 500
			FOR COUNT, 2, 5
				CALL PRINT_IMAGE(@"30_success{COUNT}", SIZE, "left")
				TWAIT LOCAL, 0
				CLEARLINE SIZE / 100
				SIF GETKEY(1)
					GOTO SKIP2
				LOCAL += 250
			NEXT
		ENDIF
		$SKIP2
		;顔射
		IF ARG == 3
			CALL PRINT_IMAGE("30_shower6", SIZE, "left")
		;未陥落
		ELSEIF BLOCK
			LOCALS = 30_%\@ SUCCESS ? blow8 # failed1 \@%
			CALL PRINT_IMAGE(LOCALS, SIZE, "left")
		;ごっくん
		ELSEIF SUCCESS
			CALL PRINT_IMAGE(@"30_success{FAVOR + 5}", SIZE, "left")
		;見せつけ
		ELSEIF NOWEX:MASTER:射精 == 1
			CALL PRINT_IMAGE(@"30_dara{FAVOR + 5}", SIZE, "left")
		;だらだら
		ELSE
			LOCAL = TALENT:恋慕 && ABL:精液中毒 > 4 ? 3 # 2
			CALL PRINT_IMAGE(@"30_failed{LOCAL}", SIZE, "left")
		ENDIF
	CASE 5
		LOCAL = 300
		FOR COUNT, 2, 5
			CALL PRINT_IMAGE(@"30_success{COUNT}", SIZE, "left")
			TWAIT LOCAL, 0
			CLEARLINE SIZE / 100
			SIF GETKEY(1)
				GOTO SKIP2
			LOCAL += 150
		NEXT
		CALL PRINT_IMAGE("30_success5", SIZE, "left")
	CASE 6
		FOR COUNT, 1, 3
			CALL PRINT_IMAGE(@"30_failed{COUNT}", SIZE, "left")
			TWAIT 500, 0
			CLEARLINE SIZE / 100
			SIF GETKEY(1)
				GOTO SKIP2
		NEXT
		CALL PRINT_IMAGE("30_success5", SIZE, "left")
	CASE 7
		FOR COUNT, 1, 5
			CALL PRINT_IMAGE(@"30_dara{COUNT}", SIZE, "left")
			TWAIT 600, 0
			CLEARLINE SIZE / 100
			SIF GETKEY(1)
				GOTO SKIP2
		NEXT
		CALL PRINT_IMAGE("30_dara5", SIZE, "left")
	CASE 8
		FOR COUNT:1 ,0, 2
			FOR COUNT, 1, 7
				CALL PRINT_IMAGE(@"30_blow{COUNT}", SIZE, "left")
				TWAIT 75, 0
				CLEARLINE SIZE / 100
				SIF GETKEY(1) && COUNT:1
					GOTO SKIP
			NEXT
		NEXT
		CALL PRINT_IMAGE(@"30_blow7", SIZE, "left")
ENDSELECT
REDRAW TEMP
RETURN
;==================================================
;PRINT_IMAGEの大きさを顔絵設定に合わせる式中関数
;自作絵は拡大に耐えられるクオリティないので最大値に制限かける
;--------------------------------------------------
;默认角色画像横幅	顔絵を表示するベース尺寸(単位:ピクセル
;角色画像表示尺寸比		ベース画像を表示する倍率(50～150の範囲
;--------------------------------------------------
@K30_SET_IMAGESIZE()
#FUNCTION
;LOCAL = 默认角色画像横幅 > 180 ? 1000 # 700
;custom code
LOCAL = 1000
RETURNF MIN(LOCAL * 角色画像表示尺寸比 / 10000 * 100 , 1800)
;--------------------------------------------------
;映姫様のおしりをUPで表示する関数
;IMAGE.ERBとやってることはだいたい同じ、角色データ使用と既存顔絵や口上削除プレイに支障でそうなので口上内に処理ベタ置き
;もっと洗練されたやり方ありそうだけどよくわかんなくなってきた、関数をライブラリ化出来る人って本当にスゴい
;--------------------------------------------------
;COM		SELECTCOM,0ならイベント等からの呼び出し
;LAYER:0	差分指定: 1=縦割れ肛門 2=ぽっかり肛門 3=中出し肛門
;LAYER:1	エフェクト指定:BIT0=尻叩き-着衣 BIT1=尻叩き-裸 BIT2=濡れ BIT3=精液 BIT4=振動棒 
;LAYER:2	ぱんつ指定: 1=通常 2=ずらし 3=振動棒ぱんつ
;LAYER:3	腕指定: 1=手で押さえ 2=振動棒掴み 3=開帳
;LAYER:4	謎の影指定: 1=指插入 2=振動棒挿入 3=舐め 4=掀裙子 5=TINKO 6=尻叩き
;L_NUM		レイヤ階層数
;--------------------------------------------------
@K30_PRINT_BUTT_IMAGE(COM, LAYER:0 = 0, LAYER:1 = 0, LAYER:2 =0, LAYER:3 = 0, LAYER:4 = 0)
#DIM L_NUM
#DIM S_NUM
#DIM COM
#DIM TEMP_SIZE
#DIM TEMP_WIDTH
#DIM TEMP_SCALE
#DIM 縦割
#DIM たっぷり
#DIM 尻叩き
#DIM 腫れ
#DIM 濡れ
#DIM 白濁
#DIM 装着
#DIM 表示拡張
#DIM セットエフェクト
#DIM LAYER, 5
#DIM レイヤ配列位置 = 0, 3, 3, 3, 7
#DIMS SET_RESOURCE, 10
#DIMS SECTIONAL
#DIMS 袖
#DIMS ぱんつ
{
#DIMS レイヤ素材 = "", "30_BASE_VERTICAL", "30_BASE_HOLE", "30_BASE_CREAMPIE",
					 "_HOLD", "_GRAB", "_OPEN", "KETSU-TOUCH",
					 "30_SHADOW_HAND", "30_SHADOW_INST", "30_SHADOW_LICK", "30_SHADOW_LIFT", "30_SHADOW_PENIS", "30_SHADOW_SPANK", "30_SHADOW_DEEPER"
}

SIF !FLAG:画像表示 || !GETBIT(CFLAG:1500, 1)
	RETURN

VARSET SET_RESOURCE
VARSET SECTIONAL
L_NUM = 1
S_NUM = 1
表示拡張 = 0
;レイヤ設定、上手く構文纏められずIF文まみれ
縦割 = GETBIT(CFLAG:30:1910, 7)
たっぷり = 充填率(30, 2) > 30
尻叩き = TIME_PROGRESS(TCVAR:30:355) < 20
;BIT0=着衣腫れ,BIT1=裸腫れ、足し算すると0～2になるから排他表示にできる
腫れ = 尻叩き + (尻叩き * K30_NUDITY())
濡れ = (PALAM:潤滑 > PALAMLV:3) * 4
白濁 = (充填率(30, 1) > 20) * 8
装着 = (TEQUIP:肛用振動棒) * 16
セットエフェクト = 腫れ + 濡れ + 白濁 + 装着
CALL K30_SELECT_PANTS_TYPE(LAYER:2)
ぱんつ = %RESULTS%
袖 = _%\@ NUDITYCHECK(30) > 3 && !K30_CHECK_HEN_T() ? WEAR # NUDE \@%

;イベント構文から直接指定時
IF COM < 0
	SELECTCASE COM
		CASE -1
			SET_RESOURCE:0 = 30_BODY_WEAR
		CASE -2
			SET_RESOURCE:0 = 30_BODY_NUDE
		CASE -3
			SET_RESOURCE:0 = 30_BODY_HEN-T
	ENDSELECT
	袖 = _%\@ COM == -1 ? WEAR # NUDE \@%
	SIF !LAYER:1
		LAYER:1 = セットエフェクト
	IF LAYER:0
		SET_RESOURCE:L_NUM = %レイヤ素材:((レイヤ配列位置:0) + LAYER:0)%
		L_NUM ++
	ENDIF
	IF LAYER:1
		CALL K30_SEARCH_EFFECT(SET_RESOURCE, LAYER:1, L_NUM)
		L_NUM += RESULT
	ENDIF
	IF LAYER:2
		SET_RESOURCE:L_NUM = %ぱんつ%
		SIF ぱんつ != ""
			L_NUM ++
	ENDIF
	IF LAYER:3
		SET_RESOURCE:L_NUM = 30_HAND%袖%%レイヤ素材:((レイヤ配列位置:3) + LAYER:3)%
		L_NUM ++
	ENDIF
	IF LAYER:4
		SET_RESOURCE:L_NUM = %レイヤ素材:((レイヤ配列位置:4) + LAYER:4)%
		L_NUM ++
	ENDIF
	GOTO PRINT_IMAGE
ENDIF

;素体を設定
SET_RESOURCE:0 = 30_BODY%\@ K30_NUDITY() ? _NUDE # _WEAR \@%
SIF !K30_NUDITY() && K30_CHECK_HEN_T()
	SET_RESOURCE:0 = 30_BODY_HEN-T

;素体差分レイヤの設定
IF 縦割
	SET_RESOURCE:1 = %\@ CFLAG:诶嘿嘿 ? 30_BASE_VERTICAL # ダミー \@%
	SIF TCVAR:30:392 > 4
		SET_RESOURCE:1 = 30_BASE_HOLE
	SIF たっぷり
		SET_RESOURCE:1 = 30_BASE_CREAMPIE
	L_NUM ++
ENDIF

;エフェクトレイヤの設定
CALL K30_SEARCH_EFFECT(SET_RESOURCE, セットエフェクト, L_NUM)
L_NUM += RESULT

;ぱんつレイヤの設定
IF ぱんつ != ""
	SET_RESOURCE:L_NUM = %ぱんつ%
	L_NUM ++
ENDIF

;指令毎に其他レイヤを決定
SELECTCASE COM
	;摸屁股
	CASE 310
		SIF CFLAG:诶嘿嘿
			RETURN
		;素体を入れ替え
		SET_RESOURCE:0 = %REPLACE(SET_RESOURCE:0, "BODY_", "BODY_KETSU-TOUCH_")%
		SET_RESOURCE:1 = 30_SHADOW_KETSU-TOUCH
		L_NUM =2
		IF TFLAG:193 < 4 && SHIRAHU(30)
			SET_RESOURCE:2 = 30_HAND%袖%_GUARD
			L_NUM ++
		ENDIF
	;肛門愛撫（日常系）
	CASE 314
		;謎の影
		SET_RESOURCE:L_NUM = 30_SHADOW_%\@ TCVAR:30:364 && TEQUIP:肛用振動棒 ? INST # HAND \@%
		L_NUM ++
	;掀裙子
	CASE 330
		;素体差分个別設定
		IF TCVAR:30:364
			SET_RESOURCE:1 = 30_BASE%\@ たっぷり ? _CREAMPIE # _HOLE \@%
		ELSEIF たっぷり && 縦割
			SET_RESOURCE:1 = 30_BASE_CREAMPIE
		ELSEIF TCVAR:30:392 && 縦割
			SET_RESOURCE:1 = 30_BASE_VERTICAL
		ENDIF
		;謎の影
		IF SELECTCOM == 330
			SET_RESOURCE:L_NUM = 30_SHADOW_LIFT
			L_NUM ++
		ENDIF
		L_NUM ++
	;アナル舐め
	CASE 4
		;映姫様の腕
		IF SHIRAHU(30) && RAND:2 
			SET_RESOURCE:L_NUM = 30_HAND%袖%_HOLD
			L_NUM ++
		ENDIF
		;謎の影
		SET_RESOURCE:L_NUM = 30_SHADOW_LICK
		L_NUM ++
	;肛門愛撫
	CASE 5
		;映姫様の腕
		IF SHIRAHU(30) && RAND:2 
			SET_RESOURCE:L_NUM = 30_HAND%袖%_HOLD
			L_NUM ++
		ENDIF
		;謎の影
		SET_RESOURCE:L_NUM = 30_SHADOW_%\@ TEQUIP:肛用振動棒 ? INST # HAND \@%
		L_NUM ++
	;自慰
	CASE 9
		SIF !TEQUIP:肛用振動棒
			RETURN
		;映姫様の腕
		SET_RESOURCE:L_NUM = 30_HAND%袖%_GRAB
		L_NUM ++
	;張開肛門
	CASE 13
		;素体差分个別設定
		SIF SET_RESOURCE:1 == "30_BASE_VERTICAL"
			SET_RESOURCE:1 = 30_BASE_HOLE
		;映姫様の腕
		SET_RESOURCE:L_NUM = 30_HAND%袖%%\@ 縦割 && RAND:3 ? _OPEN # _HOLD \@%
		L_NUM ++
	;肛用振動棒
	CASE 45
		;素体差分个別設定
		SIF !TEQUIP:肛用振動棒 && 縦割
			SET_RESOURCE:1 = 30_BASE%\@ たっぷり ? _CREAMPIE # _HOLE \@%
		;映姫様の腕
		IF SHIRAHU(30)
			SET_RESOURCE:L_NUM = 30_HAND%袖%_HOLD
			L_NUM ++
		ENDIF
		IF TEQUIP:肛用振動棒
			;謎の影
			SET_RESOURCE:L_NUM = 30_SHADOW_INST
			L_NUM ++
			;肛用振動棒断面図、本体側のフラグに従う
			表示拡張 = FLAG:射精画像種類 == 2 ? 0 # 2
		ENDIF
	;Ａ後背位/Ａ背面座位/打屁股
	CASE 63, 70, 100
		;映姫様の腕
		IF SHIRAHU(30) && COM == 63 && RAND:2 
			SET_RESOURCE:L_NUM = 30_HAND%袖%_HOLD
			L_NUM ++
		ENDIF
		;謎の影,打屁股
		IF COM == 100
			SET_RESOURCE:L_NUM = 30_SHADOW_SPANK
			SIF TEQUIP:51 == MASTER && GROUPMATCH(TEQUIP:体位, 2, 5)
				SET_RESOURCE:L_NUM = 30_SHADOW_SPANK-INST
		;謎の影,TINKO
		ELSE
			SET_RESOURCE:L_NUM = 30_SHADOW_PENIS
		ENDIF
		L_NUM ++
	;結腸責め/隔着刺激陰道
	CASE 74, 75
		SIF !縦割 || GROUPMATCH(TEQUIP:体位, 1, 3, 4)
			RETURN
		;断面図表示設定、本体側のフラグに従う
		表示拡張 = FLAG:射精画像種類 == 2 ? 0 # 1
		;素体差分个別設定
		SET_RESOURCE:1 = 30_BASE%\@ たっぷり ? _CREAMPIE # _HOLE \@%
		;謎の影
		SET_RESOURCE:L_NUM = 30_SHADOW_DEEPER
		L_NUM ++
	CASEELSE
		RETURN
ENDSELECT

$PRINT_IMAGE
TEMP_SIZE = FLAG:多尺寸
TEMP_WIDTH = 默认角色画像横幅
TEMP_SCALE = 角色画像表示尺寸比
FLAG:多尺寸 = 1
IF TEMP_WIDTH * TEMP_SCALE > 27000
	默认角色画像横幅 = 270
	角色画像表示尺寸比 = 100
ENDIF
;レイヤのセットと表示
DEBUGPRINTFORML LINECOUNT:{LINECOUNT}
FOR COUNT, 0, L_NUM
	CALL 画像セット(SET_RESOURCE:COUNT, 0, 0, 角色画像表示尺寸比, COUNT)
	DEBUGPRINTFORML レイヤ:{COUNT} %SET_RESOURCE:COUNT%
NEXT
IF FLAG:時間停止
	CALL 画像セット("時間停止", 0, 0, 角色画像表示尺寸比, L_NUM)
	L_NUM ++
ENDIF
;断面図、290スレ>>545さんのアイデア使ってます
IF 表示拡張
	CALL 画像セット(@"30_SECTIONAL_RECTUM{表示拡張}", 0, 0, 角色画像表示尺寸比, 0)
	IF たっぷり
		SECTIONAL = 30_SECTIONAL%\@ 充填率(30, 2) > 60 ? _POKKORI # _TAPPURI \@%
		CALL 画像セット(SECTIONAL, 0, 0, 角色画像表示尺寸比, 1)
		S_NUM ++
	ENDIF
	IF FLAG:時間停止
		CALL 画像セット("時間停止", 0, 0, 角色画像表示尺寸比, S_NUM)
		S_NUM ++
	ENDIF
	
	FOR COUNT, S_NUM, L_NUM 
		CALL ダミーセット(0, 0, 角色画像表示尺寸比, COUNT)
	NEXT
ENDIF
CALL 画像一斉表示("LEFT", 0, 1, -1)
FLAG:多尺寸 = TEMP_SIZE
默认角色画像横幅 = TEMP_WIDTH
角色画像表示尺寸比 = TEMP_SCALE
RETURN 1
;==================================================
;おしり画像のエフェクトを決定する関数
;REF関数の部分は路人子画像生成から拝借しました
;--------------------------------------------------
@K30_SEARCH_EFFECT(リソース, セットエフェクト, レイヤ数)
#DIM セットエフェクト
#DIM レイヤ数
#DIM L_NUM
#DIMS REF リソース, 0
#DIMS レイヤ素材 = "30_EFFECT_SPANK-W", "30_EFFECT_SPANK-N", "30_EFFECT_WET", "30_EFFECT_SPERM", "30_EFFECT_VIBE"

L_NUM = 0
FOR COUNT, 0, 5
	SIF !GETBIT(セットエフェクト, COUNT)
		CONTINUE
	リソース:(レイヤ数 + L_NUM) = %レイヤ素材:COUNT%
	L_NUM ++
NEXT
RETURN L_NUM
;==================================================
;おしり画像基準で着衣状況を判別する式中関数
;@CHECK_NUDEも@NUDITYCHECKも下半身基準だとなんか合わない
;--------------------------------------------------
;0=着衣 1=おしり丸出し
;--------------------------------------------------
@K30_NUDITY()
#FUNCTION
;上半身着衣状況(TEQUIP:1)を見る
SIF GROUPMATCH(TEQUIP:1, 1, 3)
	RETURNF 0
;下半身着衣状況(TEQUIP:0)を見る
SIF GETBIT(TEQUIP:0, 0) || GETBIT(TEQUIP:0, 3)
	RETURNF 0
;ここまで抜けたら丸出し
RETURNF 1
;==================================================
;ぱんつ画像を決定する関数
;全部描ける画力も無いので似た系統のを表示、気が向いたので追加
;--------------------------------------------------
@K30_SELECT_PANTS_TYPE(指定)
#DIM 指定
#DIM G_ID
#DIM ASIDE
#DIMS 色
#DIMS 装着
#DIMS ずらし
#DIMS ヌード
#DIMS ベースぱんつ
#DIMS TEMP_PANTS
#DIMS COLOR = "", "白", "緑", "黒"
LOCAL = 0
RESULTS =
色 = %COLOR:(DAY % 4)%
ASIDE = (CFLAG:诶嘿嘿 == 1 || TEQUIP:50 >= 0 || TEQUIP:51 >= 0 || TEQUIP:振動棒 || TEQUIP:肛用振動棒 || 指定 == 2)
ずらし = _%\@ ASIDE ? SHIFT # NORMAL \@% 
ヌード = %\@ K30_NUDITY() ? _NUDE # _WEAR \@%
装着 = %\@ TEQUIP:肛用振動棒 ? _VIBE # \@%
;指定那个ば画像をそちらに合わせる
SELECTCASE 指定
	CASE -1
		RETURN
	CASE 1, 2
		ヌード = %\@ 指定 == 1 ? _NUDE # _WEAR \@%
	CASE  3
	RESULTS = 30_PANTS_HARNESS
	RETURN
ENDSELECT
SELECTCASE EQUIP:30:下半身内衣２
	CASE 0
		;没穿内裤orガーター吊帯or振動棒吊帯
		IF TCVAR:30:364 && !CFLAG:诶嘿嘿 && TEQUIP:肛用振動棒 && !CHECK_NUDE(30)
			RESULTS = 30_PANTS_HARNESS
			RETURN
		ENDIF
		SIF TCVAR:30:354 > 0
			RESULTS = 30_PANTS%ヌード%_TYPE10_NO-PANTS
		
	CASE 1, 2, 26
		;1/男式四角裤,2/構造寬鬆的灯笼裤,26/在胯股间打了開大口的方便用拉链（屁股处可開）
		IF ASIDE
			RESULTS = 30_PANTS_COMMON_TYPE1_SHIFT
		ELSE
			RESULTS = 30_PANTS%ヌード%_TYPE1_NORMAL
		ENDIF
		
	CASE 5
		;5/凸显出臀部曲線的條紋胖次＠当然是黑白的
		RESULTS = 30_PANTS%ヌード%_TYPE4%ずらし%
		
	CASE 7, 10
		;7/毫不做作的纯色胖次,10/设计质朴的低腰胖次
		IF ASIDE
			RESULTS = 30_PANTS_COMMON_TYPE8_SHIFT
		ELSE
			RESULTS = 30_PANTS%ヌード%_TYPE8_NORMAL
		ENDIF
		LOCAL = 1
	
	CASE 11, 20
		;11/仿佛能看到股沟线条的低腰紧身胖次,20/极为短小的超短胖次
		IF ASIDE
			RESULTS = 30_PANTS_COMMON_TYPE11_SHIFT
		ELSE
			RESULTS = 30_PANTS%ヌード%_TYPE11_NORMAL
		ENDIF
		LOCAL = 1
		
	CASE 12
		;12/格外修长的高开叉胖次
		RESULTS = 30_PANTS%ヌード%_TYPE3%ずらし%
		
	CASE 13, 23
		;13/整洁并有些透明的蕾丝胖次（白）,23/重要的部位完全暴露的透明胖次
		RESULTS = 30_PANTS%ヌード%_TYPE6%ずらし%
		
	CASE 14, 16
		;14/充满成熟女人韵味的蕾丝胖次（黒）,16/用细绳系在腰上的绳带胖次
		RESULTS = 30_PANTS%ヌード%_TYPE5%ずらし%
		
	CASE 15
		;15/裸露大片臀部的大胆的Ｔ字裤
		RESULTS = 30_PANTS%ヌード%_TYPE12%ずらし%
		
	CASE 17
		;縫着竖向拉链的皮革胖次
		RESULTS = 30_PANTS%ヌード%%装着%_TYPE7%ずらし%
		
	CASE 18
		;穿旧了的棉布内衣
		RESULTS = 30_PANTS%ヌード%_TYPE0%ずらし%
		
	CASE 19
		;19/充满少女味的可爱的三角胖次
		IF ASIDE
			RESULTS = 30_PANTS_COMMON_TYPE2_SHIFT
		ELSE
			RESULTS = 30_PANTS%ヌード%_TYPE2_NORMAL
		ENDIF
		
	CASE 21
		;緊緊抱住屁股的兜襠布
		RESULTS = 30_PANTS%ヌード%_TYPE13%ずらし%
	CASE 22
		;面積極小简直跟繩一樣的丁字褲＠总觉得是吊袜带样式
		RESULTS = 30_PANTS%ヌード%_TYPE10%ずらし%
		
	CASE 24
		;清楚的暴露出恥丘的開放式胖次
		RESULTS = 30_PANTS%ヌード%%装着%_TYPE9
	
	CASEELSE
		;初期or獲得可能素質では穿かない/換装のみのぱんつはスルー
		;3/小熊胖次,4/波点胖次,6/褶边胖次,25/創口貼 = 無知ロリ用、27以降 = 着替え可能判定無
		RESULTS = 30_PANTS%ヌード%_TYPE0%ずらし%
ENDSELECT

SIF !LOCAL
	RETURN

;色違いぱんつの生成
ベースぱんつ = %RESULTS%
RESULTS = %RESULTS%%色%
;SIF RM_リソースチェック(RESULTS)
SIF RM_リソースチェック(RESULTS) ;debloat custom code
	RETURN
G_ID = 0
CALL 画像合成(G_ID, ベースぱんつ, 0, 0, 0, 0, 色)
SPRITECREATE RESULTS, G_ID
RETURN
;==================================================
;映姫様のアヘ顔を表示する関数
;行送り中に目だけ表示されると可怕的のでスプライト合成を使用
;白蓮別立絵に使った構文をぶっこ抜き
;--------------------------------------------------
@K30_PRINT_AHEGAO
#DIM G_ID
#DIM NUM
#DIM ORG
#DIMS SET_RESOURCE,7
#DIMS RESOURC_NAME
#DIMS 差分
;睡眠中は表示しない
SIF !FLAG:画像表示 || CFLAG:睡眠
	RETURN
ORG = (SUMARRAY(NOWEX:30:0, 8, 10) > 0)
;ベースと素体を設定
VARSET SET_RESOURCE
SET_RESOURCE:0= 30_AHE_BASE
差分 = %\@ CHECK_NUDE(30) ? _NUDE # _WEAR \@%
SIF !CHECK_NUDE(30) && CFLAG:衣装一時変更 == 62
	差分 = _HEN-T
SET_RESOURCE:1 = 30_AHE_BODY%差分%
SET_RESOURCE:2 = 30_AHE_ACME
RESOURC_NAME = 30_あへ顔%差分%

;口を設定-随机２種、恋慕かつ四重絶頂以上で３種類
NUM = RAND:(2 + (ORG && TALENT:恋慕))
SET_RESOURCE:3 = 30_AHE_MOUTH{NUM}
RESOURC_NAME += @"口{NUM}"

;目を設定-恋慕で目にハート付き、恋慕かつ四重/五重絶頂で差分増
NUM = TALENT:恋慕 ? 1 + RAND(ORG + (NOWEX:9 > 0) + 1) # 0
SET_RESOURCE:4 = 30_AHE_EYE{NUM}
RESOURC_NAME += @"目{NUM}"

;眉を設定-随机２択
NUM = RAND:2
SET_RESOURCE:5 = 30_AHE_BROW{NUM}
RESOURC_NAME += @"眉{NUM}"

;エフェクト-恋慕で随机４種
IF TALENT:恋慕
	NUM = RAND:4
	SET_RESOURCE:6 = 30_AHE_EFFECT{NUM}
	RESOURC_NAME += @"効果{NUM}"
ENDIF
;合成したスプライトをリソース登録して表示
IF !RM_リソースチェック(RESOURC_NAME)
	G_ID = 0
	FOR COUNT, 0, 6 + (TALENT:恋慕 > 0)
		CALL 画像合成(G_ID, SET_RESOURCE:COUNT)
	NEXT
	SPRITECREATE RESOURC_NAME, G_ID
ENDIF
CALL PRINT_IMAGE(RESOURC_NAME, 1000, "left")
PRINTFORML 
RETURN
;==================================================
;映姫様のバイブデート中顔絵を表示する関数
;--------------------------------------------------
@K30_PRINT_VIBE_DATE
#DIM G_ID
#DIM LAYER
#DIM NUM
#DIMS TEX
#DIMS TEX2
#DIM L_NUM
#DIM TEMP_SIZE
#DIM TEMP_WIDTH
#DIM TEMP_SCALE
#DIMS SET_RESOURCE,7
#DIMS RESOURC_NAME
SIF !FLAG:画像表示 || !GETBIT(CFLAG:1500, 1)
	RETURN
VARSET SET_RESOURCE
LAYER = 0
L_NUM = 2
TEX '= \@ TCVAR:30:365 == 2 || NOWEX:Ａ絶頂 ? AHE_ # \@
SET_RESOURCE:0 = 30_%TEX%BASE
SET_RESOURCE:1 = 30_AHE_BODY_WEAR
RESOURC_NAME = 30_バイブデート%TEX%
IF NOWEX:Ａ絶頂
	SET_RESOURCE:2 = 30_AHE_ACME
	RESOURC_NAME += "イき"
	L_NUM ++
ENDIF
NUM = RAND:2
SET_RESOURCE:L_NUM = 30_AHE_BROW{NUM}
RESOURC_NAME += @"眉{NUM}"
L_NUM ++
;弱
IF TCVAR:30:365 == 1
	TEX '= \@ NOWEX:Ａ絶頂 ? 4 # %TOSTR(RAND(5, 7))% \@
	TEX2 '= \@ NOWEX:Ａ絶頂 ? %TOSTR(RAND:2)% # %TOSTR(RAND(3, 6))% \@
;強
ELSE
	TEX '= \@ NOWEX:Ａ絶頂 ? %TOSTR(RAND(2, 4))% # 1 \@
	TEX2 '= TOSTR(RAND(3, 6))
ENDIF
SET_RESOURCE:L_NUM = 30_AHE_EYE%TEX%
RESOURC_NAME += @"目%TEX%"
L_NUM ++
SET_RESOURCE:L_NUM = 30_AHE_MOUTH%TEX2%
RESOURC_NAME += @"口%TEX2%"
L_NUM ++
IF NOWEX:Ａ絶頂
	TEX '= \@ TCVAR:30:365 == 1 ? 0 # %TOSTR(RAND(2, 4))% \@
	SET_RESOURCE:L_NUM = 30_AHE_EFFECT%TEX%
	RESOURC_NAME += @"効果%TEX%"
ELSEIF TCVAR:30:365 == 2
	SET_RESOURCE:L_NUM = 30_AHE_EFFECT1
	RESOURC_NAME += "効果1"
ELSEIF K30_BE_SEEN() && !TFLAG:约会道中
	SET_RESOURCE:L_NUM = 別顔_吐息_30
	RESOURC_NAME += "効果4"
	LAYER = 1
ELSE
	SET_RESOURCE:L_NUM = 汎用エフェクト_spoken squiggle
	RESOURC_NAME += "効果5"
	LAYER = 2
ENDIF
IF !RM_リソースチェック(RESOURC_NAME)
	G_ID = 0
	FOR COUNT, 0, L_NUM
		CALL 画像合成(G_ID, SET_RESOURCE:COUNT)
	NEXT
	SELECTCASE LAYER
		CASE 0
			CALL 画像合成(G_ID, SET_RESOURCE:L_NUM)
		CASE 1
			CALL 画像合成(G_ID, SET_RESOURCE:L_NUM, -15, 0, 320, 320)
		CASE 2
			CALL 画像合成(G_ID, SET_RESOURCE:L_NUM, 0, -15, 270, 270)
	ENDSELECT
	SPRITECREATE RESOURC_NAME, G_ID
ENDIF
TEMP_SIZE = FLAG:多尺寸
TEMP_WIDTH = 默认角色画像横幅
TEMP_SCALE = 角色画像表示尺寸比
FLAG:多尺寸 = 1
IF TEMP_WIDTH * TEMP_SCALE > 27000
	默认角色画像横幅 = 270
	角色画像表示尺寸比 = 100
ENDIF
CALL 画像セット(RESOURC_NAME, 0, 0, 角色画像表示尺寸比, 0)
SIF 充填率(30, 2) > 30
	CALL ダミーセット(0, 0, 角色画像表示尺寸比, 1)
CALL 画像セット(@"30_SECTIONAL_DATE{TCVAR:30:365}", 0, 0, 角色画像表示尺寸比, 0)
SIF 充填率(30, 2) > 30
	CALL 画像セット(@"30_SECTIONAL_%\@ 充填率(30, 2) > 60 ? POKKORI # TAPPURI \@%", 0, 0, 角色画像表示尺寸比, 1)
CALL 画像一斉表示("LEFT", 0, 1, -1)
FLAG:多尺寸 = TEMP_SIZE
默认角色画像横幅 = TEMP_WIDTH
角色画像表示尺寸比 = TEMP_SCALE
RETURN 1
;==================================================
;映姫様のメシ顔を表示する関数
;--------------------------------------------------
@K30_PRINT_MESHIGAO(ARGS)
SIF !FLAG:画像表示 || !顔絵付きメッセージ
	RETURN
SETANIMETIMER 150
CALL 画像セット("30_BG1", 0, 0, 100, 0)
CALL GET_BASE_RESOURCE(30, "顔絵", "", ARGS)
CALL 画像セット(RESULTS:0, 0, 0, 100, 1)
CALL 画像一斉表示("LEFT", 0, 1, -1)
RETURN

;==================================================
;汎用説教判定＆実行
@K30_LECTURE_CHECK(説教長, 理由)
;--------------------------------------------------
;説教長= 説教の行数＝憤怒具合
;理由= 説教理由（=0 胖次 =1 性騷擾指令失敗時 =2ＶＡ強制わいせつ）
;--------------------------------------------------
#DIM 説教長
#DIM 理由
#DIM ぱんつ１
#DIM ぱんつ２
#DIM 性騷擾１
#DIM 性騷擾２
#DIM 強制わいせつ
#DIM TEMP
;ガード低下時・其他状態異常・诶嘿嘿時は判定無無罪
SIF (CFLAG:1801 == 0 || SHIRAHU(30) != 1 || CFLAG:30:诶嘿嘿) && 理由 != 0
	RETURN 1
;変Ｔで目が死んでいるときは説教無し
SIF K30_CHECK_HEN_T() && TALENT:心情 > -1
	RETURN 1
;情人旅館と待客室にいても説教しない
SIF (CFLAG:30:現在位置 == 情人用宿屋 || CFLAG:30:現在位置 == 情人旅館 || CFLAG:30:現在位置 == OMANEKIBEYA()) && 理由 != 0
	RETURN 1
SELECTCASE 理由
	;胖次密輸摘発
	CASE 0
		;説教orぶん殴りで把柄はチャラ
		CFLAG:掌握把柄 -= 5
		CFLAG:1700 -= 5
		CLEARBIT CFLAG:1699, 1
		;２回まではお説教
		IF ぱんつ１ != DAY
			ぱんつ１ = DAY
			PRINTFORML 
			PRINTFORMW 「……那么,%K30_C_NAME(MASTER)%」
			GOTO LECTURE
		ELSEIF ぱんつ２ != DAY
			ぱんつ２ = DAY
			PRINTFORML 
			PRINTFORMW 「%K30_C_NAME(MASTER)%也真是够了，一点都不长记性啊……」
			GOTO LECTURE
		;３回目でぶん殴られる
		ELSE
			PRINTFORML 
			PRINTFORMW 「………%CALLNAME:MASTER%」
			PRINTFORMW 「都说到这份上了，您还不明白吗？」
			TCVAR:30:371 = 1
			CALL K30_DISCIPLINE_HEAD
		ENDIF
	;性騷擾失敗時
	CASE 1
		;憤怒具合30以下ノーカン
		IF 説教長 > 30
			;１日２回お説教
			IF 性騷擾１ != DAY
				性騷擾１ = DAY
				PRINTFORML 
				PRINTFORMW 「……有些太过分了呢」
				GOTO LECTURE
			ELSEIF 性騷擾２ != DAY
				性騷擾２ = DAY
				PRINTFORML 
				PRINTFORMW 「……真是完全不长记性啊」
				GOTO LECTURE
			;第三次被揍
			ELSE
				PRINTFORML 
				PRINTFORMW 「……适可而止吧」
				PRINTFORMW 「如果光说不听的话」
				TCVAR:30:371 = 1
				CALL K30_DISCIPLINE_HEAD
			ENDIF
		ENDIF
	;通常时VA指令的概率
	CASE 2
		;首先是说教
		IF 強制わいせつ != DAY
			強制わいせつ = DAY
			PRINTFORML 
			PRINTFORMW 「……您，太过分了」
			GOTO LECTURE
		;忍耐只有一次
		ELSE
			PRINTFORML 
			PRINTFORMW 「……我，不能再忍了」
			PRINTFORMW 「如果光说无益的话」
			TCVAR:30:371 = 2
			CALL K30_DISCIPLINE_HEAD
		ENDIF
ENDSELECT
RETURN 1
;说教
$LECTURE
PRINTFORML 「请！先给我到那里正坐」
FORCEWAIT
PRINTFORMDW 映姬指了指地面。%CALLNAME:MASTER%乖乖地跪坐了下来……
PRINTFORMW 
SELECTCASE 理由
	CASE 0
		PRINTFORMW 「你到底偷了多少内裤啊？嗯，捡到的这种借口我完全不相信」
		PRINTFORMW 「没错，你就是有点手脚太不干净了！」
	CASE 1
		PRINTFORMW 「话说回来，你不要随便碰女性啊，难道就没有自制心这种东西吗？」
		PRINTFORMW 「没错，你就是有点太忠实于欲望了！」
	CASE 2
		PRINTFORMW 「\@ TALENT:30:恋人 ? 即使是恋人 # 无论关系多好 \@，能做的事情也是有限度的！ 你到底要侮辱人到什么地步啊！」
		PRINTFORMW 「没错，你就是有点太不体贴了！」
	CASEELSE
		PRINTFORMDW 真是意料之外。 麻烦您了，请将您使用此指令前的操作一同汇报bug
		PRINTFORMW 「K30_LECTURE_CHECK，你的参数也太奇怪了！ …咦？」
ENDSELECT
PRINTFORML 
TEMP = 説教長
WHILE TEMP
	;行頭の修飾。2行に1回確率でつける
	IF  (((説教長 + TEMP) % 2 == 0) && RAND:5 == 0) || (TEMP == 説教長)
		PRINTFORM 「%TEXTR("话说回来/请你好好想想/说白了/给我好好听着/具体来说/听懂了吗/")%………………
	ELSE
		PRINT 「……
	ENDIF
	;1行の長さは随意
	REPEAT 6 + RAND:4
		;雑に百分率
		SELECTCASE RAND:100
			CASE 0 TO 9
				SELECTCASE 理由
					CASE 0
						PRINTFORM …%TEXTR("胖次/一点点/你的/善行/内裤/从哪里/胖次/用来干什么/你在听吗…？　/为了什么")%…
					CASE 1
						PRINTFORM …%TEXTR("死后/总是毫不顾忌的/纠缠不清的/明明不愿意的/你的/善行是/反省/别人的感受…/不害羞么…/他人的目光…/毫不客气地…")%…
					CASE 2
						PRINTFORM …%TEXTR("死后/意志的/说了不行了/明明不愿意的/你的/善行是/自我反省/别人的感受…/不害臊么…/他人的目光…/毫不客气地…")%…
					CASEELSE
						PRINTFORM …%TEXTR("反省/多少/罪孽/行善/地狱/你大概是/过错/你在听吗…？　/死后的")%…
				ENDSELECT
			CASE 10 TO 13
				PRINTFORM ………!
			CASEELSE
				PRINTFORM ………
		ENDSELECT
	REND
	PRINTW 」
	TEMP --
WEND
説教長 *=2
TIME += 説教長
TEMP = MAXBASE:MASTER:気力 / 3
SIF TEMP > BASE:MASTER:気力
	TEMP = MAX(BASE:MASTER:気力, 1)
CALL K30_D_LINE
PRINTFORML 「………………………………」
PRINTFORMW 「…就是这么回事。没错，你现在应该做的事情是」
SELECTCASE 理由
	CASE 0
		PRINTFORMW 「好好处理掉你囤积的内裤。这就是你现在能做的善事」
	CASE 1
		PRINTFORMW 「更多地思考并反省满足欲望的代价。这就是你现在能做的善事」
	CASE 2
		PRINTFORMW 「好好学习理解别人的心情和体谅他人。这就是你现在能做的善事」
	CASEELSE
ENDSELECT
FORCEWAIT
PRINTFORML
PRINTFORMDW 终于结束了……
PRINTFORMDL 被映姬说教了{説教長}分钟、非常疲惫……
PRINTFORMDW 正座的脚都麻了…
CALL RECOVER(MASTER, -50, "体力", "双脚都麻痹了")
CALL RECOVER(MASTER, -(TEMP), "気力", "长得令人厌烦的说教")
;映姫様が趣味を満喫
SOURCE:鬱屈 -= 1000
SIF SOURCE:鬱屈 < 0
	SOURCE:鬱屈 = 0
SOURCE:歓楽 += ABL:30:親密 * 70
SOURCE:情愛 += 300
TEMP = RAND:5 - 理由
SIF TEMP <= 0
	TEMP = 1
;日記フラグ
IF DIARY:30:5 == 0
	;かなり行儀の悪い受け渡し方だけど、ターン終了直前の茶番だからこれでいいや
	TFLAG:193 = 理由 * 1000 + 説教長
	CALL K30_WRITE_DIARY(5)
ENDIF
PRINTFORML 
PRINTFORMDL 因为老实地听了说教，映姬的信赖度稍微提高了一点
SIF 理由 == 0
	PRINTFORMDL 胖次的事好像也就这样勉强原谅我了
CALL CHANGE_CFLAG(4, 30, TEMP)
RETURN 1
;==================================================
;映姫様の折檻＠遠慮のない一撃
@K30_DISCIPLINE_HEAD
#DIM DAMAGE
;--------------------------------------------------
LOCAL = MIN(1 + 2 * TCVAR:30:371, 7)
DAMAGE = 400 + 400 * TCVAR:30:371
SIF TCVAR:30:371 > 4
	DAMAGE += BASE:MASTER:体力 + 1000 * TCVAR:30:371
IF TCVAR:30:371 < 3
	PRINTFORMW 「你得稍微吃点苦头」
	SETCOLOR 206, 8, 21
	PRINTFORML 
	PRINTFORML 「好好反省一下自己的生活！」
ELSEIF TCVAR:30:371 < 5
	PRINTFORML 「嗯，我不会对你心软的」
	PRINTFORMW 「好好地为自己犯下的罪行」
	SETCOLOR 206, 8, 21
	PRINTFORML 
	PRINTFORML 「忏悔吧！！」
ELSEIF TCVAR:30:371 == 5
	PRINTFORML 「……嗯，我决定了」
	PRINTFORMW 「你今天剩下的时间」
	SETCOLOR 206, 8, 21
	PRINTFORML 
	PRINTFORML 「在自己房间里好好反省！！」
ELSE
	PRINTFORMDW 在挥下去的瞬间清楚地告知了
	SETCOLOR 206, 8, 21
	PRINTFORML 
	PRINTFORML 「下地狱去吧！！」
ENDIF
CALL K30_QUAKE(LOCAL)
PRINTFORMW 
PRINTFORMDL 被悔悟棒狠狠地打倒了…
CALL SET_KOJO_COLOR(30)
BASE:MASTER:体力 -= DAMAGE
PRINTFORML 
SIF DIARY:30:10 == 0
	CALL K30_WRITE_DIARY(10)
IF BASE:MASTER:体力 <= 500
	IF !TALENT:恋慕
		IF TCVAR:30:371 == 1
			PRINTFORML 「啊，哎？ 是不是稍微有点过头了？」
			PRINTFORMDL 映姬困惑的声音…在脑海中，回响…
		ELSE
			PRINTFORML 「你就好好反省吧」
			IF SELECTCOM == 356
				PRINTFORMDL 映姬不再看这边，回到了%GET_JOBNAME(30)%的座位上…
			ELSE
				PRINTFORMDL 映姬无情地离开了现场…
			ENDIF
		ENDIF
	ENDIF
ELSE
	IF TCVAR:30:371 < 3
		PRINTFORML 「怎么样，体会深刻了吗」
		PRINTFORMW 「如果因此受到教训，就改正你的行为」
	ELSE
		PRINTFORML 「虽然稍微有些手下留情，但这次就到此为止」
		PRINTFORML 「好好反省，现在立刻改正你的行为」
		PRINTFORMDW ……看来低估了映姬的责任感
		PRINTFORMDW 总之还是不要妨碍她工作了
	ENDIF
ENDIF
RETURN 1
;==================================================
;映姫様の説法と折檻
;茶を飲みながらお話をのんびりと聞くイメージ
;--------------------------------------------------
;CFLAG:1701 良いお話の累計
;--------------------------------------------------
@K30_LECTURE_BENEFIT
#DIM SIN_VAL
#DIM EXC
SIN_VAL = 0
EXC = 0
CFLAG:1701 ++
PRINTFORMW 
;お話し6種類しかないので累計して6で割った余りで振り分け
LOCAL = CFLAG:1701 % 6
SELECTCASE LOCAL
	CASE 0
		PRINTFORMW 「是啊…您认为『欲望』是什么呢？」
		PRINTFORML 
		PRINTFORML 「有这样一个古老的传说」
		PRINTFORML 「从前，在某个地方，一位禅僧在他的师父门下继续修行」
		PRINTFORML 「当僧侣在自己的房间里打坐时，一个陌生的女人突然靠在禅僧身上」
		PRINTFORML 「并问他『你被这样对待是什么感觉』之类的问题」
		PRINTFORML 「这个女人是师父安排的，作为一种考验来引诱他」
		PRINTFORML 「僧侣脸色丝毫未变，断然说道：『枯木倚寒岩，三冬无暖气』」
		PRINTFORMW 「意思是『就像枯木靠在冰冷的岩石上一样，感觉就像在隆冬时节被风吹拂』」
		PRINTFORML 
		PRINTFORMW 「您觉得这位僧侣怎么样？ 您认为他很了不起吗？」
		PRINTFORML 
		PRINTFORML 「不不，师父对这个回答非常愤怒，说道：『没想到是个俗气的僧侣。我真是看走眼了』」
		PRINTFORMW 「然后就把那个僧侣赶走了。您知道他错在哪里了吗？」
		PRINTFORML 
		PRINTFORML 「实际上，作为被一个女人爱上的男人，应该采取怎样的态度，这就是研究这个问题的关键」
		PRINTFORML 「对方是素不相识的女性。当然也许对方很了解你然后爱上了你」
		PRINTFORML 「但对你来说，这是第一次见面的女性。在这种情况下，作为一个成熟的男人，即使是暂时婉拒或者果断拒绝」
		PRINTFORML 「也应该有适当的人情味的处理方式和言语，既不让女性感到被羞辱，也不辱没自己的品格」
		PRINTFORMW 「像那个僧侣一样，说出『枯木倚寒岩』这样冷漠的态度，对方可能会记恨一生，甚至可能逆反地自杀，这是一种多么糟糕的对待方式」
		PRINTFORML 
		PRINTFORML 「即使是同样的拒绝方式，也有体谅对方心情的拒绝方式。而那个僧侣为了不输给诱惑，拼命地绷着肩膀」
		PRINTFORMW 「必须死守着不近人情，师父看穿了这个僧侣内心有脆弱之处」
		PRINTFORML 
		PRINTFORML 「我断定，逃避诱惑并不是最好的生活」
		PRINTFORML 「不如像泥中的莲花一样，即使生活在各种野心、诱惑和爱欲之中」
		PRINTFORML 「也不被污秽所染，并且将那些欲望、诱惑」
		PRINTFORML 「巧妙地消化善用，从而完成优秀的人格，开出无上幸福之花」
		PRINTFORML 「刚才那个僧侣，如果不能领悟这一点，就不得不说他不如俗人」
		PRINTFORMW 「隐居避世或者害怕诱惑而拼命逃跑，这些都是错误的」
		PRINTFORML 
		PRINTFORMW 「拥有欲望并不是罪。沉溺于欲望，或者虚张声势说自己已经舍弃了欲望，这才是罪」
	CASE 1
		PRINTFORMW 「是啊，您认为『眼泪的价值』是什么呢？」
		PRINTFORML 
		PRINTFORML 「如果做事敷衍了事，就不会有眼泪。因为不痛苦」
		PRINTFORML 「如果认真思考，认真面对事物，就会有眼泪。因为痛苦」
		PRINTFORMW 「为什么敷衍了事就不痛苦，而认真起来就会痛苦呢？」
		PRINTFORML 
		PRINTFORML 「因为敷衍了事时，矛盾看起来不像矛盾，也看不到更好的东西」
		PRINTFORML 「因为即使是现在的状态也能勉强应付过去。所以不痛苦」
		PRINTFORML 「相反，认真起来，认真面对时，就会看到矛盾，渴望更好的东西」
		PRINTFORMW 「就会对现状感到强烈不满。所以会痛苦」
		PRINTFORML 
		PRINTFORML 「渴望最好的东西的心，姑且称之为『菩提心』吧」
		PRINTFORMW 「『菩提心』是希望自己变好，也希望别人变好的正直的愿望」
		PRINTFORML 
		PRINTFORML 「例如良心会随着时代而改变，也会随着周围的形势而改变」
		PRINTFORML 「即使出卖自己的肉体，也认为只要心是属于丈夫的，贞操就没有失去」
		PRINTFORMW 「封建时代女性的贞操观，早已不同于当代女性了」
		PRINTFORML 
		PRINTFORML 「但是『菩提心』不会随着时代和形势而改变」
		PRINTFORML 「只要有人类存在，它就存在于其中，指示着发展的方向，是净化和推进它的良药」
		PRINTFORML 「即使有细菌潜入，内心的良药，也就是『菩提心』，即使不知道它的存在，也会被净化」
		PRINTFORML 「我们即使不知道有『菩提心』，也被保持着心灵的纯洁」
		PRINTFORMW 「这种情况下的细菌，就是与『菩提心』相反的诱惑、恶念之类的东西」
		PRINTFORML 
		PRINTFORML 「但是这个世界上没有万能药。药生效时，也会产生一些不想要的东西」
		PRINTFORML 「当『菩提心』驱逐恶念时产生的残渣、残骸，就是最初说的眼泪和痛苦」
		PRINTFORML 「当痛苦和眼泪降临时，不要若无其事、深入追溯其原因时」
		PRINTFORMW 「一定会遇到『菩提心』的显现。然后按照那颗心的指引，选择新的正确的人生方向」
		PRINTFORML 
		PRINTFORML 「有所谓的『生命的痛苦』。从旧的生命中孕育出新的生命时，必然会有苦恼。会有眼泪」
		PRINTFORML 「树木发芽时，树皮上首先出现的是伤痕。是苦恼。然后是树脂——」
		PRINTFORMW 「也就是眼泪。从那里，新的生命，五月的新绿，才终于萌芽」
	CASE 2
		PRINTFORMW 「是啊…您认为『喜欢和讨厌』是什么呢？」
		PRINTFORML 
		PRINTFORML 「既然人类有心，心里有感情，那么每个人都会产生喜欢和讨厌的情绪，这是理所当然的」
		PRINTFORML 「我不会因此评判您。就像樱花、梅花、竹子，以及百木千草的变化让自然的风景变得有趣一样」
		PRINTFORML 「正因为人类有喜欢和讨厌的情绪的阴影，反而让人们有了变化和节奏，变得有趣」
		PRINTFORMW 「世界才不会单调地流逝。所以，有喜欢和讨厌的情绪是完全可以的」
		PRINTFORML 
		PRINTFORML 「但是说了这些之后，尤其需要补充的是，千万不要被这种喜欢和讨厌的情绪」
		PRINTFORML 「所束缚。一旦被束缚，并固执地坚持下去，那个人就会变得任性」
		PRINTFORML 「例如『我讨厌那个人，所以不和他做朋友』」
		PRINTFORML 「『所以其他人谁都不能和他做朋友』，像这样讨厌的方式绝对是任性」
		PRINTFORML 「这和『我讨厌松树。把世界上所有的松树都砍掉』」
		PRINTFORML 「『我喜欢竹子，所以只留下竹子』是一样的」
		PRINTFORMW 「这样一来，幻想乡就会只剩下竹林，变成毫无趣味的风景了」
		PRINTFORML 
		PRINTFORML 「同样地，对于人也是如此」
		PRINTFORML 「『我虽然讨厌那个人，但也许在别人看来他很好，所以他们成了朋友，这样就好』」
		PRINTFORML 「这样平静下来，自己讨厌的东西，自己一个人讨厌就好了。追着自己讨厌的人」
		PRINTFORML 「甚至闯入他受人喜欢的场合去捣乱，这是任性的行为」
		PRINTFORML 「这是扰乱世界和谐的人，是不可原谅的自私」
		PRINTFORMW 「陷入这种错误的人，自己也会陷入无法摆脱的困境」
		PRINTFORML 
		PRINTFORML 「在我看来，天地间的一切生物，甚至草木，没有任何生命不具备被尊重的资格」
		PRINTFORML 「因此，如果使用得当，没有什么是应该丢弃的，没有什么是无用的」
		PRINTFORML 「所以，对于任何事物，绝对不允许无视其价值。这是对自然的亵渎」
		PRINTFORMW 「是对天地佛神的亵渎，最终我必须惩罚你」
		PRINTFORMW 
		PRINTFORMW 「我说的惩罚，您知道会发生什么吗？ 您也要小心」
	CASE 3
		PRINTFORMW 「是啊…您对『宝塔』有什么了解吗？」
		PRINTFORML 
		PRINTFORML 「…『不是经常弄丢的那个东西吗』。请您暂时别去想那个话题」
		PRINTFORMW 「唉…她作为毘沙门天的代理人，如果能再稍微可靠一点就好了…」
		PRINTFORML 
		PRINTFORML 「…话题跑偏了，我们继续吧」
		PRINTFORML 「我在这里说的『宝塔』是指非常有价值的器物、道具」
		PRINTFORML 「而且『宝塔』是您和我都有的东西。是不可能丢失或掉落的」
		PRINTFORMW 「我们唯一的财产，最初也是最后的财产…也就是说，它指的是『身体』」
		PRINTFORML 
		PRINTFORML 「是不是说得有点夸张了？ 不不，绝不是那样」
		PRINTFORMW 「是啊，让我们来列举一下『身体』的奇妙作用吧」
		PRINTFORML 
		PRINTFORML 「在头脑中思考分辨，在胸中涌起感情」
		PRINTFORML 「将其收纳储存在腹中，实际动手脚采取行动」
		PRINTFORMW 「然后作为一个整体进行协同作业，完成各种各样的事情」
		PRINTFORML 
		PRINTFORML 「更厉害的是，即使各个部分无法协调，『身体』也能运作」
		PRINTFORML 「头脑不行就用胸，胸不行就用腹，腹不行就用手脚」
		PRINTFORMW 「即使思考、感情和行动不一致，『身体』也能勉强动起来。您有没有想到什么呢？」
		PRINTFORML 
		PRINTFORML 「大约在这个世界上，还有其他具备如此多功能的道具吗？」
		PRINTFORMW 「如果好好使用，几乎没有什么做不到的事情呢」
		PRINTFORML 
		PRINTFORML 「在某部经文中，记载了多宝如来和释迦牟尼佛并排坐在宝塔中的场景」
		PRINTFORML 「在这个场景中，多宝如来代表真理，释迦牟尼佛代表智慧」
		PRINTFORML 「而『宝塔』我认为它象征着我们的『身体』」
		PRINTFORML 「我们的这个『身体』，如果正确使用，就能取出一切真理、一切智慧……」
		PRINTFORMW 「如此珍贵的东西，我们从出生起就拥有了」
		PRINTFORML 
		PRINTFORML 「所以您也，塔中的宝物，『身体』的价值，一点一点地取出吧，即使只是一点点也好」
		PRINTFORML 「特别是您拥有连我也无法估量的力量吧。这就意味着您拥有如此多的宝物」
		PRINTFORMW 「然而您却…不是只在女性的肢体上寻找宝物吗？ 这真是太可惜了」
		PRINTFORMW 「只有正确使用自己的『身体』，才能取出真正有价值的宝物」
	CASE 4
		PRINTFORMW 「是啊…您对『苦难』有什么看法？」
		PRINTFORML 
		PRINTFORML 「经常有人说『年轻人即使花钱也要多吃苦』或者『不知道苦难的少爷不管用』等等，但是」
		PRINTFORMW 「当然实际上，比起没有经历苦难的人，经历过苦难的人更有深度，这是肯定的」
		PRINTFORML 
		PRINTFORML 「但是，这并非绝对。也有人在经历苦难的过程中完全被苦难打败了」
		PRINTFORML 「变得狡猾，变得卑屈，看到那些没有经历苦难的人就会变得有攻击性…」
		PRINTFORMW 「您有没有遇到过这样的人呢？ 为什么人会变成这样呢？」
		PRINTFORML 
		PRINTFORML 「从阎魔来看，有苦难不如没有苦难，那样肯定是更好的」
		PRINTFORML 「但是只要活在现世就会有苦难，除了死亡就没有任何逃避的方法」
		PRINTFORML 「所以，人必须面对苦难，培养超越它的力量」
		PRINTFORML 「如果培养了超越的力量，即使有苦难也如同没有一样。就不会在意了」
		PRINTFORMW 「也就是说，经历苦难不是为了苦难本身，而是为了超越苦难一样，请记住这一点」
		PRINTFORML 
		PRINTFORML 「刚才说的那些被苦难打败的人。这些人大多是忘记了经历苦难的目的的人」
		PRINTFORML 「遇到痛苦的事情时，要看清它的原因和性质并将其去除。经历苦难，就是培养这种超越力量的考验」
		PRINTFORMW 「如果不知道这一点，把痛苦的事情往后推，或者只是低下头颅忍耐，最终都会被苦难打败」
		PRINTFORML 
		PRINTFORML 「以前在我审判过的灵魂中，有一个这样断言的人」
		PRINTFORML 「人世间真是苦海，而自己很早就意识到了这一点。所以在活着的时候，一直沉浸在修行中」
		PRINTFORMW 「于是故意穿着破烂的衣服，用火炙烤身体，在贫困中死去。所以自己有资格转生到天界」
		PRINTFORML 
		PRINTFORML 「在净玻璃镜中映照出来，他确实只依靠人们的施舍获得最低限度的衣食，之后就沉浸在折磨身心的严酷修行中」
		PRINTFORML 「……为什么不能这么想呢？如果说得严厉一点，这只不过看似是了不起的行为」
		PRINTFORML 「这个灵魂生前，是依靠周围人们的施舍这种善行活着的。然而他想的却只是自己来世的幸福」
		PRINTFORMW 「因为他把修行这种苦难当作了目的而不是手段，结果最终也没能充分积累善行呢」
		PRINTFORML 
		PRINTFORML 「当然，逃避苦难躲到轻松的地方也是不行的。那只是堕落」
		PRINTFORML 「但话虽如此，死守着苦难，被它侵蚀，也不是正确的人生方式」
		PRINTFORML 「不被苦难侵蚀，将苦难作为反思的材料，努力从中一点点地体悟人生」
		PRINTFORMW 「这样一来，将人生的全貌，更深刻地、更广泛地，通过知识和经验去了解，才能不被苦难束缚，才能说是一个通过苦难获得价值的人呢」
	CASE 5
		PRINTFORMW 「是啊…您听到『因果』会想到什么？」
		PRINTFORML 
		PRINTFORML 「这个词被使用的时候，大多是遭遇人生不幸或黑暗面的时候」
		PRINTFORML 「虽然有『父母的因果报应在子女身上』的说法，但从未听过『父母的因果让子女飞黄腾达』」
		PRINTFORML 「另外，也有因果报应这个词，这也同样被这样理解」
		PRINTFORMW 「往往被用来指因为过去，或者前世的恶行而受到恶报」
		PRINTFORML 
		PRINTFORML 「这样列出来似乎是个不好的词，但本来并非是这样限定的含义」
		PRINTFORML 「『因果』指的是原因和结果。这是自然的道理，对人类既不亲切也不冷酷」
		PRINTFORMW 「任何事情都必然有原因。无因之果是万中…亿中无一的」
		PRINTFORML 
		PRINTFORML 「那么，您能想象出什么样的原因会产生什么样的结果吗」
		PRINTFORML 「不用想得太复杂。原因和结果的关系很简单」
		PRINTFORML 「善行会产生幸福的结果。这就是善因善果」
		PRINTFORMW 「恶行会产生不幸的结果。这就是恶因恶果」
		PRINTFORML 
		PRINTFORML 「这经常被比作播种。在田里播下萝卜种子就会长出萝卜芽，播下西瓜种子就会长出西瓜芽，对吧」
		PRINTFORML 「这和那个一样。善行只会产生善果，恶行只会产生恶果，这是必然的」
		PRINTFORML 「我平时教导大家要积累善行，原因就在于此。因为自己行为的报应，是由自己获得的」
		PRINTFORMW 「这正是自作自受啊。好的事情和坏的事情，一切都始于自己」
		PRINTFORML 
		PRINTFORML 「既然把『因果』比作播种，那就再举一个例子…是啊，再来种西瓜吧」
		PRINTFORMW 「在田里播下西瓜种子就会长出西瓜。这是自然的道理，理所当然」
		PRINTFORML 
		PRINTFORML 「但请您想想。即使知道道理，也不是谁都能种出西瓜，对吧」
		PRINTFORML 「吃完的西瓜籽随便一撒，又长出西瓜又吃…这种事可没那么顺利」
		PRINTFORMW 「要让种子确实发芽，需要用正确的知识好好打理」
		PRINTFORML 
		PRINTFORML 「同样地，善因-善果，善行到幸福的结果，也需要正确的知识和步骤」
		PRINTFORML 「如果没有知识，本以为是善行，结果却是办了坏事，这种事就会发生吧」
		PRINTFORML 「如果不理解因和果的联系，就会抱怨明明这么努力了却没有结果吧」
		PRINTFORML 「播下西瓜种子却长出萝卜，这种事是绝对不会发生的，但总会有人这样坚持」
		PRINTFORMW 「这样的人正是因为没有知识，才会犯下那样的错误」
		PRINTFORML 
		PRINTFORML 「所以人必须不断学习」
		PRINTFORML 「从自己、与自己相关的事情、自己重要的人那里加深理解」
		PRINTFORMW 「这样一来，即使面对未知的『因果』，也能自然而然地采取正确的行动了」
ENDSELECT
CALL K30_D_LINE
SIF DIARY:30:16 == 0 && CFLAG:30:1701 > 9
	CALL K30_WRITE_DIARY(16)
RESULT = 0
PRINTFORMW
PRINTFORMW 「您觉得如何？如果这段话能对您积累善行有所帮助的话，我也很高兴」
IF CFLAG:掌握把柄 && !GETBIT(CFLAG:1803, 1)
	FORCEWAIT
	PRINTFORMW 「……………………」
	PRINTFORMW 「话说回来%K30_C_NAME(MASTER)%」
	PRINTFORMW 「有什么事要向我道歉吗……？」
	$INPUT_LOOP
	RESETCOLOR
	CALL ASK_M("…那件事对不起",1 , "咦？什么事", 1, "…我知道了，别说这件事了。好了！！别别别", 1, "咦！？这个选项要干嘛？", 1)
	CALL SET_KOJO_COLOR(30)
	SELECTCASE RESULT
		CASE 0
			PRINTFORMDW 自己心里有数，所以姑且先道个歉
			PRINTFORML 「…我没看到诚意呢。话说回来，你自己知道在为什么道歉吗？」
			PRINTFORML 「嘛，为了惩罚像您这样的人，我可是随身带着这块笏板的」
			PRINTFORMDW 映姬将手中拿着的悔悟棒刺向%CALLNAME:MASTER%…
			PRINTFORML 
			PRINTFORML 「您那些无法在镜中清晰显现的罪孽，将由我在记忆范围内进行裁决…」
			PRINTFORML 「当然对此没有异议吧？」
			RESETCOLOR
			CALL ASK_M("是", 1, "没有", 1, "当然", 1)
			CALL SET_KOJO_COLOR(30)
			PRINTFORML 「那真是太好了。那么…」
			PRINTFORMDW 映姬拿出笔在悔悟棒上写了起来…
			IF GETBIT(CFLAG:1699, 1)
				PRINTFORML 
				PRINTFORML 「盗取胖次之罪」
				PRINTFORMW 「到底是用来干什么的」
				SIN_VAL ++
			ENDIF
			IF GETBIT(CFLAG:1699, 2)
				PRINTFORML 
				PRINTFORML 「公共场所沉溺于通奸之罪」
				PRINTFORMW 「给我学会自制」
				SIN_VAL ++
			ENDIF
			;私室ヤリ部屋化、睡姦バレは口上で拾えないのでこの辺ガバガバ
			;仕事場ヤリ部屋化のぶんでさらにガバガバガーバ。仕事案本体取り込まれたら書き直す
			IF (CFLAG:掌握把柄 - CFLAG:1700 - CFLAG:1698) > 15
				PRINTFORML 
				IF CFLAG:MASTER:初期位置 == CFLAG:30:初期位置
					PRINTFORML 「在咱俩的房间里和其他女性沉溺于通奸之罪」
					PRINTFORML 「是的，在我们重要的爱巢里…」
					PRINTFORMDW 拿着笔的手气得发抖…
					SIN_VAL += 100
				ELSE
					PRINTFORML 「在我房间里沉溺于通奸的罪」
					SIF TALENT:恋人
						PRINTFORML 「而且，已经有我这个恋人…」
					PRINTFORMW 「真是…真是真是！不可原谅！」
					SIN_VAL ++
					SIF TALENT:恋人
						SIN_VAL += 100
				ENDIF
			ELSEIF (CFLAG:掌握把柄 - CFLAG:1700 - CFLAG:1698)
					PRINTFORML 
					PRINTFORML 「骚扰正在就寝的女性之罪」
					PRINTFORMW 「这是非常卑鄙可耻的行为」
			ELSEIF GETBIT(CFLAG:1699, 3)
				PRINTFORML 
				SIF TALENT:恋人
					PRINTFORML 「…而且有我这个恋人」
				PRINTFORML 「在我房间里沉溺于通奸之罪」
				PRINTFORMW 「真是…真是真是！不可原谅！」
				SIN_VAL ++
				SIF TALENT:恋人
					SIN_VAL += 100
			ENDIF
			PRINTFORML 
			PRINTFORML 「…以此判定您有罪」
			PRINTFORML 「这根笏的重量，就是您罪孽的重量」
			PRINTFORMW 「好好尝尝这痛苦，彻底了解自己的罪孽深重吧」
			PRINTFORML 
			PRINTFORMDL 映姬举起了写完的悔悟棒…
			IF SIN_VAL < 100
				PRINTFORMDL 从她双手拿着的样子来看，似乎相当重。
				PRINTFORMDW 可能需要有点心理准备
				LOCAL = 4
			ELSE
				PRINTFORMDL 从她举起来的样子来看，重量非同寻常
				PRINTFORMDW 这次裁决明显带着了个人情绪！
				LOCAL = 6
			ENDIF
			PRINTFORML 
			PRINTFORMW 「用这制裁来为自己的罪孽…」
			SETCOLOR 206, 8, 21
			PRINTFORML 
			PRINTFORML 「忏悔吧！！！」
			CALL K30_QUAKE(LOCAL)
			PRINTFORMW 
			CALL SET_KOJO_COLOR(30)
			PRINTFORMDW 被悔悟棒狠狠地打倒了…
			IF SIN_VAL > 100
				LOCAL = BASE:MASTER:体力 + 1
				TCVAR:30:371 = 2
			ELSE
				LOCAL = BASE:MASTER:体力 / 4
				TCVAR:30:371 = 1
			ENDIF
			CALL RECOVER(MASTER, -(LOCAL), "体力", "悔悟棒的绝强一击")
			LOCAL = GETBIT(CFLAG:1699, 0) ? 1 # 0
			CFLAG:掌握把柄 = 0
			SETCOLOR C_YELLOW
			PRINTFORML 把柄全部被打消了
			CALL SET_KOJO_COLOR(30)
			VARSET CFLAG:30:0, 0, 1698, 1701
			SIF LOCAL
				SETBIT CFLAG:1699, 0
			IF BASE:MASTER:体力 <= 500
				IF !TALENT:恋慕
					PRINTFORMDL 在毫不留情的一击下眼前一片漆黑…
					IF SIN_VAL < 100
						PRINTFORML 「哎、哎？ 是不是稍微有点过头了？」
						PRINTFORMDL 映姬稍稍为难的嘟囔…在脑海中，嗡嗡地…回响…
					ELSE
						PRINTFORML 「你就好好反省吧」
						PRINTFORMDL 映姬无情地离开了现场…
					ENDIF
				ENDIF
			ELSE
				PRINTFORMDL 虽然挨了毫不留情的一击，但总算站稳了…
				PRINTFORMW 「把那份痛苦当作反省的食粮，今后要努力行善哦」
			ENDIF
			IF DIARY:30:10 == 0
				CALL K30_WRITE_DIARY(10)
				SIF BASE:MASTER:体力 <= 500
					SETBIT CFLAG:1910, 3
			ENDIF
			RETURN 1
		CASE 1
			PRINTFORMDW …悔悟棒微微一颤。映姬似乎有点恼火了
			PRINTFORML 「如果您说不记得了…那也没办法」
			PRINTFORML 「往空空的东西里敲打，效果似乎也不大呢」
			PRINTFORMW 「…等您好好想起来了，到时候我们再谈吧」
			GOTO IGNORE
		CASE 2
			PRINTFORML 「……………」
			PRINTFORMDW 拿着悔悟棒的手颤抖着。也许稍微刺激映姬过度了…
			PRINTFORML 「…如果您这么说，那也没关系」
			PRINTFORML 「您只是错过了弥补罪过的机会。总有一天它会到来」
			PRINTFORMW 「为了到时候不后悔，请好好积累善行」
			SETBIT CFLAG:1803, 1
			GOTO IGNORE
		CASE 3
			PRINTFORML 
			PRINTFORMDL 这是选择是否开启用悔悟棒的一顿训斥来消除『把柄』的事件
			PRINTFORMDL 从上往下依次是允许事件、这次先不触发下次再选择、以及之后都不再出现此事件
			PRINTFORMDL 最大伤害是体力上限→当天无法行动的程度
			PRINTFORML 
			PRINTFORMDL 保留或选择不显示的选项中会话可能有点负面
			PRINTFORMDL 但是都不会产生不好的影响
			PRINTFORMW 
			PRINTFORML 「…怎么了？ 好好想起来了吗？」
	ENDSELECT
	GOTO INPUT_LOOP
ELSE
	$IGNORE
	PRINTFORMDW 陪着听了讲法，映姬的好感度和信赖度\@ RESULT ? 真的只是 # \@稍微上升了一点
	LOCAL = RESULT
	CALL CHANGE_CFLAG(2, TARGET, RAND:5 + 7 - RESULT)
	CALL CHANGE_CFLAG(4, TARGET, RAND:3 + 3 - RESULT)
	SELECTCASE RAND:3
		CASE 0
			LOCALS = 非常难得
		CASE 1
			LOCALS = 受益匪浅
		CASE 2
			LOCALS = 意味深长
	ENDSELECT
	CALL RECOVER(MASTER, -100, "気力", @"%LOCALS%但是很长很长的说教")
	SOURCE:反感 = 0
	SOURCE:鬱屈 = 0
	SOURCE:歓楽 += ABL:30:親密 * 50
	SOURCE:情愛 += 1500
	RETURN LOCAL
ENDIF
RETURN 1

;==================================================
;角色介绍文関数。早苗口上を参考にしてます
;--------------------------------------------------
@CHARA_INFO_KOJO_K30
;紹介文前半は花映塚角色介绍文本とか参考に
PRINTFORML %CSVCSTR(30, 10)%
PRINTFORML 
PRINTFORML 居住在地狱，裁决死者的神明。每天的大部分时间都花费在死者的审判和地狱的管理工作上。
PRINTFORML 性格非常认真。但是，由于工作的影响稍微有点爱说教。
PRINTFORML 例如，如果眼前出现了身负罪孽的人类（或者妖怪，总之是谁都行），阎魔大人绝对不会放过。
CALL COLORMESSAGE("个人爱好是", 0XD5DEDF, 0, 4)
PRINTFORM 花费宝贵的时间
CALL COLORMESSAGE("絮絮叨叨地", 0XD5DEDF, 0, 4)
PRINTFORML 给人灌输一大堆难得的道理和教诲。
SIF !CFLAG:30:面識
	RETURN 1
PRINTFORML 
;恋慕取得前
IF !TALENT:30:恋慕
	IF TALENT:30:炮友 || TALENT:30:風騷 || TALENT:30:愛欲
		PRINTFORML 对自己身为阎魔却做出如此淫乱的行为，内心深感罪恶…
	ELSEIF TALENT:30:思慕
		PRINTFORML 眼下的烦恼是，本应不受任何人影响、毫不犹豫的自己，却被某个人完全迷住了。
	ELSEIF STRCOUNT(CSTR:30:0, "変Ｔお披露目")
		PRINTFORM 目前烦恼的事情似乎是，最近引进的阎魔制服过于
		CALL COLORMESSAGE("奇特", 0XD5DEDF, 0, 4)
		PRINTFORML ，穿着很不自在…
	ELSE
		PRINTFORML 最近认识了一个用净琉璃之镜看不清的人。
	ENDIF
	PRINTFORML
	;無自覚孕ませ
	IF CFLAG:1805
		CALL K30_UNWANTED_PREGNANCY
	ENDIF
	RETURN 1
ENDIF
;恋慕取得後
PRINTFORML 接受了受他人影响而变化的自己，开始想要珍惜与%\@ TALENT:30:恋人 ? 恋人 # 心上人 \@%共度的时光。

;推倒并实行后，暗示独自事件。文章的措辞很难把握
IF CFLAG:30:1801 && STRCOUNT(CSTR:30:0, "推倒")
	PRINTFORML 同时也因为自己跨越了底线，所以希望%\@ TALENT:30:恋人 ? 恋人 # 心上人 \@%能够接受自己至今为止一直压抑的全部欲望…
	IF GETBIT(CFLAG:30:1910, 7)
		PRINTFORML 　・似乎想要%CALLNAME:MASTER%尽情疼爱被%CALLNAME:MASTER%亲手调教出的菊穴…
	ELSE
		PRINTFORML 　・似乎想要%CALLNAME:MASTER%尽情疼爱自己的屁股…
	ENDIF
	SIF ABL:30:露出癖 > 4 && CFLAG:30:1607 > 10
		PRINTFORML 　・%CALLNAME:MASTER%进攻自己的屁股，让自己喷潮的感觉非常棒，似乎已经变成癖好了…
	SIF !STRCOUNT(CSTR:30:0, "朝から") && ABL:30:精液中毒 > 2
		PRINTFORML 　・\@ CFLAG:MASTER:初期位置 == CFLAG:30:初期位置 ? 如果情绪高涨，似乎想要用『特别』的 # 如果能和%CALLNAME:MASTER%同居，早上会就用『特别』的 \@方式叫醒%CALLNAME:MASTER%…
	SIF CFLAG:30:信賴度 >= 2000 && !STRCOUNT(CSTR:30:0, "キメセク") && CFLAG:30:允许无套 == 2
		PRINTFORML 　・似乎想要和%CALLNAME:MASTER%身心交融，即使被骗喝下奇怪的茶也愿意…
	SIF TALENT:MASTER:恋人 != 76 && TALENT:76:恋慕 && GET_UWAKI_HISTORY(30, 76) == 2 && ABL:30:精液中毒 > 3 && ABL:76:精液中毒 > 3
		PRINTFORML 　・似乎想要%CALLNAME:MASTER%像自己一样疼爱小町，希望两人一起被爱和承担侍奉…
	SIF TALENT:30:母乳体質 && !CFLAG:30:1802 && !STRCOUNT(CSTR:30:0, "授乳PLAY")
		PRINTFORML 　・似乎偷偷地想着，要让%CALLNAME:MASTER%含着自己的乳头，然后尽情地宠溺他…
ENDIF
PRINTFORML 
;無自覚孕ませ
IF CFLAG:1805
	CALL K30_UNWANTED_PREGNANCY
	RETURN 1
ENDIF
;怀孕愿望。即使是恋慕后，考虑到时奸妊娠之类的鬼畜情节，也会变成难以理解的分支
;愿望再次取得时
IF GETBIT(CFLAG:30:1502, 4)
	PRINTFORML 渴望再次拥有与%CALLNAME:MASTER%的孩子。
	PRINTFORML 
	RETURN 1
ELSEIF TALENT:30:妊娠願望
	PRINTFORML 梦想着与%CALLNAME:MASTER%拥有孩子的那一天。
	PRINTFORML 
	RETURN 1
;怀孕愿望再次取得可
ELSEIF GETBIT(CFLAG:30:1502, 3) && !TALENT:30:育児中 && !TALENT:30:妊娠
	PRINTFORML 期待着如果再次[祈愿]的话，是不是就能拥有孩子。
	PRINTFORML 
	RETURN 1
ENDIF
SIF !INRANGE(CFLAG:30:1502, 1, 7) || TALENT:30:育児中 || TALENT:30:妊娠 || EXP:30:出産経験
	RETURN 1
;bit演算を十進数で捉えてCASE分け
SELECTCASE CFLAG:30:1502
	CASE 1
		PRINTFORML 虽然身体已经允许%CALLNAME:MASTER%进入，但没有成为母亲的自信。
		PRINTFORML 　・想着要是能更加[信赖]%CALLNAME:MASTER%就好了…
		PRINTFORML 　・阎魔大人想要孩子之类的，即使[祈愿]也很难实现吧…
	CASE 2
		PRINTFORML 因为%CALLNAME:MASTER%坦白说想要孩子而动摇。
		PRINTFORML 　・想着是不是太着急了…明明还没有允许他跨过最后一步，
		PRINTFORML 　・要是能更加[信赖]%CALLNAME:MASTER%就好了…
	CASE 3
		PRINTFORML 正在犹豫是否真的想要%CALLNAME:MASTER%的孩子。
		PRINTFORML 　・要是能更加[信赖]%CALLNAME:MASTER%就好了…
	CASE 4
		PRINTFORML 已经开始隐约考虑与%CALLNAME:MASTER%的孩子的事情。
		PRINTFORML 　・想着是不是太着急了…明明还没有允许他跨过最后一步，
		PRINTFORML 　・阎魔大人想要孩子之类的，即使[祈愿]也很难实现吧…
	CASE 5
		PRINTFORML 已经明确地开始考虑与%CALLNAME:MASTER%的孩子的事情。
		PRINTFORML 　・阎魔大人想要孩子之类的，即使[祈愿]也很难实现吧…
	CASE 6
		PRINTFORML 无法下定决心与%CALLNAME:MASTER%造孩子。
		PRINTFORML 　・还在犹豫要不要允许他跨过最后一步…
	CASE 7
		PRINTFORML 迷茫已经烟消云散，终于下定决心与%CALLNAME:MASTER%孕育孩子。
ENDSELECT
RETURN 1

@K30_UNWANTED_PREGNANCY
IF INRANGE(CFLAG:妊娠経過日数, 1, 19)
ELSEIF INRANGE(CFLAG:妊娠経過日数, 20, 59)
	PRINTFORML 故意对自身身体的变化视而不见
ELSEIF CFLAG:妊娠経過日数
	PRINTFORML 对怀上不知是谁的孩子的异常事态感到恐惧战栗
ELSEIF CFLAG:出産経過日
	PRINTFORML 被罪恶感折磨，只能依靠必须好好养育%CSTR:30:83%来支撑自己
ELSE
	CALL COLORMESSAGE, "将自己置于严酷的惩罚之下，身体再也无法怀孕", C_RED, 1, 1
ENDIF
RETURN

;==================================================
;映姫様の尻穴と抵抗を緩くするEXTRASOURCE関数群
;豊姫口上参考にしました
;--------------------------------------------------
;COMF4 舐肛
@M_KOJO_EXTRASOURCE_COM_K30_4
CALL K30_LESS_RESISTANCE_ANAL

;COMF5 肛門愛撫
@M_KOJO_EXTRASOURCE_COM_K30_5
CALL K30_LESS_RESISTANCE_ANAL

;COMF45 肛用振動棒
@M_KOJO_EXTRASOURCE_COM_K30_45
CALL K30_LESS_RESISTANCE_ANAL

;COMF62 正常位肛交
@M_KOJO_EXTRASOURCE_COM_K30_62
CALL K30_LESS_RESISTANCE_ANAL

;COMF63 後背位肛交
@M_KOJO_EXTRASOURCE_COM_K30_63
CALL K30_LESS_RESISTANCE_ANAL

;COMF66 騎乗位肛交
@M_KOJO_EXTRASOURCE_COM_K30_66
CALL K30_LESS_RESISTANCE_ANAL

;COM74(SCOMF14) 玩弄Ｓ状結腸
@M_KOJO_EXTRASOURCE_SCOM_K30_14
CALL K30_LESS_RESISTANCE_ANAL

;COM75(SCOMF15) 隔着刺激陰道
@M_KOJO_EXTRASOURCE_SCOM_K30_15
CALL K30_LESS_RESISTANCE_ANAL

;SCOMF16 肛門交互挿入
@M_KOJO_EXTRASOURCE_SCOM_K30_16
CALL K30_LESS_RESISTANCE_ANAL

;COMF314 肛門愛撫
@M_KOJO_EXTRASOURCE_COM_K30_314
CALL K30_LESS_RESISTANCE_ANAL

;COMF318 動起腰身（Ａ）
@M_KOJO_EXTRASOURCE_COM_K30_318
CALL K30_LESS_RESISTANCE_ANAL

;COMF323 对方主導後背位Ａ
@M_KOJO_EXTRASOURCE_COM_K30_323
CALL K30_LESS_RESISTANCE_ANAL

;軽減処理関数
@K30_LESS_RESISTANCE_ANAL
#DIM DIVIDED
SIF !(TALENT:思慕 || TALENT:恋慕 || TALENT:炮友 || TALENT:愛欲)
	RETURN
SOURCE:逸脱 /= 4
SOURCE:不潔 /= 6
SIF TALENT:心情 == -1 || MARK:反発刻印
	RETURN
DIVIDED = 3 + (TALENT:恋慕 * 2) - (K30_BE_SEEN() * 2)
SOURCE:鬱屈 = SOURCE:鬱屈 / DIVIDED
SOURCE:反感 = SOURCE:反感 / DIVIDED
RETURN
;==================================================
;映姫様が仕事中に口交抜きしてくれるEXTRASOURCE関数群
;本体側改造しない都合で1ターンで你を発射させる仕様
;仕事中ならどうしても我慢できないのを手早く済ます…というシチュとしてごまかす
;--------------------------------------------------
;COMF300 会話
@M_KOJO_EXTRASOURCE_COM_K30_300
IF K30_CHECK_BLOW_JOB()
	CALL K30_SET_BLOW_JOB
	TFLAG:193 = -999
;ELSEIF K30_AFTERSEX_FELLA()
;	CALL K30_SET_BLOW_JOB
;	TFLAG:193 = -900
ENDIF
RETURN 1

;COMF304 幫忙干活
@M_KOJO_EXTRASOURCE_COM_K30_304
IF K30_CHECK_BLOW_JOB() && BASE:30:工作量 > 0
	CALL K30_SET_BLOW_JOB
	DOWNBASE:MASTER:体力 -= 50
	DOWNBASE:MASTER:気力 -= 180
	;経過時間の減少と工作量を戻す
	TIME -= 50
	BASE:工作量 = TFLAG:194
	TFLAG:193 = -999
ENDIF

;仕事中口交抜き判別用式中関数
@K30_CHECK_BLOW_JOB()
#FUNCTION
SIF K30_BE_SEEN() || !仕事中チェック(30) || INRANGE(TIME, 540, 719) || CFLAG:職種 != 43
	RETURNF 0
SIF !SHIRAHU(30) || !TALENT:恋慕 || MARK:反発刻印 || TALENT:心情 == -1
	RETURNF 0
SIF ABL:技巧 < 3 || ABL:精液中毒 < 5 || EXP:精飲経験 < 100
	RETURNF 0 
SIF !GETBIT(CFLAG:30:既成事實, 1) || TALENT:妊娠願望 || GETBIT(CFLAG:1502, 4)
	RETURNF 0
SIF !(TALENT:PLAYER:2 & 2) || 挿入不可(30) == 3 || K30_CHECK_HEN_T()
	RETURNF 0
SIF !(TCVAR:発情 || CFLAG:积攒度 > 850 || TCVAR:媚薬)
	RETURNF 0
SIF !ONCE("口交抜き")
	RETURNF 0
;ここまで抜けたら確定
RETURNF 1

;口交抜きソース処理関数
@K30_SET_BLOW_JOB
CALL COM81
;映姫様と你を同時絶頂させる
TCVAR:MASTER:要去了 = 1
BASE:MASTER:射精 = MAXBASE:MASTER:射精
PALAM:30:4 = MAX(PALAMLV:4, PALAM:30:4)
SOURCE:反感 = 0

;==================================================
;映姫様にオーバードーズする関数群
;媚薬成分でうふふアスリートになれるよう頑張ってみました
;--------------------------------------------------
;キメセク判定用式中関数
@K30_KIMESEKU()
#FUNCTION
#DIM YANUSHI
IF !CFLAG:诶嘿嘿
	SIF ITEM:媚薬 < 2
		RETURNF 0
	;押し倒され.ERB から条件コピペ
	FOR COUNT, 1, CHARANUM
		SIF COUNT == 30
			CONTINUE
		SIF CAN_MOVE(CFLAG:MASTER:現在位置, CFLAG:COUNT:現在位置) == 2
			RETURNF 0
	NEXT
ENDIF
SIF TALENT:心情 < 0 || MARK:30:反発刻印 || !TALENT:恋慕 || K30_CREAMPIE() || FLAG:99
	RETURNF 0
SIF !GETBIT(CFLAG:既成事實, 1) || CFLAG:妊娠自覚状態 || (STRFIND(CSTR:30:0, "推倒") ==-1 && CFLAG:30:1801)
	RETURNF 0
SIF CFLAG:信賴度 < 2000 || !CHARA_HOLIDAY(30) || K30_CHECK_HEN_T()
	RETURNF 0
SIF ROOMDATA:(CFLAG:MASTER:現在位置 % 100) & 場所_公共の場
	RETURNF 0
SIF !GROUPMATCH(PRIVATEROOM:(CFLAG:現在位置 % 100), 0, 30)
	RETURNF 0
RETURNF 1

;キメセク用BUFF関数
;陥落後の茶番なのでバフ盛り盛り・自重しない
@K30_KIMESEKU_BUFF
IF !CFLAG:诶嘿嘿
	CALL BUFF_BASE(TARGET, BASE_気力,666)
	CALL BUFF_BASE(TARGET, BASE_体力,666)
	BASE:MASTER:勃起 = MAXBASE:MASTER:勃起
	BASE:30:理性 /= 10
	CFLAG:30:积攒度 = MIN(CFLAG:30:积攒度 + 666, 1000)
	BASE:30:情緒 = MIN(BASE:30:情緒 + 666, 1200)
	TCVAR:30:媚薬 = 630
	CFLAG:30:同行 = 60
	CFLAG:MASTER:同行 = 60
ENDIF
SIF PALAM:潤滑 < PALAMLV:6
	PALAM:潤滑 = PALAMLV:6
SIF PALAM:欲情 < PALAMLV:9
	PALAM:欲情 = PALAMLV:9
TEQUIP:MASTER:結界発動 = 1
SETBIT TCVAR:30:367, 2
TCVAR:30:発情 = 1
IF !TALENT:淫乱
	TALENT:淫乱 = 1
	SETBIT TCVAR:30:367, 1
ENDIF
TEQUIP:30:子宮 = 1
SIF EXP:子宮口開発経験 > 49 && ABL:膣 > 1
	TEQUIP:30:子宮 = 2
;キメセク用押し倒し関数
@K30_KIMESEKU_PUSHDOWN
CFLAG:MASTER:诶嘿嘿 = 2
CFLAG:30:诶嘿嘿 = 2
TFLAG:COMABLE管理 = 3
TCVAR:30:随便 = 1
SETBIT TCVAR:30:367, 3
CALL CTRL_CLOTHES_SET(30, "現在衣装の変更_全裸")
CALL CTRL_CLOTHES_SET(MASTER, "現在衣装の変更_全裸")
CALL CLOTHES_SETTING_TRAIN(30)
CALL CLOTHES_SETTING_TRAIN(MASTER)
COM_STR = 
;==================================================
;複数恋人を制御する関数
;TALENT:MASTER:恋人が変動する仕様変更に対応できないため
;--------------------------------------------------
;ARG	状況分類(0=破局チェック（ターン毎） 1=デート後告白イベント制御 2=告白するコマンド制御
;				 3=破局後チェック
;--------------------------------------------------
@K30_DENY_POLYAMORY(ARG)
;↓のコメントアウト外すと制御解除。エラーは出ないはずだけど@K30_C_NAME関連で变なことにはなる
SIF !CFLAG:1801 || TALENT:30:妻 ;custom code, skip the check if married
	RETURN 0
SELECTCASE ARG
	CASE 0
		SIF !TALENT:30:恋人
			RETURN
		SIF TALENT:MASTER:恋人 == 30
			RETURN
		CLEARBIT CFLAG:30:既成事實, 2
		TALENT:30:恋人 = 0
		SETBIT CFLAG:30:1505, 1
		PRINTFORML 
		PRINTFORMDW …在%CALLNAME:MASTER%得到了新的恋情的同时，又觉得有什么东西在某处破碎了。
		CALL COLORMESSAGE, "和映姬的恋人关系结束了", C_YELLOW, 2, 1
	CASE 1
		IF TALENT:MASTER:恋人
			TCVAR:30:中止接吻 = 1
			SETBIT CFLAG:30:约会後イベントフラグ, 1
		ELSEIF !(TALENT:MASTER:恋人 || TALENT:30:恋人)
			CLEARBIT CFLAG:30:约会後イベントフラグ, 1
		ENDIF
	CASE 2
		SIF !TALENT:MASTER:恋人
			RETURN 0
		SIF GETBIT(CFLAG:1505, 1)
			RETURN 2
		RETURN 1
	CASE 3
		SIF GETBIT(CFLAG:30:1505, 1)
			RETURN 1
ENDSELECT
