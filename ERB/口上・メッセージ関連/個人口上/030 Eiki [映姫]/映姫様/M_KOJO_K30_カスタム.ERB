;================================
;custom shop button
;カスタムSHOPボタン
;y is the id of the button, range from 0 to 9
;yはボタンのID、0~9に対应
;使用する際はSKIPSTART~SKIPENDを外してください。
;================================
;judge the button can be shown, if return 1, it will show
;これをRETURN 1するとボタンが表示される
;4975不再需要
[SKIPSTART]
@KOJO_CUSTOM_BUTTON_CONDITION_K30_0
#LOCALSIZE 1
#LOCALSSIZE 1
SIF !GETBIT(CFLAG:30:1803, 8)
	RETURN 0
custom_button_name:30:0 = 变更台词颜色
RETURN 1

;when the button is clicked
;ボタンがクリックされた场合
@KOJO_CUSTOM_BUTTON_K30_0
#LOCALSIZE 1
#LOCALSSIZE 5
#DIM K_COLOR = 0x337D5C, 0x52C487, 0x7FD8A7, GETDEFCOLOR(), 0xFF93FB
LOCALS '= "冷静的", "普通的", "稍微高调的", "无个性的", "色情的"
LOCAL = FINDELEMENT(K_COLOR, CFLAG:30:1501, 0)
LOCAL = INRANGE(LOCAL, 0, 3) ? LOCAL + 1 # 0
CFLAG:30:1501 = K_COLOR:LOCAL
SETCOLOR CFLAG:30:1501
PRINTFORML 「已更改为%LOCALS:LOCAL%声音」
RESETCOLOR
RETURN 1

@KOJO_备份_CUSTOM_BUTTON_CONDITION_K30_1
#LOCALSIZE 1
#LOCALSSIZE 1
custom_button_name:30:1 = 切换剧本
RETURN 1

@KOJO_备份_CUSTOM_BUTTON_K30_1
#LOCALSIZE 1
#LOCALSSIZE 1
CFLAG:30:口上セレクタ = 0
PRINTFORML 「变为新译版了」

@KOJO_CUSTOM_BUTTON_CONDITION_K30_1
#LOCALSIZE 2
#LOCALSSIZE 1
custom_button_name:30:1 = 切换剧本
LOCAL = RESULT
RESULT= 0
TRYCALL M_KOJO_K30_4
LOCAL:1 = RESULT
RESULT = LOCAL
RETURN LOCAL:1

@KOJO_CUSTOM_BUTTON_K30_1
#LOCALSIZE 1
#LOCALSSIZE 1
CFLAG:30:口上セレクタ = 4
PRINTFORML 「变为接力翻译版了」
[SKIPEND]

;================================
;カスタム素質
;テンプレの便宜上、独立した関数としてまとめました。
;使用する際はSKIPSTART~SKIPENDを外してください。
;================================
@KOJO_CUSTOM_TALENT_SET_K30

;表示判定 真=表示/偽=非表示
CUSTOM_TALENT:30:0 = GETBIT(CFLAG:1910, 7)
;素質名
CUSTOM_TALENT_NAME:30:0 = 纵裂菊穴
;(省略可)表示色、0/通常 1/ピンク 2/赤色+太字
CUSTOM_TALENT_COLOR:30:0 = 0xFFCCFF
;(省略可)表示タイプ 1/种族 2/性的 3/身体的 4/精神的 5/技术的 他は「その他」
CUSTOM_TALENT_TYPE:30:0 = 3

CUSTOM_TALENT:30:1 = !INRANGE(TCVAR:30:363, -1, 0)
CUSTOM_TALENT_NAME:30:1 = %\@ TCVAR:30:363 < 0 ? 一早 # 嘴巴 \@%大满足
CUSTOM_TALENT_COLOR:30:1 = 0xFFCCFF
CUSTOM_TALENT_TYPE:30:1 = 0

;================================
;カスタムコマンド0_接受无休止的说教
;================================
;コマンド名
@KOJO_COM_NAME_K30_0
RESULTS = 接受无休止的说教

;コマンド使用条件。通常のCOM_ABLE関数と同じくRETURN 1すれば实行可能としてコマンドが表示される
@KOJO_COM_ABLE_K30_0
SIF SHIRAHU(MASTER) != 1 || SHIRAHU(30) != 1 || CFLAG:30:1701 < 10 || TFLAG:COMABLE管理 > 1
	RETURN 0
SIF GET_TARGETNUM() > 1 || CHK_DATENOW(CFLAG:30:约会中) || MARK:30:反発刻印 > 1
	RETURN 0
SIF NEMUKE() > 240 || !CHARA_HOLIDAY(30) || K30_CHECK_HEN_T()
	RETURN 0
RETURN 1

;コマンド实行内容。経験・SOURCE变动を记述
;RETURN 1=コマンド实行 / RETURN 0=キャンセル(SOURCE計算SKIP) / RETURN -1=コマンド取り消し
@KOJO_COM_K30_0
#DIM 怒意
怒意 = CFLAG:ブチギレ || MARK:反発刻印
PRINTFORML ……真的要接受吗？\n（\@ 怒意 ? 映姬的怒气会平息，但 # 这样的话 \@一天就结束了 ※体·气力归零，但有凤凰羽就可以恢复）
CALL ASK_YN
SIF RESULT
	RETURN -1
;好感度低いなら效果强め、でも效率考えると使う意味なし
TFLAG:194 = 720 - NEMUKE() / 60
SOURCE:情愛 = 75 * TFLAG:194
SOURCE:達成 = 150 * TFLAG:194
IF CFLAG:30:好感度 <= 500
	TIMES SOURCE:情愛, 1.50
	TIMES SOURCE:達成, 1.50
ELSEIF CFLAG:30:好感度 <= 5000
	TIMES SOURCE:情愛, 1.25
	TIMES SOURCE:達成, 1.25
ENDIF
BASE:MASTER:体力 = 0
BASE:MASTER:気力 = 0
IF 怒意
	SOURCE:情愛 = 0
	SOURCE:達成 /= 3
	CFLAG:ブチギレ = 0
	CFLAG:心情不快 = 0
	BASE:憤怒 = 0
	BASE:MASTER:体力 = -500
	TFLAG:193 = 1
	IF MARK:反発刻印
		SOURCE:達成 = 0
		MARK:反発刻印 = 0
		TFLAG:193 = 2
		BASE:MASTER:体力 = -2500
	ENDIF
ENDIF
TFLAG:194 = MIN(TFLAG:194, 6)
TIME += TFLAG:194  * 60
RETURN 1

;コマンドメッセージ。口上色が指定される。
;Vの值は(270 + Y)。SELECTCOM/PREVCOMの代入值も同じ
@M_KOJO_MESSAGE_COM_K30_270
#DIM TEMP
#DIM P_REDRAW
#DIM L_COUNT
#DIM LOOP_C
#DIM LINE_C
{
#DIMS WORDS =	 "首先", "再次强调", "坦率地说", "客观地看待",
				 "比如说", "话题转变", "重要的是", "反思自己",
				 "是的，您稍微", "借此机会", "反思自己", "如果继续这样下去"
}
{
#DIMS MODIFY =	"永远地", "无论何时", "无尽地", "永不停歇", "无限地",
				"无限制地", "一直", "不断地", "专心致志地", "仍然",
				"无穷尽地", "持续地", "不知疲倦地", "无止境的", "永不结束",
				"直到点击为止", "依然", "无法停止地", "持续不断地", "不间断地"
}
CALL FISHER_YATES_SHAFFLE(12)
CALLF FUNC_FISHER_YATES_SHAFFLE(LOCAL, 20)
VARSET LOCALS
TEMP = 0
MODIFY:(LOCAL:0) '= ""
IF TFLAG:193
	PRINTFORMDL 映姬开始%TEXTR("猛烈地/如烈火/如奔流")%地训诫…
ELSE
	PRINTFORMDL 听取了%CALLNAME:MASTER%的愿望，映姬%TEXTR("兴高采烈地/欣喜若狂地/仿佛心领神会地/")%开始训诫…
ENDIF
PRINTW
PRINTFORML 「！！」
IF !TFLAG:193
	PRINTDATAW 
		DATAFORM 「太棒了……真是值得称赞的心态」\n「我对%K30_C_NAME(MASTER)%刮目相看了」
		DATAFORM 「没想到你这么想听我的教诲……我没注意到」\n「积德行善的重要性，%K30_C_NAME(MASTER)%也终于明白了吧」
		DATAFORM 「%K30_C_NAME(MASTER)%竟然变得如此虔诚……」\n「我平日的教导，感觉也多少得到了回报」
	ENDDATA
	PRINTFORML 「……好吧」
	PRINTFORML 「幸好今天日程空着」
	PRINTFORML 「我会%TEXTR("比平时更卖力地，/比平时花更多时间，/连同以前没来得及说的事情，好好地")%向%K30_C_NAME(MASTER)%传授的」
ELSE
	PRINTDATAW 
		DATAFORM 「终于……终于下定决心改过自新了吗」
		DATAFORM 「既然有反省的意愿，我也不能不帮忙」
		DATAFORM 「在我的忍耐达到极限之前，你能自己意识到真是太好了」
	ENDDATA
	PRINTFORML 「今天我的日程也空着……你也不用客气了」
	PRINTDATAL 
		DATAFORM 「那么，虽然可能会有点长……请你好好听着」
		DATAFORM 「那么，该从何开始呢……我们一边传授教诲一边整理吧」
		DATAFORM 「那么……为了不让反省止步于此，我们好好传授教诲吧」
	ENDDATA
ENDIF
PRINTL

P_REDRAW = CURRENTREDRAW()
LINE_C = LINECOUNT
REDRAW 0
WHILE 1
	FOR LOOP_C, 0, 5
		IF !TEMP
			LOCALS:99 = 明白了吗
		ELSEIF TEMP % 2
			LOCALS:99 = 
		ELSE
			SIF !LOOP_C
				LOCAL:99 = LOCAL:99 == 11 ? 0 # LOCAL:99 + 1
			LOCALS:99 = \@ !LOOP_C ? %WORDS:(SHAFFLE_ARRAY:(LOCAL:99))% # %LOCALS:99% \@
		ENDIF
		LOCALS:0 = 「%LOCALS:99%%UNICODE(0x2026) * (9 - STRLENSU(LOCALS:99))%」
		FOR L_COUNT, 1, 4
			LOCALS:L_COUNT '= \@ LOOP_C >= L_COUNT ? 「%UNICODE(0x2026) * ((4 - L_COUNT) * 2 + 1)%」 # \@
		NEXT
		LOCALS:4 '= \@ LOOP_C == 4 ? （%MODIFY:(LOCAL:TEMP)%少女说教中…） # \@
		FOR L_COUNT, 0, 5
			IF L_COUNT < 4
				PRINTFORML %LOCALS:L_COUNT%
			ELSE
				PRINTFORMDL %LOCALS:L_COUNT%
			ENDIF
		NEXT
		TWAIT 350 + ((LOOP_C == 4) * 1050), 0
		SIF GETKEY(1) && TEMP > 1
			GOTO LOOP_BREAK
		CLEARLINE LINECOUNT - LINE_C
	NEXT
	CLEARLINE LINECOUNT - LINE_C
	TEMP = TEMP < 19 ? TEMP + 1 # 1
WEND
$LOOP_BREAK
REDRAW P_REDRAW
LOCALS:0 '= "「………」", "「……………」", "「…………哎呀？」"
CALL PRINT_GROUP(LOCALS, 3, 350)
IF TFLAG:193
	PRINTFORMDL ……多亏了持续说教了{TFLAG:194}小时，映姬的心情似乎好转了
	PRINTFORMDW 映姬%\@ TFLAG:193 > 1 ? 失去了反感刻印LV1 # 的怒气平息了 \@%
ELSE
	PRINTFORMDL ……明明已经说教了{TFLAG:194}小时，映姬却丝毫没有疲惫的样子
	PRINTFORMDL 反而比平时更有活力，非常开心地继续着说教
ENDIF
PRINTFORML
PRINTFORMDW 然而，%CALLNAME:MASTER%这边已经到极限了……
RETURN 1

;================================
;カスタムコマンド1_スイッチ操作
;================================
@KOJO_COM_NAME_K30_1
RESULTS = 遥控振动棒

@KOJO_COM_ABLE_K30_1
SIF !TEQUIP:30:肛用振動棒 || !SHIRAHU(30)
	RETURN 0
SIF TFLAG:COMABLE管理 > 1 || CFLAG:30:诶嘿嘿 || CFLAG:MASTER:诶嘿嘿
	RETURN 0
SIF TCVAR:30:364 < 1 || !TCVAR:30:365 || TIME:1 - TCVAR:30:368 < 10
	RETURN 0
RETURN 1

@KOJO_COM_K30_1
;强弱切替え
TCVAR:30:365 = TCVAR:30:365 == 1 ? 2 # 1
;-------------------------------------------------
;COM别FLAGの设定
;-------------------------------------------------
;行为者
TCVAR:116 = PLAYER
TFLAG:理性削り = 1
;-------------------------------------------------
;適当にソース追加处理
;-------------------------------------------------
;弱
IF TCVAR:30:365 == 1
	SOURCE:露出 = 50
	SOURCE:屈従 = 50
	SOURCE:欲情 = 100
	SOURCE:受動 = 100
	SOURCE:反感 = 10
	LOCAL = 2
;强
ELSE
	SOURCE:露出 = 150
	SOURCE:屈従 = 150
	SOURCE:欲情 = 50
	SOURCE:受動 = 50
	SOURCE:反感 = 20
	LOCAL = 5
ENDIF
SIF HITOKE(1)
	TIMES SOURCE:露出 , 2.50
;-------------------------------------------------
;経験の处理
;-------------------------------------------------
EXP:Ａ経験 ++
EXP:PLAYER:道具使用経験 ++
TIME += LOCAL
TCVAR:30:368 = TIME:1 + LOCAL 
RETURN 1

@M_KOJO_MESSAGE_COM_K30_271
#DIM TGT
LOCAL = K30_FIND_AROUND()
TGT = GROUPMATCH(LOCAL, 49, RANDOM_CHARANUM) ? 0 # LOCAL
IF TCVAR:30:365 == 1
	PRINTFORMDL %CALLNAME:MASTER%将怀里的开关调回「弱」
	IF NOWEX:Ａ絶頂
		PRINTFORMDL 但映姬情欲的身体已经无可抵御地高潮了……
		CALL K30_PRINT_VIBE_DATE
		PRINTFORMW
		STRDATA LOCALS
			DATAFORM 「%K30_C_NAME(MASTER)%，现在才……就算你调慢，也晚了HPH」\n「已经，停不下来了……HPH」
			DATAFORM 「……训练我『忍耐』，是吗？」\n「那种事……不把这个拔出来的话……不可能做得到呀HPH」
			DATAFORM 「……在这里，寸止什么的……怎么可能做到HPH」\n「就这样，慢慢地，就这样……唔唔HPH」
		ENDDATA
		CALL HPH_PRINT, LOCALS, "W"
		RETURN 1
	ELSE
		PRINTDATADL
			DATAFORM 映姬整理了凌乱的衣着，深呼吸着。
			DATAFORM 从过强的快感中解放出来，映姬松了一口气。
			DATAFORM 映姬擦拭着额头上的汗水，大口呼气。
		ENDDATA
		IF K30_BE_SEEN() && !TFLAG:约会道中
			PRINTFORMDL 确认%\@ TGT ? %CALLNAME:TGT%没有 # 谁都没有 \@%注意到后，她才终于平静下来。
		ELSE
			PRINTFORMDL ……但是，她转向%CALLNAME:MASTER%的眼神似乎有些意犹未尽。
		ENDIF
	ENDIF
	CALL K30_PRINT_VIBE_DATE
	PRINTFORMW
	CALL HPH_PRINT, @"「哈啊HPH　哈啊……哈啊……HPH」", "L"
	PRINTFORML
	IF K30_BE_SEEN() && !TFLAG:约会道中
		STRDATA LOCALS
			DATAFORM 「没关系……%K30_C_NAME(TGT,2)%没有、发现……HPH」\n「是啊，没关系……无论被做多少次……都没关系，所以……HPH」
			DATAFORM 「……感觉比平时，更……敏感了……HPH」\n「在%K30_C_NAME(TGT,2)%面前……做这种事，我……」\n「已经，回不去了……对吧HPH」
			DATAFORM 「在%K30_C_NAME(TGT,2)%面前也……毫不留情呢，%K30_C_NAME(MASTER)%HPH」\n「……多亏了你，我……脑袋，都快要融化了……HPH」
		ENDDATA
	ELSE
		STRDATA LOCALS
			DATAFORM 「……稍微，平静下来了」\n「但是……」\n「一想到，下一次刺激，什么时候会来……HPH」
			DATAFORM 「刚才为止……还在想，会持续到什么时候……」\n「一旦，变弱了……」\n「……就又想要了……HPH」
			DATAFORM 「呼……HPH」\n「我，好好地，忍住了哦HPH」\n「下一次的奖励，就更……期待了呢……HPH」
		ENDDATA
	ENDIF
	CALL HPH_PRINT, LOCALS, "W"
ELSE
	PRINTFORMDL %CALLNAME:MASTER%将怀里的开关调到「强」……
	PRINTFORMW
	CALL K30_PRINT_VIBE_DATE
	FONTSTYLE 3
	PRINTFORML
	LOCALS = %TEXTR("呜、啊、啊…/呜啊…/啊…咿…/啊…唔、唔/啊嗯HPH啊、唔HPHHPH啊呜……")%HPHHPHHPH
	IF K30_BE_SEEN() && !TFLAG:约会道中
		CALL HPH_PRINT, @"（%LOCALS%）", "L"
		FONTREGULAR
		PRINTFORML
		PRINTFORMDW ……映姬在千钧一发之际忍住了声音
	ELSE
		CALL HPH_PRINT, @"「%LOCALS%」", "L"
		FONTREGULAR
		PRINTFORML
		PRINTFORMDW ……漏出了完全不像阎魔大人的声音
	ENDIF
	PRINTFORML
	IF TCVAR:30:382 
		PRINTDATADL
			DATAFORM 映姬扭动着身体，试图缓解强烈的快感对刚刚高潮的身体的追击
			DATAFORM 敏感的身体再次受到追击，映姬光是站着就竭尽全力了
			DATAFORM 映姬似乎无法忍受无休止的快感，摇着头表示不要
		ENDDATA
	ELSEIF K30_BE_SEEN() && !TFLAG:约会道中
		PRINTDATADL
			DATAFORM 映姬颤抖着身体慢慢后退，试图逃离\@ TGT ? %CALLNAME:TGT% # 周围 \@的视线
			DATAFORM 映姬试图不让\@ TGT ? %CALLNAME:TGT% # 周围 \@察觉到她明显古怪的样子
			DATAFORM 映姬不自然地扭动着身体，\@ TGT ?在%CALLNAME:TGT%面前 # 顾忌着旁人的目光、\@拼命装作平静
		ENDDATA
	ELSE
		PRINTDATADL
			DATAFORM 映姬抱住自己的身体，拼命忍耐着
			DATAFORM 映姬屏住呼吸，完全无法动弹
			DATAFORM 映姬不自然地内八字夹紧双腿，紧握着裙摆
		ENDDATA
	ENDIF
	IF NOWEX:Ａ絶頂
		PRINTFORMDL 然而肛门震动棒强烈的震动，毫不留情地将映姬逼入绝境……
	ELSEIF K30_BE_SEEN() && !TFLAG:约会道中
		PRINTFORMDL 她顾忌旁人目光忍耐快感的模样，极大地满足了%CALLNAME:MASTER%的施虐欲
	ELSE
		PRINTFORMDL 但映姬发现周围没有人，便毫不客气地发出了愉悦的尖叫
	ENDIF
	PRINTFORML
	IF NOWEX:Ａ絶頂
		TFLAG:194 = 19
		STRDATA LOCALS
			DATAFORM 啊啊……！！不行HPH　等等HPH　等等啊HPHHPH
			DATAFORM 不行HPH　这个HPH　太厉害了，太厉害了HPH　不行啊HPHHPH
			DATAFORM 啊啊……HPH　啊HPHHPH　啊啊啊～～HPHHPH
			DATAFORM 要飞起来了HPH脑袋HPH要坏掉了哈HPH
		ENDDATA
		IF K30_BE_SEEN() && !TFLAG:约会道中
			CALL HPH_PRINT, @"（%LOCALS%）", "L"
			CALL HPH_PRINT, @"（要去了HPH在%K30_C_NAME(TGT,2)%面前HPH　要去了！　用屁股HPH　要去了HPHHPH）", "W"
		ELSE
			CALL HPH_PRINT, @"「%LOCALS%」", "L"
			CALL HPH_PRINT, @"「好舒服HPH屁股HPH非常舒服HPHHPH」", "W"
		ENDIF
	ELSEIF K30_BE_SEEN() && !TFLAG:约会道中
		CALL HPH_PRINT, @"（%K30_C_NAME(MASTER)%！？　竟然%TEXTR("现在……在这里吗/在这种地方/盯上了……这个时候")%…怎么h…吼哦哦HPHHPH）", "L"
		CALL HPH_PRINT, @"（%K30_C_NAME(TGT,2)%，明明还HPH在、这边HPH明明在看着HPHHPH）", "L"
		CALL HPH_PRINT, @"（啊HPH好舒服…%\@ TCVAR:30:382 ? 停不下来HPH完全停不下来# HPH这种事…要忍不住了\@%HPHHPH）", "W"
	ELSE
		STRDATA LOCALS
			DATAFORM 「…声音！！　要漏出来了HPH这个HPH好激烈…咿咿咿HPHHPH」
			DATAFORM 「…好厉害HPH好厉害HPH酥酥麻麻HPH停不下来HPHHPH」
			DATAFORM 「…明知不行…也忍不住HPH太舒服了…真的、不行…哦哦哦HPHHPH」
		ENDDATA
		CALL HPH_PRINT, LOCALS, "W"
	ENDIF
ENDIF
RETURN 1
