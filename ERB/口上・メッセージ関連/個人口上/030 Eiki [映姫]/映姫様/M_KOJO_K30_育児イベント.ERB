;TW你は育児に係わるべきではないダメ人类…というのが口上作者の脳内設定
;なので育児イベント内では全力でパージされています

@M_KOJO_EVENT_K30_CHILD_RAISING_回復
SIF GETBIT(CFLAG:1804, TALENT:育児中 % 64)
	RETURN 
LOCAL = TALENT:育児中 % 2
IF LOCAL
	PRINTFORML 「呼……总算恢复状态了，吧…」
	PRINTFORML 「产后恢复期也早就结束了，不能一直这么懒散下去了呢」
	PRINTFORMW 「%SHOW_CHILDNAME(30)%…妈妈从今以后会更加更加努力的哦」
ELSE
	PRINTFORML 「……身体方面，好像已经没问题了，呢」
	PRINTFORML 「这样准备就完成了」
	PRINTFORMW 「为了%SHOW_CHILDNAME(30)%，从今以后必须好好努力」
ENDIF
@M_KOJO_EVENT_K30_CHILD_RAISING_離乳
SIF GETBIT(CFLAG:1804, TALENT:育児中 % 64)
	RETURN 
IF TALENT:母乳体質 == 2
	IF RAND:2
		PRINTFORML 「…就算再怎么能出奶，也差不多到了断奶的时候了…呢」
		PRINTFORML 「%SHOW_CHILDNAME(30)%酱…对不起哦，我们忍忍，不要再吃奶了哦」
	ELSE
		PRINTFORML 「…虽然%SHOW_CHILDNAME(30)%大口大口地喝奶很可爱」
		PRINTFORML 「但如果一直这样下去，对这孩子也不好呢…」
	ENDIF
	PRINTFORML 「唉…」
	PRINTFORMDW 映姬对哺乳期即将结束感到寂寞…
ELSE
	PRINTFORML 「%SHOW_CHILDNAME(30)%酱，来，啊呜…」
	PRINTFORML 「乖乖，好好吃…真棒」
	PRINTFORML 「…也该告别母乳了……可能有点寂寞」
	PRINTFORML
	IF TALENT:母乳体質 == 1 && EXP:噴乳経験 > 500 && EXP:出産経験 > 1
		PRINTFORML 「但是…奶水这边，一点都没有要停下来的迹象呢…」
		PRINTFORMW 「这果然还是，%K30_C_NAME(MASTER)%的错…？」
	ELSEIF RAND:2
		PRINTFORML 「%SHOW_CHILDNAME(30)%酱，你吃奶时那可爱的样子」
		PRINTFORML 「妈妈最喜欢了哦…」
		PRINTFORMDW 映姬对哺乳期即将结束感到寂寞…
	ELSE
		PRINTFORML 「%SHOW_CHILDNAME(30)%酱，从今以后和妈妈一起一起去寻找」
		PRINTFORMW 「喜欢的东西，好吃的东西吧」
	ENDIF
ENDIF

@M_KOJO_EVENT_K30_CHILD_RAISING_玩具
SIF GETBIT(CFLAG:1804, TALENT:育児中 % 64)
	RETURN 
PRINTFORMDL 映姬正在为%SHOW_CHILDNAME(30)%玩的玩具绞尽脑汁……
PRINTFORML 
IF RAND:2
	PRINTFORML 「%SHOW_CHILDNAME(30)%用着安全又结实」
	PRINTFORML 「玩着玩着还能变聪明的那种玩具…哪里有呢…？」
	PRINTFORMW 「…也得让小町和久侘歌帮忙找找」
ELSE
	PRINTFORML 「%SHOW_CHILDNAME(30)%用的东西，绝对得是好的才行…所以…」
	PRINTFORML 「也得派人去人里和香霖堂…」
	PRINTFORML 「…拜托赫卡提亚大人，或许也不错…」
	PRINTFORML 「…干脆，让隙间妖怪…从外面的世界…」
	PRINTFORMDW 映姬为了自己的孩子，似乎会动用一切关系
ENDIF

@M_KOJO_EVENT_K30_CHILD_RAISING_つかまり立ち
IF GETBIT(CFLAG:1804, TALENT:育児中 % 64)
	CALL K30_CLEAR_TEXTLINE
	RETURN 
ENDIF
IF RAND:2
	PRINTFORML 「啊…第一次站起来的样子，错过了…」
	PRINTFORMDL 在映姬叹气旁边，%SHOW_CHILDNAME(30)%扶着桌子站着，笑眯眯的
	PRINTFORML 「稍微没看着的时候，好像就越长越大了呢…」
	PRINTFORMW 「真是的……从现在开始，就真的不能移开视线了呢…」
ELSE
	PRINTFORML 「啊！%SHOW_CHILDNAME(30)%、危险！」
	PRINTFORMDL 练习扶站的时候，%SHOW_CHILDNAME(30)%啪嗒一下摔了个屁股蹲
	PRINTFORMDL 映姬慌慌张张的，但是%SHOW_CHILDNAME(30)%那边又想站起来，正向映姬伸出手
	PRINTFORML 「好像没事…啊，太好了…」
	PRINTFORMW 「%SHOW_CHILDNAME(30)%酱。虽然很努力很好…再慢一点也没关系哦？」
ENDIF

@M_KOJO_EVENT_K30_CHILD_RAISING_よちよち
IF GETBIT(CFLAG:1804, TALENT:育児中 % 64)
	CALL K30_CLEAR_TEXTLINE
	RETURN 
ENDIF
IF RAND:2
	PRINTFORML 「走路真棒 摔跤真笨…」
	PRINTFORMDL 映姬一边唱着儿歌一边走向等待的地方，%SHOW_CHILDNAME(30)%摇摇晃晃地走了过去
	PRINTFORML 「好～，%SHOW_CHILDNAME(30)%酱，做得真棒♪」
	PRINTFORMDW 映姬紧紧抱住了顺利走完的孩子
ELSE
	PRINTFORML 「哎！？等等…什么时候跑到这里来了！？」
	PRINTFORMDL 发现蹒跚学步的小逃犯差点溜出房间
	PRINTFORMDL 映姬发出了与阎魔大人身份不符的、慌乱的尖叫
	PRINTFORML 「%SHOW_CHILDNAME(30)%酱，活泼是好事…但别把妈妈吓得太厉害了」
	PRINTFORML 「呼…要是能有像小町那样的能力，就能稍微安心一点了…」
	PRINTFORMDW 映姬抱着天真地笑着的%SHOW_CHILDNAME(30)%疲惫地嘟囔道…
ENDIF

@M_KOJO_EVENT_K30_CHILD_RAISING_会話寸前
;3日間同じイベントは微妙なので1日のみ
IF CFLAG:出産経過日 > CHILD_喋る - 3 || GETBIT(CFLAG:1804, TALENT:育児中 % 64)
	CALL K30_CLEAR_TEXTLINE
	RETURN
ENDIF
PRINTFORMDL 正因为是阎魔的孩子，%SHOW_CHILDNAME(30)%学说话也很快…
PRINTFORML 
PRINTFORML 「妈妈」
CALL COLORMESSAGE(@"「妈妈」", K30_CHILDWORD() ,1 ,0)
PRINTFORML 「早上好」
CALL COLORMESSAGE(@"「早上好」", K30_CHILDWORD() ,1 ,0)
PRINTFORML 「真乖真乖、做的真棒♪」
PRINTFORMDL 映姬高兴得不得了，抚摸着%SHOW_CHILDNAME(30)%头顶的发旋…
CALL COLORMESSAGE(@"「做得很好！」", K30_CHILDWORD() ,2 ,0)

@M_KOJO_EVENT_K30_CHILD_RAISING_喋る
IF GETBIT(CFLAG:1804, TALENT:育児中 % 64)
	CALL K30_CLEAR_TEXTLINE
	RETURN 
ENDIF
PRINTFORMDL 随着快速的成长，%SHOW_CHILDNAME(30)%向映姬抛出各种问题……
PRINTFORML 
CALL COLORMESSAGE(@"「妈妈，这是什么呀？」", K30_CHILDWORD() ,1 ,0)
PRINTFORML 「那是花哦。它开得很漂亮，说是想让大家看看呢」
CALL COLORMESSAGE(@"「妈妈，为什么这朵花是红色的呀？」", K30_CHILDWORD() ,1 ,0)
PRINTFORML 「呵呵，那个妈妈也不知道呢……你觉得为什么呢？」
PRINTFORMDL 映姬认真地回答着问题，同时对孩子广泛的兴趣感到惊讶
PRINTFORML 
CALL COLORMESSAGE(@"「呐——呐——，妈妈！」", K30_CHILDWORD() ,1 ,0)
CALL COLORMESSAGE(@"「为什么那个姐姐，总是在睡午觉呀？」", K30_CHILDWORD() ,1 ,0)
PRINTFORMDL %SHOW_CHILDNAME(30)%所指的方向，小町正酣睡着……
PRINTFORML 「……说起来为什么呢。妈妈也一点都不知道呢」
PRINTFORMW 「我们去向那个姐姐，好——好——问问吧——……」

@M_KOJO_EVENT_K30_CHILD_RAISING_語彙
IF GETBIT(CFLAG:1804, TALENT:育児中 % 64)
	CALL K30_CLEAR_TEXTLINE
	RETURN 
ENDIF
PRINTFORMDL %SHOW_CHILDNAME(30)%也变得相当聪明，开始做出各种各样的行为
PRINTFORMDL 映姬觉得自己的孩子应该已经能理解了，正在考虑如何进行教育……
PRINTFORML 
PRINTFORML 「听好了，常言道『说谎是盗窃的开始』」
PRINTFORML 「如果习惯了做小坏事，那么即使是非常糟糕的事，也会变得不在意了哦」
PRINTFORML 「%SHOW_CHILDNAME(30)%今后也可能会有害怕说『对不起』的时候」
PRINTFORML 「但是，在那时如果说谎的话……」
PRINTFORML 
PRINTFORMDL 映姬尽量用浅显的语言解释，结果还是变成了平时的说教
PRINTFORMDL 聪明的%SHOW_CHILDNAME(30)%虽然努力跟上话题，但是…
CALL COLORMESSAGE(@"「…妈妈的话，好——长——啊！」", K30_CHILDWORD() ,1 ,0)
PRINTFORMDW 看来这和能否忍受是两回事……

@M_KOJO_EVENT_K30_CHILD_RAISING_しつけ
IF GETBIT(CFLAG:1804, TALENT:育児中 % 64)
	CALL K30_CLEAR_TEXTLINE
	RETURN 
ENDIF
CALL SET_KOJO_COLOR([[小町]])
LOCAL = GETCOLOR()
LOCALS = %\@ CHILD_SEX:30:(TALENT:育児中) == 1 ? 我 # 我 \@%
IF RAND:2
	CALL SET_KOJO_COLOR(30)
	CALL COLORMESSAGE(@"「姐姐喜欢睡午觉，%LOCALS%知道哦」", K30_CHILDWORD() ,1 ,0)
	CALL COLORMESSAGE(@"「工作结束了我们再好好睡午觉吧」", K30_CHILDWORD() ,1 ,0)
	CALL COLORMESSAGE(@"「但是，现在不行！」", K30_CHILDWORD() ,1 ,0)
	CALL COLORMESSAGE(@"「姐姐要是偷懒不好好工作，大家会很困扰的哦！」", K30_CHILDWORD() ,1 ,0)
	CALL COLORMESSAGE(@"「是，说得没错呢……」", LOCAL ,1 ,0)
	PRINTFORMDW ……路过的映姬正看见，正坐着的小町和正努力对她说教的%SHOW_CHILDNAME(30)%
	PRINTFORML
	PRINTFORMW 「呃？%SHOW_CHILDNAME(30)%和…小町…？」
	PRINTFORML 「……唉」
	PRINTFORML 「需要管教、需要操心的…没想到会是大孩子…」
	PRINTFORMDW 映姬立刻察觉到原因，像傻眼了一样叹了口气
	PRINTFORML
	PRINTFORML 「%SHOW_CHILDNAME(30)%，妈妈也和这位姐姐有『好多好多』话要说」
	PRINTFORML 「你到别的地方去玩一会儿吧」
	CALL COLORMESSAGE(@"「好～！」", K30_CHILDWORD() ,1 ,0)
	CALL COLORMESSAGE(@"「呜哇……四季大人简直像变成了两个，唠叨也变成两倍了啊……」", LOCAL ,1 ,0)
	PRINTFORMDW 确定还要继续挨训的小町垂头丧气……
ELSE
	PRINTFORML 「……听着，%SHOW_CHILDNAME(30)%，自古以来不是有句话叫『好运睡着等』吗？」
	PRINTFORML 「我这样躺着，也不是因为讨厌工作」
	PRINTFORML 「这是为了当幸福来敲门的时候，不用费劲找我，我安安静静地待着呢」
	PRINTFORML 「嘛，人类也好，神明也罢，当然连死神都需要有这份从容才行啊……懂了吗？」
	PRINTFORMDW 正在偷懒的小町枕着胳膊躺着，对%SHOW_CHILDNAME(30)%进行着可疑的教导……
	PRINTFORML
	CALL COLORMESSAGE, @"「但是……不好好工作果然还是不对的哦……」", K30_CHILDWORD() ,1 ,0
	PRINTFORML 「啊～啊，%SHOW_CHILDNAME(30)%真是和妈妈一样认真的\@ CHILD_SEX:30:(TALENT:育児中) == 1 ? 乖乖女 # 乖乖仔 \@呢」
	PRINTFORML 「这——么小就绷着脸，不会觉得喘不过气吗？」
	PRINTFORML 「啊哈哈哈，这样下去将来会长大成什么样，姐姐我真是非常担心啊」
	CALL COLORMESSAGE, @"「…呜姆」", K30_CHILDWORD() ,1 ,0
	PRINTFORMDW 小町一边打发时间一边逗弄着%SHOW_CHILDNAME(30)%玩…
	PRINTFORML
	CALL SET_KOJO_COLOR(30)
	PRINTFORML 「……您不必担心，至少他/她绝不会长成您那样」
	CALL COLORMESSAGE, @"「啊，妈妈♪」", K30_CHILDWORD() ,1 ,0
	CALL COLORMESSAGE, @"「四、四季大人……」", LOCAL ,1 ,0
	PRINTFORMDW 映姬的登场让气氛为之一变……
	PRINTFORML
	PRINTFORML 「不光是玩忽职守，居然还偏偏对%SHOW_CHILDNAME(30)%灌输这种无聊的东西」
	PRINTFORML 「小町，今天我绝不轻饶你……做好觉悟吧」
	CALL COLORMESSAGE, @"「咿！　咿诶……」", LOCAL ,1 ,0
	PRINTFORMDL 映姬没有提高音量，只是淡淡地陈述，反而更能看出映姬的怒火有多大
	PRINTFORMDW 小町完全畏缩了，连声音都发不出来，只是颤抖着…
	PRINTFORML
	CALL COLORMESSAGE, @"「妈妈，我去那边玩哦……」", K30_CHILDWORD() ,1 ,0
	PRINTFORML 「是啊，%SHOW_CHILDNAME(30)%去别的地方玩吧」
	PRINTFORML 「今天可不能再靠近这里了哦」
	CALL COLORMESSAGE, @"「……好～」", K30_CHILDWORD() ,1 ,0
	PRINTFORML
	PRINTFORMDW 察觉到什么的%SHOW_CHILDNAME(30)%离开后，映姬的地狱流说教开始了……
ENDIF

@M_KOJO_EVENT_K30_CHILD_RAISING_就学
IF GETBIT(CFLAG:1804, TALENT:育児中 % 64)
	CALL K30_CLEAR_TEXTLINE
	RETURN 
ENDIF
PRINTFORMDL 寺子屋开学的第一天，映姬送走了充满活力出门的%SHOW_CHILDNAME(30)%
PRINTFORML 
PRINTFORML 「…听好了，在寺子屋要好好听老师的话」
PRINTFORML 「还有，如果有遇到困难的朋友，要主动去帮助他们」
PRINTFORML 「那么，一路顺风。路上小心哦」
CALL COLORMESSAGE(@"「好——！　我走啦——！！」", K30_CHILDWORD() ,1 ,0)
PRINTFORML 
PRINTFORMDW 映姬感慨万分地目送着%SHOW_CHILDNAME(30)%充满活力地跑走……

@M_KOJO_EVENT_K30_CHILD_RAISING_自立前
IF GETBIT(CFLAG:1804, TALENT:育児中 % 64)
	CALL K30_CLEAR_TEXTLINE
	RETURN 
ENDIF
IF CHILD_SEX:30:(TALENT:育児中) == 1
	PRINTFORMDL %SHOW_CHILDNAME(30)%也长高了，长得和母亲一模一样美丽
ELSE
	PRINTFORMDL %SHOW_CHILDNAME(30)%早已超过母亲的身高，茁壮成长
ENDIF
PRINTFORMDL 映姬正和孩子讨论着将来……
PRINTFORML 
PRINTFORML 「%SHOW_CHILDNAME(30)%能帮我的忙，我非常高兴」
PRINTFORML 「如果能一直继续下去，确实会轻松很多」
PRINTFORML 「但是，作为母亲，我不能允许那样」
PRINTFORML 「…审判罪行本身也是一种罪，即使是审判者也无法逃脱罪孽」
PRINTFORML 「我不想让%SHOW_CHILDNAME(30)%背负那样的重担……你明白吗？」
CALL COLORMESSAGE(@"「………」", K30_CHILDWORD() ,1 ,0)
PRINTFORML 
PRINTFORMDW 映姬和%SHOW_CHILDNAME(30)%的谈话一直持续到深夜……

@K30_CHILD_INDEPENDENCE
CALL K30_D_LINE
IF CFLAG:1805
	PRINTFORMDL 映姬悄然抚养长大的孩子终于迎来了独立的时候
	PRINTFORMDL 考虑到今后的事情，%CSTR:30:83%似乎也要离开幻想乡了
	PRINTFORMDL 悄悄地告别后，在映姬一人目送下，%CSTR:30:83%离巢远去……
	PRINTFORMDW 映姬只能默默地祈祷着孩子的平安，流着泪
	PRINTFORML 
	PRINTFORMDL …送走%CSTR:30:83%后，映姬执行了她早前就考虑好的惩戒
	CALL COLORMESSAGE, "映姬再也不能怀孕了……", C_RED, 2 ,1
	PRINTFORML 
	CALL COLORMESSAGE, "映姬的最大体力减少了200", C_YELLOW ,1 ,1
	CALL COLORMESSAGE, "映姬的最大气力减少了200", C_YELLOW ,2 ,1
	BASE:30:体力 -= 200
	MAXBASE:30:体力 -= 200
	BASE:30:気力 -= 200
	MAXBASE:30:気力 -= 200
ELSE
	LOCALS = %\@ CHILD_SEX:30:(TALENT:育児中) == 1 ? 女儿 # 儿子 \@%
	PRINTFORMDL 映姬的%LOCALS%——%SHOW_CHILDNAME(30)%顺利迎来了独立的那一天
	PRINTFORML 
	PRINTFORML 「…你总有一天会回到我的身边吧」
	PRINTFORML 「届时，作为审判者和被审判者……我会好好回顾%SHOW_CHILDNAME(30)%所做的一切」
	PRINTFORML 「不过没关系，您是我引以为傲的%LOCALS%……」
	PRINTFORML 「如果%SHOW_CHILDNAME(30)%能好好思考并行动，一定不会有错的」
	PRINTFORML 「为了能昂首挺胸、笑着再次相遇，今后也要继续积累善行哦」
	PRINTFORML 「去吧」
	PRINTFORML 
	IF CFLAG:妊娠経過日数 >= 20 && CFLAG:妊娠自覚状態
		PRINTFORMW 映姬目送着孩子远去的背影，同时默默发誓也要将腹中的孩子平安抚养长大
	ELSE
		PRINTFORMW 映姬看着孩子远去的背影，祈祷着%LOCALS%的平安
	ENDIF
	CHILD_INDEPENDENT:30:(TALENT:育児中) = 1
	CALL 自立先選定(30, TALENT:育児中)
	CALL ASK_DIARY(@"%SHOW_CHILDNAME(30)%独立了。")
	TALENT:育児中 = 0
	CFLAG:育児人数 = 0
	CFLAG:MASTER:育児人数 -= 1
ENDIF
CFLAG:出産経過日 = 0
PRINTFORML 

@M_KOJO_EVENT_K30_CHILD_RAISING_登下校(ARGS,ARG,ARG:1)
SIF GETBIT(CFLAG:1804, TALENT:育児中 % 64)
	RETURN
IF !ARG
	PRINTDATAL
		DATAFORM 「%SHOW_CHILDNAME(30)%，去吧」\n「别太晚回来哦」
		DATAFORM 「没忘东西吧？　没问题吧？」\n「好的，那么去吧」
		DATAFORM 「哎呀？　今天出门不早吗？」\n「…是吗，有事要做啊」\n「那么路上小心，去吧」
	ENDDATA
ELSE
	PRINTDATAL
		DATAFORM 「好的，欢迎回来」\n「先去洗手吧」
		DATAFORM 「%SHOW_CHILDNAME(30)%，欢迎回来」\n「…呵呵，看起来很高兴呢。发生什么好事了吗？」
		DATAFORM 「欢迎回来，今天回来得真早啊」
	ENDDATA
ENDIF

@M_KOJO_EVENT_K30_CHILD_RAISING_吃飯(ARGS,ARG,ARG:1)
SIF GETBIT(CFLAG:1804, TALENT:育児中 % 64) || CFLAG:MASTER:現在位置 != CFLAG:30:現在位置
	RETURN
IF ARGS == "授乳"
	PRINTDATAL
		DATAFORM 「乖乖……%SHOW_CHILDNAME(30)%酱，喝奶，真棒……」
		DATAFORM 「多喝点，快快长大……」
		DATAFORM 「%SHOW_CHILDNAME(30)%酱，乖乖……」\n「好好享用妈妈的奶吧……」
	ENDDATA
ELSEIF ARGS == "离乳食"
	PRINTDATAL
		DATAFORM 「哎呀呀，撒了这么多……」\n「来，再吃一口，啊呜……」
		DATAFORM 「啊，不好……可能还有点烫……」\n「呼——，呼——……」\n「来，请用」
		DATAFORM 「嗯——，现在是心情不好吗……」\n「拜托了，再张开一次嘴好吗？嗯？」
	ENDDATA
ELSEIF CFLAG:出産経過日 < 75
	PRINTDATAL
		DATAFORM 「咦？　想吃和妈妈一样的东西……？」\n「嗯——，%SHOW_CHILDNAME(30)%吃的都是会让小嘴巴感到惊讶的东西……」\n「还是等再长大一点再吃吧」
		DATAFORM 「%SHOW_CHILDNAME(30)%，饭要好好嚼碎了再吃哦」
		DATAFORM 「%SHOW_CHILDNAME(30)%，不能挑食哦」
	ENDDATA
ELSE
	SELECTCASE RAND:3
		CASE 0
			PRINTFORML 「今天妈妈准备了%SHOW_CHILDNAME(30)%喜欢吃的东西哦」
			PRINTFORML 「多吃点」
			PRINTFORMDL %SHOW_CHILDNAME(30)%正津津有味地吃着光是靠近就会辣得眼睛疼的辛辣食物……
			PRINTFORMDL 味觉和肠胃的强度似乎都遗传了映姬
		CASE 1
			PRINTFORML 「%SHOW_CHILDNAME(30)%，听好了，一粒米里住着七位神明」
			PRINTFORML 「浪费粮食会遭到报应的……当然，剩饭就更不用说了」
			PRINTFORML 「还有啊………」
			PRINTFORMDL %SHOW_CHILDNAME(30)%一边认真听着映姬的说教，一边规规矩矩地吃饭
		CASE 2
			PRINTFORML 「要表达对日常饮食的感谢，最重要的是好好吃饭」
			PRINTFORML 「%SHOW_CHILDNAME(30)%总是吃得很干净，妈妈非常高兴」
			PRINTFORMDL 映姬微笑着看着%SHOW_CHILDNAME(30)%举止优雅地用餐
	ENDSELECT
ENDIF
[SKIPSTART]
@M_KOJO_EVENT_K30_CHILD_RAISING_BATH(ARGS,ARG,ARG:1)
IF ARGS == "幼児期"

ELSEIF ARGS == "幼少期"

ELSE

ENDIF

@M_KOJO_EVENT_K30_CHILD_RAISING_SLEEPING(ARGS,ARG,ARG:1)
IF ARGS == "幼児期"

ELSEIF ARGS == "幼少期"

ELSE

ENDIF

@M_KOJO_EVENT_K30_CHILD_RAISING_OYASUMI(ARGS,ARG,ARG:1)

@M_KOJO_EVENT_K30_CHILD_RAISING_TOY(ARGS,ARG,ARG:1)
IF ARGS == "幼児期"

ELSEIF ARGS == "幼少期"

ELSE

ENDIF
[SKIPEND]
;兒童自由行動
;同室者にあいさつをする
@M_KOJO_EVENT_K30_CHILD_RAISING_OTHER(ARGS,ARG,ARG:1)
#DIM CHR
#DIM L_COUNT
#DIMS 角色名
#DIMS あいさつ
SIF GETBIT(CFLAG:1804, TALENT:育児中 % 64) || !GET_TARGETNUM()
	RETURN
VARSET LOCAL
IF GET_TARGETNUM() == 1
	CHR = TARGET
ELSE
	CHR = TARGET:(RAND:(GET_TARGETNUM()) + 1)
	SIF !CHR
		RETURN
ENDIF
IF ARGS == "幼児期"
	CHR = RAND:3 ? CHR # MASTER
	CHR = RAND:2 ? 30 # CHR
	PRINTDATADL
		DATAFORM %SHOW_CHILDNAME(30)%向%CALLNAME:CHR%挥了挥手
		DATAFORM %SHOW_CHILDNAME(30)%正盯着%CALLNAME:CHR%的脸看……
		DATAFORM %SHOW_CHILDNAME(30)%看着%CALLNAME:CHR%的脸，开心地笑着……
		DATAFORM %SHOW_CHILDNAME(30)%正盯着%CALLNAME:CHR%的头顶上方看\n是看到了什么吗……？
		DATAFORM %SHOW_CHILDNAME(30)%看着%CALLNAME:CHR%的脸，睁大了眼睛\n是在惊讶什么呢……？
	ENDDATA
ELSEIF CFLAG:出産経過日 < 75
	SELECTCASE CHR
		CASE 23
			角色名 = %\@ RAND:3 ? 妖梦 # 半灵 \@%
		CASE 24
			角色名 = %\@ RAND:3 ? 橙 # %TEXTR("小猫/喵喵")% \@%
		CASE 25
			角色名 = %\@ RAND:3 ? 蓝 # %TEXTR("狐狸/好多尾巴的")% \@%
		CASE 30
			角色名 = 妈妈
		CASE 26
			角色名 = %\@ RAND:3 ? 紫 # 缝隙 \@%
		CASE 66
			角色名 = %\@ RAND:3 ? 幽幽子 # %TEXTR("幽灵/大胃王")% \@%
		CASE 67
			角色名 = %\@ RAND:3 ? 慧ー音 # \@%老师
		CASE 76
			角色名 = %\@ RAND:2 ? 小町 # \@%姐姐
		CASE 113
			角色名 = 漂亮的
		CASE 127
			角色名 = %\@ RAND:3 ? 璎花 # %TEXTR("水母/垒石头的")% \@%
		CASE 128
			角色名 = %\@ RAND:3 ? 润美 # %TEXTR("哞哞/卖鱼的")% \@%
		CASE 129
			角色名 = %\@ RAND:2 ? 久侘歌 # \@%姐姐
		CASE 130
			角色名 = %\@ RAND:3 ? 八千慧 # 龟甲 \@%
		CASE 131
			角色名 = %\@ RAND:3 ? 磨弓 # 土俑 \@%
		CASE 132
			角色名 = %\@ RAND:3 ? 袿ー姬 # 玩泥巴的 \@%
		CASE 133
			角色名 = %\@ RAND:3 ? 早鬼 # 大马 \@%
		CASE 143
			角色名 = %\@ RAND:3 ? 尤魔 # 绵羊 \@%
		CASE 156
			角色名 = %\@ RAND:3 ? 日狭美 # 茄子 \@%
		CASE 157
			角色名 = %\@ RAND:3 ? 残无 # 鬼 \@%
		CASEELSE
			角色名 = 
	ENDSELECT
	SIF !GROUPMATCH(CHR, 30, 67, 76, 129)
		角色名 = %角色名%姐姐
	IF ONCE(@"幼%TOSTR(CHR)%")
		あいさつ = %\@ INRANGE(TIME, 240, 599) ? 早上好 # %K30_GREETING_EIKI()% \@%
		CALL COLORMESSAGE, @"「%角色名%、%あいさつ%！」", K30_CHILDWORD() ,1 ,0
		PRINTFORMDL %SHOW_CHILDNAME(30)%向%\@ CHR == 30 ? 映姬反复打招呼 # %CALLNAME:CHR%元气满满地打招呼 \@%
	ELSE
		あいさつ = 来画画吧ー/%\@ TALENT:CHR:体型 < 0 ? 一起玩吧ー # 要抱抱ー \@%/
		あいさつ += @"%\@ TALENT:CHR:音楽知識 == 2 ? 唱首歌吧！ # 讲个故事吧！ \@%/%\@ TALENT:CHR:動物耳 ? 摸摸你的耳朵！ # \@%/"
		あいさつ += @"%\@ CFLAG:CHR:妊娠経過日数 > 39 ? 肚子好大啊！ # 你还好吗ー？ \@%/%\@ TALENT:CHR:心情 > 0 ? 有开心事吗？ # \@%/"
		あいさつ += @"%\@ K30_JOBCHECK(CHR) ? 工作忙吗？ # \@%/%\@ BASE:CHR:酒気 <= 2 ? # 酒臭味好ー重！ \@%/%\@ TALENT:CHR:心情 < 0 ? 不要生气哦！ # \@%/"
		あいさつ += @"%\@ PALAM:CHR:潤滑 >= PALAMLV:7 ? 尿裤子了吗！？ # \@%/"
		あいさつ += @"%\@ TCVAR:CHR:発情 || CFLAG:CHR:积攒度 > 850 || TCVAR:CHR:媚薬 ? 脸红红的哦？没事吧？ # \@%/"
		CALL COLORMESSAGE, @"「%角色名%、%TEXTR(あいさつ)%」", K30_CHILDWORD() ,1 ,0
		IF 角色名 == "姐姐"
			PRINTFORMDL %SHOW_CHILDNAME(30)%正在和%CALLNAME:CHR%说话
		ELSE
			PRINTFORMDL %SHOW_CHILDNAME(30)%好像想让%CALLNAME:CHR%陪她玩
		ENDIF
	ENDIF
ELSEIF ONCE(@"自%TOSTR(CHR)%")
	あいさつ = %\@ INRANGE(TIME, 240, 599) ? 早上好 # %K30_GREETING_EIKI()% \@%
	SELECTCASE CHR
		;映姫
		CASE 30
			IF CHILD_SEX:30:(TALENT:育児中) == 1
				CALL COLORMESSAGE, @"「妈妈，那个呢…」", K30_CHILDWORD() ,1 ,0
			ELSE
				CALL COLORMESSAGE, @"「妈，那个…」", K30_CHILDWORD() ,1 ,0
			ENDIF
			PRINTFORMDL %SHOW_CHILDNAME(30)%正和映姬攀谈着
		;小町/久侘歌
		CASE 76, 129
			CALL COLORMESSAGE, @"「%CALLNAME:CHR%姐%あいさつ%」", K30_CHILDWORD() ,1 ,0
			SIF K30_JOBCHECK(ARG)
				CALL COLORMESSAGE, @"「工作%\@ CHR == 76 ? …有在努力吧？ # 请加油哦 \@%」", K30_CHILDWORD() ,1 ,0
			PRINTDATADL
				DATAFORM %SHOW_CHILDNAME(30)%向%CALLNAME:CHR%%TEXTR("随意地/亲昵地")%打招呼
				DATAFORM %CALLNAME:CHR%和%SHOW_CHILDNAME(30)%亲密交谈的样子简直就像姐%\@ CHILD_SEX:30:(TALENT:育児中) == 1 ? 妹 # 弟 \@%一样
			ENDDATA
		;ヘカーティア
		CASE 113
			CALL COLORMESSAGE, @"「赫喀提亚大人%あいさつ%」", K30_CHILDWORD() ,1 ,0
			PRINTDATADL
				DATAFORM %SHOW_CHILDNAME(30)%向赫喀提亚恭恭敬敬地打招呼
				DATAFORM 在赫喀提亚面前%SHOW_CHILDNAME(30)%似乎有些紧张
			ENDDATA
		;さとり/阿求/幽々子/紫/妖夢
		CASE 49, 80, 66, 26, 23
			CALL COLORMESSAGE, @"「%CALLNAME:CHR%桑%あいさつ%」", K30_CHILDWORD() ,1 ,0
			SIF K30_JOBCHECK(ARG)
				CALL COLORMESSAGE, @"「您工作辛苦了」", K30_CHILDWORD() ,1 ,0
			PRINTDATADL
				DATAFORM %SHOW_CHILDNAME(30)%正笑着向%CALLNAME:CHR%打招呼
				DATAFORM %CALLNAME:CHR%和%SHOW_CHILDNAME(30)%正在聊%TEXTR("家常/工作")%
			ENDDATA
		;神霊廟住人
		CASE 56, 89 TO 92
			CALL COLORMESSAGE, @"「%CALLNAME:CHR%桑%あいさつ%」", K30_CHILDWORD() ,1 ,0
			PRINTFORMDL  %SHOW_CHILDNAME(30)%对%CALLNAME:CHR%打招呼
		CASEELSE
			角色名 = %CALLNAME:CHR%%\@ TALENT:CHR:神霊 ? 大人 # 桑 \@%
			CALL COLORMESSAGE, @"「%角色名%%あいさつ%」", K30_CHILDWORD() ,1 ,0
			SIF K30_JOBCHECK(ARG)
				CALL COLORMESSAGE, @"「您工作辛苦了」", K30_CHILDWORD() ,1 ,0
			PRINTFORMDL  %SHOW_CHILDNAME(30)%正%TEXTR("彬彬有礼地/恭敬地")%向%CALLNAME:CHR%打招呼
	ENDSELECT
ENDIF

;仕事中の声掛けか判定する関数
@K30_JOBCHECK(ARG)
#FUNCTION
SIF !仕事中チェック(ARG)
	RETURNF 0
;玩耍/宴会に参加/宴会後の酔い/女子会
SIF GROUPMATCH(CFLAG:ARG:職種, 46, 49, 51, 52)
	RETURNF 0
;其他職種
RETURNF 1

;@M_KOJO_EVENT_K30_CHILD_DAIRY_口上手紙(ARG)


;@M_KOJO_EVENT_K30_CHILD_DAIRY_共通手紙(ARG, ARGS)

@M_KOJO_EVENT_K30_CHILD_DAIRY_寺子屋(ARGS,ARG,ARG:1)
SIF GETBIT(CFLAG:1804, TALENT:育児中 % 64)
	RETURN
IF ARGS == "怪我させた"
	CALL K30_CLEAR_TEXTLINE(4)
	PRINTFORMDL 看来是%SHOW_CHILDNAME(30)%为了保护被欺负的孩子，独自一人对抗欺凌者们的结果……
	PRINTFORML 
	PRINTFORML 「……嗯，她挥舞着尺子，毫不留情地狠狠教训了那些欺凌者」
	PRINTFORML 「最后还让那些孩子全部跪坐着，一边继续说教一边狠狠地打他们……」
	PRINTFORML 「听说被她教训的孩子哭着道歉的场景，连老师都有些退缩了」
	PRINTFORML 「说是像我吧，确实是像……但做得太过火了可不好呢……」
	PRINTFORML 
	PRINTFORMDL 被小阎魔大人毫不留情地鞭打，即使是大人也吃不消
	PRINTFORMDL 那些孩子需要治疗和心理疏导吧……
ELSEIF ARGS == "褒められた"
	CALL K30_CLEAR_TEXTLINE(2)
	PRINTFORML 
	IF RAND:2
		PRINTFORML 「呵呵呵♪　虽然%SHOW_CHILDNAME(30)%不让说，说是『会不好意思』」
		PRINTFORML 「但那孩子每天早上都早早出门，为了大家打扫教室呢」
		PRINTFORML 「真是太好了呢，%K30_C_NAME(MASTER)%」
		PRINTFORML 「%SHOW_CHILDNAME(30)%好好地继承了你为数不多的优点哦」
		PRINTFORMDL ……虽然被映姬拐弯抹角地贬低了，但因为是事实，所以什么也说不出来
	ELSE
		PRINTFORML 「………就是这样，听说她妥善地调解了朋友之间的争吵」
		PRINTFORML 「%SHOW_CHILDNAME(30)%虽然说着『没什么大不了的。只是模仿妈妈』……」
		PRINTFORML 「但那孩子是经过自己思考，做出了她认为正确的行动」
		PRINTFORML 「呵呵呵……不只是身体，%SHOW_CHILDNAME(30)%也茁壮成长了呢」
		PRINTFORMDL 映姬似乎为自己孩子的成长感到无比欣慰……
	ENDIF
ENDIF

;無自覚孕ませ関連口上
@K30_INSENSIBLE_PREGNANCY
#DIM NUM
LOCAL = FLAG:立絵種類
FLAG:立絵種類 = 1
LOCAL:1 = CFLAG:30:差し替え適用
CFLAG:30:差し替え適用 = 1
IF CFLAG:妊娠経過日数 == 40
	COM_STR = K30_無表情
	CALL PRINT_FACE, 30, "無表情", "妊娠服"
	PRINTFORML 「是啊，这一定是哪里搞错了……一定是这样……」
	PRINTFORML 「肚子变大只是变胖了而已……」
	PRINTFORML 「容易疲劳，月经不来……只是身体不适而已」
	PRINTFORML 「没错，一定是这样……这种事，怎么可能发生……！」
	PRINTFORMDW 映姬拼命地试图欺骗自己……
ELSEIF CFLAG:妊娠経過日数 == 60
	CALL PRINT_FACE, 30, "絶頂", "妊娠服"
	PRINTFORML 「怀孕……我，有孩子了……？」
	PRINTFORML 「为什么，为什么……我会，变成这样……」
	PRINTFORML 「为什么？为什么？！为什么！！！」
	PRINTFORML 「呜……呜呜、呜……」
	PRINTFORMDW 映姬因巨大的冲击而只能哭泣……
ELSEIF CFLAG:出産経過日
	COM_STR = K30_無表情
	CALL PRINT_FACE, 30, "無表情", "服_睡衣"
	PRINTFORML 「虽然最终也不知道孩子的父亲是谁」
	PRINTFORML 「但是，忍着剧痛生下这个孩子的是我」
	PRINTFORML 「%SHOW_CHILDNAME(30)%的母亲无疑就是我……」
	PRINTFORML 「我，只有我能保护你……」
	PRINTFORMDW 在熟睡的孩子身旁，映姬独自一人下定决心要抚养%SHOW_CHILDNAME(30)%
	;出産隠蔽処理
	NUM = TALENT:育児中
	CSTR:30:83 = %SHOW_CHILDNAME(30)%
	CHILDNAME:30:NUM =
	CHILD_WEATHER:30:NUM = 
	CHILD_AREA:30:NUM = 0
	CHILD_SEX:30:NUM = 0
	CHILD_BIRTHDAY:30:NUM = 0
	CHILD_BIRTHDATE:30:NUM = 0
	CHILD_LOVE:30:NUM = 0
	CFLAG:育児人数 --
	CFLAG:兒童人数 --
	CFLAG:MASTER:兒童人数 --
	CFLAG:MASTER:育児人数 --
	TALENT:育児中 = 0
	SIF TALENT:母乳体質 == 1
		TALENT:母乳体質 = 0
	IF CFLAG:乳品质提升 == 1 && TALENT:胸圍 > -2
		TALENT:胸圍 -= 1
		CFLAG:乳品质提升 = 0
	ENDIF
ENDIF
FLAG:立絵種類 = LOCAL
CFLAG:30:差し替え適用 = LOCAL:1
COM_STR = 
RETURN 1