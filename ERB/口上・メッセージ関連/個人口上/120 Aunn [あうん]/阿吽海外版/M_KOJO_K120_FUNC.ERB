;更新日期：2024年12月31日
;--------------------------------------------------------
;K120_INSEMINATION_POSSIBLE
;Getter method to determine if insemination is possible.
;This means: Not pregnant, permission to do it raw on dangerous day and not using contraceptives (also having no condom equipped).
;
;Return: True if insemination possible, false otherwise.
;--------------------------------------------------------
@K120_INSEMINATION_POSSIBLE
#FUNCTION
RETURNF (!TALENT:妊娠 && CFLAG:生解禁 >= 2 && !TCVAR:口服避孕薬 && !TEQUIP:PLAYER:避孕套)


;--------------------------------------------------------
;K120_IS_DANGEROUS_DAYS
;Getter method to determine if currently on either "Before dangerous day", "Dangerous day" or "Ovulation".
;
;Return: True if on (any) dangerous day, false otherwise.
;--------------------------------------------------------
@K120_IS_DANGEROUS_DAYS
#FUNCTION
RETURNF (CFLAG:900 > 5 && CFLAG:900 < 9)

;--------------------------------------------------------
;K120_PLAYER_EJACULATION_IMMINENT
;Getter method to determine if player is about to ejaculate and not currently ejaculating.
;
;Return: True if ejaculation imminent, false otherwise.
;--------------------------------------------------------
@K120_PLAYER_EJACULATION_IMMINENT
#FUNCTION
RETURNF (TCVAR:PLAYER:イきそう && !NOWEX:PLAYER:射精)

;--------------------------------------------------------
;K120_INSEMINATION_PLEA
;Method encapsulating the logic and dialogue of her wanting to experience the pleasure of being cummed inside.
;Note: To be used at the end of any and all V penetration calls; if applicable/wished.
;      Also has a case for when Player already came inside, where she begs for even more.
;
;The following is needed for her to beg for insemination:
;- Love
;- Insemination possible (s. function)
;- On dangerous days (s. function)
;- Player is about to climax
;- Current target is Aunn (ID:120) - should always be the case, but we'll make sure here just in case
;
;TODO: Should she try and force insemination, i.e. force player climax at the end of it with flavor text?
;TODO: If yes, make it a toggle in update/first encounter to not spring it on people?
;
;Return: void (simply prints dialogue)
;--------------------------------------------------------
@K120_INSEMINATION_PLEA
IF (TALENT:恋慕 && K120_INSEMINATION_POSSIBLE() && K120_IS_DANGEROUS_DAYS() && K120_PLAYER_EJACULATION_IMMINENT() && EX:TARGET:膣内精液 <= 0 && TARGET == 120)
    PRINTFORML 
    PRINTFORMDW 阿吽 %SPLIT_G(@"注意到你即将高潮:感受到体内的肉棒%FSYN_CN("搏动")%:意识到你已达极限:预感到你马上要%FSYN_CN("射精:v")%")%。
    PRINTFORML 「%MOAN_CN("m",,3)%...%CALLNAME:MASTER%桑...!」
    PRINTFORMDW 当她被%SPLIT_G("受孕:授精:怀孕:怀胎:繁殖:生育")%的%SPLIT_G("可能性:概率:机会:前景:几率:期望")%所%SPLIT_G("迷住:蛊惑:吸引:陶醉:着迷:沉迷:狂喜:亢奋")%时，%SPLIT_G("瞳孔化作心形:舌头耷拉出来:开始流口水:表情变得呆滞:眼神完全失焦:全身发烫:声音开始发抖:体内的野兽苏醒了:深层欲望浮现")%。
    PRINTFORML 「%SPLIT_G(@"里面...!求您全部%FSYN_CN("灌注")%进来!:今天...请务必射在里面!:拜托了、今天就全部%FSYN_CN("灌注")%进来吧!:今天是特别的日子、请射在里面好吗?:我的子宫...请全部填满吧!:求您...今天就全部%FSYN_CN("灌注")%进来...:今天不要拔出去、拜托了!")%」
    PRINTFORMDW 尽管说着『拜托』，她的态度却强势到近乎蛮横。
    IF ABL:Ｖ感覚 >= 5
        PRINTFORMDW 她的%SLIT(TARGET)%内部响应着恳求，\@ RAND:2 == 0 ? 像%FSYN_CN("蛇缠")%般# 紧紧%FSYN_CN("夹紧")%你的\@ %FSYN_CN("阴茎")%，誓要%FSYN_CN("榨取")%干你的%FSYN_CN("蛋蛋")%。
    ENDIF
    SELECTCASE TEQUIP:TARGET:体位
        CASE POSE_MISSIONARY
        ;missionary
            PRINTFORMD 阿吽%SPLIT_G("用腿锁住你的后背:双腿缠住你:用手将你的腰拉向自己:抓住你的手臂往体内拖拽")% 
        CASE POSE_DOGGYSTYLE
        ;doggy style
            PRINTFORMD 阿吽%SPLIT_G(@"用脚踝推你进得更深:将%SPLIT_G("屁股:臀部:后臀:后身")%向你顶来:反手抓住你的手臂往里拉")% 
        CASE POSE_COWGIRL
        ;cowgirl
            PRINTFORMD 阿吽%SPLIT_G("扣住你的手腕压向床铺:将体重压在你胸口:抓住你的肩膀:紧贴着你拥抱:趁你不备急切地摇动腰肢")% 
        CASE POSE_LOTUS
        ;lotus
            PRINTFORMD 阿吽%SPLIT_G("双腿锁住你的后背:用腿缠住你:紧紧抱住你:扭腰阻止你起身")% 
        CASE POSE_REVLOTUS
        ;reverse lotus
            PRINTFORMD 阿吽%SPLIT_G(@"用体重压制你:后仰身体阻止你起身:趁你不备急切地晃动翘臀")% 
        CASEELSE
        ;generic
            PRINTFORMD 阿吽死死缠住你
    ENDSELECT
    PRINTFORMDW 试图强迫你%SPLIT_G("射精:发射:释放:交出")%在里面...
ELSEIF (TALENT:恋慕 && K120_INSEMINATION_POSSIBLE() && K120_IS_DANGEROUS_DAYS() && K120_PLAYER_EJACULATION_IMMINENT() && EX:TARGET:膣内精液 > 0 && TARGET == 120)
    PRINTFORML 
    PRINTFORMDW 阿吽%SPLIT_G(@"注意到你又将高潮:感受到肉棒在体内%FSYN_CN("搏动")%:察觉你再次濒临极限:预感到你马上要%FSYN_CN("射精:v")%")%。
    PRINTFORML 「%MOAN_CN("m",,3)%...%SPLIT_G("射在里面:释放进来:不要拔出去")%啊、%CALLNAME:MASTER%桑! 求您...!」
    IF ABL:Ｖ感覚 >= 5
        PRINTFORMDW 她的%SLIT(TARGET)%内部响应着恳求，\@ RAND:2 == 0 ? 像%FSYN_CN("蛇缠")%般# 狠狠%FSYN_CN("夹紧")%你的\@ %FSYN_CN("阴茎")%，誓要进一步%FSYN_CN("榨取")%干你的%FSYN_CN("蛋蛋")%。
    ENDIF
    PRINTFORMDW 因你先前已顺从她的要求，此刻她不再强行逼迫，只用充满渴求的眼睛凝视着你...
ENDIF

;--------------------------------------------------------
;K120_CAN_INSEMINATION_BE_WASTED
;Getter method to determine if insemination can be wasted by not cumming in the womb.
;Can be used in conjunction with KOJO_EGG files to use before insemination waste comment for extra context.
;
;Return: True if wastable, false otherwise.
;--------------------------------------------------------
@K120_CAN_INSEMINATION_BE_WASTED
#FUNCTION
RETURNF (TALENT:恋慕 && K120_INSEMINATION_POSSIBLE() && K120_IS_DANGEROUS_DAYS() && NOWEX:PLAYER:射精)

;--------------------------------------------------------
;K120_INSEMINATION_WASTE_COMMENT
;Method encapsulating the logic and dialogue of her complaining about the Player wasting his seed.
;Note: To be used at the end of any and all non-V penetration calls; if applicable/wished.
;
;The following is needed for her to comment on the waste (s. K120_CAN_INSEMINATION_BE_WASTED):
;- Love
;- Insemination possible
;- On dangerous days
;- Player is ejaculating
;
;Parameters:
;- ARGS -- Passable string of ejaculation destination used for determining appropriate dialogue
;
;Return: void (simply prints dialogue)
;--------------------------------------------------------
@K120_INSEMINATION_WASTE_COMMENT(ARGS)
IF K120_CAN_INSEMINATION_BE_WASTED()
    PRINTFORML 
    PRINTFORMDL 阿吽看起来%SPLIT_G("悲伤的:不开心的:阴郁的:怅然若失的:沮丧的:闷闷不乐的:凄凉的")%，因为你的%SAMEN_DESCRIPTION(,1)%目标不是她的子宫...
    SELECTCASE ARGS
        CASE "inMouth"
            PRINTFORMW 「我的嘴里... 真是浪费...」
        CASE "onHand"
            PRINTFORMW 「我的手上... 真是浪费...」
        CASE "onChest"
            PRINTFORMW 「我的胸上... 真是浪费...」
        CASE "onPubicArea"
            PRINTFORMW 「这么近却又那么远... 真是浪费...」
        CASE "onFeet"
            PRINTFORMW 「我的脚上... 真是浪费...」
        CASE "inAnus"
            PRINTFORMW 「进错洞了... 真是浪费...」
        CASE "onArmpits"
            PRINTFORMW 「我的腋下... 真是浪费...」
        CASE "inHair"
            PRINTFORMW 「我的头发上... 真是浪费...」
        CASE "onPubicHair"
            PRINTFORMW 「我的阴毛上... 这么近却又那么远... 真是浪费...」
    ENDSELECT
ENDIF

;--------------------------------------------------------
;K120_KISS_DIALOGUE
;Method encapsulating the logic and dialogue of kissing Aunn. Includes first kiss flair.
;
;Return: void (simply prints dialogue)
;--------------------------------------------------------
@K120_KISS_DIALOGUE
;Intro of initiating the kiss
IF CFLAG:诶嘿嘿
    PRINTFORMDW 你%SPLIT_G("抹去了最后那点距离:贴近阿吽:将身体压向阿吽")%，%SPLIT_G("热情地:激烈地:炽热地:渴求地")% %SPLIT_G("将唇覆上她的唇瓣:与她的双唇相触:亲吻她:与她唇齿交缠")%。
ELSE
    PRINTFORMDW 你%SPLIT_G("缩短距离:将阿吽拉近:向前倾身")%，%SPLIT_G("温柔地:怜爱地:轻柔地:小心翼翼地")% %SPLIT_G("将唇贴上她的唇瓣:与她的双唇相触:亲吻她:将唇印在她的唇上")%。
ENDIF

;Kiss dialogue (and end of it) + Handling of first kiss (which does not go through the final block)
PRINTFORML 「%RANDKISS_CN("吻")%... %FSYN_CN("嘴唇闷音")%...」
IF (TALENT:恋慕 && !TALENT:無接吻経験)
    PRINTFORMDW 你用舌头%SPLIT_G("撬开:分开:探入")%她%SPLIT_G("柔软的:娇嫩的:如丝绒般的:柔滑的")%唇瓣，当深入时使亲吻变得%SPLIT_G("深情:热烈:充满爱意:性感")%。
    PRINTFORML 「%RANDLIPS_CN(,"kiss:heart:name")%...」
    PRINTFORMDW 阿吽%SPLIT_G("回应:反触:应和:回探")%着，%SPLIT_G("温柔地:怜爱地:轻柔地:羞怯地")%用舌%SPLIT_G("轻抚:挑逗:缠绕:顶弄:轻戳")%你的舌尖。
    PRINTFORMDW 你%SPLIT_G("抽离:结束亲吻:后仰")%时，%SPLIT_G("银丝在二人唇间相连:形成一道唾液之桥:扯出晶莹的丝线:分不清口中唾液属于谁")%。
ELSEIF TALENT:無接吻経験
    PRINTFORMDW 阿吽睁大双眼，初次感受他人唇瓣的柔软触感。
    IF (TALENT:思慕 || TALENT:恋慕)
        PRINTFORMDW 当短暂接触结束，她的震惊迅速化为喜悦，开心地环抱住你。
        PRINTFORML 「%FSYN_CN("唇间溢出")%... 我的初吻...」
        PRINTFORMDW 你问她是否讨厌这样，她用力摇头。
        PRINTFORML 「不是的... 我很高兴初吻对象是%CALLNAME:MASTER%桑... 您呢...？」
        IF TALENT:MASTER:無接吻経験
            PRINTFORMDW 你微笑点头告诉她这也是你的初次。
            PRINTFORML 「真的吗？！我好开心！您竟然在众人中选择了我...」
            PRINTFORMDW 她更紧地抱住你，指尖带着爱意抚过你的背脊。
        ELSE
            PRINTFORMDW 你带着歉疚苦笑挠挠脸颊，但她再次摇头。
            PRINTFORML 「别道歉！没关系的... 我... 我不该问的... 但、但绝对不是伤心哦！」
            PRINTFORMDW 她把脸埋进你胸口蹭着。
        ENDIF
        PRINTFORML 「接吻...」
        PRINTFORMDW 阿吽松开怀抱，指尖描摹着自己的唇瓣，用小狗般的眼神望着你。
        PRINTFORML 「可、可以再来一次吗...？」
        PRINTFORMDW 你用行动回答——倾身再次封住她的唇...
    ELSE
        PRINTFORMDW 当你结束轻吻，她的震惊逐渐消退，指尖描摹着自己的唇瓣。
        PRINTFORML 「我的初吻...」
        PRINTFORMDW 你问她是否讨厌这样，她摇摇头。
        PRINTFORML 「不是的... 只是觉得进展有点太快了...」
        PRINTFORMDW 阿吽用探究的眼神抬眼望你。
        PRINTFORML 「这该不会... 也是您的... 第一次？」
        IF TALENT:MASTER:無接吻経験
            PRINTFORMDW 你肯定地点头。
            PRINTFORML 「啊... 哈哈... 这样真的好吗...？」
        ELSE
            PRINTFORMDW 你歉疚地皱眉挠挠脸颊。
            PRINTFORML 「啊... 这样啊...」
        ENDIF
        PRINTFORMDW 她似乎十分懊悔...
    ENDIF
    RETURN 1
ELSE
    PRINTFORML 「%FSYN_CN("唇间溢出")%...」
ENDIF

;Finishing dialogue
IF TALENT:恋人
    PRINTFORML 「%MOAN_CN("m")%...%CALLNAME:MASTER%桑... 呜嘿嘿～，坠喜欢你了～♥」
    PRINTFORMDW 她神情融化般痴痴笑着，醉倒在爱意里...
ELSE
    PRINTFORMDW 她淫荡的视线无声地缠绕着你...
ENDIF

;--------------------------------------------------------
;K120_DO_NOTHING_CHOICES
;Method encapsulating the dialogue for extra options in the "Do Nothing" command.
;
;Return: void (prints dialogue)
;--------------------------------------------------------
@K120_DO_NOTHING_CHOICES
PRINTFORMDL 你想对阿吽做什么？
PRINTFORMDL
PRINTFORMDL [ 0] 舔她的腋下
PRINTFORMDL [ 1] 用阴茎摩擦她的腋间
PRINTFORMDL [ 2] 舔她的肚脐
PRINTFORMDL [ 3] 与她的头发交合
PRINTFORMDL [ 4] 检查并爱抚她的阴毛
PRINTFORMDL [ 5] 阴毛素股
PRINTFORMDL [ 6] 抚摸毛茸茸的尾巴
PRINTFORMDL
PRINTFORMDL [99] 什么也不做

;--------------------------------------------------------
;K120_LICK_ARMPIT
;Custom function which handles COST, EXP gain and SOURCE for licking Aunn's armpit.
;Mix of COMF15 (Clit caress) and COMF1 (Cunnilingus) to make it worthwhile
;
;Sets the following:
;Cost:
;Stamina      + 5
;Energy       + 50
;
;Gain:
;Sex Exp         + 1
;Oral Sex Exp    + 1
;
;Source:
;Player M Pleas. = 50
;Pleasure        = 100
;Affection       = 150
;Desire          = 150
;Exposure        = 100
;Animosity       = 370
;Passive         = 145 + 270 * ABL:Submission
;
;Return: void
;--------------------------------------------------------
@K120_LICK_ARMPIT
DOWNBASE:体力 += 5
DOWNBASE:気力 += 50

SOURCE:PLAYER:快Ｍ += 50
SOURCE:歓楽 = 100
SOURCE:情愛 = 150
SOURCE:欲情 = 150
SOURCE:露出 = 100
SOURCE:反感 = 370
SOURCE:受動 = 145 + 270 * ABL:従順

IF HETEROSEX(TARGET,PLAYER)
    EXP:(50 + HETEROSEX(TARGET,PLAYER)) += 3
    EXP:PLAYER:(50 + HETEROSEX(TARGET,PLAYER)) += 3
ENDIF

EXP:PLAYER:口淫経験 ++

;--------------------------------------------------------
;K120_ARMPIT_SEX
;Custom function which handles COST, EXP gain and SOURCE for fucking Aunn's armpit.
;Mix of COMF83 (Sumata) and COMF43 (Onahole)
;
;Sets the following:
;Cost:
;Stamina      + 20
;Energy       + 90
;
;Gain:
;Sex Exp         + 3
;
;Source:
;Player C Pleas. = 120
;Affection       = 60
;Sex. behav.     = 150
;Accomplishment  = 50
;Submission      = 500
;Deviance        = 140
;Antipathy       = 120
;
;Return: void
;--------------------------------------------------------
@K120_ARMPIT_SEX
DOWNBASE:体力 += 20
DOWNBASE:気力 += 90

SOURCE:PLAYER:快Ｃ = 120
SOURCE:情愛 = 60
SOURCE:性行動 = 150
SOURCE:達成 = 50
SOURCE:屈従 = 500
SOURCE:逸脱 = 140
SOURCE:反感 = 120

IF HETEROSEX(TARGET,PLAYER)
    EXP:(50 + HETEROSEX(TARGET,PLAYER)) += 3
    EXP:PLAYER:(50 + HETEROSEX(TARGET,PLAYER)) += 3
ENDIF

;--------------------------------------------------------
;K120_LICK_BELLY_BUTTON
;Custom function which handles COST, EXP gain and SOURCE licking Aunn's belly button.
;A shittier version of cunnilingus essentially
;
;Sets the following:
;Cost:
;Stamina      + 2
;Energy       + 25
;
;Source:
;Player M Pleas. = 50
;Affection       = 40
;Desire          = 40
;Lubrication     = 100
;Exposure        = 30
;Deviance        = 15
;Submission      = 300
;
;Return: void
;--------------------------------------------------------
@K120_LICK_BELLY_BUTTON
DOWNBASE:体力 += 2
DOWNBASE:気力 += 25

SOURCE:PLAYER:快Ｍ += 50
SOURCE:情愛 = 40
SOURCE:欲情 = 40
SOURCE:液体 = 100
SOURCE:露出 = 30
SOURCE:逸脱 = 15
SOURCE:屈従 = 300

;--------------------------------------------------------
;K120_HAIR_SEX
;Custom function which handles COST, EXP gain and SOURCE for fucking Aunn's hair.
;Worthless outside of a good submission score
;
;Sets the following:
;Cost:
;Stamina      + 5
;Energy       + 20
;
;Source:
;Player C Pleas. = 90
;Exposure        = 10
;Deviance        = 50
;Submission      = 1200
;
;Return: void
;--------------------------------------------------------
@K120_HAIR_SEX
DOWNBASE:体力 += 5
DOWNBASE:気力 += 20

SOURCE:PLAYER:快Ｃ += 90
SOURCE:露出 += 10
SOURCE:逸脱 += 50
SOURCE:屈従 = 1200

;--------------------------------------------------------
;K120_ARMPITS_NOT_UNLOCKED_DIALOGUE
;Method encapsulating and printing the dialogue for when the Player has not yet encountered the armpit scene through the caress command.

;Return: void (prints dialogue)
;--------------------------------------------------------
@K120_ARMPITS_NOT_UNLOCKED_DIALOGUE
PRINTFORMDW 你在心中敲定了主意，却突然听见脑海中回响起一个声音。
PRINTFORMDL 
SETCOLOR C_AQUA
FONTBOLD
PRINTFORMW ...听得见吗... ...听得见吗... %CALLNAME:MASTER%...？
PRINTFORMW ...此刻...我正直接向你的心倾诉...你能听见吗...？
PRINTFORMW ...阿吽...尚未知晓腋下的趣味...用抚摸让她体会吧...
PRINTFORMW ...抚摸吧...你必须抚摸她... %CALLNAME:MASTER%...
CALL SET_KOJO_COLOR(120)
FONTREGULAR
PRINTFORMDW 那声音逐渐微弱，最终彻底消失了...

;--------------------------------------------------------
;K120_ARMPITS_REJECTED_DIALOGUE
;Method encapsulating and printing the dialogue and choice for when the Player has previously rejected armpit stuff and now tries to access it in the "Do Nothing" menu.

;Return: void (prints dialogue)
;--------------------------------------------------------
@K120_ARMPITS_REJECTED_DIALOGUE
PRINTFORMDW 你在心中敲定了主意，却突然听见脑海中回响起一个声音。
PRINTFORMDL 
SETCOLOR C_AQUA
FONTBOLD
PRINTFORMW ...听得见吗... ...听得见吗... %CALLNAME:MASTER%...？
PRINTFORMW ...此刻...我正直接向你的心倾诉...你能听见吗...？
PRINTFORMW ...你曾拒绝过腋下的趣味...要纠正自己的错误吗...？
PRINTFORMW ...这般重要的抉择...若此刻接受腋下的趣味...就再不能反悔了...
PRINTFORML 
CALL ASK_YN("接受腋下的趣味", "拒绝腋下的趣味")
IF !RESULT
    PRINTFORMW ...欢庆吧...欢庆腋下的趣味... %CALLNAME:MASTER%...
    CFLAG:1001 = 1
ELSE
    PRINTFORMW ...那就这样吧... %CALLNAME:MASTER%...
ENDIF
CALL SET_KOJO_COLOR(120)
FONTREGULAR
PRINTFORMDW 那声音逐渐微弱，最终彻底消失了...

;--------------------------------------------------------
;K120_TOUCH_FLUFFY_TAIL
;Custom function which handles COST, EXP gain and SOURCE for touching Aunn's fluffy tail.
;Good submission score, good lubrication (half of cunnilingus) and cheap cost.
;But adds no pleasure or anything. Best used for foreplay or for the joy of touching fluffy tail!
;
;Sets the following:
;Cost:
;Stamina      + 3
;Energy       + 10
;
;Source:
;Submission  = 1000
;Lubrication = 300
;
;Return: void
;--------------------------------------------------------
@K120_TOUCH_FLUFFY_TAIL
DOWNBASE:体力 += 3
DOWNBASE:気力 += 10

SOURCE:屈従 = 1000
SOURCE:液体 = 500

;--------------------------------------------------------
;K120_CARESS_PUBIC_HAIR
;Custom function which handles COST, EXP gain and SOURCE for caressing Aunn's pubic hair
;Literally just clit caress, but used with descriptive focus on pubic hair.
;
;Sets the following:
;Cost:
;Stamina             + 5
;Energy              + 50
;
;Source:
;Aunn C pleas.       = 80 (+ hand skill * 10 OR + 50 if hand skill >= 4) * 1.5 if dexterous fingers
;Pleasure            = 100
;Affection           = 150
;Desire              = 150
;Exposure            = 100
;Antipathy           = 370
;Passive             = 145 + (270 * Submission level)
;
;Sex exp.            + 3
;Player handjob exp. + 1
;
;Return: void (sets EXP etc)
;--------------------------------------------------------
@K120_CARESS_PUBIC_HAIR
DOWNBASE:体力 += 5
DOWNBASE:気力 += 50

SOURCE:快Ｃ = 80
IF ABL:PLAYER:指 >= 4
    SOURCE:快Ｃ += 50
ELSE
    SOURCE:快Ｃ += ABL:PLAYER:指 * 10
ENDIF

IF TALENT:PLAYER:靈活手指
    TIMES SOURCE:快Ｃ , 1.50
    TIMES SOURCE:液体 , 1.50
ENDIF


SOURCE:歓楽 = 100
SOURCE:情愛 = 150
SOURCE:欲情 = 150
SOURCE:露出 = 100
SOURCE:反感 = 370
SOURCE:受動 = 145 + 270 * ABL:従順

IF HETEROSEX(TARGET,PLAYER)
    EXP:(50 + HETEROSEX(TARGET,PLAYER)) += 3
    EXP:PLAYER:(50 + HETEROSEX(TARGET,PLAYER)) += 3
ENDIF

EXP:PLAYER:手淫経験 ++

;--------------------------------------------------------
;K120_PUBIC_HAIR_SEX
;Custom function which handles COST, EXP gain and SOURCE for fucking Aunn's pubic hair.
;Literally just sumata, but used with descriptive focus on pubic hair.
;
;Sets the following:
;Cost:
;Stamina         + 40
;Energy          + 130
;
;Source:
;Player C pleas. = 150
;Aunn C pleas.   = 150
;Affection       = 80
;Sex behav.      = 250
;Accomp.         = 50
;Submission      = 500
;Deviancy        = 120
;Antipathy       = 120
;
;Sex exp.        + 7
;
;Return: void (sets EXP etc)
;--------------------------------------------------------
@K120_PUBIC_HAIR_SEX
DOWNBASE:体力 += 40
DOWNBASE:気力 += 130

SOURCE:PLAYER:快Ｃ = 150
SOURCE:快Ｃ = 150
SOURCE:情愛 = 80
SOURCE:性行動 = 250
SOURCE:達成 = 50
SOURCE:屈従 = 500
SOURCE:逸脱 = 120
SOURCE:反感 = 120

IF HETEROSEX(TARGET,PLAYER)
    EXP:(50 + HETEROSEX(TARGET,PLAYER)) += 7
    EXP:PLAYER:(50 + HETEROSEX(TARGET,PLAYER)) += 7
ENDIF

;--------------------------------------------------------
;K120_PILLOW_TALK_CHOICES
;Method encapsulating the dialogue for the options presented by the "Pillow Talk" command
;
;Return: void (prints dialogue)
;--------------------------------------------------------
@K120_PILLOW_TALK_CHOICES
PRINTFORMDL 你想和阿吽聊些什么呢？
PRINTFORMDL （每种技巧达到里程碑时都会解锁专属对话！）
PRINTFORMDL （里程碑：Lv0, Lv3 和 Lv5）
PRINTFORMDL
PRINTFORMDL [ 0] 聊聊她的手部技巧
PRINTFORMDL [ 1] 聊聊她的私处技巧
PRINTFORMDL [ 2] 聊聊她的后庭技巧
PRINTFORMDL [ 3] 聊聊她的胸部技巧
PRINTFORMDL [ 4] 聊聊她的舌技
PRINTFORMDL [ 5] 聊聊她的腰技
PRINTFORMDL
PRINTFORMDL [99] 在她耳边轻诉甜言蜜语

;--------------------------------------------------------
;K120_BE_SEDUCED_CHOICES
;Method encapsulating the dialogue for the options presented by the "Be Seduced" command
;
;Return: void (prints dialogue)
;--------------------------------------------------------
@K120_BE_SEDUCED_CHOICES
PRINTFORMDL 您想让阿吽谈论什么话题呢？
PRINTFORMDL
PRINTFORMDL [ 0] 谈谈您的手部技巧
PRINTFORMDL [ 1] 谈谈您的私处技巧
PRINTFORMDL [ 2] 谈谈您的肛门技巧
PRINTFORMDL [ 3] 谈谈您的胸部技巧
PRINTFORMDL [ 4] 谈谈您的舌技
PRINTFORMDL [ 5] 谈谈您的腰技
PRINTFORMDL
PRINTFORMDL [99] 在您耳边甜言蜜语

;--------------------------------------------------------
;K120_FIRST_TIME_VAGINAL_SEX
;Method encapsulating the dialogue for Aunn's first vaginal intercourse with the Player.
;
;Parameters:
;- ARGS -- Passable string of current pose used for determining appropriate dialogue and description;
;       -- Passables: lyingMissionary, standingMissionary, doggyStyle, standingDoggyStyle, cowGirl, lotusPosition, reverseLotus
;
;Return: void (prints dialogue)
;--------------------------------------------------------
@K120_FIRST_TIME_VAGINAL_SEX(ARGS)
SELECTCASE ARGS
    CASE "lyingMissionary", "standingMissionary"
        PRINTFORMDW 阿吽将松垮如狗爪般的拳头护在胸前，先是看了看你的%TINKO(MASTER)%，然后看向你。
    CASE "doggyStyle", "standingDoggyStyle", "reverseLotus"
        PRINTFORMDW 阿吽的尾巴僵直竖起，她侧过肩膀先瞥了瞥你的%TINKO(MASTER)%，再转向你。
    CASE "cowGirl", "lotusPosition"
        PRINTFORMDW 阿吽小心翼翼地跨坐上来，将你%TINKO(MASTER)%的根部压在她隆起的阴户上。
ENDSELECT

SELECTCASE ARGS
    CASE "lyingMissionary", "standingMissionary", "doggyStyle", "standingDoggyStyle"
        PRINTFORML 「要、要放进来了吗？」
        PRINTFORMDW 你点头并将肉棒对准她的入口。
    CASE "cowGirl", "lotusPosition", "reverseLotus"
        PRINTFORMW 「我、我要放进去了哦？」
        PRINTFORMDW 你点头协助她将你的肉棒对准入口。
ENDSELECT

PRINTFORML 「哈哇...等、等一下！」
SELECTCASE ARGS
    CASE "lyingMissionary", "standingMissionary"
        PRINTFORMDW 她用手抵住你的胸口轻轻推搡。
    CASE "doggyStyle", "standingDoggyStyle"
        PRINTFORMDW 她弓起后背将臀部挪远。
    CASE "cowGirl", "lotusPosition"
        PRINTFORMDW 她双手撑住你的胸膛抬起腰肢。
    CASE "reverseLotus"
        PRINTFORMDW 她扶着你的双腿提起腰臀。
ENDSELECT

PRINTFORMDW 你询问是否不适或需要时间，她却摇头回应。
PRINTFORML 「不是啦...只是...哈哈...有、有点怕吧？」
PRINTFORMDW 你注意到她微微颤抖，怜爱地揉揉她的脑袋。
PRINTFORML 「唔！对、对不起让你担心了...」
PRINTFORMDW 你告诉她可以按自己节奏慢慢来。
PRINTFORML 「我不想让你等太久，所以...」
SELECTCASE ARGS
    CASE "lyingMissionary", "standingMissionary"
        PRINTFORMDW 阿吽收回双手紧握成拳，深深呼吸数次。
    CASE "doggyStyle", "standingDoggyStyle"
        PRINTFORMDW 阿吽重新挺直腰背，将臀部朝你敞开。
    CASE "cowGirl", "lotusPosition", "reverseLotus"
        PRINTFORMDW 阿吽再度沉下腰肢，让穴口悬停在你的肉棒上方。
ENDSELECT

SELECTCASE ARGS
    CASE "lyingMissionary", "standingMissionary", "doggyStyle", "standingDoggyStyle"
        PRINTFORMW 「我准备好了...」
        PRINTFORMDL 你刚调整姿势，却又被她阻止。
        PRINTFORMW 「等、等等！先告诉你...我是处子...」
    CASE "cowGirl", "lotusPosition", "reverseLotus"
        PRINTFORMW 「我、我要动了哦...」
        PRINTFORMDW 虽如此宣告，她却迟迟未动只是羞怯地望着你。
        PRINTFORMW 「先、先说明...我可是处子...」
ENDSELECT

PRINTFORMDW 你轻笑说这早看出来了。
PRINTFORML 「啊...哈哈...那、那你呢？」
IF IS_DOUTEI(MASTER)
    PRINTFORMDW 你尴尬地搔着脸颊点头。
    PRINTFORML 「呜哇...等等，你居然选我当第一次对象...」
    PRINTFORMW 「明明你自己也紧张，我却在这边害怕...」
    PRINTFORMDW 阿吽拍打自己的脸，朝你挥舞拳头。
ELSE
    PRINTFORMDW 你歉疚地搔着脸颊摇头。
    PRINTFORML 「啊...没关系。知道你有经验反而让我安心些。」
    PRINTFORMDW 阿吽深呼吸几次坚定了决心。
ENDIF
SELECTCASE ARGS
    CASE "lyingMissionary", "standingMissionary", "doggyStyle", "standingDoggyStyle"
        PRINTFORMDW 你们互相点头，你再度调整肉棒位置。
    CASE "cowGirl", "lotusPosition", "reverseLotus"
        PRINTFORMDW 你们互相点头，她重新对准肉棒缓缓坐下。
ENDSELECT

SELECTCASE ARGS
    CASE "lyingMissionary", "standingMissionary", "cowGirl", "lotusPosition"
        PRINTFORMDW 你抬头确认时，看见她紧闭双眼僵硬如石。
    CASE "doggyStyle", "standingDoggyStyle", "reverseLotus"
        PRINTFORMDW 你侧身窥探她面容，发现她紧闭双眼僵硬如石。
ENDSELECT

PRINTFORMDW 你摸索到她的手并十指相扣。
PRINTFORML 「啊...」
PRINTFORMDW 她用力回握，掌心颤抖传递着强烈紧张。
PRINTFORML 「真、真的准备好了！这次是真的！」
SELECTCASE ARGS
    CASE "lyingMissionary", "standingMissionary", "doggyStyle", "standingDoggyStyle"
        PRINTFORMDW 你缓缓推入肉棒，阿吽随着深入不断抬高腰臀。
        PRINTFORMW 「进、进来了...」
        PRINTFORMDW 肉棒逐渐被温热的内部包裹，你谨慎突破着惊人紧致。
        PRINTFORMDW 龟头触到处女膜时稍加施力，阻碍无声瓦解。
    CASE "cowGirl", "lotusPosition", "reverseLotus"
        PRINTFORMDW 阿吽缓缓吞入肉棒，随着深入而咬紧嘴唇。
        PRINTFORMW 「进、进来了...」
        PRINTFORMDW 肉棒逐渐被温热的内部包裹，你谨慎突破着惊人紧致。
        PRINTFORMDW 龟头触到处女膜时阿吽稍加用力，阻碍无声瓦解。
ENDSELECT

PRINTFORML 「疼...呜呜...」
PRINTFORMDW 阿吽将相扣的手拉到嘴边，把你的食指含入口中。
PRINTFORMDW 误以为是自己的手指，她狠狠咬下立时见血。
PRINTFORMDW 你忍痛紧闭双眼咬紧牙关，避免惨叫吓到她。
SELECTCASE ARGS
    CASE "lyingMissionary", "standingMissionary", "doggyStyle", "standingDoggyStyle"
        PRINTFORMDW 如同她忍耐疼痛般，你强忍痛楚稍歇后开始轻柔抽送。
    CASE "cowGirl", "lotusPosition", "reverseLotus"
        PRINTFORMDW 如同她忍耐疼痛般，你强忍痛楚稍歇后她开始上下摆动腰肢。
ENDSELECT

PRINTFORML 「%MOAN_CN()%...哈啊...啊...真的在做了...」
PRINTFORMDW 她睁眼凝视你，潮红面容上痛楚渐褪，欢愉取而代之。
PRINTFORML 「我的第一次...%MOAN_CN()%...和%CALLNAME:MASTER%桑...」
SELECTCASE ARGS
    CASE "lyingMissionary", "standingMissionary", "cowGirl", "lotusPosition"
        PRINTFORMDW 她低头看见你食指的血痕与齿印。
    CASE "doggyStyle", "standingDoggyStyle", "reverseLotus"
        PRINTFORMDW 她瞥向交握的手，发现你食指的血痕与齿印。
ENDSELECT

PRINTFORML 「咬痕...？咦但是——」
PRINTFORMDW 阿吽举起自己食指，却不见伤痕。
PRINTFORML 「啊糟了！我是不是咬到你的手了？！」
PRINTFORMDW 阿吽惊慌失措地松手看向你。
PRINTFORMDW 你爱怜地用伤手抚摸她脸颊，说两人痛楚相连，仅她流血才不公平。
PRINTFORML 「%CALLNAME:MASTER%桑...」
PRINTFORMDW 阿吽欣喜地再度握紧你的手，用脸颊磨蹭...
PRINTFORMW 「对不起还有...」
PRINTFORMW 「谢谢你...」
SELECTCASE ARGS
    CASE "lyingMissionary", "standingMissionary", "doggyStyle", "standingDoggyStyle"
        PRINTFORMDW 她示意继续，你欣然从命...
    CASE "cowGirl", "lotusPosition", "reverseLotus"
        PRINTFORMDW 她重新摆动腰肢，加快节奏迈向下一阶段...
ENDSELECT

;--------------------------------------------------------
;K120_EJAC_STAMINA_COMMENT
;Method encapsulating the dialogue for continuing intercourse after the Player's ejaculation.
;This is to avoid copy-pasta. Called in: K120_CONTINUED_VAGINAL_AFTER_EJAC, K120_CONTINUED_ANAL_AFTER_EJAC
;
;Return: void (prints dialogue)
;--------------------------------------------------------
@K120_EJAC_STAMINA_COMMENT
PRINTFORMW 「%MOAN_CN(,,2)%... %SPLIT_G(@"才刚结束就要继续真的不要紧吗？:明明才射过...:体力居然还有剩余吗？:你的耐力——%MOAN_CN()%——真是厉害呀！:还想要继续做吗？")%」

;--------------------------------------------------------
;K120_CONTINUED_VAGINAL_AFTER_EJAC
;Method encapsulating the dialogue for continued vaginal intercourse after the Player's ejaculation
;
;Return: void (prints dialogue)
;--------------------------------------------------------
@K120_CONTINUED_VAGINAL_AFTER_EJAC
IF (TALENT:恋慕 && K120_INSEMINATION_POSSIBLE() && K120_IS_DANGEROUS_DAYS())
    PRINTFORML 「%MOAN_CN("m",,2)%... %SPLIT_G("嗯，我还要:再多给我一些:你还要继续啊:我肚子里还没填够呢:更多、还要更多:再射进来吧:继续、不要停:真开心你还没结束")%～♥!」
PRINTFORMDW 阿吽%SPLIT_G("发出愉悦的叹息:仰起头:咬住嘴唇:开始出汗:将自己完全交给你:同样热情地回应你的欲望")%，而你继续%SPLIT_G("搅动她的内部:勤奋地抽插:与她交合:与她做爱:你们的恩爱行为:你们的交欢:你们的性事:撞击她的蜜穴")%...
ELSE
    CALL K120_EJAC_STAMINA_COMMENT()
ENDIF

;--------------------------------------------------------
;K120_CONTINUED_VAGINAL_AFTER_EJAC_PROACTIVE
;Method encapsulating the dialogue for continued vaginal intercourse after the Player's ejaculation while Aunn has initiative
;
;Return: void (prints dialogue)
;--------------------------------------------------------
@K120_CONTINUED_VAGINAL_AFTER_EJAC_PROACTIVE
IF (TALENT:恋慕 && K120_INSEMINATION_POSSIBLE() && K120_IS_DANGEROUS_DAYS())
    PRINTFORMDW 她的%SPLIT_G("表情变得阴暗:双眼俯视着你:喘息加剧")%，并且她%SPLIT_G("沉溺于本能欲望中:加快了动作:不再给你休息的时间")%。
    PRINTFORMW 「%MOAN_CN("m",,2)%... %SPLIT_G(@"抱歉...我停不下来...:我还要更多，%CALLNAME:MASTER%桑...！:你还有存货对吧？:我的子宫还想要更多...:更多...还要更多...！")%」
ELSE
    PRINTFORMW 「%MOAN_CN("m",,2)%... %SPLIT_G(@"明明今天不是...:我的身体自己动起来了...:明明今天不是危险期，身体却渴求更多...:为什么我-%MOAN_CN()%-停不下来？:我知道你才射过，但是...对不起！")%」
ENDIF
PRINTFORMDW 阿吽%SPLIT_G("发出愉悦的叹息:向后仰头:咬住嘴唇:开始出汗:热切地摇动腰肢:同样热情地回应着你的欲望")%，同时她继续%SPLIT_G("搅动自己的内部:勤勉地侍奉你:与你交配:与你做爱:你们的恩爱行为:你们的交欢:你们的性事:将你的肉棒撞击进她的蜜穴")%...

;--------------------------------------------------------
;K120_CONTINUED_ANAL_AFTER_EJAC
;Method encapsulating the dialogue for continued anal intercourse after the Player's ejaculation
;
;Return: void (prints dialogue)
;--------------------------------------------------------
@K120_CONTINUED_ANAL_AFTER_EJAC
IF (TALENT:恋慕 && K120_INSEMINATION_POSSIBLE() && K120_IS_DANGEROUS_DAYS())
    PRINTFORML 「%MOAN_CN("m",,2)%... %SPLIT_G("你确定要在那里做吗？:明明另一个地方已经准备好了...:你只打算照顾这一个地方吗...？:我...你、你不打算用该用的洞吗？")%」
    PRINTFORMDW 尽管抱怨着，阿吽仍%SPLIT_G("发出愉悦的叹息:向后仰头:咬住嘴唇:开始出汗:将自己完全交给你:同样热情地回应你的欲望")%，而你继续%SPLIT_G("搅动她的内部:勤奋地抽插她:与她的屁股做爱:与她做爱:你们的恩爱行为:你们的交欢:你们的性事:撞击她的臀部")%...
ELSE
    CALL K120_EJAC_STAMINA_COMMENT()
ENDIF

;--------------------------------------------------------
;K120_CONTINUED_ANAL_AFTER_EJAC_PROACTIVE
;Method encapsulating the dialogue for continued vaginal intercourse after the Player's ejaculation while Aunn has initiative
;
;Return: void (prints dialogue)
;--------------------------------------------------------
@K120_CONTINUED_ANAL_AFTER_EJAC_PROACTIVE
IF (TALENT:恋慕 && K120_INSEMINATION_POSSIBLE() && K120_IS_DANGEROUS_DAYS())
    PRINTFORMW 「%MOAN_CN("m",,2)%... %SPLIT_G(@"明明今天不该做这里的，但我停不下来...:本该在你射进去前停下的，但是...！:虽然一直命令腰部停下却根本不听使唤！:舒服得根本停不下来...")%」
ELSE
    PRINTFORMW 「%MOAN_CN("m",,2)%... %SPLIT_G(@"我的身体自己在动...::为什么我-%MOAN_CN()%-停不下来？:我知道你才射过，但是...对不起！")%」
ENDIF
PRINTFORMDW 阿吽%SPLIT_G("发出愉悦的叹息:向后仰头:咬住嘴唇:开始出汗:热切地摇动腰肢")%，同时她继续%SPLIT_G("搅动她的内部:勤勉地侍奉你:你们的恩爱行为:你们的交欢:你们的性事:将你的肉棒撞击进她的肛门")%...

;--------------------------------------------------------
;K120_FIRST_TIME_ANAL_SEX
;Method encapsulating the dialogue for Aunn's first anal intercourse with the Player.
;
;Parameters:
;- ARGS -- Passable string of current pose used for determining appropriate dialogue and description;
;       -- Passables: lyingMissionary, standingMissionary, doggyStyle, standingDoggyStyle, cowGirl, lotusPosition, reverseLotus
;
;Return: void (prints dialogue)
;--------------------------------------------------------
@K120_FIRST_TIME_ANAL_SEX(ARGS)
SELECTCASE ARGS
    CASE "lyingMissionary"
        PRINTFORMDW 你抓住阿吽的膝窝，将她的双腿压向腹部，用阴茎戳弄她暴露在外的肛门。
    CASE "standingMissionary"
        PRINTFORMDW 你将阿吽的右腿架在肩上，双手掰开臀瓣，用阴茎抵住她的肛门。
    CASE "doggyStyle", "standingDoggyStyle", "reverseLotus"
        PRINTFORMDW 你掰开阿吽的臀瓣，用阴茎在她肛门上摩擦，随后用龟头顶端戳弄。
    CASE "cowGirl", "lotusPosition"
        PRINTFORMDW 你环住阿吽的腰肢掰开臀瓣，用龟头顶端戳弄她的肛门。
ENDSELECT

PRINTFORML 「诶？要进那里吗？可是那里...不是该进去的洞哦？」
PRINTFORMDW 你挠着脸颊坚持说要进去。
PRINTFORML 「呜...既然是%CALLNAME:MASTER%桑的话...但、但要轻点哦？」
PRINTFORMDW 你缓缓推进，看着龟头没入她丰满的臀部。
PRINTFORMDW 越是深入，阿吽的表情就越发扭曲，发出压抑的痛楚喘息。
SELECTCASE ARGS
    CASE "lyingMissionary", "standingMissionary", "cowGirl", "lotusPosition"
        PRINTFORMDW 她忍耐疼痛时双腿不断轻颤。
        PRINTFORMDW 你插到一半停下，阿吽立刻摇头拍着你的胸膛示意继续。
    CASE "doggyStyle", "reverseLotus"
        PRINTFORMDW 她僵直的尾巴随着深入不停抖动。
        PRINTFORMDW 你插到一半停下，阿吽立刻摇头示意继续。
ENDSELECT

PRINTFORML 「%GROAN()%...」
PRINTFORMDW 听令将肉棒推至根部时，阿吽猛然弓身咬紧牙关。
PRINTFORMDW 见她痛楚难当，你说要放弃抽出来。
SELECTCASE ARGS
    CASE "lyingMissionary", "standingMissionary"
        PRINTFORMDW 她抓住你的腰阻止抽离，拼命摇头。
    CASE "cowGirl", "lotusPosition", "reverseLotus"
        PRINTFORMDW 她将体重压下来阻止抽离，拼命摇头。
    CASE "doggyStyle"
        PRINTFORMDW 她向后顶住肉棒阻止抽离，拼命摇头。
ENDSELECT

PRINTFORMW 「哈啊...哈啊...%CALLNAME:MASTER%桑想做的事，我会忍到习惯为止的」
IF TALENT:恋人
    PRINTFORMW 「恋人就是要这样相互包容吧？以后我也会向您撒娇的...」
ENDIF
PRINTFORMW 「所以别顾虑我...继续动吧...」
PRINTFORMDW 你小心翼翼地摆动腰肢，但她尚未显露快感。
PRINTFORMDW 直肠不断挤压推顶着肉棒，试图驱逐异物。
PRINTFORMDW 抬头看见阿吽脸颊滑落泪珠，却仍没放弃。
PRINTFORMDW 你咽下忧虑——此刻退缩才最伤她。
PRINTFORMDW 于是改为缓慢节奏抽插，让她能预判每次进入。
PRINTFORMDW 持续此节奏直到她体内逐渐放松时——
PRINTFORML 「%MOAN_CN()%...」
PRINTFORMDW ——她唇间漏出了第一声娇吟。
SELECTCASE ARGS
    CASE "lyingMissionary", "standingMissionary", "cowGirl", "lotusPosition"
        PRINTFORMDW 你抚摸她的脸颊加快节奏，呻吟声很快盖过痛哼。
    CASE "doggyStyle", "reverseLotus"
        PRINTFORMDW 你抚摸她的腰侧加速抽插，呻吟声逐渐压过痛呼。
ENDSELECT

PRINTFORML 「%MOAN_CN(,,2)%...屁股里面烧起来了...这、这是什么感觉？」
PRINTFORMDW 你以正常速度抽插作为回答，快感电流窜过她的直肠。
PRINTFORML 「%MOAN_CN()%！变得好舒服...欸嘿嘿，看吧？我就说没问题的！%MOAN_CN()%！」
PRINTFORMDW 你为嘉奖她的忍耐，更用力地摆动腰肢...


;--------------------------------------------------------
;K120_ENDING_POSSIBLE
;Getter method to determine if ending is achievable.
;Following requirements are needed:
; - Lover
; - Had at least one child
; - Finished the story time events (via Conversation command)
;
; - Finished talking to Aunn in the following locations during a date (you will know if you got the right convo if you are asked for a diary entry at the end of it):
; --- TH6:  Scarlet Devil Mansion -> Hall, Dining Room, Library, Basement (Sakuya)
; --- TH7:  Netherworld -> Hakugyokurou/Hakugyokurou Garden (Yuyuko)
; --- TH8:  Bamboo Forest -> anywhere that isn't Eientei or around Yuuka's place (Mokou)
; --- TH9:  Netherworld -> Sanzu River or Higan (Komachi)
; --- TH10: Youkai Mountain (Summit) -> Moriya Shrine Main + Shrine grounds (Kanako)
; --- TH11: Underground -> Remains of Blazing Hell (Okuu)
; --- TH12: Myouren temple -> Main Temple (Shou)
; --- TH13: Myouren temple -> Grounds (Kyouko)
; --- TH14: Youkai Mountain (Foot) -> Deep Forest (Seija)
; --- TH15: Dream World/Moon -> Sea of Tranquility (Junko)
; --- TH16: Netherworld -> Mayohiga (Okina)
; --- TH17: Netherworld -> Primate Spirit Garden (Keiki)
; --- TH18: Youkai Mountain (Summit) -> Mountain Lake (Chimata)
;
; - Beat her in Danmaku
; - Kissed her
; - Saw human village sweets shop event (visit during date)
; - Bought any hat while dating in Liquor Road/Shopping District of the Human Village (conversation command)
; - CURRENTLY DISABLED: Encountered all the cameos in the Residential Area [Tanjiro, Sasha and Char] (conversation command)
; - Found the two piece in the Lunar Capital after getting progression count to 12 (Conversation command while in Pavillion, Capital or Rabbit District)
; - Went on a festival date in the Road of Liminality (conversation option while on a date there)
; - Took a picture with Aunn (visit Kourindou during date and keep using the conversation option)
; - Fished up at least one piece of driftwood together with Aunn
; - Touched fluffy tail to your heart's content via the "Do Nothing" command during sex.
; - Helped Aunn get her revenge against Cirno (talk to Aunn during a date while at the Misty Lake)
;
;Return: True if ending possible, false otherwise.
;--------------------------------------------------------
@K120_ENDING_POSSIBLE
#FUNCTION
RETURNF (TALENT:恋人 && CFLAG:兒童人数 > 0 && CFLAG:1003 >= 18 && CFLAG:1002 >= 13 && CFLAG:TARGET:弾幕勝負勝利 == 1 && !TALENT:無接吻経験 && !FIRSTTIME("AunnSweetsShopEvent", 1) && !FIRSTTIME("AunnBoughtHeadpiece", 1) && !FIRSTTIME("AunnKourindouFindsPolaroidCamera", 1) && CFLAG:1004 && !FIRSTTIME("AunnFishingCaughtDriftwood", 1) && !FIRSTTIME("AunnFindsTheTwoPiece", 1) && !FIRSTTIME("AunnRoLFestivalDate", 1) &&!FIRSTTIME("AunnWinAgainstCirno", 1))

;--------------------------------------------------------
;K120_ENDING_CHOICES
;Method encapsulating the dialogue for the options presented during the ending event
;
;Return: void (prints dialogue)
;--------------------------------------------------------
@K120_ENDING_CHOICES
PRINTFORMDL [ 0] 往后你将每天与她相伴
PRINTFORMDL [ 1] 往后你会不时去探望她
PRINTFORMDL [ 2] 你将离开去追求其他目标

;--------------------------------------------------------
;K120_CLIMAX_DIALOGUE
;Method encapsulating the dialogue for the various climax cases. Adds dialogue and descriptions according to climax source(s).
;
;Return: void (prints dialogue)
;--------------------------------------------------------
@K120_CLIMAX_DIALOGUE
#DIM climaxCount
#DIMS aunnClimaxDialogue
#DIMS aunnClimaxDescription

climaxCount = 0
aunnClimaxDialogue = 「
aunnClimaxDescription =

IF NOWEX:Ｃ絶頂
    aunnClimaxDialogue += "阴蒂..." + SPLIT_G("好舒服:要疯掉了:变得超敏感:电流窜到小腹了:让人家好舒服") + "～♥... "
    aunnClimaxDescription += "阴蒂" + SPLIT_G("挺立着:完全凸出来:不停颤抖:完全显露出来:微微跳动:直挺挺立着")
    climaxCount++
ENDIF

IF NOWEX:Ｖ絶頂
    aunnClimaxDialogue += "小穴..." + SPLIT_G("里面好舒服:被顶到最深处了:有电流窜过小腹:要疯掉了:被搅得一塌糊涂:还要更多") + "～♥... "
    aunnClimaxDescription += climaxCount == 0 ? "蜜穴" # "，蜜穴"
    IF TEQUIP:体位
        aunnClimaxDescription += RAND:2 == 0 ? FSYN_CN("蛇缠:s") + "紧紧缠绕着肉棒" # SPLIT_G("死死夹住:用力压迫:剧烈收缩") + "肉棒"
    ELSE
        aunnClimaxDescription += RAND:2 == 0 ? FSYN_CN("蛇缠:s") + "紧紧绞着异物" # SPLIT_G("死死夹住:用力压迫:剧烈收缩") + "异物"
    ENDIF
    climaxCount++
ENDIF

IF NOWEX:Ａ絶頂
    aunnClimaxDialogue += "肛门..." + SPLIT_G("被插得好深:全身都在发抖:里面烫得要命:完全放不开了") + "～♥... "
    aunnClimaxDescription += climaxCount == 0 ? "菊穴" # "，菊穴"
    IF TEQUIP:体位
        aunnClimaxDescription += RAND:2 == 0 ? FSYN_CN("蛇缠:s") + "紧紧缠绕着肉棒" # SPLIT_G("死死夹住:用力压迫:剧烈收缩") + "肉棒"
    ELSE
        aunnClimaxDescription += RAND:2 == 0 ? FSYN_CN("蛇缠:s") + "紧紧绞着异物" # SPLIT_G("死死夹住:用力压迫:剧烈收缩") + "异物"
    ENDIF
    climaxCount++
ENDIF

IF NOWEX:Ｂ絶頂
    aunnClimaxDialogue += "乳头..." + SPLIT_G("硬得发疼:敏感得不行:已经不是平常的样子了:脑袋都麻掉了") + "～♥... "
    aunnClimaxDescription += climaxCount == 0 ? "乳头" # "，乳头" + SPLIT_G("硬挺起来:变得滚烫:直直挺立:肿胀发硬")
    climaxCount++
ENDIF

IF NOWEX:Ｍ絶頂
    ;along with my mouth
    aunnClimaxDialogue += "嘴唇..." + SPLIT_G("连这里都:酥酥麻麻的:舒服得要命:光是嘴唇就") + "～♥... "
    aunnClimaxDescription += climaxCount == 0 ? "双唇" # "，双唇" + SPLIT_G("接触就浑身发抖:达到敏感极限:向脑袋输送快感电流")
    climaxCount++
ENDIF

SELECTCASE NOWEX:TotalEX
        ;strong climax
        ;強
    CASE 5 TO 15
        PRINTFORM %aunnClimaxDialogue%%MOAN_CN("h")%～♥♥♥...%SLUR(SPLIT_G("要高潮了:忍不住了:要、要去了:人家要:太舒服了"), -2)%～♥♥!!
        ;中高潮
    CASE 2 TO 4
        PRINTFORM %aunnClimaxDialogue%%MOAN_CN("m")%～♥♥...%SPLIT_G("要高潮了:忍不住了:要、要去了:人家要:太舒服了")%～♥♥!
        ;弱高潮
    CASE 1
        PRINTFORM %aunnClimaxDialogue%%MOAN_CN("l")%～♥...%SPLIT_G("要去了:忍不住了:要、要来了:我要:真的好舒服")%～♥!
    CASEELSE
        ;后备
        PRINTFORM %aunnClimaxDialogue%%MOAN_CN("l")%～♥...%SPLIT_G("要去了:忍不住了:要、要来了:我要:真的好舒服")%～♥!
ENDSELECT
PRINTFORML 」

aunnClimaxDescription += "，全身" + FSYN_CN("淫颤:s")
IF NOWEX:四重絶頂
    aunnClimaxDescription += "，四重" + FSYN_CN("高潮") + "席卷全身"
ELSEIF NOWEX:三重絶頂
    aunnClimaxDescription += "，三重" + FSYN_CN("高潮") + "贯穿身体"
ELSEIF NOWEX:二重絶頂
    aunnClimaxDescription += "，双重" + FSYN_CN("高潮") + "冲击全身"
ELSE
    aunnClimaxDescription += "，她" + FSYN_CN("高潮:s")
ENDIF
PRINTFORMDW %aunnClimaxDescription%...

;--------------------------------------------------------
;K120_FISHING_INTRO_DIALOGUE
;Method encapsulating the dialogue for Aunn's line tensing
;
;Return: void (prints dialogue)
;--------------------------------------------------------
@K120_FISHING_INTRO_DIALOGUE
PRINTFORML 「%SPLIT_G("钓到东西了:有东西在拉扯:鱼线被扯动了:鱼线在动:浮标在晃")%!」

;--------------------------------------------------------
;K120_FISHING_LIGHT_CATCH_DIALOGUE
;Method encapsulating the dialogue for Aunn catching something light (fish size < 50, paper, rotor, misc items)
;
;Return: void (prints dialogue)
;--------------------------------------------------------
@K120_FISHING_LIGHT_CATCH_DIALOGUE
PRINTFORML 「%SPLIT_G("但是轻飘飘的啦:没什么力道呢:感觉没怎么拉扯呀:这个很轻松嘛:鱼线都没绷紧:钓竿完全不吃力")%!」
PRINTFORMDL 她%SPLIT_G("轻松地收着线:毫不费力地卷线:轻巧地提起钓竿")%，可%SPLIT_G("钓到的东西:上钩的玩意儿")%并不%SPLIT_G("如期待般有趣:像想象中那么特别:符合她的期望:是她想要的东西:称心如意")%。

;--------------------------------------------------------
;K120_FISHING_MEDIUM_CATCH_DIALOGUE
;Method encapsulating the dialogue for Aunn catching a medium sized fish (size 50 to 150)
;
;Return: void (prints dialogue)
;--------------------------------------------------------
@K120_FISHING_MEDIUM_CATCH_DIALOGUE
PRINTFORML 「%SPLIT_G("力道十足呢:肯定是个大家伙:这次绝对错不了:它在拼命挣扎呢:看到影子了:休想逃掉:非得把你拉上来不可")%!」

;--------------------------------------------------------
;K120_FISHING_HEAVY_CATCH_DIALOGUE
;Method encapsulating the dialogue for Aunn catching a heavy sized fish (size > 150)
;
;Return: void (prints dialogue)
;--------------------------------------------------------
@K120_FISHING_HEAVY_CATCH_DIALOGUE
PRINTFORML 「%SPLIT_G("鱼线绷得太紧了:咕...反抗得好厉害:绝对是大家伙:影子超大的:拉扯力道太强了:好强的力气...再这样钓竿要撑不住了")%!」
PRINTFORMDW 她与%SPLIT_G("猎物:可能的收获:上钩物")%激烈对抗着，%SPLIT_G("钓竿弯成完美的U字形:钓线眼看就要断裂:整个身子都向后仰着:奋力拉扯着钓竿")%。
PRINTFORML 「%SPLIT_G("嗯嗯...给我上来:唔唔...就差一点了:嗯咕...再加把劲:嘎呜...快放弃吧:嘎啊...别再挣扎了")%!!」
PRINTFORMDW %SPLIT_G("紧绷感终于减轻了:她的耐力最终取胜:终于夺得胜利:钓竿坚持到了最后")%，随着猛力一扯将%SPLIT_G("猎物:上钩物")%甩出水面。
PRINTFORMDW 她晃着尾巴将战利品举到你面前，脸上洋溢着%SPLIT_G("满足:成就满满")%的表情。

;--------------------------------------------------------
;K120_FISHING_CALLOUT_DIALOGUE
;Method encapsulating the dialogue for Aunn calling out your catch
;
;Return: void (prints dialogue)
;--------------------------------------------------------
@K120_FISHING_CALLOUT_DIALOGUE
PRINTFORML 「%SPLIT_G("浮漂在上下跳动:鱼线在晃动:钓到东西了哦:水面在漾开波纹:浮漂下面有动静:看到黑影游过去了:钓竿梢在抖呢")%」！

;--------------------------------------------------------
;CHARA_INFO_KOJO_EGG_K120
;Method encapsulating Aunn's character info.
;Included title, character race and ability (hard-coded).
;Shows Events seen, Who's Who Progress and Ending Progress.
;
;Return: 1
;--------------------------------------------------------
@CHARA_INFO_KOJO_EGG_K120()
CALL M_KOJO_EGG_COLOR_K120
;PRINTFORML %CSVCSTR(120, 10)% ;JP
;PRINTFORML %CSTR:120:10% ;EN (Main)
;PRINTFORML %nameArray:120:6% ;Neodev
;Due to the conflict between Main and Neodev the "Race, Ability" etc. profile line is hard-coded here. If you came here after changing the CSV and not seeing it change in-game... I'm sorry!
PRINTFORML ～醉心神佛的守护神兽～　●种族: 狛犬　●能力: 发现神佛程度的能力
RESETCOLOR
PRINTL 

;Aunn unique events. These are the conditions needed for the ending.
PRINTFORML 
FONTBOLD
PRINTFORML 【已触发事件】
FONTREGULAR
CALL K120_PRINT_ACHIEVEMENT_STATE(CFLAG:TARGET:弾幕勝負勝利 == 1, "在弹幕对决中战胜了她")
CALL K120_PRINT_ACHIEVEMENT_STATE(!FIRSTTIME("AunnWinAgainstCirno", 1), "帮助阿吽向琪露诺复仇")
CALL K120_PRINT_ACHIEVEMENT_STATE(CFLAG:1003 >= 18, "告知了她所有异变的经过")
CALL K120_PRINT_ACHIEVEMENT_STATE(CFLAG:1002 >= 13, "一同探访了所有异变发生地点")
CALL K120_PRINT_ACHIEVEMENT_STATE(!FIRSTTIME("AunnSweetsShopEvent", 1), "一同造访了甜品店")
CALL K120_PRINT_ACHIEVEMENT_STATE(!FIRSTTIME("AunnBoughtHeadpiece", 1), "给阿吽买了帽子")
CALL K120_PRINT_ACHIEVEMENT_STATE(!FIRSTTIME("AunnKourindouFindsPolaroidCamera", 1), "与阿吽一起拍了照片")
;(当前已停用) CALL K120_PRINT_ACHIEVEMENT_STATE(!FIRSTTIME("AunnMeetsMysteryCoalSeller", 1) && !FIRSTTIME("AunnMeetsMysteryLieutenant", 1) && !FIRSTTIME("AunnMeetsMysteryBreadGirl", 1), "与所有神秘陌生人相遇")
CALL K120_PRINT_ACHIEVEMENT_STATE(!FIRSTTIME("AunnFishingCaughtDriftwood", 1), "一起钓到了浮木")
CALL K120_PRINT_ACHIEVEMENT_STATE(!FIRSTTIME("AunnFindsTheTwoPiece", 1), "潜入月之都并找到了『Two Piece』")
CALL K120_PRINT_ACHIEVEMENT_STATE(!FIRSTTIME("AunnRoLFestivalDate", 1), "与阿吽进行了祭典约会")
CALL K120_PRINT_ACHIEVEMENT_STATE(!TALENT:無接吻経験, "双方自愿地接吻了")
CALL K120_PRINT_ACHIEVEMENT_STATE(TALENT:恋人, "成为了恋人")
CALL K120_PRINT_ACHIEVEMENT_STATE(CFLAG:兒童人数 > 0, "共同育有孩子")
CALL K120_PRINT_ACHIEVEMENT_STATE(CFLAG:1004, "尽情抚摸了她毛茸茸的尾巴")
PRINTFORML 

;Show Who's Who Progress if Player shared incident knowledge of EoSD or later.
IF CFLAG:1003 >= 6
    PRINTFORML 
    FONTBOLD
    PRINTFORML 【“谁是谁”进度】
    FONTREGULAR
    PRINTFORM 　　● 当前章节: 
    CALL COLORMESSAGE(@"  东方%TOSTR(CFLAG:1003)%", CFLAG:1003 >= 18 ? C_GREEN # C_YELLOW, 1)
    PRINTFORM 　　● 已探访地点:
    CALL COLORMESSAGE(@"%TOSTR(CFLAG:1002)%/13", CFLAG:1002 >= 13 ? C_GREEN # C_YELLOW, 1)
    PRINTFORML 
ENDIF

;Display if ending is achievable or was achieved.
PRINTFORML 
FONTBOLD
PRINTFORML 【结局进度】
FONTREGULAR
IF !FIRSTTIME("AunnEndingAchievedVerTH18", 1)
    CALL COLORMESSAGE("　　结局已达成！", C_GREEN, 1)
ELSEIF K120_ENDING_POSSIBLE()
    PRINTFORM 　　结局可达成: 
    CALL COLORMESSAGE("是", C_YELLOW, 1)
ELSE
    PRINTFORM 　　结局可达成: 
    CALL COLORMESSAGE("否", C_RED, 1)
ENDIF

RETURN 1

;--------------------------------------------------------
;K120_PRINT_ACHIEVEMENT_STATE
;Private method handling the logic for printing the state of given achievement
;
;Parameters:
; - bool   ARG  -- Achievement to write state of
; - string ARGS -- Line to print if achievement was achieved
;
;Return: void (prints dialogue)
;--------------------------------------------------------
@K120_PRINT_ACHIEVEMENT_STATE(ARG, ARGS)
IF ARG
    PRINTFORML 　　● %ARGS%
ELSE
    PRINTFORML 　　● ?????????
ENDIF

;--------------------------------------------------------
;K120_RETURN_HAT_NAME
;Internal method which returns wanted string from double language hat name (ex. "hatNameEn_hatNameJp")
;
;Parameters:
; - string ARGS -- Hat name string to split
; - int    ARG  -- 0 for EN, 1 for JP
;
;Return: Hat name in given language
;--------------------------------------------------------
@K120_RETURN_HAT_NAME(ARGS, ARG)
#FUNCTIONS
#DIMS DYNAMIC HAT_NAMES_ARRAY, 2 ;Array that includes split hat name into EN and JP counterparts

SPLIT ARGS, "_", HAT_NAMES_ARRAY
RETURNF HAT_NAMES_ARRAY:ARG

;--------------------------------------------------------
;K120_BUY_AND_EQUIP_HAT
;Internal method which handles actually buying and equipping the chosen hat.
;
;Parameters:
; - int    ARG  -- Price of chosen hat
; - string ARGS -- Unsplit name of hat
;
;Return: void
;--------------------------------------------------------
@K120_BUY_AND_EQUIP_HAT(ARG, ARGS)
PRINTFORMDW 你为阿吽购买了%ARGS%。
PRINTFORML 「%SPLIT_G("那个看起来真棒！:你真的可以给我这个吗？:哦～！那是给我的吗？:它绝对看起来很适合我！")%」
EQUIP:120:帽子 = CLOTHES_ID("帽子", ARGS)
CFLAG:120:202 = CLOTHES_ID("帽子", ARGS)
MONEY -= ARG

;--------------------------------------------------------
;K120_CALCULATE_FAVOR_GAIN
;Internal method which calculates and returns the Favor gain for given price
;
;Parameters:
; - int ARG  -- Price of hat
;
;Return: Calculated Favor gain
;--------------------------------------------------------
@K120_HAT_CALCULATE_FAVOR_GAIN(ARG)
#FUNCTION
RETURNF ARG / 10

;--------------------------------------------------------
;K120_HAT_CALCULATE_TRUST_GAIN
;Internal method which calculates and returns the Trust gain for given price
;
;Parameters:
; - int ARG  -- Price of hat
;
;Return: Calculated Trust gain
;--------------------------------------------------------
@K120_HAT_CALCULATE_TRUST_GAIN(ARG)
#FUNCTION
RETURNF ARG / 14

;--------------------------------------------------------
;K120_HAT_SHOP_HANDLER
;Method to print hat choices in hat shop and handle logic for buying and browsing
;LOCALS:1 - 4 = hat/ribbons types
;LOCAL:1 - 4 = respective price
;
;Parameters:
; - int ARG -- 1 for hat, 2 for ribbon
; - int ARG:1 -- Total time spent browsing
;
;Return: void
;--------------------------------------------------------
@K120_HAT_SHOP_HANDLER(ARG, ARG:1)
#DIMS color, 4
#DIMS hatString, 4
#DIM price, 4
#DIM choice
#DIM totalMinutesBrowsing
#DIM FavorGain
#DIM TrustGain

;Intro text
PRINTFORMDW 你走进商店，门铃叮当作响。
CALL COLORMESSAGE("「欢迎光临!」", C_P_PURPLE, 1)
PRINTFORMDW 年轻的店员愉快地向你打招呼。
$BROWSE_FOR_HEADGEAR
PRINTFORML 
CALL COLORMESSAGE(@"当前资金: %DIGIT_GROUP(MONEY)%", C_YELLOW, 1)
PRINTFORMDL 你和阿吽一起浏览店里的\@ ARG == 1 ? 帽子 # 发带与头饰 \@。
PRINTFORML 

;Build the 4 choices with color and hat/ribbon type + price
FOR choice, 0, 4
    IF ARG == 1
        hatString:choice = %SPLIT_G("帽子:头巾:乌帽子:鸭舌帽:小碗:睡帽:头巾帽:兜帽:草帽:学生帽")%
    ELSEIF ARG == 2
        hatString:choice = %SPLIT_G("发饰:发箍:女仆发箍:发箍:兔耳发箍:护士帽:遮阳帽")%
    ENDIF
    price:choice = RAND(100, 2000)
    ;打印选项
    PRINTFORMDL [ %TOSTR(choice)%] %SPLIT_G("红:蓝:黄:橙:绿:紫:棕:白:黑:灰:粉:方格纹:波点")%%hatString:choice% - %TOSTR(price:choice)%￥
NEXT
PRINTFORMDL [ 4] 继续浏览（+10分钟）
PRINTFORMDL [ 5] 离开商店

DO
    INPUT
    SELECTCASE RESULT
        CASE 0
            IF price:0 > MONEY
                PRINTFORMDL 你没有那么多钱...
            ELSE
                CALL K120_BUY_AND_EQUIP_HAT(price:0, hatString:0)
                FavorGain = K120_HAT_CALCULATE_FAVOR_GAIN(price:0)
                TrustGain = K120_HAT_CALCULATE_TRUST_GAIN(price:0)
                BREAK
            ENDIF
        CASE 1
            IF price:1 > MONEY
                PRINTFORMDL 你没有那么多钱...
            ELSE
                CALL K120_BUY_AND_EQUIP_HAT(price:1, hatString:1)
                FavorGain = K120_HAT_CALCULATE_FAVOR_GAIN(price:1)
                TrustGain = K120_HAT_CALCULATE_TRUST_GAIN(price:1)
                BREAK
            ENDIF
        CASE 2
            IF price:2 > MONEY
                PRINTFORMDL 你没有那么多钱...
            ELSE
                CALL K120_BUY_AND_EQUIP_HAT(price:2, hatString:2)
                FavorGain = K120_HAT_CALCULATE_FAVOR_GAIN(price:2)
                TrustGain = K120_HAT_CALCULATE_TRUST_GAIN(price:2)
                BREAK
            ENDIF
        CASE 3
            IF price:3 > MONEY
                PRINTFORMDL 你没有那么多钱...
            ELSE
                CALL K120_BUY_AND_EQUIP_HAT(price:3, hatString:3)
                FavorGain = K120_HAT_CALCULATE_FAVOR_GAIN(price:3)
                TrustGain = K120_HAT_CALCULATE_TRUST_GAIN(price:3)
                BREAK
            ENDIF
        CASE 4
            IF totalMinutesBrowsing <= 60
                totalMinutesBrowsing += 10
                GOTO BROWSE_FOR_HEADGEAR
            ENDIF
            BREAK
        CASE 5
            BREAK
    ENDSELECT
LOOP 1

IF RESULT == 4
    ;Ran out of time
    PRINTFORML 「选不到合适的呢...」
    PRINTFORMDW 阿吽看起来逛腻了，你们决定暂时停下...
ELSEIF RESULT == 5
    ;未购买直接离开
    PRINTFORML 「啊，要走了吗？」
    PRINTFORMDW 你空手离开了商店。
    PRINTFORML 「那里没有适合我的东西呢...？」
    PRINTFORMDW 阿吽显得有些失落...
ELSE
    ;成功购买饰品
    PRINTFORMDW 阿吽将它戴在头上转了个圈。
    PRINTFORML 「怎么样，适合我吗？」
    PRINTFORMDW 你竖起了大拇指。
    PRINTFORMW 「诶嘿嘿～！真是太谢谢你啦！」
    IF TALENT:恋慕
        PRINTFORML 「我会永远珍惜它的！」
        PRINTFORMDW 阿吽蹭着你的手臂。
    ELSE
        PRINTFORML 「必须找机会回礼才行呢！」
    ENDIF
    PRINTFORMDW 你与雀跃的阿吽离开了商店...
    CFLAG:好感度 += FavorGain
    CFLAG:信頼度 += TrustGain
    CALL COLORMESSAGE(@"好感度＋%TOSTR(FavorGain)%!", C_YELLOW, 1)
    CALL COLORMESSAGE(@"信赖度＋%TOSTR(TrustGain)%!", C_YELLOW, 1)
    CALLF FIRSTTIME("AunnBoughtHeadpiece")
ENDIF
TIME += totalMinutesBrowsing

;--------------------------------------------------------
;K120_SET_TWO_PIECE_EVENT_PROGRESS
;Method to print and set two piece (lunar capital) event progress (CFLAG:1005) based ony given magic string
;
;Parameters:
; - string ARGS -- Magic string for outputting progress: "positive" (+1), "none" (0), "negative" (-1)
;
;Return: void (prints dialogue)
;--------------------------------------------------------
@K120_SET_TWO_PIECE_EVENT_PROGRESS(ARGS)
SELECTCASE ARGS
    CASE "positive"
        CALL COLORMESSAGE("在绘制都城地图方面取得了一些进展...", C_YELLOW, 2)
        CFLAG:1005 += 1
    CASE "none"
        CALL COLORMESSAGE("在绘制都城地图方面没有进展...", C_AQUA, 2)
    CASE "negative"
        CALL COLORMESSAGE("在绘制都城地图方面丢失了一些进展...", C_RED, 1)
        CFLAG:1005 -= 1
ENDSELECT