;
;这些函数可以按口上设置，但是custom_button_name、custom_talent等变量是角色变量。
;有多个口供的场合，如果盖上ID的话，会覆盖其他人的口上，所以要注意鉴别CFLAG:X:998是否是自己的口上编号。
;
;该部分是4.920之后更新的，更多的能给予口上自定义的接口
;暂未翻译

;================================
;统一SUCCESS函数
;如果与 com 对应的SUCCESS函数不存在，它将被调用
;你可以用它,在 com 之前进行一些数据处理。
;================================
;@M_KOJO_SUCCESS_K77_GENERAL

;================================
;统一EXTRASOURCE函数
;你可以用它,在某些 com 添加SOURCE补正
;请检测SELECTCOM
;================================
;@M_KOJO_EXTRASOURCE_SCOM_K77_GENERAL
@M_KOJO_EXTRASOURCE_COM_K77_GENERAL
SIF SELECTCOM < 0
	SELECTCOM = TFLAG:194

;================================
;自定义商店按钮
;Y 是按钮的编号，范围是0-9
;使用的时候请删除SKIPSTART~SKIPEND。
;================================
[SKIPSTART]
;判断按钮是否可以显示，如果返回1，则显示
@KOJO_CUSTOM_BUTTON_CONDITION_K77_Y
;custom_button_name:XX:Y = ボタンY
RETURN 1

;按钮被按下后，触发以下函数
@KOJO_CUSTOM_BUTTON_K77_Y

RETURN 1
[SKIPEND]

;================================
;自定义素质名
;使用的时候请删除SKIPSTART~SKIPEND。
;================================
[SKIPSTART]
@KOJO_CUSTOM_TALENT_SET_K77
;Z是素质ID，对应0~29
;如果这个变量不是0，就表示该素质
CUSTOM_TALENT:XX:Z = 1
;素質名
CUSTOM_TALENT_NAME:XX:Z = 自定义素质名
;(省略可)自定义素质的显示颜色，0/普通1/粉红2/红色+粗体
CUSTOM_TALENT_COLOR:XX:Z = 1
;(省略可)表示类别 1/種族 2/性的特徴 3/身体的特徴 4/精神的特徴 5/技術的特徴 他は「その他」
CUSTOM_TALENT_TYPE:XX:Z = 2
[SKIPEND]

;================================
;自定义指令
;Y 是自定义指令的编号，范围是0-29
;使用的时候请删除SKIPSTART~SKIPEND。
;================================
[SKIPSTART]
;指令名
@KOJO_COM_NAME_K77_Y
	RESULTS = com_name

;指令使用条件、和通常的com_able函数一样，如果返回1，指令就会显示为可执行。
@KOJO_COM_ABLE_K77_Y
	RETURN 1

;指令数据处理。记述经验、参数增减
;RETURN 1=指令実行 / RETURN 0=部分取消(SOURCE計算SKIP) / RETURN -1=指令失败
@KOJO_COM_K77_Y
	RETURN 1

;指令的文本内容。地文/口上请写在这里
;W的値等于(270 + Y)。SELECTCOM/PREVCOM也会变化成这个值
@M_KOJO_MESSAGE_COM_K77_W
	RETURN 1

;指令实行的可能判定
;可以在实行指令前显示一个条件实行可否的权重比较，并且
;可以参考 @CAN_COM9(ARG)，不能实行时请RETURN 0 ，允许实行时RETURN 1 
;或者可以不用写这部分
;@KOJO_CAN_COM_K77_Y(skipFlag)
;	#DIM skipFlag
;	SKIPDISP skipFlag
;	SKIPDISP 0
[SKIPEND]

;================================
;加载游戏时会自动读取以下函数并更新
;如果您的kojo无需持续更新，请忽略它
;;================================
;@KOJO_VERSION_K77
	;返回当前口上的版本数
	; RETURN now_version

;当存档记录的变量CFLAG:kojo_version比上面的口上版本号低，即为旧版本的时候，此处将调用
;@KOJO_VERSION_UPDATE_K77
	;
	; RETURN now_version

;-------------------------------------------------
;自定义陷落条件
;YEARNING-思慕 LOVE-恋慕 FUCK BUDDY-炮友 LUST-爱欲
;-------------------------------------------------
;覆写判定
;RETURN=1，以自定义条件覆盖原版条件 =0，自定义条件与原版条件需要同时满足 =-1 禁止转变为该陷落状态
;使用的时候请删除SKIPSTART~SKIPEND。
;-------------------------------------------------
[SKIPSTART]
@M_KOJO_CHECK_K77_FALL_STATE_OVERRIDE(ARGS,ARG,ARG:1)
SELECTCASE ARGS
	CASE "YEARNING"

	CASE "LOVE"

	CASE "FUCK BUDDY"

	CASE "LUST"

ENDSELECT
RETURN 0
[SKIPEND]

;-------------------------------------------------
;为角色设置自定义陷落条件
;RETURN 自定义条件的数量
;RESULT(S) 存储每个条件的结果及其描述文本
;使用的时候请删除SKIPSTART~SKIPEND。
;-------------------------------------------------
[SKIPSTART]
@M_KOJO_CHECK_K77_FALL_STATE(ARGS,ARG,ARG:1)
SELECTCASE ARGS
	CASE "YEARNING"
		RESULTS:1 = 需要很会聊天
		RESULT:1 = ABL:MASTER:話術技能 > 2
		RETURN 1

	CASE "LOVE"
		RESULTS:1 = 妹妹也愿意接受他
		RESULT:1 = ABL:38:9 > 8
		RESULTS:2 = 不能伤到妹妹的心
		RESULT:2 = !TALENT:38:3
		SIF 姐妹交心
			RESULT:2 = 1
		RETURN 2

	CASE "FUCK BUDDY"

	CASE "LUST"
		RESULTS:1 = 采用绝对不可实现的条件，来禁止转变向该状态
		RESULT:1 = 0
		RETURN 1

ENDSELECT
RETURN 0
[SKIPEND]

;-------------------------------------------------
;自定义指令控制
;-------------------------------------------------
;返回每个可能的CCOM的修改后权重
;ARG是CCOM编号，ARG:1是原始权重
;返回0表示不改变，-1表示完全禁用
;使用的时候请删除SKIPSTART~SKIPEND。
;-------------------------------------------------
[SKIPSTART]
@M_KOJO_CHECK_K77_CCOM_ORDER(ARGS,ARG,ARG:1)
SELECTCASE ARG
	CASE 3
		RETURN ARG:1 * 3
ENDSELECT
RETURN 0
[SKIPEND]

;-------------------------------------------------
;自定义自慰
;决定@ONABARE中的自慰是否限制玩弄哪个洞
;返回0表示使用原版判定，1指定阴道，2指定肛门
;使用的时候请删除SKIPSTART~SKIPEND。
;-------------------------------------------------
[SKIPSTART]
@M_KOJO_CHECK_K77_ONABARE_HOLE(ARGS,ARG,ARG:1)
SIF TALENT:処女
	RETURN 2

RETURN 0
[SKIPEND]

;-------------------------------------------------
;自定义逆推
;@诶嘿嘿中特殊事件 - ERB\イベント関連\EVENTCOMEND2.ERB
;来自于蛇版4.973-关于逆推的修改
;-------------------------------------------------
;ARGS=OVERRIDE 覆写判定
;RETURN =1 以自定义条件判定角色夺取主导成功 =0 采用原版的概率机制 =-1 以自定义条件判定禁止角色夺取主导
;ARGS=ABLE 在原版的概率机制上引入更多条件或者重写计算式
;ARG=原本计算出的夺主导权概率 ※ARGS=OVERRIDE时不能获得该参数
;概率判定 格式为：1/概率 角色越强势概率越高→分母越小
;RETURN =正值 则返回新的夺主导权概率 若=0或负数 则采用原版的概率机制
;使用的时候请删除SKIPSTART~SKIPEND。
;-------------------------------------------------
[SKIPSTART]
@M_KOJO_CHECK_K77_OSHITAOSARE(ARGS,ARG,ARG:1)
#DIM 夺主导权概率
SELECTCASE ARGS
	CASE "OVERRIDE"
		IF CFLAG:积攒度 > 700
			RETURN 1
		ELSEIF CFLAG:积攒度 > 500
			RETURN 0
		ELSE
			RETURN -1
		ENDIF
	CASE "ABLE"
		夺主导权概率 = ARG
		夺主导权概率 += ABL:MASTER:戦闘能力 - ABL:TARGET:戦闘能力
		夺主导权概率 += TALENT:MASTER:体型 - TALENT:TARGET:体型
		RETURN MAX(夺主导权概率,1)
ENDSELECT
[SKIPEND]

;-------------------------------------------------
;自定义工作
;-------------------------------------------------
;完全遵循@仕事内容設定和@JOBMOVE两函数的结构
;@ERB\MOVEMENTS\JOB_仕事内容.ERB
;RETURN 1以启用自定义内容
;使用的时候请删除SKIPSTART~SKIPEND。
;-------------------------------------------------
@M_KOJO_CHECK_K77_JOB(ARGS,ARG,ARG:1)
SELECTCASE ARGS
	CASE "SETUP"
		CUSTOM_JOB_DESC:77:0 = 降下落叶
		CSTR:77:工作情報 = 降下落叶 秋之月3～7日 13:00～17:00
		CSTR:77:職場 = 妖怪之山山脚附近
		IF DAY:2 == 3 && INRANGE(DAY:3, 3, 7) && TIME:5 != 5
			MAXBASE:77:工作量 = 600 * 4
			TCVAR:77:工作開始 = CLOCK_TO_TIME(13, 0)
			TCVAR:77:工作結束 = CLOCK_TO_TIME(17, 0)
			CFLAG:77:職場 = P705山道・中腹
			CFLAG:77:職種 = JOB_採集
			TCVAR:77:工资 = 0
		ENDIF
	CASE "MOVE"

ENDSELECT
RETURN 0

;-------------------------------------------------
;自定义移动倾向以及固有料理
;@ERB\キャラデータ\Chara_data_X_xxxxx.ERB
;-------------------------------------------------
;料理部分，请完全遵循CharaData的格式
;移动部分与CharaData有格式差异，请仔细参考样例
;使用的时候请删除SKIPSTART~SKIPEND。
;-------------------------------------------------
[SKIPSTART]
@M_KOJO_CHECK_K77_CHARADATA(ARGS,ARG,ARG:1)
SELECTCASE ARGS
	CASE "SPECIAL_MENU"
		DISH_NAME = 红白馒头
		RESULTS = 甜点
		CALL 材料設定("甜味料")
		DISH_TASTE = 和食/饅頭/甜/上品/芸術的/软糯的/祝福/
		DISH_COMMENT = 庆祝喜事时食用的喜庆馒头。具有[祝福]的效果。
		RETURN 1
	CASE "MOVE"
		SELECTCASE ARG
			CASE 1
				;MOVE-1依附移动:依赖某一角色的移动行为，并总是试图移动到与他们相同的位置
				;如果返回值一个有效的角色编号，将依附该角色
				;如果返回0，将使用原版的数值
				;如果返回-1，将忽略原版的数值
				;如果RESULT:1-3被设置，但未设计返回值/RESULT:0/RESULT，将开启对等连动
				;所谓的对等连动，本质上是一种特殊的依附移动；在三个可能的连动对象中，总是会试图选择[恋慕]状态列表中第一个找到的角色，再是[思慕]列表中的第一个，最后是编号顺序中第一个找到的角色
				IF DAY:2 == 1 ;在春天依附到这些角色中的一个
					RESULT:1 = [[霊夢]]
					RESULT:2 = [[る～こと]]
					RESULT:3 = [[カナ]]
				ELSEIF DAY:2 == 2 ;在夏天依附到妖梦
					RETURN [[妖夢]]
				ELSEIF DAY:2 == 3 ;在秋天不依附任何人，并忽略原版数据
					RETURN -1
				ELSE ;不依附任何人，但如果原版数据可用，则使用原版数据
					RETURN 0
				ENDIF
			CASE 3
				;move-3生活时间补正:增加非睡眠时间
				;如果返回1，将使用自定义数值）不然原版数值将覆盖赋值
				;如果返回0，将使用原版的数值
				;如果返回-1，则什么也不会发生
				IF DAY:2 == 3 
					CFLAG:生活時間補正 = -120
				ELSEIF DAY:2 == 4 
					CFLAG:生活時間補正 = 60
				ELSE
					CFLAG:生活時間補正 = 0
				ENDIF
				RETURN 1 
			CASE 4
				;move-4据点地图中的移动倾向：（±10 = 倾向去/不容易去, -100 = 绝对不会去）
				;细分为停留概率和移动概率两类，可以分别赋值，也可以按原版那样用同一个值处理
				;如果返回1，将覆盖原版数值，以RESULT:1/RESULT:2作为停留概率和移动概率，如果没有RESULT:2则用RESULT:1代替
				;如果返回0，将使用原版的数值
				;如果返回-1，将忽略原版的数值
				SELECTCASE ARG:1
					;不去永远亭
					CASE P401永遠亭入口 TO P430地下室
						RESULT:1 = -100
					;很愿意探索梦幻馆
					CASE P450玄関 TO P466庭
						RESULT:1 = 10
					;不会赖在地图入口不动
					CASE P447竹林入口
						RESULT:1 = -5
				ENDSELECT
				RETURN 1
			CASE 7
				;move-7小地图中的移动倾向：判定只有能否出现在该区域
				;如果返回1，则能出现在该区域
				;如果返回0，将使用该区域的原版数值
				;如果返回-1，将忽略该区域的原版数值
				SELECTCASE ARG:1
					CASE 桃林～砂浜, 静かの海
						RETURN 1
					CASE 白玉楼庭, 白玉楼
						RETURN 1
					CASEELSE
					;要完全覆盖原版设定，就必须避免出现返回0的情况。因此在CASEELSE分枝返回-1必不可少
						RETURN -1
				ENDSELECT
				RETURN 0
			CASE 8
				;move-8引力点：设置此值的角色将始终尝试移动到MAIN_MAP上的此位置
				;如果返回1，将使用自定义数值）不然原版数值将覆盖赋值
				;如果返回0，将使用原版的数值
				;如果返回-1，则什么也不会发生
				SIF DAY:3 == 18 ;在秋月18号，将不惜一切代价尝试访问幽香卧室
					TCVAR:引力点 = P463幽香私室
				RETURN 1 ;覆盖默认值
		ENDSELECT
ENDSELECT
[SKIPEND]
