;判断人物位置是否在妖怪之山室外（如索道站一类的地点就不算）
@K77_LOCATION_IS_YOUKAIMOUNTAIN_OUTSIDE(ARG)
#FUNCTION
IF (CFLAG:ARG:現在位置 >= 701 && CFLAG:ARG:現在位置 <= 712 && CFLAG:ARG:初期位置 / 100 == 7) || (((CFLAG:ARG:現在位置 % 10 >= 71 && CFLAG:ARG:現在位置 % 10 <= 74) || CFLAG:ARG:現在位置 / 10 == 77) || (CFLAG:ARG:現在位置 / 10 >= 81 && CFLAG:ARG:現在位置 / 10 <= 83) && CFLAG:ARG:初期位置 / 100 != 7)
    RETURNF 1
ELSE
    RETURNF 0
ENDIF

;从小铃那借（chao）的画像显示函数，默认为通常
@K77_IMAGESHOW_WITHOTHER(ARG)
CALL 画像セット("顔絵_服_通常_77", 0, 0, 75, 0, 0, "", "顔絵_通常")
SIF ARG
	CALL 画像セット(@"顔絵_服_通常_{ARG}", 0, 0, 75, 0, 0, "", "顔絵_通常")
CALL 画像一斉表示("左", 0, 1)

;从慧音那里借的函数，用来确保你拒绝4次告白后静叶再也不会和你互动（4次了那这把是不是你打的有问题？）
@K77_RELATIONCLEAR
ABL:77:9 = 0
ABL:77:10 = 0
CFLAG:77:2 = 0
CFLAG:77:4 = 0
TALENT:77:思慕 = 0
TALENT:77:恋慕 = 0
TFLAG:193 = 0
BASE:77:10 = 0
RETURN RESULT

;获取ARG个人时除静叶外另一人的人物编号，若无则返回0
@K77_GETNO_ANOTHERONE(ARG = 2 , ARG:1 = 77)
#FUNCTION
SIF ARG < 2
RETURNF 0
FOR LOCAL, 1, ARG + 1
    SIF TARGET:LOCAL == ARG:1
        CONTINUE
    RETURNF TARGET:LOCAL
NEXT

;获取在场多人时除静叶外的指定人物编号，若无则返回0。目前指定的人物有：灵梦(1)、魔理沙(11)、文(29)、果(42)、椛(65)、早苗(31)、神奈子(32)、诹访子(33)、荷取（51）以及穰子(78)
;但是一旦互动过一次后则会跳过到下一人的检测中，优先级从高到低为78、51、29、42、65、31、32、33、1、11
;经群里代码大佬指导写出了如下的函数，呜呜呜我太菜了
;使用数组进行了改进
@K77_MATCHPERSON_INPRESENCE
#FUNCTION
#DIM RELATED_PEOPLE = 78, 51, 29, 42, 65, 31, 32, 33, 1, 11
FOR LOCAL, 0, 9
    IF CFLAG:(RELATED_PEOPLE:LOCAL):現在位置 == CFLAG:77:現在位置 && !GETBIT(TCVAR:77:TALK_WITHOTHER_STATUS, LOCAL)
        SETBIT TCVAR:77:TALK_WITHOTHER_STATUS, LOCAL
        RETURNF RELATED_PEOPLE:LOCAL
    ENDIF
NEXT
RETURNF 0

;获取好感度最高的前ARG个角色的编号
;同样也是经过大佬指点迷津后写出来的（划掉，其实是完完全全给我重新写了一个），主要是明白了排序还是要自己来整的，没有现成的数据用
@K77_RANK_FAVOR_INHIGH(ARG)
#DIM RANK
#DIM ra
#DIM flagnum
#DIM ranum
RANK = 0
ra = 0
flagnum = 0
ranum = 0
VARSET LOCAL
flagnum = 2 ;好感度
FOR RANK,1,CHARANUM
    FOR ra,1,CHARANUM
        IF CFLAG:RANK:flagnum < CFLAG:ra:flagnum || (CFLAG:RANK:flagnum == CFLAG:ra:flagnum && RANK > ra)
            LOCAL:RANK ++
        ENDIF
    NEXT
NEXT
FOR ranum,1,ARG+1
    FOR RANK,1,CHARANUM
        IF LOCAL:RANK==ranum-1
            SIF CFLAG:(RANK):flagnum == 0
                BREAK
            ;PRINTFORM {ranum}位  %CALLNAME:RANK%  
            ;PRINTFORML 好感度{CFLAG:RANK:flagnum}
            RESULT:ranum = RANK
        ENDIF
    NEXT
NEXT

;概率函数，返回1的概率为MIN(ARG/ARG:1, 1)，这里/为浮点运算
@K77_PROB(ARG, ARG:1)
#FUNCTION
SIF ARG >= ARG:1 || (1 + RAND:(ARG:1) > ARG:1 - ARG)
    RETURNF 1
RETURNF 0

;用于给出静叶知不知道你的茶里加了些“东西”
;概率是随机的，但是随着泡怪茶的次数变得越来越大，到10及以上（或者知道5次）后固定为1
@K77_KNOW_WEIRDTEA
#FUNCTION
#DIM TEA_COUNT
#DIM KNOWING_COUNT
TEA_COUNT = CFLAG:77:MAKING_WEIRDTEA_COUNT
KNOWING_COUNT = CFLAG:77:KNOWING_WEIRDTEA_COUNT
SIF COUNT >= 10 || KNOWING_COUNT >= 5
    RETURNF 1
SIF (TALENT:恋人 || TALENT:恋慕) && K77_PROB(KNOWING_COUNT + 3, TEA_COUNT + 3)
    RETURNF TALENT:MASTER:調合知識 ? K77_PROB(KNOWING_COUNT + 3, TEA_COUNT + 3) # 1
SIF (TALENT:思慕 || TALENT:炮友) && K77_PROB(KNOWING_COUNT + 2, TEA_COUNT + 2)
    RETURNF TALENT:MASTER:調合知識 ? K77_PROB(KNOWING_COUNT + 2, TEA_COUNT + 2) # 1
SIF !CHECK_CHARA(77, "陥落") && K77_PROB(KNOWING_COUNT + 1, TEA_COUNT + 1)
    RETURNF TALENT:MASTER:調合知識 ? K77_PROB(KNOWING_COUNT + 1, TEA_COUNT + 1) # 1
RETURNF 0

;用于随机抽取孩子的编号（ARG = 0-不限性别，1-只挑女孩，2-只挑男孩；ARG:1 = 0-不限阶段，1-不会说话，2-会说话，3-长大了一些但尚未自立，4-已经自立）
;LOCAL:1 = 0-男孩，1-女孩
;LOCAL:2 = 0-尚未自立，1-自立
;LOCAL:3和LOCAL:4用于确保是否存在这样的孩子
@K77_RANDOMSELECT_CHILDNUM(ARG = 0, ARG:1 = 0)
#FUNCTION
#DIM EXIST
EXIST = 0
VARSET LOCAL
SIF !CFLAG:77:兒童人数
    RETURNF -1
FOR LOCAL, 0, CFLAG:77:兒童人数
    SELECTCASE ARG
        CASE 0
            LOCAL:3 = 1
        CASE 1
            SIF CHILD_SEX:77:LOCAL == 1
                LOCAL:3 = 1
        CASE 2
            SIF CHILD_SEX:77:LOCAL != 1
                LOCAL:3 = 1
    ENDSELECT
    SELECTCASE ARG:1
        CASE 0
            LOCAL:4 = 1
        CASE 1
            SIF DAY - CHILD_BIRTHDAY:77:LOCAL < CHILD_語彙
                LOCAL:4 = 1
        CASE 2
            SIF DAY - CHILD_BIRTHDAY:77:LOCAL >= CHILD_語彙
                LOCAL:4 = 1
        CASE 3
            SIF DAY - CHILD_BIRTHDAY:77:LOCAL >= CHILD_しつけ && DAY - CHILD_BIRTHDAY:77:LOCAL < CHILD_自立
                LOCAL:4 = 1
        CASE 4
            SIF DAY - CHILD_BIRTHDAY:77:LOCAL >= CHILD_自立
                LOCAL:4 = 1
    ENDSELECT
    IF LOCAL:3 == 1 && LOCAL:4 == 1
        EXIST = 1
        BREAK
    ENDIF
NEXT
SIF !EXIST
    RETURNF -1
WHILE (1)
    LOCAL = RAND:(CFLAG:77:兒童人数)
    SELECTCASE ARG
        CASE 0
            LOCAL:1 = 1
        CASE 1
            SIF CHILD_SEX:77:LOCAL == 1
                LOCAL:1 = 1
        CASE 2
            SIF CHILD_SEX:77:LOCAL != 1
                LOCAL:1 = 1
    ENDSELECT
    SELECTCASE ARG:1
        CASE 0
            LOCAL:2 = 1
        CASE 1
            SIF DAY - CHILD_BIRTHDAY:77:LOCAL < CHILD_語彙
                LOCAL:2 = 1
        CASE 2
            SIF DAY - CHILD_BIRTHDAY:77:LOCAL >= CHILD_語彙
                LOCAL:2 = 1
        CASE 3
            SIF DAY - CHILD_BIRTHDAY:77:LOCAL >= CHILD_しつけ && DAY - CHILD_BIRTHDAY:77:LOCAL < CHILD_自立
                LOCAL:2 = 1
        CASE 4
            SIF DAY - CHILD_BIRTHDAY:77:LOCAL >= CHILD_自立
                LOCAL:2 = 1
    ENDSELECT
    SIF LOCAL:1 != 1 || LOCAL:2 != 1
        CONTINUE
    RETURNF LOCAL
WEND

;获取自机恋人（ARG等于1）或恋慕（ARG等于2）数量，默认同时检测二者
@K77_GET_LOVENUM(ARG = 0)
#FUNCTION
VARSET LOCAL
FOR LOCAL, 1, CHARANUM
    SELECTCASE ARG
        CASE 0
            SIF TALENT:LOCAL:恋人 || TALENT:LOCAL:恋慕
                LOCAL:1 += 1
        CASE 1
            SIF TALENT:LOCAL:恋人
                LOCAL:1 += 1
        CASE 2
            SIF TALENT:LOCAL:恋慕
                LOCAL:1 += 1
    ENDSELECT
NEXT
RETURNF LOCAL:1

;EVENTCOMEND重构函数
@EVENTCOMEND
;这东西不能随便就被调用
SIF CFLAG:77:998 || TARGET != 77 || CFLAG:77:诶嘿嘿 || FLAG:時間停止 || CFLAG:77:陪睡中 || CFLAG:77:睡眠 || CFLAG:77:心情不快 || MARK:77:反発刻印 || BATHCHECK(CFLAG:MASTER:300)
    RETURN 0
SIF TFLAG:约会道中 && CFLAG:77:好感度 > 100 && FIRSTTIME("K77_GETCAMERA",,77)
    CALL K77_GETCAMERA
IF GROUPMATCH(SELECTCOM, 301, 331) && INROOM(CFLAG:MASTER:300) && !GETBIT(TCVAR:77:MAKING_WEIRDTEA, 0)
    SIF TALENT:77:恋人 && K77_PROB(3, 7)
        CALL K77_MAKING_WEIRDTEA_SHIZUHA(1)
    SIF TALENT:77:恋慕 && K77_PROB(2, 7)
        CALL K77_MAKING_WEIRDTEA_SHIZUHA(1)
ENDIF