;==================================================
;进爱爱判定
;用来在指令之外进爱爱（默认男上）
;==================================================
@K43_WOYAOSESE
CFLAG:TARGET:诶嘿嘿 = 1
TFLAG:COMABLE管理 = 2
IF INROOM(CFLAG:MASTER:現在位置) == 2
    CALL DATUI_SHOES(LOCAL,0)
    CALL DATUI_SHOES(MASTER,0)
ENDIF
SIF !FLAG:70
    TIME += 1
IF !UFUFU_START_TIME
	UFUFU_START_TIME = TIME
	UFUFU_START_DAY = DAY
ENDIF 
RETURN 1

;==================================================
;界限判定
;用来检测日常性骚扰时拒绝/允许分界
;==================================================
@K43_JIEXIAN
IF TALENT:恋人 
	IF TALENT:15 == 1
		IF BASE:情緒 <= 75 && BASE:理性 >= 950
			CFLAG:43:1011 = 1
		ELSE
			CFLAG:43:1011 = 0
		ENDIF
	ELSEIF TALENT:15 == -1
		IF BASE:情緒 <= 225 && BASE:理性 >= 850
			CFLAG:43:1011 = 1
		ELSE
			CFLAG:43:1011 = 0
		ENDIF		
	ELSE
		IF BASE:情緒 <= 150 && BASE:理性 >= 900
			CFLAG:43:1011 = 1
		ELSE
			CFLAG:43:1011 = 0
		ENDIF
	ENDIF
ELSEIF TALENT:恋慕 
	IF TALENT:15 == 1
		IF BASE:情緒 <= 225 && BASE:理性 >= 850
			CFLAG:43:1011 = 1
		ELSE
			CFLAG:43:1011 = 0
		ENDIF
	ELSEIF TALENT:15 == -1
		IF BASE:情緒 <= 375 && BASE:理性 >= 750
			CFLAG:43:1011 = 1
		ELSE
			CFLAG:43:1011 = 0
		ENDIF		
	ELSE
		IF BASE:情緒 <= 300 && BASE:理性 >= 800
			CFLAG:43:1011 = 1
		ELSE
			CFLAG:43:1011 = 0
		ENDIF
	ENDIF
ELSEIF TALENT:思慕 || ABL:親密 >= 5
	IF TALENT:15 == 1
		IF BASE:情緒 <= 375 && BASE:理性 >= 750
			CFLAG:43:1011 = 1
		ELSE
			CFLAG:43:1011 = 0
		ENDIF
	ELSEIF TALENT:15 == -1
		IF BASE:情緒 <= 525 && BASE:理性 >= 650
			CFLAG:43:1011 = 1
		ELSE
			CFLAG:43:1011 = 0
		ENDIF		
	ELSE
		IF BASE:情緒 <= 450 && BASE:理性 >= 700
			CFLAG:43:1011 = 1
		ELSE
			CFLAG:43:1011 = 0
		ENDIF
	ENDIF
ELSE
	IF TALENT:15 == 1
		IF BASE:情緒 <= 525 && BASE:理性 >= 650
			CFLAG:43:1011 = 1
		ELSE
			CFLAG:43:1011 = 0
		ENDIF
	ELSEIF TALENT:15 == -1
		IF BASE:情緒 <= 675 && BASE:理性 >= 550
			CFLAG:43:1011 = 1
		ELSE
			CFLAG:43:1011 = 0
		ENDIF		
	ELSE
		IF BASE:情緒 <= 600 && BASE:理性 >= 600
			CFLAG:43:1011 = 1
		ELSE
			CFLAG:43:1011 = 0
		ENDIF
	ENDIF
ENDIF

;==================================================
;爱爱之后判定
;模拟一顿翻云覆雨后的数值加减
;==================================================
@K43_AIAIZHIHOU
#DIM CX0
#DIM CX1
#DIM CX2
#DIM CX3

CX0 = 0
CX1 = 0
CX2 = 0
CX3 = 0

CX0 += (CFLAG:340 - 100) / 100
SIF TCVAR:142
	CX0 += 4
SIF TCVAR:140
	CX0 += 4
CX1 = RAND:(CX0 + 1)
CX2 = RAND:(CX0 - CX1 + 1)
CX3 = RAND:(CX0 - CX1 - CX2 + 1)

DOWNBASE:PLAYER:体力 += (30 * CX0)
DOWNBASE:PLAYER:気力 += (40 * CX0)
DOWNBASE:体力 += (40 * CX0 + 5 * (CX1 + CX3))
DOWNBASE:気力 += (50 * CX0 + 50 * (CX1 + CX2 + CX3))

SOURCE:情愛 += (300 * CX0 + MIN(ABL:親密 * 15,500) * CX1 + (400 + (ABL:親密 * 15)) * CX2 + 150 * CX3)
SOURCE:露出 += (300 * CX0 + 150 * CX1 + 200 * CX2 + 250 * CX3)
SOURCE:歓楽 += (100 * CX1 + (400 + (ABL:親密 * 10)) * CX2 + 100 * CX3)
SOURCE:欲情 += (MIN(ABL:親密 * 15,500) * CX1 + (300 + 50 * ABL:欲望) * CX2 + 150 * CX3)
SOURCE:苦痛 += (50 * CX0)
SOURCE:受動 += ((120 + 240 * ABL:従順) * CX1 + (300 + 50 * ABL:従順) * CX2 + (145 + 270 * ABL:従順) * CX3)
SOURCE:PLAYER:快Ｃ += (400 * CX0 + 130 * CX3)
SOURCE:快Ｂ += (150 * CX1)
SOURCE:快Ｖ += ((400 + EXP:愛情経験) * CX0)	

PALAM:欲情 += (SOURCE:情愛 + SOURCE:露出 + SOURCE:歓楽) * 3 / 2 + SOURCE:欲情 + SOURCE:苦痛 * 4 / 5 + SOURCE:快Ｂ * (2 + TALENT:Ｂ感度) * SOURCE_REVISION2(ABL:欲望) / 2000 + SOURCE:快Ｖ * (2 + TALENT:Ｖ感度) * SOURCE_REVISION1(ABL:Ｖ感覚) * SOURCE_REVISION2(ABL:欲望) * 9 / 10000
PALAM:恥情 += SOURCE:露出
PALAM:好意 += SOURCE:歓楽 * 3 / 2
PALAM:苦痛 += SOURCE:苦痛 * 4 / 5
PALAM:恭順 += SOURCE:受動 / 2
PALAM:快Ｂ += SOURCE:快Ｂ * (2 + TALENT:Ｂ感度) * SOURCE_REVISION1(ABL:Ｂ感覚) * 3 / 40
PALAM:快Ｖ += SOURCE:快Ｖ * (2 + TALENT:Ｖ感度) * SOURCE_REVISION1(ABL:Ｖ感覚) * 3 / 40
PALAM:PLAYER:快Ｃ += SOURCE:PLAYER:快Ｃ * (2 + TALENT:PLAYER:Ｃ感度) * SOURCE_REVISION1(ABL:PLAYER:Ｃ感覚) * 3 / 40
PALAM:PLAYER:欲情 += MIN(SOURCE:歓楽 / 5 , 100) * 3 / 2 + SOURCE:PLAYER:快Ｃ * (2 + TALENT:PLAYER:Ｃ感度) * SOURCE_REVISION2(ABL:PLAYER:欲望) / 2000

EXP:PLAYER:手淫経験 ++
EXP:PLAYER:挿入経験 ++
EXP:Ｖ経験 ++
EXP:Ｖ性交経験 ++

EXP:50 += (4 * CX0)
EXP:PLAYER:50 += (4 * CX0)
EXP:愛情経験 += TALENT:恋慕 * 3 * CX0
TIME += (5 * CX0)

CFLAG:340 = 100
TCVAR:142 = 0
TCVAR:140 = 0

;==================================================
;工作相关（特别感谢33035（神子口上作者）的协助）
;-1=工作完成了 0=今天没有工作 1=今天将有工作 2=正在工作
;也能视奸其他人的工作状态，不过真的需要吗？
;==================================================
@K43_K56_TSMIKO_WORKCHECK(ARG)
#FUNCTION
SIF TCVAR:ARG:工作開始 == -1
    RETURNF 0
SIF TCVAR:ARG:工作開始 >= 0 && TIME < TCVAR:ARG:工作開始
    RETURNF 1
SIF TCVAR:ARG:工作開始 >= 0 && !BASE:ARG:工作量
    RETURNF -1
RETURNF 2

;==================================================
;胖次检定（特别感谢MOE（露米娅、拉尔瓦口上作者）的协助）
;请注意，这是一个式中函数
;从爱丽丝初期版中摘抄的
;这个算式的值即为自机身上属于拉尔瓦的胖次的数量，ARG = 118 中的118可换为自己的角色编号
;==================================================
@K43_CHECK_STOLEN_PANTIES(ARG = 43)
#FUNCTION
#DIM iPants_Type
#DIM iPants_Num
SIF !FLAG:853
	RETURNF 0

iPants_Num = 0
FOR iPants_Type,0,MAXPANTS
	SIF PANTS_TEMP:ARG:iPants_Type
		iPants_Num += PANTS_TEMP:ARG:iPants_Type
NEXT
RETURNF iPants_Num

;==================================================
;钓鱼气氛检测代码
;其实就是抄的蛇TW的钓鱼代码（直接用变量会报错所以只能Waaagh!了）
;==================================================
@K43_DIAOYU
#DIM DY1	;活性
#DIM DY2	;钓鱼运
;季节补正
SELECTCASE DAY:2
	CASE 1  ;春
		DY1 = 10
	CASE 2  ;夏
		DY1 = 7
	CASE 3  ;秋
		DY1 = 10
	CASE 4  ;冬
		DY1 = 3
ENDSELECT
;时间补正
SELECTCASE TIME:2
	CASE 1, 4  ;早朝と昼下がり
		DY1 += 10
	CASE 5, 6  ;夕と夜
		DY1 += 5
ENDSELECT
;天气补正
SELECTCASE TIME:5
	CASE 3, 6 ;云/雾雨
		DY1 += 5
	CASE 4, 5, 14, 15 ;雨
		DY1 += 7
	CASE 13
		IF GET_HEAT_THUNDERSTORM() == 1 || GET_FRONTAL_THUNDERSTORM() == 1
			DY1 += 7
		ELSE
			DY1 -= 3
		ENDIF
	CASE 8 TO 12 ;雪
		DY1 -= 3
ENDSELECT
;出彩虹
SELECTCASE TIME:7
	CASE 1
		DY1 += 7
ENDSELECT
;(天候パッチ)連続降水Counter分活性にプラス(最大+10)
DY1 += MIN(10, CONTINUOUS_RAINFALL)
;日替わりで-50～+50,道具やお祈り等で補正をかけることも可
DY2 = CFLAG:MASTER:今日の運勢 / 10 - 50
TCVAR:43:353 = DY1 + DY2

;==================================================
;上衣情况检测代码（对应判断：Tequip:1）
;用类似于原版检测的方法做一个更简易的检测
;下身赋值：bit3指安全裤，bit2指内裤，bit1指裙子(/连衣裙/和服)
;如：裸和服，Tequip:0 == 1 (== "001")
;==================================================
@K43_SHANGYI
TCVAR:43:354 = 0
SIF	EQUIP:上身衣服１	;可以敞开探进去揉胸（如早苗巫女服）
	TCVAR:43:354 += 10
SIF EQUIP:上身衣服２	;必须要先脱才能揉胸（如灵梦巫女服）
	TCVAR:43:354 += 20
SIF EQUIP:連衣裙
	TCVAR:43:354 += 30
SIF EQUIP:和服
	TCVAR:43:354 += 40
SIF EQUIP:上半身内衣１	;脱了后才能进行完整胸爱抚
	TCVAR:43:354 += 1
SIF EQUIP:上半身内衣２	;稍微扒开点就能玩乳头（默认比基尼）
	TCVAR:43:354 += 2
	
;==================================================
;调用其他角色口上代码
;用来在模拟别人说话的时候上对应的色
;==================================================
@K43_OFTE(ARG)
IF CFLAG:ARG:口上セレクタ
	TRYCALLFORM M_KOJO_K{ARG}_{CFLAG:ARG:口上セレクタ}
ELSE
	TRYCALLFORM M_KOJO_K{ARG}
ENDIF
TRYCCALLFORM M_KOJO%RESULTS%_COLOR_K{ARG}
CATCH
RESETCOLOR
ENDCATCH
GETCOLOR
RETURN  RESULT

@K43_OFTE_SET(ARG)
CALL K43_OFTE(ARG)
SETCOLOR RESULT