;-------------------------------------------------
;HTMLタグ函数
;-------------------------------------------------

@HTML_SPACE(ARG)
#FUNCTIONS
RETURNF @"<shape type='space' param='{50*ARG}'>"

@HTML_BUTTON(ARGS, ARG, digit=1)
#FUNCTIONS
#DIM DYNAMIC digit
digit = MAX(digit, STRLENS(@"{ARG}"))
RETURNF @"<button value='{ARG}'>" + @"[{ARG, digit}]"+ HTML_SPACE(1) + ARGS + "</button>"

@HTML_COLOR(ARGS, ARGS:1)
#FUNCTIONS
RETURNF @"<font color='#%ARGS:1%'>" + ARGS + "</font>"


@HTML_TEXT(CONTENT, COLOR, BOLD, ITALIC, STRIKE)
#FUNCTIONS
#DIMS CONTENT
#DIM COLOR
#DIM BOLD
#DIM ITALIC
#DIM STRIKE

SIF BOLD
    CONTENT = <b>%CONTENT%</b>
SIF ITALIC
    CONTENT = <i>%CONTENT%</i>
SIF STRIKE
    CONTENT = <s>%CONTENT%</s>

CONTENT = <font color='#%TOSTR(COLOR, "X6")%'>%CONTENT%</font>

RETURNF CONTENT

;splits HTML tags across lines cleanly
@AUTOMATIC_HTML_LINEBREAK(HTML_TEXT, MAX_LINE_WIDTH, SEPARATOR = "", INDENT = "")
#DIMS HTML_TEXT
#DIMS DYNAMIC HTML_CURRENT_LINE
#DIM MAX_LINE_WIDTH
#DIM DYNAMIC CURRENT_LINE_WIDTH
#DIM CURRENT_TAG_WIDTH
#DIM TAG_COUNT
#DIMS SEPARATOR
#DIMS INDENT
#LOCALSSIZE 1000

HTML_TEXT '= REPLACE(HTML_TEXT, "<br>", "") ;remove existing line breaks

IF SEPARATOR != "" && STRCOUNT(HTML_TEXT, SEPARATOR) ;provide a separator character to use pairs of it as a manual tag
	VARSET LOCALS
	LOCAL = 0
	SPLIT HTML_TEXT, SEPARATOR, LOCALS
	HTML_TEXT = 
	WHILE LOCALS:LOCAL != "" || LOCAL % 2
		HTML_TEXT += LOCALS:LOCAL + @"<\@LOCAL % 2 ?/#\@nonbutton>" ;nonbutton is effectively a blank since buttons are always tagged
		LOCAL++
	WEND
ENDIF

CALL HTML_TAGSPLIT_COMBINED(HTML_TEXT, LOCALS, TAG_COUNT)

FOR LOCAL, 0, TAG_COUNT
    CURRENT_TAG_WIDTH = STRLENS(@"%HTML_TOPLAINTEXT(LOCALS:LOCAL)%")
	IF SUBSTRINGU(LOCALS:(LOCAL + 1), 0) == "," ;comma cleaning
		LOCALS:LOCAL += ","
		CURRENT_TAG_WIDTH += 1
		LOCALS:(LOCAL + 1) = SUBSTRINGU(LOCALS:(LOCAL + 1), 1, -1)
	ENDIF
    IF CURRENT_LINE_WIDTH + CURRENT_TAG_WIDTH > MAX_LINE_WIDTH
        HTML_PRINT HTML_CURRENT_LINE
        CURRENT_LINE_WIDTH = STRLENS(INDENT)
        HTML_CURRENT_LINE = %INDENT%
        IF STRCOUNT(LOCALS:LOCAL, "　")
            LOCALS:LOCAL '= REPLACE(LOCALS:LOCAL, "　", "")
            CURRENT_TAG_WIDTH--
        ENDIF
    ENDIF
    HTML_CURRENT_LINE += LOCALS:LOCAL
    CURRENT_LINE_WIDTH += CURRENT_TAG_WIDTH
NEXT
HTML_PRINT HTML_CURRENT_LINE

;combines the tags returned by HTML_TAGSPLIT to list all tags as direct children
@HTML_TAGSPLIT_COMBINED(HTML_TEXT, ELEMENTS, ELEMENT_COUNT)
#DIMS HTML_TEXT
#DIMS REF ELEMENTS
#DIM REF ELEMENT_COUNT
#DIM DYNAMIC TAG_DEPTH
#DIM DYNAMIC TAG_COUNT
#DIM DEPTH_CHANGE
#DIMS DYNAMIC CURRENT_ELEMENT
#LOCALSSIZE 1000
VARSET ELEMENTS
TAG_COUNT = 0

HTML_TAGSPLIT HTML_TEXT, LOCALS, ELEMENT_COUNT

FOR LOCAL, 0, ELEMENT_COUNT
    TAG_DEPTH += STRCOUNT(LOCALS:LOCAL, "<[^\/].*?>")
    TAG_DEPTH -= STRCOUNT(LOCALS:LOCAL, "<\/.*?>")

    CURRENT_ELEMENT += LOCALS:LOCAL
    IF TAG_DEPTH == 0
        SIF STRLENS(CURRENT_ELEMENT)
            ELEMENTS:(TAG_COUNT++) '= CURRENT_ELEMENT
        CURRENT_ELEMENT =
    ENDIF
NEXT

SIF STRLENS(CURRENT_ELEMENT)
    ELEMENTS:(TAG_COUNT++) '= CURRENT_ELEMENT
